const Elf = @This();

const std = @import("std");
const Io = std.Io;
const assert = std.debug.assert;
const log = std.log.scoped(.link);

const codegen = @import("../codegen.zig");
const Compilation = @import("../Compilation.zig");
const Dwarf = @import("Dwarf2.zig");
const InternPool = @import("../InternPool.zig");
const link = @import("../link.zig");
const MappedFile = link.MappedFile;
const target_util = @import("../target.zig");
const tracy = @import("../tracy.zig");
const Type = @import("../Type.zig");
const Value = @import("../Value.zig");
const Zcu = @import("../Zcu.zig");
const Alignment = MappedFile.Alignment;

base: link.File,
options: link.File.OpenOptions,
mf: MappedFile,
ni: struct {
    elf: MappedFile.Node.Index,
    ehdr: MappedFile.Node.Index,
    shdr: MappedFile.Node.Index,
    rodata: MappedFile.Node.Index,
    phdr: MappedFile.Node.Index,
    text: MappedFile.Node.Index,
    data: MappedFile.Node.Index,
    data_rel_ro: MappedFile.Node.Index,
    tls: MappedFile.Node.Index.Optional,
    gnu_eh_frame: MappedFile.Node.Index.Optional,
},
archive: ?Archive,
nodes: std.MultiArrayList(Node),
/// Does not contain an item for `SHN_UNDEF`.
shdrs: std.ArrayList(Section),
phdrs: std.ArrayList(MappedFile.Node.Index.Optional),
shndx: struct {
    got: Section.Index,
    /// Always `.UNDEF` on some targets (e.g. SPARC).
    got_plt: Section.Index,
    plt: Section.Index,
    /// Only created for x86 targets; `.UNDEF` everywhere else.
    plt_sec: Section.Index,
    dynsym: Section.Index,
    dynstr: Section.Index,
    dynamic: Section.Index,
    hash: Section.Index,
    tdata: Section.Index,
    rela_dyn: Section.Index,
    rela_plt: Section.Index,
    gnu_version: Section.Index,
    gnu_version_d: Section.Index,
    gnu_version_r: Section.Index,
    debug_abbrev: Section.Index,
    debug_addr: Section.Index,
    eh_frame_hdr: Section.Index,
    eh_frame: Section.Index,
    debug_frame: Section.Index,
    debug_info: Section.Index,
    debug_line: Section.Index,
    debug_line_str: Section.Index,
    debug_rnglists: Section.Index,
    debug_str: Section.Index,
    debug_str_offsets: Section.Index,
    // These sections are created only as needed, and are initially `.UNDEF`.
    init_array: Section.Index,
    fini_array: Section.Index,
    preinit_array: Section.Index,
},
dynamic: struct {
    flags: u32,
    flags_1: u32,
    rpath: String(.dynstr),
    soname: String(.dynstr),
},
symtab: std.ArrayList(Symbol),
globals: std.ArrayList(Symbol.Global),
/// Accessed with `Symbol.Global.VersionedName.Adapter`. Items map 1--1 to `globals`.
globals_by_name: std.array_hash_map.Custom(void, void, void, true),
/// Accessed with `Symbol.Global.DefaultVersionAdapter`.
default_version_globals: std.array_hash_map.Custom(Symbol.Global.Index, void, void, true),
/// Set of all strong undef globals which we have also not yet seen defined in any input DSO. We
/// maintain this set to allow efficient reporting of "undefined global symbol" errors.
unknown_globals: std.array_hash_map.Auto(Symbol.Global.Index, void),
/// Key is a global which has multiple definitions, one an actual definition for that global, and
/// the other because it is an alias for another global (see `Symbol.Global.Index.resolveAlias`).
defined_alias_globals: std.array_hash_map.Auto(Symbol.Global.Index, void),
/// Key is an undef global for which we have created a "copy relocation" (`R_*_COPY`).
copied_globals: std.array_hash_map.Auto(Symbol.Global.Index, struct {
    node: MappedFile.Node.Index,
    /// The index of this global's runtime relocation in `.rela.dyn`.
    rela_index: Section.RelaIndex,
}),
/// When rearranging entries in the dynamic symbol table, we need to map a dynamic symbol index to
/// the corresponding `Symbol.Global.Index` which "owns" that dynsym entry. Usually this is usually
/// by simply looking up the dynamic symbol's name in `globals_by_name`, but versioned symbols may
/// have different names in the dynamic symbol table than the normal symbol table. If they do, they
/// are added to this map, which is checked in `Elf.globalByDynsym` before doing the aforementioned
/// name lookup.
///
/// Key is index into `.dynsym`.
versioned_dynsym_owners: std.array_hash_map.Auto(u32, Symbol.Global.Index),
/// Accessed with `VerdefAdapter`. Entries in `.gnu.version_d` are contiguous verdef/verdaux pairs
/// (see `VerdefEntry`), and the "base" version is not in this map, so a given `index` in this map
/// refers to the version defined at offset `(index + 1) * @sizeOf(VerdefEntry)` into the section.
verdef: std.array_hash_map.Custom(void, void, void, true),
/// Accessed with `VerneedAdapter`. Entries in `.gnu.version_r` are either `std.elf.Verneed` or
/// `std.elf.Vernaux`---these types are the same size, and the entry at a given `index` in this map
/// refers to the verneed/vernaux at offset `index * @sizeOf(VerneedEntry)`. The stored key
/// indicates whether the entry is a verneed or a vernaux.
verneed: std.array_hash_map.Custom(VerneedAdapter.StoredKey, void, void, true),
/// Index into `Elf.verneed` of the last entry which represented a file. Tracked so that new files
/// can be added to the linked list in the verneed section.
last_verneed_file_index: usize,
/// The next unused symbol version ID, referenced by entries in the `.gnu.version` section and
/// defined by entries in the `.gnu.version_d` and `.gnu.version_r` sections. Initially 2, because
/// 0 and 1 are reserved. When `verdefId` or `verneedId` adds a new version, they will assign it
/// the ID stored here (and increment this field).
///
/// Note that documentation and ELF headers call these "version indices" rather than "version IDs",
/// but that's a misnomer: they are opaque IDs with no requirement to be contiguous or to be defined
/// in order. That said, dynamic linker implementations may internally create a version lookup table
/// indexed by the ID, so for efficiency it's good to keep them dense.
next_version_id: u16,
/// Key is a node which is a valid `Symbol.node` value, value is the first global symbol in that
/// node. That symbol is the head of a linked list: see `Symbol.Global.next_in_node`.
///
/// We use a separate hash map for this data rather than storing it in `navs` etc to save memory,
/// because the vast majority of nodes which can export global symbols actually will not.
node_global_symbols: std.array_hash_map.Auto(MappedFile.Node.Index, Symbol.Global.Index),
/// See the definition of `DsoGlobals`.
dso_globals: DsoGlobals,

shstrtab: StringTable,
strtab: StringTable,
dynstr: StringTable,

/// Indices map 1--1 to indices into the actual `.got` section.
///
/// Value is the output relocation in `.rela.dyn` for the GOT entry.
got: std.array_hash_map.Auto(GotKey, Section.RelaIndex.Optional),
/// Key is a global with a PLT entry.
///
/// Indices map 1--1 to indices into the actual `.got.plt` section. These also equal indices into
/// the relocations in `.rela.plt`, because every PLT entry has one output relocation (if a runtime
/// relocation is no longer necessary, then neither is the corresponding PLT entry!).
///
/// PLT entries in this map may be "dead", meaning the PLT entry has been deemed unnecessary so is
/// available for reuse---see `Elf.pltEntryIsDead`. Such entries must not be targeted by relocs.
plt: std.array_hash_map.Auto(Symbol.Global.Index, void),
/// The `.plt` section contains zero or more symbol relocations starting at this index.
plt_first_symbol_reloc: SymbolReloc.Index,
/// The `.eh_frame_hdr` section contains zero or more symbol relocations starting at this index.
eh_frame_hdr_first_symbol_reloc: SymbolReloc.Index,

needed: std.array_hash_map.Auto(String(.dynstr), void),
inputs: std.ArrayList(struct {
    path: std.Build.Cache.Path,
    member: ?[]const u8,
    extra: union {
        /// Active for static libraries.
        node: MappedFile.Node.Index,
        /// Active otherwise.
        file_symbol: Symbol.LocalIndex,
    },
}),
input_pending_index: u32,
input_sections: std.ArrayList(InputSection),
input_section_pending_index: u32,
/// SPARC has some weird relocations which involve setting some bits to fixed constant values. When
/// we encounter such a relocation, we queue the action here, and apply them during `idle`.
one_shot_fixups: std.ArrayList(struct {
    node: MappedFile.Node.Index,
    offset: u64,
    /// The syntax in these tag names matches the syntax used in `SymbolReloc.Type.Simple.dest`.
    action: enum {
        @"32[12:10] = 0b000",
        @"32[12:10] = 0b111",
        @"32[12:12] = 0b0",
    },
}),
navs: std.array_hash_map.Auto(InternPool.Nav.Index, struct {
    lsi: Symbol.LocalIndex,
    /// The start index of the contiguous sequence of symbol relocations in this NAV.
    first_symbol_reloc: SymbolReloc.Index,
    /// The start index of the contiguous sequence of GOT relocations in this NAV.
    first_got_reloc: GotReloc.Index,
}),
uavs: std.array_hash_map.Auto(InternPool.Index, struct {
    lsi: Symbol.LocalIndex,
    /// The start index of the contiguous sequence of symbol relocations in this UAV.
    first_symbol_reloc: SymbolReloc.Index,
    // No `first_got_reloc` field because a UAV never contains GOT relocations.
}),
lazy: std.EnumArray(link.File.LazySymbol.Kind, struct {
    map: std.array_hash_map.Auto(InternPool.Index, struct {
        lsi: Symbol.LocalIndex,
        /// The start index of the contiguous sequence of symbol relocations in this lazy code/data.
        first_symbol_reloc: SymbolReloc.Index,
        /// The start index of the contiguous sequence of GOT relocations in this lazy code/data.
        first_got_reloc: GotReloc.Index,
    }),
    pending_index: u32,
}),
pending_uavs: std.ArrayList(Node.UavMapIndex),
symbol_relocs: std.ArrayList(SymbolReloc),
node_relocs: std.ArrayList(NodeReloc),
got_relocs: std.ArrayList(GotReloc),
/// Set of relocations which must be re-applied if the size of the TLS segment changes.
tls_size_symbol_relocs: std.array_hash_map.Auto(SymbolReloc.Index, void),
/// Index matches the index into `shdrs`. Like `shdrs`, this map excludes `SHN_UNDEF`.
section_by_name: std.array_hash_map.Auto(String(.shstrtab), void),
/// Key is a global symbol which has been moved to a new index in the symbol table. Any relocation
/// entries which target that symbol must be updated to reference the correct symbol index.
///
/// * In an `ET_REL`, this means the index in `.symtab` has changed.
/// * In a DSO (or static PIE), this means the index in `.dynsym` has changed.
/// * Otherwise this is always empty.
changed_symtab_index: std.array_hash_map.Auto(Symbol.Global.Index, void),
/// Counts how many relocations are currently in `.rela.dyn` which would require a `DT_TEXTREL`
/// entry in the `.dynamic` section. This allows adding `DT_TEXTREL` to the output `.dynamic`
/// section in `flush` only when it is actually necessary. See also `nodeWantsDsoRelocation`.
textrel_count: u32,

dwarf: Dwarf,
dwarf_shared: std.enums.EnumArray(Dwarf.SharedSection, dwarf_relocs.Shared),
dwarf_addr: dwarf_relocs.Addr,
dwarf_str_offsets: dwarf_relocs.StrOffsets,
dwarf_units: []dwarf_relocs.Unit,
dwarf_consts: std.array_hash_map.Auto(link.ConstPool.Index, dwarf_relocs.Const),
dwarf_globals: std.ArrayList(dwarf_relocs.Global),
dwarf_funcs: std.ArrayList(dwarf_relocs.Func),
dwarf_decls: std.array_hash_map.Auto(Dwarf.Decl.Index, dwarf_relocs.Decl),

overflowed_reloc_count: u32,
misaligned_reloc_count: u32,

const_prog_node: std.Progress.Node,
input_prog_node: std.Progress.Node,

const Error = link.Error || error{MappedFileIo};

const Node = union(enum) {
    deleted,

    /// Only used when emitting a static library.
    ///
    /// Contains a header node which is an `.archive_header`.
    ///
    /// Contains the following footer nodes:
    /// * One `.archive_input_member` for each external input in the archive
    /// * One `.archive_elf_member_header` containing the `ar_hdr` for the ZCU
    /// * One `.elf` containing the ZCU's actual ELF object
    ///
    /// Padding between the headers and footers is absorbed into the "//" member (whose actual
    /// content is in the `.archive_header` node).
    archive,
    /// Only used when emitting a static library.
    ///
    /// Contains the archive magic (`ARMAG`), as well as the `ar_hdr` and content for the long file
    /// name string table member ("//").
    archive_header,
    /// Only used when emitting a static library.
    ///
    /// Contains the `ar_hdr` and content for one non-ZCU archive member (external link input). Also
    /// includes the single byte '\n' padding at the end of this archive member, if necessary.
    archive_input_member: InputIndex,
    /// Only used when emitting a static library.
    ///
    /// Contains the `ar_hdr` for the `.elf` node.
    archive_elf_member_header,

    elf,
    ehdr,
    shdr,
    segment: u32,
    section: Section.Index,
    /// The section '.plt' may contain relocations via `elf.plt_first_symbol_reloc`.
    section_manual_size: Section.Index,
    /// May contain relocations.
    input_section: InputSection.Index,
    /// Value is a global which has an entry in `elf.copied_globals`, so, a global for which we have
    /// emitted a copy relocation.
    ///
    /// TODO it would be better to emit these into `.bss` or `.bss.rel.ro`, once we support those.
    ///
    /// TODO: currently, the `elf.copied_globals` entry may not be there---this case exists because
    /// `MappedFile` does not (yet?) support deleting nodes. See logic in `updateGlobalDynamic`.
    copied_global: Symbol.Global.Index,
    /// May contain relocations.
    nav: NavMapIndex,
    /// May contain relocations.
    uav: UavMapIndex,
    /// May contain relocations.
    lazy_code: LazyMapRef.Index(.code),
    /// May contain relocations.
    lazy_const_data: LazyMapRef.Index(.const_data),

    debug_shared: Dwarf.SharedSection,
    debug_addr,
    eh_frame_footer,
    debug_str_offsets,
    unit_padding,
    unit_frame: Dwarf.Unit.Index,
    unit_frame_cie: Dwarf.Unit.Index,
    unit_debug_info: Dwarf.Unit.Index,
    unit_debug_info_header: Dwarf.Unit.Index,
    unit_debug_info_footer: Dwarf.Unit.Index,
    unit_debug_line: Dwarf.Unit.Index,
    unit_debug_line_header: Dwarf.Unit.Index,
    unit_debug_rnglists: Dwarf.Unit.Index,

    const_debug_info: link.ConstPool.Index,
    global_debug_info: Dwarf.Global.Index,
    func_frame_fde: Dwarf.Func.Index,
    func_debug_info: Dwarf.Func.Index,
    func_debug_line: Dwarf.Func.Index,
    decl_debug_info: Dwarf.Decl.Index,

    pub const InputIndex = enum(u32) {
        _,

        pub fn path(ii: InputIndex, elf: *const Elf) std.Build.Cache.Path {
            return elf.inputs.items[@backingInt(ii)].path;
        }

        pub fn member(ii: InputIndex, elf: *const Elf) ?[]const u8 {
            return elf.inputs.items[@backingInt(ii)].member;
        }

        pub fn node(ii: InputIndex, elf: *const Elf) MappedFile.Node.Index {
            return elf.inputs.items[@backingInt(ii)].extra.node;
        }

        pub fn fileSymbol(ii: InputIndex, elf: *const Elf) Symbol.LocalIndex {
            return elf.inputs.items[@backingInt(ii)].extra.file_symbol;
        }

        pub fn localSymbolRange(ii: InputIndex, elf: *Elf) [2]Symbol.LocalIndex {
            if (@backingInt(ii) + 1 < elf.inputs.items.len) {
                const next_ii: InputIndex = @fromBackingInt(@backingInt(ii) + 1);
                return .{ ii.fileSymbol(elf), next_ii.fileSymbol(elf) };
            } else {
                const local_symbols_len = switch (elf.shdrPtr(.symtab)) {
                    inline else => |shdr| elf.targetLoad(&shdr.info),
                };
                return .{ ii.fileSymbol(elf), @fromBackingInt(local_symbols_len) };
            }
        }
    };

    pub const NavMapIndex = enum(u32) {
        _,

        pub fn nav(nmi: NavMapIndex, elf: *const Elf) InternPool.Nav.Index {
            return elf.navs.keys()[@backingInt(nmi)];
        }

        pub fn symbol(nmi: NavMapIndex, elf: *const Elf) Symbol.LocalIndex {
            return elf.navs.values()[@backingInt(nmi)].lsi;
        }

        fn firstSymbolReloc(nmi: NavMapIndex, elf: *const Elf) SymbolReloc.Index {
            return elf.navs.values()[@backingInt(nmi)].first_symbol_reloc;
        }
        fn firstGotReloc(nmi: NavMapIndex, elf: *const Elf) GotReloc.Index {
            return elf.navs.values()[@backingInt(nmi)].first_got_reloc;
        }
    };

    pub const UavMapIndex = enum(u32) {
        _,

        pub fn uavValue(umi: UavMapIndex, elf: *const Elf) InternPool.Index {
            return elf.uavs.keys()[@backingInt(umi)];
        }

        pub fn symbol(umi: UavMapIndex, elf: *const Elf) Symbol.LocalIndex {
            return elf.uavs.values()[@backingInt(umi)].lsi;
        }

        fn firstSymbolReloc(umi: UavMapIndex, elf: *const Elf) SymbolReloc.Index {
            return elf.uavs.values()[@backingInt(umi)].first_symbol_reloc;
        }
        fn firstGotReloc(umi: UavMapIndex, elf: *const Elf) GotReloc.Index {
            _ = umi;
            _ = elf;
            return .none;
        }
    };

    pub const LazyMapRef = struct {
        kind: link.File.LazySymbol.Kind,
        index: u32,

        pub fn Index(comptime kind: link.File.LazySymbol.Kind) type {
            return enum(u32) {
                _,

                pub fn ref(lmi: @This()) LazyMapRef {
                    return .{ .kind = kind, .index = @backingInt(lmi) };
                }

                pub fn lazySymbol(lmi: @This(), elf: *const Elf) link.File.LazySymbol {
                    return lmi.ref().lazySymbol(elf);
                }

                pub fn symbol(lmi: @This(), elf: *const Elf) Symbol.LocalIndex {
                    return lmi.ref().symbol(elf);
                }

                fn firstSymbolReloc(lmi: @This(), elf: *const Elf) SymbolReloc.Index {
                    return elf.lazy.getPtrConst(kind).map.values()[@backingInt(lmi)].first_symbol_reloc;
                }
                fn firstGotReloc(lmi: @This(), elf: *const Elf) GotReloc.Index {
                    return elf.lazy.getPtrConst(kind).map.values()[@backingInt(lmi)].first_got_reloc;
                }
            };
        }

        pub fn lazySymbol(lmr: LazyMapRef, elf: *const Elf) link.File.LazySymbol {
            return .{ .kind = lmr.kind, .ty = elf.lazy.getPtrConst(lmr.kind).map.keys()[lmr.index] };
        }

        pub fn symbol(lmr: LazyMapRef, elf: *const Elf) Symbol.LocalIndex {
            return elf.lazy.getPtrConst(lmr.kind).map.values()[lmr.index].lsi;
        }
    };

    comptime {
        if (!std.debug.runtime_safety) std.debug.assert(@sizeOf(Node) == 8);
    }

    /// In this linker implementation, `link.File.AtomId` is a type-erased `MappedFile.Node.Index`.
    fn toAtom(ni: MappedFile.Node.Index) link.File.AtomId {
        return @fromBackingInt(@backingInt(ni));
    }
    /// In this linker implementation, `link.File.AtomId` is a type-erased `MappedFile.Node.Index`.
    fn fromAtom(atom: link.File.AtomId) MappedFile.Node.Index {
        return @fromBackingInt(@backingInt(atom));
    }
};

const InputSection = struct {
    input: Node.InputIndex,
    file_location: MappedFile.Node.FileLocation,
    vaddr: u64,
    /// The node corresponding to this input section.
    node: MappedFile.Node.Index,
    /// The start index of the contiguous sequence of symbol relocations in this input section.
    first_symbol_reloc: SymbolReloc.Index,
    /// The start index of the contiguous sequence of GOT relocations in this input section.
    first_got_reloc: GotReloc.Index,

    const Index = enum(u32) {
        _,

        fn ptr(isi: InputSection.Index, elf: *Elf) *InputSection {
            return &elf.input_sections.items[@backingInt(isi)];
        }

        fn ptrConst(isi: InputSection.Index, elf: *const Elf) *const InputSection {
            return &elf.input_sections.items[@backingInt(isi)];
        }

        fn input(isi: InputSection.Index, elf: *const Elf) Node.InputIndex {
            return isi.ptrConst(elf).input;
        }

        fn fileLocation(isi: InputSection.Index, elf: *const Elf) MappedFile.Node.FileLocation {
            return isi.ptrConst(elf).file_location;
        }

        fn node(isi: InputSection.Index, elf: *const Elf) MappedFile.Node.Index {
            return isi.ptrConst(elf).node;
        }
    };
};

const Archive = struct {
    ni: MappedFile.Node.Index,
    header_ni: MappedFile.Node.Index,
    elf_member_header_ni: MappedFile.Node.Index,

    elf_member_too_big: bool,
    strtab_member_too_big: bool,
};

const Section = struct {
    /// The node corresponding to this section.
    ni: MappedFile.Node.Index,
    /// A symbol which is exactly at the start of this section.
    ///
    /// When not emitting a relocatable, or for special section types, this is `.null`.
    lsi: Symbol.LocalIndex,
    rela: union {
        /// This field is active if and only if this section is *not* a `SHT_RELA` section.
        ///
        /// This field's value refers to this section's corresponding relocation section, if it
        /// currently has one. If this section does not currently have a relocation section, the
        /// value is `.UNDEF`.
        ///
        /// This field is only ever non-`.UNDEF` when emitting a relocatable (`ET_REL`). While there
        /// are also output relocations in DSOs, they are all placed in the `.rela.dyn`
        /// (`elf.shdnx.rela_dyn`) and `.rela.plt` (`elf.shndx.rela_plt`) sections, rather than
        /// having separate relocation sections for each section.
        shndx: Section.Index,

        /// This field is active if and only if this section *is* a `SHT_RELA` section.
        ///
        /// This is the head of a single-linked list of free `ElfN.Rela` entries in this section.
        /// Entries in this list have `info.type` set to `R_*_NONE`, have `info.sym` set to 0, and
        /// have `offset` set to `@enumFromInt(next)` where `next` is `RelaIndex.Optional`. Also,
        /// `addend` is set to the length of the list starting from this point; so the last node in
        /// the list has `addend = 1`, the one before it has `addend = 2`, etc. This is so that the
        /// head node always contains the current length of the list.
        ///
        /// It would be okay to store these values (in the `offset` and `addend` fields) in the
        /// compiler's host endianness, because they will never be read by other tooling. However,
        /// we nonetheless use target endianness, because using host endianness would introduce an
        /// unnecessary dependency of the output binary on the compiler's host architecture.
        free_head: RelaIndex.Optional,
    },

    const RelaIndex = enum(u32) {
        none,
        _,

        const Optional = enum(u32) {
            none = std.math.maxInt(u32),
            _,

            fn unwrap(opt: RelaIndex.Optional) ?RelaIndex {
                return switch (opt) {
                    .none => null,
                    _ => @fromBackingInt(@backingInt(opt)),
                };
            }
        };

        fn toOptional(i: RelaIndex) RelaIndex.Optional {
            return @fromBackingInt(@backingInt(i));
        }
    };

    pub const Index = enum(Tag) {
        UNDEF = std.elf.SHN_UNDEF,
        LIVEPATCH = reserve(std.elf.SHN_LIVEPATCH),
        ABS = reserve(std.elf.SHN_ABS),
        COMMON = reserve(std.elf.SHN_COMMON),

        symtab = 1,
        shstrtab,
        strtab,
        rodata,
        text,
        data,
        data_rel_ro,

        _,

        pub const Tag = u32;

        pub const LORESERVE: Index = .fromSection(std.elf.SHN_LORESERVE);
        pub const HIRESERVE: Index = .fromSection(std.elf.SHN_HIRESERVE);
        comptime {
            assert(@backingInt(HIRESERVE) == std.math.maxInt(Tag));
        }

        fn reserve(sec: std.elf.Section) Tag {
            assert(sec >= std.elf.SHN_LORESERVE and sec <= std.elf.SHN_HIRESERVE);
            return @as(Tag, std.math.maxInt(Tag) - std.elf.SHN_HIRESERVE) + sec;
        }

        pub fn fromSection(sec: std.elf.Section) Index {
            return switch (sec) {
                std.elf.SHN_UNDEF...std.elf.SHN_LORESERVE - 1 => @fromBackingInt(sec),
                std.elf.SHN_LORESERVE...std.elf.SHN_HIRESERVE => @fromBackingInt(reserve(sec)),
            };
        }
        pub fn toSection(shndx: Index) ?std.elf.Section {
            return switch (@backingInt(shndx)) {
                std.elf.SHN_UNDEF...std.elf.SHN_LORESERVE - 1 => |sec| @intCast(sec),
                std.elf.SHN_LORESERVE...reserve(std.elf.SHN_LORESERVE) - 1 => null,
                reserve(std.elf.SHN_LORESERVE)...reserve(std.elf.SHN_HIRESERVE) => |sec| @intCast(
                    sec - reserve(std.elf.SHN_LORESERVE) + std.elf.SHN_LORESERVE,
                ),
            };
        }

        fn get(shndx: Index, elf: *Elf) *Section {
            return &elf.shdrs.items[@backingInt(shndx) - 1]; // overflow means you tried to get the `.UNDEF` section
        }

        fn name(shndx: Index, elf: *Elf) String(.shstrtab) {
            return switch (elf.shdrPtr(shndx)) {
                inline else => |shdr| @fromBackingInt(elf.targetLoad(&shdr.name)),
            };
        }

        fn vaddr(shndx: Index, elf: *Elf) u64 {
            return switch (elf.shdrPtr(shndx)) {
                inline else => |shdr| elf.targetLoad(&shdr.addr),
            };
        }

        fn size(shndx: Index, elf: *Elf) u64 {
            return switch (elf.shdrPtr(shndx)) {
                inline else => |shdr| elf.targetLoad(&shdr.size),
            };
        }

        fn setSize(shndx: Index, elf: *Elf, new_size: u64) void {
            return switch (elf.shdrPtr(shndx)) {
                inline else => |shdr| {
                    elf.targetStore(&shdr.type, switch (new_size) {
                        0 => .NULL,
                        else => .PROGBITS,
                    });
                    elf.targetStore(&shdr.size, @intCast(new_size));
                },
            };
        }

        fn flags(s: Index, elf: *Elf) std.elf.SHF {
            return switch (elf.shdrPtr(s)) {
                inline else => |shdr| elf.targetLoad(&shdr.flags).shf,
            };
        }

        fn rename(shndx: Index, elf: *Elf, new_name: []const u8) Error!void {
            const shstrtab_entry = try elf.string(.shstrtab, new_name);
            switch (elf.shdrPtr(shndx)) {
                inline else => |shdr| elf.targetStore(&shdr.name, @backingInt(shstrtab_entry)),
            }
        }

        fn ensureAligned(shndx: Index, elf: *Elf, min_align: Alignment) Error!void {
            switch (elf.shdrPtr(shndx)) {
                inline else => |shdr| {
                    if (elf.targetLoad(&shdr.addralign) >= min_align.toByteUnits()) {
                        return; // already aligned
                    }
                    elf.targetStore(&shdr.addralign, @intCast(min_align.toByteUnits()));
                },
            }
            const ni = shndx.get(elf).ni;
            if (min_align.compare(.gt, ni.alignment(&elf.mf))) {
                try ni.realign(elf.base.comp.gpa, &elf.mf, min_align);
            }
            switch (elf.getNode(ni.parent(&elf.mf).unwrap().?)) {
                .elf => {},
                .segment => |phndx| try elf.ensureSegmentAligned(phndx, min_align),
                else => unreachable,
            }
        }

        /// Asserts that `rela_shndx` is a `SHT_RELA` section and ensures that its node has enough
        /// unused space to hold `n` additional `ElfN.Rela` entries.
        fn relaEnsureAdditionalCapacity(rela_shndx: Index, elf: *Elf, n: usize) Error!void {
            const node = rela_shndx.get(elf).ni;
            const need_size: u64 = switch (elf.shdrPtr(rela_shndx)) {
                inline else => |shdr, class| need_size: {
                    assert(elf.targetLoad(&shdr.type) == .RELA);
                    const cur_size = elf.targetLoad(&shdr.size);
                    const ent_size = @sizeOf(class.ElfN().Rela);
                    assert(elf.targetLoad(&shdr.entsize) == ent_size);
                    const free_len: u32 = free_len: {
                        const opt_free_head = rela_shndx.get(elf).rela.free_head;
                        const free_head = opt_free_head.unwrap() orelse break :free_len 0;
                        const relas: []const class.ElfN().Rela = @ptrCast(@alignCast(
                            node.slice(&elf.mf)[0..@intCast(cur_size)],
                        ));
                        const free_len = elf.targetLoad(&relas[@backingInt(free_head)].addend);
                        assert(free_len > 0);
                        break :free_len @intCast(free_len);
                    };
                    const need_additional = n -| free_len;
                    break :need_size cur_size + need_additional * ent_size;
                },
            };
            try node.ensureMinimumSize(elf.base.comp.gpa, &elf.mf, need_size);
        }

        /// Asserts that `rela_shndx` is a `SHT_RELA` section and deletes the `ElfN.Rela` entry at
        /// the given `index` in it. The entry is added to the free-list for reuse later. Asserts
        /// that the relocation entry at `index` is not already free.
        fn relaDeleteOne(rela_shndx: Index, elf: *Elf, index: RelaIndex) void {
            switch (elf.shdrPtr(rela_shndx)) {
                inline else => |shdr, class| {
                    assert(elf.targetLoad(&shdr.type) == .RELA);
                    assert(elf.targetLoad(&shdr.entsize) == @sizeOf(class.ElfN().Rela));
                    const relas: []class.ElfN().Rela = @ptrCast(@alignCast(
                        rela_shndx.get(elf).ni.slice(&elf.mf)[0..@intCast(elf.targetLoad(&shdr.size))],
                    ));
                    const opt_free_head = rela_shndx.get(elf).rela.free_head;
                    const old_free_len: u32 = free_len: {
                        const free_head = opt_free_head.unwrap() orelse break :free_len 0;
                        const free_len = elf.targetLoad(&relas[@backingInt(free_head)].addend);
                        assert(free_len > 0);
                        break :free_len @intCast(free_len);
                    };
                    const none_reloc_type = MachineRelocType.none(elf).unwrap(elf);
                    {
                        const old_type = elf.targetLoad(&relas[@backingInt(index)].info).type;
                        assert(old_type != none_reloc_type); // bug: `index` is already in the free-list
                    }
                    relas[@backingInt(index)] = .{
                        .offset = @backingInt(opt_free_head), // next
                        .info = .{
                            .type = @intCast(none_reloc_type),
                            .sym = 0,
                        },
                        .addend = @intCast(old_free_len + 1), // list length
                    };
                    if (elf.targetEndian() != std.lang.Endian.native) {
                        std.mem.byteSwapAllFields(class.ElfN().Rela, &relas[@backingInt(index)]);
                    }
                },
            }
            rela_shndx.get(elf).rela.free_head = index.toOptional();
        }

        /// Asserts that `rela_shndx` is a `SHT_RELA` section and adds a new `ElfN.Rela` entry to it
        /// with the given field values. Returns the index of the populated entry. Asserts that
        /// capacity for this operation was already guaranteed using `relaEnsureAdditionalCapacity`.
        fn relaAddOneAssumeCapacity(rela_shndx: Index, elf: *Elf, opts: struct {
            type: MachineRelocType,
            offset: u64,
            /// This is a raw `u32` because whether this is an index into `.symtab` (`Symbol.Index`)
            /// or an index into `.dynsym` is contextual.
            raw_sym_index: u32,
            addend: i64,
        }) RelaIndex {
            switch (elf.shdrPtr(rela_shndx)) {
                inline else => |shdr, class| {
                    assert(elf.targetLoad(&shdr.type) == .RELA);
                    const ent_size = @sizeOf(class.ElfN().Rela);
                    assert(elf.targetLoad(&shdr.entsize) == ent_size);
                    const new_index: RelaIndex = if (rela_shndx.get(elf).rela.free_head.unwrap()) |free_head| new_index: {
                        const relas: []class.ElfN().Rela = @ptrCast(@alignCast(
                            rela_shndx.get(elf).ni.slice(&elf.mf)[0..@intCast(elf.targetLoad(&shdr.size))],
                        ));
                        const next: RelaIndex.Optional = @fromBackingInt(@intCast(elf.targetLoad(
                            &relas[@backingInt(free_head)].offset,
                        )));
                        rela_shndx.get(elf).rela.free_head = next;

                        const old_free_len: u32 = @intCast(
                            elf.targetLoad(&relas[@backingInt(free_head)].addend),
                        );
                        const new_free_len: u32 = if (next.unwrap()) |i| @intCast(
                            elf.targetLoad(&relas[@backingInt(i)].addend),
                        ) else 0;
                        assert(new_free_len == old_free_len - 1);

                        break :new_index free_head;
                    } else new_index: {
                        const old_size = elf.targetLoad(&shdr.size);
                        const new_size = old_size + ent_size;
                        elf.targetStore(&shdr.size, new_size);
                        break :new_index @fromBackingInt(@intCast(@divExact(old_size, ent_size)));
                    };
                    const relas: []class.ElfN().Rela = @ptrCast(@alignCast(
                        rela_shndx.get(elf).ni.slice(&elf.mf)[0..@intCast(elf.targetLoad(&shdr.size))],
                    ));
                    relas[@backingInt(new_index)] = .{
                        .offset = @intCast(opts.offset),
                        .info = .{
                            .type = @intCast(opts.type.unwrap(elf)),
                            .sym = @intCast(opts.raw_sym_index),
                        },
                        .addend = @intCast(opts.addend),
                    };
                    if (elf.targetEndian() != std.lang.Endian.native) {
                        std.mem.byteSwapAllFields(class.ElfN().Rela, &relas[@backingInt(new_index)]);
                    }
                    return new_index;
                },
            }
        }

        /// Asserts that `rela_shndx` is a `SHT_RELA` section and updates the `info.sym` field of
        /// the `ElfN.Rela` entry at the given index. As with `relaAddOneAssumeCapacity`, the symbol
        /// index is a raw `u32`, because it may be an index into `.symtab` or an index into
        /// `.dynsym`. Asserts that `index` is not in the free-list (i.e. is not deleted).
        fn relaUpdateSym(rela_shndx: Index, elf: *Elf, index: RelaIndex, raw_sym_index: u32) void {
            switch (elf.shdrPtr(rela_shndx)) {
                inline else => |shdr, class| {
                    assert(elf.targetLoad(&shdr.type) == .RELA);
                    assert(elf.targetLoad(&shdr.entsize) == @sizeOf(class.ElfN().Rela));
                    const relas: []class.ElfN().Rela = @ptrCast(@alignCast(
                        rela_shndx.get(elf).ni.slice(&elf.mf)[0..@intCast(elf.targetLoad(&shdr.size))],
                    ));
                    const rela_info = elf.targetLoad(&relas[@backingInt(index)].info);
                    {
                        const none_reloc_type = MachineRelocType.none(elf).unwrap(elf);
                        assert(rela_info.type != none_reloc_type); // bug: `index` is in the free-list
                    }
                    elf.targetStore(&relas[@backingInt(index)].info, .{
                        .type = rela_info.type,
                        .sym = @intCast(raw_sym_index),
                    });
                },
            }
        }

        /// Asserts that `rela_shndx` is a `SHT_RELA` section and updates the `offset` field of the
        /// `ElfN.Rela` entry at the given index. Asserts that `index` is not in the free-list (i.e.
        /// it is not deleted).
        fn relaSetOffset(rela_shndx: Index, elf: *Elf, index: RelaIndex, new_offset: u64) void {
            switch (elf.shdrPtr(rela_shndx)) {
                inline else => |shdr, class| {
                    assert(elf.targetLoad(&shdr.type) == .RELA);
                    assert(elf.targetLoad(&shdr.entsize) == @sizeOf(class.ElfN().Rela));
                    const relas: []class.ElfN().Rela = @ptrCast(@alignCast(
                        rela_shndx.get(elf).ni.slice(&elf.mf)[0..@intCast(elf.targetLoad(&shdr.size))],
                    ));
                    {
                        const rela_info = elf.targetLoad(&relas[@backingInt(index)].info);
                        const none_reloc_type = MachineRelocType.none(elf).unwrap(elf);
                        assert(rela_info.type != none_reloc_type); // bug: `index` is in the free-list
                    }
                    elf.targetStore(&relas[@backingInt(index)].offset, @intCast(new_offset));
                },
            }
        }

        /// Asserts that `rela_shndx` is a `SHT_RELA` section and updates the `offset` field of the
        /// `ElfN.Rela` entry at the given index, by subtracting `old_base` and adding `new_base`.
        /// Asserts that `index` is not in the free-list (i.e. it is not deleted).
        fn relaAdjustOffset(rela_shndx: Index, elf: *Elf, index: RelaIndex, old_base: u64, new_base: u64) void {
            switch (elf.shdrPtr(rela_shndx)) {
                inline else => |shdr, class| {
                    assert(elf.targetLoad(&shdr.type) == .RELA);
                    assert(elf.targetLoad(&shdr.entsize) == @sizeOf(class.ElfN().Rela));
                    const relas: []class.ElfN().Rela = @ptrCast(@alignCast(
                        rela_shndx.get(elf).ni.slice(&elf.mf)[0..@intCast(elf.targetLoad(&shdr.size))],
                    ));
                    {
                        const rela_info = elf.targetLoad(&relas[@backingInt(index)].info);
                        const none_reloc_type = MachineRelocType.none(elf).unwrap(elf);
                        assert(rela_info.type != none_reloc_type); // bug: `index` is in the free-list
                    }
                    const old_offset = elf.targetLoad(&relas[@backingInt(index)].offset);
                    elf.targetStore(&relas[@backingInt(index)].offset, @intCast(
                        old_offset - old_base + new_base,
                    ));
                },
            }
        }

        /// Asserts that `rela_shndx` is a `SHT_RELA` section and updates the `addend` field of the
        /// `ElfN.Rela` entry at the given index. Asserts that `index` is not in the free-list (i.e.
        /// it is not deleted).
        fn relaSetAddend(rela_shndx: Index, elf: *Elf, index: RelaIndex, new_addend: u64) void {
            switch (elf.shdrPtr(rela_shndx)) {
                inline else => |shdr, class| {
                    assert(elf.targetLoad(&shdr.type) == .RELA);
                    assert(elf.targetLoad(&shdr.entsize) == @sizeOf(class.ElfN().Rela));
                    const relas: []class.ElfN().Rela = @ptrCast(@alignCast(
                        rela_shndx.get(elf).ni.slice(&elf.mf)[0..@intCast(elf.targetLoad(&shdr.size))],
                    ));
                    {
                        const rela_info = elf.targetLoad(&relas[@backingInt(index)].info);
                        const none_reloc_type = MachineRelocType.none(elf).unwrap(elf);
                        assert(rela_info.type != none_reloc_type); // bug: `index` is in the free-list
                    }
                    const unsigned: class.ElfN().Addr = @intCast(new_addend);
                    elf.targetStore(&relas[@backingInt(index)].addend, @bitCast(unsigned));
                },
            }
        }

        fn debugFrameFormat(shndx: Index, elf: *Elf) ?Dwarf.Frame.Format {
            if (shndx == elf.shndx.eh_frame) return .eh_frame;
            if (shndx == elf.shndx.debug_frame) return .debug_frame;
            return null;
        }
    };
};
fn debugFrameFooterSize(elf: *Elf, frame_format: Dwarf.Frame.Format) usize {
    return switch (frame_format) {
        .eh_frame => switch (elf.ehdrType()) {
            .REL => 0,
            .EXEC, .DYN => 4,
        },
        .debug_frame => 0,
    };
}

const dwarf_relocs = struct {
    const Shared = struct {
        first_target_reloc: NodeReloc.Index,
    };
    const Addr = struct {
        first_target_reloc: NodeReloc.Index,
        symbol_relocs: std.ArrayList(SymbolReloc.Index),
    };
    const StrOffsets = struct {
        first_target_reloc: NodeReloc.Index,
        node_relocs: std.ArrayList(NodeReloc.Index),
    };
    const Unit = struct {
        frame_cie_first_target_reloc: NodeReloc.Index,
        debug_info_header_first_target_reloc: NodeReloc.Index,
        debug_info_header_first_node_reloc: NodeReloc.Index,
        debug_line_header_first_target_reloc: NodeReloc.Index,
        debug_line_header_first_node_reloc: NodeReloc.Index,
        debug_rnglists_first_target_reloc: NodeReloc.Index,
        debug_rnglists_symbol_relocs: std.ArrayList(SymbolReloc.Index),
    };
    const Const = struct {
        debug_info_first_target_reloc: NodeReloc.Index,
        debug_info_first_symbol_reloc: SymbolReloc.Index,
        debug_info_first_node_reloc: NodeReloc.Index,
    };
    const Global = struct {
        debug_info_first_target_reloc: NodeReloc.Index,
        debug_info_first_symbol_reloc: SymbolReloc.Index,
        debug_info_first_node_reloc: NodeReloc.Index,
    };
    const Func = struct {
        frame_fde_first_symbol_reloc: SymbolReloc.Index,
        frame_fde_first_node_reloc: NodeReloc.Index,
        debug_info_first_target_reloc: NodeReloc.Index,
        debug_info_first_symbol_reloc: SymbolReloc.Index,
        debug_info_first_node_reloc: NodeReloc.Index,
        debug_line_first_symbol_reloc: SymbolReloc.Index,
        debug_line_first_node_reloc: NodeReloc.Index,
    };
    const Decl = struct {
        debug_info_first_target_reloc: NodeReloc.Index,
        debug_info_first_node_reloc: NodeReloc.Index,
    };
};

pub const MachineRelocType = union {
    AARCH64: std.elf.R_AARCH64,
    LARCH: std.elf.R_LARCH,
    PPC64: std.elf.R_PPC64,
    RISCV: std.elf.R_RISCV,
    SPARC: std.elf.R_SPARC,
    X86_64: std.elf.R_X86_64,

    pub const Format = struct {
        rt: MachineRelocType,
        elf: *const Elf,

        pub fn format(f: Format, w: *Io.Writer) Io.Writer.Error!void {
            switch (f.elf.ehdrMachine()) {
                .AARCH64 => try w.print("R_AARCH64_{t}", .{f.rt.AARCH64}),
                .LOONGARCH => try w.print("R_LARCH_{t}", .{f.rt.LARCH}),
                .PPC64 => try w.print("R_PPC64_{t}", .{f.rt.PPC64}),
                .RISCV => try w.print("R_RISCV_{t}", .{f.rt.RISCV}),
                .SPARCV9 => try w.print("R_SPARC_{t}", .{f.rt.SPARC}),
                .X86_64 => try w.print("R_X86_64_{t}", .{f.rt.X86_64}),
            }
        }
    };

    pub fn fmt(rt: MachineRelocType, elf: *const Elf) Format {
        return .{ .rt = rt, .elf = elf };
    }

    pub fn none(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = .NONE },
            .LOONGARCH => .{ .LARCH = .NONE },
            .PPC64 => .{ .PPC64 = .NONE },
            .RISCV => .{ .RISCV = .NONE },
            .SPARCV9 => .{ .SPARC = .NONE },
            .X86_64 => .{ .X86_64 = .NONE },
        };
    }
    pub fn copy(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = .COPY },
            .LOONGARCH => .{ .LARCH = .COPY },
            .PPC64 => .{ .PPC64 = .COPY },
            .RISCV => .{ .RISCV = .COPY },
            .SPARCV9 => .{ .SPARC = .COPY },
            .X86_64 => .{ .X86_64 = .COPY },
        };
    }
    pub fn relative(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = .RELATIVE },
            .LOONGARCH => .{ .LARCH = .RELATIVE },
            .PPC64 => .{ .PPC64 = .RELATIVE },
            .RISCV => .{ .RISCV = .RELATIVE },
            .SPARCV9 => .{ .SPARC = .RELATIVE },
            .X86_64 => .{ .X86_64 = .RELATIVE },
        };
    }
    pub fn jumpSlot(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = .JUMP_SLOT },
            .LOONGARCH => .{ .LARCH = .JUMP_SLOT },
            .PPC64 => .{ .PPC64 = .JMP_SLOT },
            .RISCV => .{ .RISCV = .JUMP_SLOT },
            .SPARCV9 => .{ .SPARC = .JMP_SLOT },
            .X86_64 => .{ .X86_64 = .JUMP_SLOT },
        };
    }
    pub fn globDat(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = .GLOB_DAT },
            .LOONGARCH => .{ .LARCH = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .@"32",
                .@"64" => .@"64",
            } },
            .PPC64 => .{ .PPC64 = .GLOB_DAT },
            .RISCV => .{ .RISCV = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .@"32",
                .@"64" => .@"64",
            } },
            .SPARCV9 => .{ .SPARC = .GLOB_DAT },
            .X86_64 => .{ .X86_64 = .GLOB_DAT },
        };
    }
    pub fn dtpMod(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .P32_TLS_DTPMOD,
                .@"64" => .TLS_DTPMOD,
            } },
            .LOONGARCH => .{ .LARCH = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .TLS_DTPMOD32,
                .@"64" => .TLS_DTPMOD64,
            } },
            .PPC64 => .{ .PPC64 = .DTPMOD64 },
            .RISCV => .{ .RISCV = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .TLS_DTPMOD32,
                .@"64" => .TLS_DTPMOD64,
            } },
            .SPARCV9 => .{ .SPARC = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .TLS_DTPMOD32,
                .@"64" => .TLS_DTPMOD64,
            } },
            .X86_64 => .{ .X86_64 = .DTPMOD64 },
        };
    }
    pub fn dtpOff(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .P32_TLS_DTPREL,
                .@"64" => .TLS_DTPREL,
            } },
            .LOONGARCH => .{ .LARCH = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .TLS_DTPREL32,
                .@"64" => .TLS_DTPREL64,
            } },
            .PPC64 => .{ .PPC64 = .DTPREL64 },
            .RISCV => .{ .RISCV = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .TLS_DTPREL32,
                .@"64" => .TLS_DTPREL64,
            } },
            .SPARCV9 => .{ .SPARC = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .TLS_DTPOFF32,
                .@"64" => .TLS_DTPOFF64,
            } },
            .X86_64 => .{ .X86_64 = .DTPOFF64 },
        };
    }
    pub fn tpOff(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .P32_TLS_TPREL,
                .@"64" => .TLS_TPREL,
            } },
            .LOONGARCH => .{ .LARCH = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .TLS_TPREL32,
                .@"64" => .TLS_TPREL64,
            } },
            .PPC64 => .{ .PPC64 = .TPREL64 },
            .RISCV => .{ .RISCV = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .TLS_TPREL32,
                .@"64" => .TLS_TPREL64,
            } },
            .SPARCV9 => .{ .SPARC = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .TLS_TPOFF32,
                .@"64" => .TLS_TPOFF64,
            } },
            .X86_64 => .{ .X86_64 = .TPOFF64 },
        };
    }
    pub fn absAddr(elf: *const Elf) MachineRelocType {
        return switch (elf.identClass()) {
            .NONE, _ => unreachable,
            .@"32" => .abs32(elf),
            .@"64" => .abs64(elf),
        };
    }
    pub fn abs32(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = .P32_ABS32 },
            .LOONGARCH => .{ .LARCH = .@"32" },
            .PPC64 => .{ .PPC64 = .ADDR32 },
            .RISCV => .{ .RISCV = .@"32" },
            .SPARCV9 => .{ .SPARC = .@"32" },
            .X86_64 => .{ .X86_64 = .@"32" },
        };
    }
    pub fn abs64(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = .ABS64 },
            .LOONGARCH => .{ .LARCH = .@"64" },
            .PPC64 => .{ .PPC64 = .ADDR64 },
            .RISCV => .{ .RISCV = .@"64" },
            .SPARCV9 => .{ .SPARC = .@"64" },
            .X86_64 => .{ .X86_64 = .@"64" },
        };
    }
    pub fn rel32(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = .PREL32 },
            .LOONGARCH => .{ .LARCH = .@"32_PCREL" },
            .PPC64 => .{ .PPC64 = .REL32 },
            .RISCV => .{ .RISCV = .@"32_PCREL" },
            .SPARCV9 => .{ .SPARC = .DISP32 },
            .X86_64 => .{ .X86_64 = .PC32 },
        };
    }
    pub fn rel64(elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = .PREL64 },
            .LOONGARCH => unreachable,
            .PPC64 => .{ .PPC64 = .REL64 },
            .RISCV => unreachable,
            .SPARCV9 => .{ .SPARC = .DISP64 },
            .X86_64 => .{ .X86_64 = .PC64 },
        };
    }
    pub fn size32(elf: *const Elf) ?MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64,
            .LOONGARCH,
            .PPC64,
            .RISCV,
            => null,

            .SPARCV9 => .{ .SPARC = .SIZE32 },
            .X86_64 => .{ .X86_64 = .SIZE32 },
        };
    }
    pub fn size64(elf: *const Elf) ?MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64,
            .LOONGARCH,
            .PPC64,
            .RISCV,
            => null,

            .SPARCV9 => .{ .SPARC = .SIZE64 },
            .X86_64 => .{ .X86_64 = .SIZE64 },
        };
    }

    pub fn wrap(int: u32, elf: *const Elf) MachineRelocType {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => .{ .AARCH64 = @fromBackingInt(int) },
            .LOONGARCH => .{ .LARCH = @fromBackingInt(int) },
            .PPC64 => .{ .PPC64 = @fromBackingInt(int) },
            .RISCV => .{ .RISCV = @fromBackingInt(int) },
            .SPARCV9 => .{ .SPARC = @fromBackingInt(int) },
            .X86_64 => .{ .X86_64 = @fromBackingInt(int) },
        };
    }
    pub fn unwrap(rt: MachineRelocType, elf: *const Elf) u32 {
        return switch (elf.ehdrMachine()) {
            .AARCH64 => @backingInt(rt.AARCH64),
            .LOONGARCH => @backingInt(rt.LARCH),
            .PPC64 => @backingInt(rt.PPC64),
            .RISCV => @backingInt(rt.RISCV),
            .SPARCV9 => @backingInt(rt.SPARC),
            .X86_64 => @backingInt(rt.X86_64),
        };
    }
};

/// A relocation targeting an arbitrary symbol with a fixed addend.
const SymbolReloc = struct {
    /// The node containing this relocation. Possible values are:
    /// * An input section
    /// * A section
    /// * A NAV, UAV, or lazy code/data
    /// * `.none`, if this relocation was deleted (in which case it should be ignored)
    node: MappedFile.Node.Index.Optional,
    /// The offset of the relocation inside of `node`.
    offset: u64,
    /// A symbol used to compute the relocated value. Precise meaning depends on `@"type"`.
    target: Symbol.Id,
    /// A signed constant used to compute the relocated value. Precise meaning depends on `@"type"`.
    addend: i64,
    /// Specifies how to apply the relocation.
    ///
    /// When emitting a relocatable, this field is `undefined`.
    type: SymbolReloc.Type,
    /// Forms a linked list of all symbol relocations with the same `target`. This list exists so
    /// that all relocations targeting a particular symbol can be re-applied if that symbol moves.
    /// Doubly-linked so that relocations can be removed.
    next: SymbolReloc.Index,
    /// Back-reference in a doubly-linked list---see `next`.
    prev: SymbolReloc.Index,
    /// If this relocation has a corresponding output relocation, this is its index within the
    /// appropriate SHT_RELA section (see `relaSection`). If there is no output relocation
    /// corresponding to this relocation, this is `.none`.
    ///
    /// If we are producing a relocatable, this field is always populated, because all relocations
    /// are emitted as output relocations.
    ///
    /// If we are producing a DSO, this field is populated if this relocation requires a runtime
    /// relocation entry. The entry will be removed if we discover a definition which allows us to
    /// statically resolve the relocation.
    rela_index: Section.RelaIndex.Optional,
    result: enum(u8) { ok, overflowed, misaligned },

    /// Determines the section in which this relocation will be placed if it is outstanding.
    ///
    /// When producing a relocatable (ET_REL), the relocation section is `Section.rela.shndx` for
    /// the section of `node`, and this function asserts that the aforementioned `rela.shndx` field
    /// is populated.
    ///
    /// When producing a DSO, the relocation section is always `.rela.dyn`. It is not `.rela.plt`
    /// because relocations in the GOTPLT are handled specially, without `SymbolReloc` entries.
    fn relaSection(sr: *const SymbolReloc, elf: *Elf) Section.Index {
        const shndx = switch (elf.ehdrType()) {
            .REL => elf.getNodeShndx(sr.node.unwrap().?).get(elf).rela.shndx,
            .EXEC, .DYN => elf.shndx.rela_dyn,
        };
        assert(shndx != .UNDEF);
        return shndx;
    }

    /// Instead of using the ELF relocation enums, we have our own internal representation for
    /// relocation types. This representation is more compact (requiring only 16 bits), and allows
    /// sharing a lot of relocation handling between multiple relocs and target architectures.
    ///
    /// A relocation type can be "simple" or "special".
    ///
    /// "Simple" relocations are designed to cover the majority of cases. They can represent most
    /// relocations which either write 8-bit, 16-bit, 32-bit, or 64-bit integers, or which write one
    /// contiguous bit-field within such an integer (e.g. an instruction operand). For more details,
    /// see `Simple`.
    ///
    /// "Special" relocations handle anything which does not fit into the above category, such as
    /// relocations which write multiple sequences of bits or which need to do unusual arithmetic on
    /// a symbol value. The representation is simply a big enum containing all of these exceptional
    /// cases---see `Special`. This representation is in use when `Type.target == .special`.
    const Type = packed struct(u16) {
        /// Helper function for constructing a "simple" relocation type. This mainly exists to
        /// improve readability in the relocation lowering logic in `addRelocAssumeCapacity`.
        fn simple(target: Target, action: Simple) SymbolReloc.Type {
            assert(target != .special);
            return .{ .target = target, .action = .{ .simple = action } };
        }

        /// Helper function for constructing a "special" relocation type. This mainly exists to
        /// improve readability in the relocation lowering logic in `addRelocAssumeCapacity`.
        fn special(s: Special) SymbolReloc.Type {
            return .{ .target = .special, .action = .{ .special = s } };
        }

        /// See doc comment on `Target`.
        target: Target,
        /// If `target == .special`, the `special` field is used.
        ///
        /// Otherwise, the `.simple` field is used.
        action: packed union {
            simple: Simple,
            special: Special,
        },

        /// If a relocation is "special", indicates that using the value `.@"special"`.
        ///
        /// Otherwise (for "simple" relocations), `Target` indicates the first step in computing the
        /// relocation---whether we care about the target symbol's absolute address, its PC-relative
        /// address, its PLT entry, etc.
        const Target = enum(u3) {
            /// This is a "special" relocation whose specific type is in the `action.special` field.
            special,

            /// Absolute value of the target symbol.
            abs,
            /// Offset from the relocation itself to the target symbol ("PC-relative").
            rel,
            /// Address of the target symbol's PLT entry.
            ///
            /// If the target symbol does not have a PLT entry, equivalent to `.abs`.
            pltabs,
            /// Offset from the relocation itself to the target symbol's PLT entry ("PC-relative").
            ///
            /// If the target symbol does not have a PLT entry, equivalent to `.rel`.
            pltrel,
            /// Offset of the target TLS symbol from the base of this DSO's own TLS region.
            dtpoff,
            /// Offset of the target TLS symbol from the raw thread pointer.
            tpoff,
            /// Size of the target symbol.
            size,
        };

        /// For a "simple" relocation, after the initial value is computed according to `Target`, a
        /// `Simple` value communicates how to shift, truncate, and store that value into memory.
        const Simple = packed struct(u13) {
            /// The field being written to, represented as a sequence of bits in a backing integer
            /// of 8, 16, 32, or 64 bits.
            ///
            /// The `.@"8"`, `.@"16"`, `.@"32"`, and `.@"64"` fields simply write to all bits of the
            /// backing integer; i.e. the existing value is entirely overwritten.
            ///
            /// Other fields are named like "B[H:L]", where "B" is the backing integer type, and
            /// "H" and "L" are the indices of the highest and lowest bits in the bit field (in
            /// other words, an inclusive bit range). This notation was chosen because it seems to
            /// be one of the more common ways that bit relocations are written in ABIs.
            ///
            /// e.g. 8[6:3] writes the relocated value to this 4-bit field in an 8-bit integer:
            ///
            ///        MSB ___ ### ### ### ### ___ ___ ___ LSB
            ///             7   6   5   4   3   2   1   0
            ///                       bit index
            ///
            /// This enum is not intended to be able to represent every possible bit field in the
            /// backing integer types. Instead, to keep `SymbolReloc.Type` compact, fields are added
            /// to this enum only as needed. If the enum ever becomes full, some lesser-used tags
            /// can have their handling moved into `Special` to free up space.
            dest: enum(u6) {
                @"8",
                @"16",
                @"32",
                @"64",

                @"32[4:0]",
                @"32[5:0]",
                @"32[6:0]",
                @"32[9:0]",
                @"32[10:0]",
                @"32[11:0]",
                @"32[12:0]",
                @"32[21:0]",
                @"32[21:10]",
                @"32[24:5]",
                @"32[25:10]",
                @"32[29:0]",

                /// Returns `true` iff `dest` writes a full address for the target.
                ///
                /// i.e. checks for `.@"32"` on 32-bit targets; for `.@"64"` on 64-bit targets.
                fn isAddr(dest: @This(), elf: *const Elf) bool {
                    return switch (elf.identClass()) {
                        .NONE, _ => unreachable,
                        .@"32" => dest == .@"32",
                        .@"64" => dest == .@"64",
                    };
                }
            },

            /// After the relocation value is shifted (see `shift`), it is truncated to the size of
            /// the bit field (see `dest`). This field specifies whether the linker will check for,
            /// and error in the case of, truncated bits (in other words, relocation overflow).
            cast: enum(u2) {
                /// Do not perform any check when truncating unused bits.
                trunc,
                /// Error if the truncated value cannot be zero-extended back to the original value,
                /// i.e. if the truncated value is different when interpreted as unsigned.
                unsigned,
                /// Error if the truncated value cannot be sign-extended back to the original value.
                /// i.e. if the truncated value is different when interpreted as signed.
                signed,
            },

            /// The relocation value (computed based on the `Target`) gets shifted to the right by
            /// this amount. By default, the shifted-out bits can be anything, but tags ending in
            /// "_exact" introduce a check that the shifted-out bits are all zeroes (an error is
            /// emitted if not), similar to the behavior of `@shrExact`.
            shift: enum(u5) {
                @"0",
                @"2_exact",
                @"10",
                @"12",
                @"22",
                @"32",
                @"52",
            },

            /// Given a value (computed based on the `Target`), applies the shift and truncation
            /// operations specified by `s`, then writes the result to the start of `dest_slice` as
            /// specified by `s.dest`.
            fn write(
                s: Simple,
                val: u64,
                dest_slice: []u8,
                target_endian: std.lang.Endian,
            ) error{ RelocationMisaligned, RelocationOverflow }!void {
                const shift: u6, const shift_exact: bool = switch (s.shift) {
                    .@"0" => .{ 0, false },
                    .@"2_exact" => .{ 2, true },
                    .@"10" => .{ 10, false },
                    .@"12" => .{ 12, false },
                    .@"22" => .{ 22, false },
                    .@"32" => .{ 32, false },
                    .@"52" => .{ 52, false },
                };

                if (shift_exact and (val >> shift) << shift != val) {
                    return error.RelocationMisaligned;
                }

                const dest_word_bits: u8, const dest_high_bit: u6, const dest_low_bit: u6 = switch (s.dest) {
                    // zig fmt: off
                    .@"8"  => .{  8,  7, 0 },
                    .@"16" => .{ 16, 15, 0 },
                    .@"32" => .{ 32, 31, 0 },
                    .@"64" => .{ 64, 63, 0 },
                    .@"32[4:0]"   => .{ 32,  4,  0 },
                    .@"32[5:0]"   => .{ 32,  5,  0 },
                    .@"32[6:0]"   => .{ 32,  6,  0 },
                    .@"32[9:0]"   => .{ 32,  9,  0 },
                    .@"32[10:0]"  => .{ 32, 10,  0 },
                    .@"32[11:0]"  => .{ 32, 11,  0 },
                    .@"32[12:0]"  => .{ 32, 12,  0 },
                    .@"32[21:0]"  => .{ 32, 21,  0 },
                    .@"32[21:10]" => .{ 32, 21, 10 },
                    .@"32[24:5]"  => .{ 32, 24,  5 },
                    .@"32[25:10]" => .{ 32, 25, 10 },
                    .@"32[29:0]"  => .{ 32, 29,  0 },
                    // zig fmt: on
                };

                // The number of bits we are truncating from the full 64-bit relocation value.
                const trunc_bits: u6 = 63 - dest_high_bit + dest_low_bit;

                // When we shift, whether we do an arithmetic or logical shift depends on what cast
                // behavior we are going to use. If we'll be doing a signed int cast, we must shift
                // in sign bits so that we don't incorrectly cause a failure, and vice versa for an
                // unsigned int cast. Either is fine when truncating (here we pick logical shift).
                const shifted_val: u64 = switch (s.cast) {
                    .trunc => val >> shift,
                    inline else => |cast| shifted: {
                        const ShiftInt = if (cast == .signed) i64 else u64;
                        const x: ShiftInt = @bitCast(val);
                        const shifted: ShiftInt = x >> shift;

                        if ((shifted << trunc_bits) >> trunc_bits != shifted) {
                            return error.RelocationOverflow;
                        }

                        break :shifted @bitCast(shifted);
                    },
                };

                // Create a bit-mask for the field being populated, e.g. 8[3:1] -> 0b00001110
                const field_mask = (~@as(u64, 0) >> trunc_bits) << dest_low_bit;

                // Shift and mask the value to be in the correct bits, leaving the others zeroed.
                const masked_field: u64 = (shifted_val << dest_low_bit) & field_mask;

                // Now we just need to actually apply the relocation by loading a word, replacing
                // the field bits with those in `masked_field`, and storing the result back.
                switch (dest_word_bits) {
                    inline 8, 16, 32, 64 => |bits| {
                        const word_slice = dest_slice[0..@divExact(bits, 8)];
                        const Int = @Int(.unsigned, bits);
                        const old: u64 = std.mem.readInt(Int, word_slice, target_endian);
                        const new: u64 = (old & ~field_mask) | masked_field;
                        std.mem.writeInt(Int, word_slice, @intCast(new), target_endian);
                    },
                    else => unreachable,
                }
            }
        };

        /// Enum representing "special" relocation types, i.e. those which cannot be represented
        /// just with `Target` and `Simple`. These relocations have completely custom handling in
        /// the `Special.applyInner` function.
        const Special = enum(u13) {
            larch_pcala_hi20,
            larch_pcala64_lo20,
            larch_pcala64_hi12,
            larch_b21,
            larch_b26,
            larch_call36,

            sparc_le_hix22,

            fn applyInner(
                s: Special,
                elf: *Elf,
                target: Symbol.Id,
                addend: u64,
                dest_vaddr: u64,
                dest_slice: []u8,
            ) error{ RelocationMisaligned, RelocationOverflow }!void {
                switch (s) {
                    .larch_pcala_hi20 => {
                        const val = target.value(elf) +% addend;
                        const inst: *align(1) link.loongarch.J20 = @ptrCast(dest_slice[0..4]);
                        elf.targetStore(inst, .{
                            .b0_4 = elf.targetLoad(inst).b0_4,
                            .j20 = link.loongarch.pcalaHi20(val, dest_vaddr),
                            .b25_31 = elf.targetLoad(inst).b25_31,
                        });
                    },
                    .larch_pcala64_lo20 => {
                        const val = target.value(elf) +% addend;
                        const inst: *align(1) link.loongarch.J20 = @ptrCast(dest_slice[0..4]);
                        elf.targetStore(inst, .{
                            .b0_4 = elf.targetLoad(inst).b0_4,
                            .j20 = link.loongarch.pcala64Lo20(val, dest_vaddr),
                            .b25_31 = elf.targetLoad(inst).b25_31,
                        });
                    },
                    .larch_pcala64_hi12 => {
                        const val = target.value(elf) +% addend;
                        const inst: *align(1) link.loongarch.K12 = @ptrCast(dest_slice[0..4]);
                        elf.targetStore(inst, .{
                            .b0_9 = elf.targetLoad(inst).b0_9,
                            .k12 = link.loongarch.pcala64Hi12(val, dest_vaddr),
                            .b22_31 = elf.targetLoad(inst).b22_31,
                        });
                    },
                    .larch_b21, .larch_b26, .larch_call36 => {
                        const target_vaddr: u64 = elf.pltEntryTargetAddr(target) orelse target.value(elf);
                        const jump_offset: i64 = @bitCast(target_vaddr +% addend -% dest_vaddr);
                        if ((jump_offset >> 2) << 2 != jump_offset) {
                            return error.RelocationMisaligned;
                        }
                        const shifted_jump_offset: i64 = @shrExact(jump_offset, 2);
                        switch (s) {
                            .larch_b21 => {
                                if ((shifted_jump_offset << (64 - 21)) >> (64 - 21) != shifted_jump_offset) {
                                    return error.RelocationOverflow;
                                }
                                const truncated: i21 = @intCast(shifted_jump_offset);
                                const parts: packed struct { lo16: u16, hi5: u5 } = @bitCast(truncated);
                                const inst: *align(1) link.loongarch.D5K16 = @ptrCast(dest_slice[0..4]);
                                elf.targetStore(inst, .{
                                    .d5 = parts.hi5,
                                    .b5_9 = elf.targetLoad(inst).b5_9,
                                    .k16 = parts.lo16,
                                    .b26_31 = elf.targetLoad(inst).b26_31,
                                });
                            },
                            .larch_b26 => {
                                if ((shifted_jump_offset << (64 - 26)) >> (64 - 26) != shifted_jump_offset) {
                                    return error.RelocationOverflow;
                                }
                                const truncated: i26 = @intCast(shifted_jump_offset);
                                const parts: packed struct { lo16: u16, hi10: u10 } = @bitCast(truncated);
                                const inst: *align(1) link.loongarch.D10K16 = @ptrCast(dest_slice[0..4]);
                                elf.targetStore(inst, .{
                                    .d10 = parts.hi10,
                                    .k16 = parts.lo16,
                                    .b26_31 = elf.targetLoad(inst).b26_31,
                                });
                            },
                            .larch_call36 => {
                                // The allowed range of destination addresses here is non-trivial:
                                // [PC - 128 GiB - 0x20_000, PC + 128 GiB - 0x20_000 - 4]
                                const gib = 1024 * 1024 * 1024;
                                if (jump_offset < -128 * gib - 0x20_000 or
                                    jump_offset > 128 * gib - 0x20_000 - 4)
                                {
                                    return error.RelocationOverflow;
                                }
                                // The values we write into the instructions are a little weird too:
                                const hi: i20 = @intCast((shifted_jump_offset +% 0x8000) >> 16);
                                const lo: i16 = @truncate(shifted_jump_offset);

                                const inst0: *align(1) link.loongarch.J20 = @ptrCast(dest_slice[0..4]);
                                const inst1: *align(1) link.loongarch.K16 = @ptrCast(dest_slice[4..8]);

                                const old0 = elf.targetLoad(inst0);
                                elf.targetStore(inst0, .{ .b0_4 = old0.b0_4, .j20 = @bitCast(hi), .b25_31 = old0.b25_31 });

                                const old1 = elf.targetLoad(inst1);
                                elf.targetStore(inst1, .{ .b0_9 = old1.b0_9, .k16 = @bitCast(lo), .b26_31 = old1.b26_31 });
                            },
                            else => unreachable,
                        }
                    },
                    .sparc_le_hix22 => {
                        const tls_phndx = elf.getNode(elf.ni.tls.unwrap().?).segment;
                        const tls_size: u64 = switch (elf.phdrSlice()) {
                            inline else => |phdr| tls_size: {
                                assert(elf.targetLoad(&phdr[tls_phndx].type) == .TLS);
                                break :tls_size elf.targetLoad(&phdr[tls_phndx].memsz);
                            },
                        };
                        const dest_ptr: *align(1) packed struct(u32) {
                            imm22: u22,
                            b22_31: u10,
                        } = @ptrCast(dest_slice);
                        elf.targetStore(dest_ptr, .{
                            .imm22 = @truncate(~(target.value(elf) +% addend -% tls_size) >> 10),
                            .b22_31 = elf.targetLoad(dest_ptr).b22_31,
                        });
                    },
                }
            }
        };

        fn dependsOnTlsSize(t: SymbolReloc.Type, elf: *const Elf) bool {
            return switch (elf.targetTlsVariant()) {
                // In TLS variant I, the executable's TLS block starts at a fixed offset from the
                // thread pointer, so everything is fine...
                .I_original, .I_modified => false,
                // ...but in variant II, the executable's TLS block *ends* at a fixed offset from
                // the thread pointer, so the offset from the thread pointer to the *start* of the
                // TLS block depends on the size of the block, and we need that offset to resolve
                // 'tpoff' relocations.
                .II => switch (t.target) {
                    .abs,
                    .rel,
                    .pltabs,
                    .pltrel,
                    .dtpoff,
                    .size,
                    => false,

                    .tpoff => true,

                    .special => switch (t.action.special) {
                        .sparc_le_hix22,
                        => true,

                        .larch_pcala_hi20,
                        .larch_pcala64_lo20,
                        .larch_pcala64_hi12,
                        .larch_b21,
                        .larch_b26,
                        .larch_call36,
                        => false,
                    },
                },
            };
        }
    };

    const Index = enum(u32) {
        none = std.math.maxInt(u32),
        _,

        fn get(index: SymbolReloc.Index, elf: *Elf) *SymbolReloc {
            return &elf.symbol_relocs.items[@backingInt(index)];
        }
    };

    fn flushMovedNode(reloc: *SymbolReloc, elf: *Elf, node_vaddr: u64) void {
        if (reloc.rela_index.unwrap()) |rela_index| {
            // The node has moved, so the offset of the relocation within the section might have
            // changed, so update the `offset` field of the `ElfN.Rela` entry.
            reloc.relaSection(elf).relaSetOffset(elf, rela_index, node_vaddr + reloc.offset);
        }
        // This is not just the inverse of the above condition, because if `reloc` is relative
        // to the base of this DSO, then `rela_index` is an `R_*_RELATIVE` relocation, but we
        // still need to call `SymbolReloc.apply` to update that relocation's addend.
        if (elf.ehdrType() != .REL) {
            reloc.apply(elf);
        }
    }

    fn apply(reloc: *SymbolReloc, elf: *Elf) void {
        assert(elf.ehdrType() != .REL);
        const node = reloc.node.unwrap() orelse return; // deleted
        if (node.hasMoved(&elf.mf) or reloc.target.hasMoved(elf)) {
            // There's no point applying the relocation now, because it will be re-applied by
            // `flushMoved` at some point anyway.
            return;
        }
        switch (reloc.result) {
            .ok => {},
            .overflowed => elf.overflowed_reloc_count -= 1,
            .misaligned => elf.misaligned_reloc_count -= 1,
        }
        if (reloc.applyInner(elf)) {
            @branchHint(.likely);
            reloc.result = .ok;
        } else |err| switch (err) {
            error.RelocationOverflow => {
                reloc.result = .overflowed;
                elf.overflowed_reloc_count += 1;
            },
            error.RelocationMisaligned => {
                reloc.result = .misaligned;
                elf.misaligned_reloc_count += 1;
            },
        }
    }
    fn applyInner(reloc: *const SymbolReloc, elf: *Elf) error{ RelocationOverflow, RelocationMisaligned }!void {
        const node = reloc.node.unwrap().?;
        const dest_vaddr = elf.getNodeVAddr(node) + reloc.offset;
        const dest_slice = node.slice(&elf.mf)[@intCast(reloc.offset)..];

        const addend: u64 = @bitCast(reloc.addend);
        const target_val: u64 = type: switch (reloc.type.target) {
            .abs => reloc.target.value(elf) +% addend,
            .rel => reloc.target.value(elf) +% addend -% dest_vaddr,
            .pltabs => {
                const plt_entry_addr = elf.pltEntryTargetAddr(reloc.target) orelse continue :type .abs;
                break :type plt_entry_addr +% addend;
            },
            .pltrel => {
                const plt_entry_addr = elf.pltEntryTargetAddr(reloc.target) orelse continue :type .rel;
                break :type plt_entry_addr +% addend -% dest_vaddr;
            },
            .dtpoff => reloc.target.value(elf) +% addend,
            .tpoff => switch (elf.targetTlsVariant()) {
                .I_original => |tls| tls.tcb_size +% reloc.target.value(elf) +% addend,
                .I_modified => |tls| 0 -% tls.tp_off +% reloc.target.value(elf) +% addend,
                .II => {
                    const tls_phndx = elf.getNode(elf.ni.tls.unwrap().?).segment;
                    const tls_size: u64 = switch (elf.phdrSlice()) {
                        inline else => |phdr| tls_size: {
                            assert(elf.targetLoad(&phdr[tls_phndx].type) == .TLS);
                            break :tls_size elf.targetLoad(&phdr[tls_phndx].memsz);
                        },
                    };
                    break :type reloc.target.value(elf) +% addend -% tls_size;
                },
            },
            .size => reloc.target.size(elf),
            .special => return reloc.type.action.special.applyInner(
                elf,
                reloc.target,
                addend,
                dest_vaddr,
                dest_slice,
            ),
        };

        // Check for the `R_*_RELATIVE` case now, because it is possible only when no shift or cast
        // is required, meaning we can handle it now and return early.
        if (reloc.rela_index.unwrap()) |rela_index| switch (elf.classifySymbolValue(reloc.target)) {
            .static => unreachable,
            .dynamic => return, // the relocation happens at runtime
            .static_relative => {
                // We have emitted an R_*_RELATIVE relocation to help lower an absolute-address
                // relocation. The value computed above is valid, but instead of writing it to the
                // destination slice, we actually want to write it to the runtime relocation entry.
                switch (elf.identClass()) {
                    .NONE, _ => unreachable,
                    .@"32" => assert(reloc.type.action.simple.dest == .@"32"),
                    .@"64" => assert(reloc.type.action.simple.dest == .@"64"),
                }
                assert(reloc.type.action.simple.cast == .unsigned);
                assert(reloc.type.action.simple.shift == .@"0");
                elf.shndx.rela_dyn.relaSetAddend(elf, rela_index, target_val);
                return;
            },
        };

        try reloc.type.action.simple.write(target_val, dest_slice, elf.targetEndian());
    }

    fn delete(reloc: *SymbolReloc, elf: *Elf, index: SymbolReloc.Index) void {
        assert(index.get(elf) == reloc);

        reloc.deleteOutputRel(elf);
        if (reloc.type.dependsOnTlsSize(elf)) {
            assert(elf.tls_size_symbol_relocs.swapRemove(index));
        }

        switch (reloc.prev) {
            .none => {
                const first_target_reloc = &reloc.target.index(elf).ptr(elf).first_target_reloc;
                assert(first_target_reloc.* == index);
                first_target_reloc.* = reloc.next;
            },
            else => |prev| prev.get(elf).next = reloc.next,
        }
        switch (reloc.next) {
            .none => {},
            else => |next| next.get(elf).prev = reloc.prev,
        }
        switch (reloc.result) {
            .ok => {},
            .overflowed => elf.overflowed_reloc_count -= 1,
            .misaligned => elf.misaligned_reloc_count -= 1,
        }

        reloc.* = undefined;
        reloc.node = .none;
    }

    /// If `reloc.rela_index` is populated, reset it to `.none` and delete the relocation, updating
    /// `elf.textrel_count` if necessary.
    fn deleteOutputRel(reloc: *SymbolReloc, elf: *Elf) void {
        const rela_index = reloc.rela_index.unwrap() orelse return;
        reloc.relaSection(elf).relaDeleteOne(elf, rela_index);
        switch (elf.ehdrType()) {
            .REL => {},
            .EXEC, .DYN => switch (elf.nodeWantsDsoRelocation(reloc.node.unwrap().?)) {
                .no => unreachable, // there *was* a dynamic relocation!
                .yes => {},
                .yes_textrel => elf.textrel_count -= 1,
            },
        }
        reloc.rela_index = .none;
    }
};

/// A relocation targeting an arbitrary node (within a section) with a fixed addend.
/// This represents a symbol reloc against the section symbol containing the node
/// with a variable addend that changes when the target node moves.
const NodeReloc = struct {
    node: MappedFile.Node.Index.Optional,
    offset: u64,
    target: MappedFile.Node.Index,
    addend: i64,
    type: NodeReloc.Type,
    next: NodeReloc.Index,
    prev: NodeReloc.Index,
    rela_index: Section.RelaIndex.Optional,
    result: enum(u8) { ok, overflowed, misaligned },

    const Type = enum { abs32, abs64 };

    const Index = enum(u32) {
        none = std.math.maxInt(u32),
        _,

        fn get(index: NodeReloc.Index, elf: *Elf) *NodeReloc {
            return &elf.node_relocs.items[@backingInt(index)];
        }
    };

    fn flushMovedNode(reloc: *NodeReloc, elf: *Elf, node_vaddr: u64) void {
        if (reloc.rela_index.unwrap()) |rela_index| {
            assert(elf.ehdrType() == .REL);
            // The node has moved, so the offset of the relocation within the section might have
            // changed, so update the `offset` field of the `ElfN.Rela` entry.
            elf.getNodeShndx(reloc.node.unwrap().?).get(elf).rela.shndx.relaSetOffset(elf, rela_index, node_vaddr + reloc.offset);
        } else {
            assert(elf.ehdrType() != .REL);
            reloc.apply(elf);
        }
    }

    fn flushMovedTarget(reloc: *NodeReloc, elf: *Elf, target_section_offset: u64) void {
        if (reloc.rela_index.unwrap()) |rela_index| {
            assert(elf.ehdrType() == .REL);
            // The target has moved, so the `addend` field of the `ElfN.Rela` entry needs to be updated.
            elf.getNodeShndx(reloc.node.unwrap().?).get(elf).rela.shndx.relaSetAddend(elf, rela_index, target_section_offset +% @as(u64, @bitCast(reloc.addend)));
        } else {
            assert(elf.ehdrType() != .REL);
            reloc.apply(elf);
        }
    }

    fn apply(reloc: *NodeReloc, elf: *Elf) void {
        const node = reloc.node.unwrap() orelse return; // deleted
        if (reloc.rela_index.unwrap()) |rela_index| {
            assert(elf.ehdrType() == .REL);
            _ = rela_index;
        } else {
            assert(elf.ehdrType() != .REL);
            if (node.hasMoved(&elf.mf) or reloc.target.hasMoved(&elf.mf)) {
                // There's no point applying the relocation now, because it will be re-applied by
                // `flushMoved` at some point anyway.
                return;
            }
            switch (reloc.result) {
                .ok => {},
                .overflowed => elf.overflowed_reloc_count -= 1,
                .misaligned => elf.misaligned_reloc_count -= 1,
            }
            if (reloc.applyInner(elf)) {
                @branchHint(.likely);
                reloc.result = .ok;
            } else |err| switch (err) {
                error.RelocationOverflow => {
                    reloc.result = .overflowed;
                    elf.overflowed_reloc_count += 1;
                },
                error.RelocationMisaligned => {
                    reloc.result = .misaligned;
                    elf.misaligned_reloc_count += 1;
                },
            }
        }
    }
    fn applyInner(reloc: *const NodeReloc, elf: *Elf) error{ RelocationOverflow, RelocationMisaligned }!void {
        const simple: SymbolReloc.Type.Simple = .{ .dest = switch (reloc.type) {
            .abs32 => .@"32",
            .abs64 => .@"64",
        }, .cast = .unsigned, .shift = .@"0" };
        const addend: u64 = @bitCast(reloc.addend);
        const target_val = elf.getNodeVAddr(reloc.target) +% addend;
        const dest_slice = reloc.node.unwrap().?.slice(&elf.mf)[@intCast(reloc.offset)..];
        try simple.write(target_val, dest_slice, elf.targetEndian());
    }

    fn delete(reloc: *NodeReloc, elf: *Elf) void {
        reloc.deleteOutputRel(elf);

        switch (reloc.prev) {
            .none => {
                const first_target_reloc = switch (elf.getNode(reloc.target)) {
                    else => unreachable,
                    .debug_shared => |ss| &elf.dwarf_shared.getPtr(ss).first_target_reloc,
                    .unit_frame_cie => |ui| &elf.dwarf_units[@backingInt(ui)].frame_cie_first_target_reloc,
                    .unit_debug_info_header => |ui| &elf.dwarf_units[@backingInt(ui)].debug_info_header_first_target_reloc,
                    .unit_debug_line_header => |ui| &elf.dwarf_units[@backingInt(ui)].debug_line_header_first_target_reloc,
                    .unit_debug_rnglists => |ui| &elf.dwarf_units[@backingInt(ui)].debug_rnglists_first_target_reloc,
                    .const_debug_info => |cpi| &elf.dwarf_consts.getPtr(cpi).?.debug_info_first_target_reloc,
                    .global_debug_info => |gi| &elf.dwarf_globals.items[@backingInt(gi)].debug_info_first_target_reloc,
                    .func_debug_info => |fi| &elf.dwarf_funcs.items[@backingInt(fi)].debug_info_first_target_reloc,
                    .decl_debug_info => |di| &elf.dwarf_decls.getPtr(di).?.debug_info_first_target_reloc,
                };
                first_target_reloc.* = reloc.next;
            },
            else => |prev| prev.get(elf).next = reloc.next,
        }
        switch (reloc.next) {
            .none => {},
            else => |next| next.get(elf).prev = reloc.prev,
        }
        switch (reloc.result) {
            .ok => {},
            .overflowed => elf.overflowed_reloc_count -= 1,
            .misaligned => elf.misaligned_reloc_count -= 1,
        }

        reloc.* = undefined;
        reloc.node = .none;
    }

    /// If `reloc.rela_index` is populated, reset it to `.none` and delete the relocation.
    fn deleteOutputRel(reloc: *NodeReloc, elf: *Elf) void {
        const rela_index = reloc.rela_index.unwrap() orelse return;
        assert(elf.ehdrType() == .REL);
        elf.getNodeShndx(reloc.node.unwrap().?).get(elf).rela.shndx.relaDeleteOne(elf, rela_index);
        reloc.rela_index = .none;
    }
};

/// Identifies a single entry in the GOT.
const GotKey = union(enum) {
    /// The entry is a reserved word, initialized to zero. `initHeaders` will add as many of these
    /// as the target machine ABI requires.
    ///
    /// This `u32` value exists to allow reserving multiple words with distinct keys.
    reserved: u32,

    /// Value is the address of the given symbol.
    symbol: Symbol.Id,

    /// Value is the signed offset of the given symbol from the TLS pointer.
    tpoff: Symbol.Id,

    /// Value is the TLS module ID of the DSO we are creating.
    ///
    /// Used for the first of the two GOT entries generated by a TLSLD relocation.
    tlsld0,
    /// Value is always 0.
    ///
    /// Used for the second of the two GOT entries generated by a TLSLD relocation.
    tlsld1,

    /// Value is the TLS module ID for the given STT_TLS symbol.
    ///
    /// Used for the first of the two GOT entries generated by a TLSGD relocation.
    tlsgd0: Symbol.Id,
    /// Value is the offset of the given STT_TLS symbol from the base of the per-module TLS area.
    ///
    /// Used for the second of the two GOT entries generated by a TLSGD relocation.
    tlsgd1: Symbol.Id,
};

/// A relocation targeting a particular GOT entry.
const GotReloc = struct {
    /// The node containing this relocation. Possible values are:
    /// * An input section
    /// * A section
    /// * A NAV, UAV, or lazy code/data
    /// * `.none`, if this relocation was deleted (in which case it should be ignored)
    node: MappedFile.Node.Index.Optional,
    /// The offset of the relocation inside of `node`.
    offset: u64,
    target: GotKey,
    addend: i64,
    type: GotReloc.Type,
    result: enum(u8) { ok, overflowed, misaligned },

    /// `GotReloc.Type` has the same structure as `SymbolReloc.Type`, just with different `Target`
    /// and `Special` enums---consult doc comments on `SymbolReloc.Type` for an overview.
    const Type = packed struct(u16) {
        fn simple(target: Target, action: Simple) GotReloc.Type {
            assert(target != .special);
            return .{ .target = target, .action = .{ .simple = action } };
        }

        fn special(s: Special) GotReloc.Type {
            return .{ .target = .special, .action = .{ .special = s } };
        }

        target: Target,
        action: packed union {
            simple: Simple,
            special: Special,
        },

        /// Like `SymbolReloc.Target`, but for GOT relocations. There are fewer tags because there
        /// are fewer different kinds of GOT relocation.
        const Target = enum(u3) {
            /// This is a "special" relocation whose specific type is in the `action.special` field.
            special,

            /// Absolute address of the GOT entry.
            abs,
            /// Offset from the relocation itself to the GOT entry ("PC-relative").
            rel,
            /// Offset from the base of the GOT to the GOT entry.
            offset,
        };

        const Simple = SymbolReloc.Type.Simple;

        /// Like `SymbolReloc.Special`, but for GOT relocations.
        const Special = enum(u13) {
            larch_pcala_hi20,
            larch_pcala64_lo20,
            larch_pcala64_hi12,

            sparc_op_lox10,
            sparc_op_hix22,

            fn applyInner(
                s: Special,
                elf: *Elf,
                got_vaddr: u64,
                got_offset: u64,
                addend: u64,
                dest_vaddr: u64,
                dest_slice: []u8,
            ) error{ RelocationMisaligned, RelocationOverflow }!void {
                switch (s) {
                    .larch_pcala_hi20 => {
                        const val = got_vaddr +% got_offset +% addend;
                        const inst: *align(1) link.loongarch.J20 = @ptrCast(dest_slice[0..4]);
                        elf.targetStore(inst, .{
                            .b0_4 = elf.targetLoad(inst).b0_4,
                            .j20 = link.loongarch.pcalaHi20(val, dest_vaddr),
                            .b25_31 = elf.targetLoad(inst).b25_31,
                        });
                    },
                    .larch_pcala64_lo20 => {
                        const val = got_vaddr +% got_offset +% addend;
                        const inst: *align(1) link.loongarch.J20 = @ptrCast(dest_slice[0..4]);
                        elf.targetStore(inst, .{
                            .b0_4 = elf.targetLoad(inst).b0_4,
                            .j20 = link.loongarch.pcala64Lo20(val, dest_vaddr),
                            .b25_31 = elf.targetLoad(inst).b25_31,
                        });
                    },
                    .larch_pcala64_hi12 => {
                        const val = got_vaddr +% got_offset +% addend;
                        const inst: *align(1) link.loongarch.K12 = @ptrCast(dest_slice[0..4]);
                        elf.targetStore(inst, .{
                            .b0_9 = elf.targetLoad(inst).b0_9,
                            .k12 = link.loongarch.pcala64Hi12(val, dest_vaddr),
                            .b22_31 = elf.targetLoad(inst).b22_31,
                        });
                    },
                    .sparc_op_lox10 => {
                        const dest_ptr: *align(1) packed struct(u32) {
                            imm13: u13,
                            b13_31: u19,
                        } = @ptrCast(dest_slice);
                        elf.targetStore(dest_ptr, .{
                            .imm13 = @as(u10, @truncate(got_offset)),
                            .b13_31 = elf.targetLoad(dest_ptr).b13_31,
                        });
                    },
                    .sparc_op_hix22 => {
                        const dest_ptr: *align(1) packed struct(u32) {
                            imm22: u22,
                            b22_31: u10,
                        } = @ptrCast(dest_slice);
                        elf.targetStore(dest_ptr, .{
                            .imm22 = @truncate(got_offset >> 10),
                            .b22_31 = elf.targetLoad(dest_ptr).b22_31,
                        });
                    },
                }
            }
        };
    };

    const Index = enum(u32) {
        none = std.math.maxInt(u32),
        _,

        fn get(index: GotReloc.Index, elf: *Elf) *GotReloc {
            return &elf.got_relocs.items[@backingInt(index)];
        }
    };

    fn apply(reloc: *GotReloc, elf: *Elf) void {
        assert(elf.ehdrType() != .REL);
        const node = reloc.node.unwrap() orelse return; // deleted
        if (node.hasMoved(&elf.mf) or elf.shndx.got.get(elf).ni.hasMoved(&elf.mf)) {
            // There's no point applying the relocation now, because it will be re-applied by
            // `flushMoved` at some point anyway.
            return;
        }
        switch (reloc.result) {
            .ok => {},
            .overflowed => elf.overflowed_reloc_count -= 1,
            .misaligned => elf.misaligned_reloc_count -= 1,
        }
        if (reloc.applyInner(elf)) {
            @branchHint(.likely);
            reloc.result = .ok;
        } else |err| switch (err) {
            error.RelocationOverflow => {
                reloc.result = .overflowed;
                elf.overflowed_reloc_count += 1;
            },
            error.RelocationMisaligned => {
                reloc.result = .misaligned;
                elf.misaligned_reloc_count += 1;
            },
        }
    }
    fn applyInner(reloc: *const GotReloc, elf: *Elf) error{ RelocationOverflow, RelocationMisaligned }!void {
        const node = reloc.node.unwrap().?;
        const dest_vaddr = elf.getNodeVAddr(node) + reloc.offset;
        const dest_slice = node.slice(&elf.mf)[@intCast(reloc.offset)..];

        const got_vaddr = elf.shndx.got.vaddr(elf);
        const got_index: u64 = elf.got.getIndex(reloc.target).?;
        const got_offset: u64 = switch (elf.identClass()) {
            .NONE, _ => unreachable,
            inline else => |class| @sizeOf(class.ElfN().Addr) * got_index,
        };
        const addend: u64 = @bitCast(reloc.addend);

        const target_val: u64 = switch (reloc.type.target) {
            .abs => got_vaddr +% got_offset +% addend,
            .rel => got_vaddr +% got_offset +% addend -% dest_vaddr,
            .offset => got_offset +% addend,
            .special => return reloc.type.action.special.applyInner(
                elf,
                got_vaddr,
                got_offset,
                addend,
                dest_vaddr,
                dest_slice,
            ),
        };
        try reloc.type.action.simple.write(target_val, dest_slice, elf.targetEndian());
    }

    fn delete(reloc: *GotReloc, elf: *Elf) void {
        switch (reloc.result) {
            .ok => {},
            .overflowed => elf.overflowed_reloc_count -= 1,
            .misaligned => elf.misaligned_reloc_count -= 1,
        }
        reloc.* = .{
            .node = .none,
            .offset = undefined,
            .target = undefined,
            .addend = undefined,
            .type = undefined,
            .result = undefined,
        };
    }
};

/// Records all global symbols defined in any DSO we depend on. For each symbol, its name, version,
/// type, size, and alignment are all stored, as well as the soname of the DSO defining it. If one
/// of these symbols ends up being referenced by the ELF module we are producing, we need this
/// information for a few reasons:
///
/// * If an undefined symbol has an external definition with type `STT_FUNC`, we know to create a
///   PLT entry for it.
///
/// * If an undefined symbol has an external definition with type `STT_OBJECT`, we can create a copy
///   relocation for it if necessary.
///
/// * If an undefined versioned symbol has an external definition, we can add the required entry to
///   `.gnu.version_r` because we know the soname of the DSO defining it.
///
/// * Similarly, if an undefined unversioned symbol matches an external definition of a default
///   symbol version, we know to emit the default version for the dynamic symbol table entry even
///   though that symbol version was not explicitly requested.
///
/// * If an undefined symbol does *not* have any external definition, when emitting a dynamic
///   executable, we can emit an "undefined global symbol" link error for it.
const DsoGlobals = struct {
    string_bytes: std.ArrayList(u8),

    /// Contains all global symbols defined in any needed DSO.
    ///
    /// Accessed using `DsoGlobals.Symbol.Adapter`. Adapted key type is `DsoGlobals.Symbol.Key`.
    symbols: std.array_hash_map.Custom(DsoGlobals.Symbol, void, void, true),

    /// Contains all default-version global symbols defined in any needed DSO.
    ///
    /// Accessed using `DsoGlobals.Symbol.DefaultVersionAdapter`. Adapted key type is `[]const u8`.
    ///
    /// Stored key is an index into `DsoGlobals.symbols`.
    default_sym_vers: std.array_hash_map.Custom(u32, void, void, true),

    /// Index into `DsoGlobals.string_bytes`. This is a different type than `Elf.String` because
    /// these string bytes are in a `std.ArrayList` rather than a section of the output binary.
    const String = enum(u32) {
        _,
        fn slice(s: DsoGlobals.String, dso_globals: *const DsoGlobals) [:0]const u8 {
            const overlong = dso_globals.string_bytes.items[@backingInt(s)..];
            return overlong[0..std.mem.findScalar(u8, overlong, 0).? :0];
        }
        const Optional = enum(u32) {
            none = std.math.maxInt(u32),
            _,
            fn unwrap(os: DsoGlobals.String.Optional) ?DsoGlobals.String {
                return switch (os) {
                    .none => null,
                    _ => @fromBackingInt(@backingInt(os)),
                };
            }
            fn wrap(s: DsoGlobals.String) DsoGlobals.String.Optional {
                return @bitCast(s);
            }
        };
    };

    const Symbol = struct {
        name: DsoGlobals.String,
        /// `.none` if the symbol is unversioned.
        version: DsoGlobals.String.Optional,
        type: std.elf.STT,
        soname: Elf.String(.dynstr),
        size: u64,
        /// This is usually unnecessary, but if a symbol is given a copy relocation (`R_*_COPY`) and
        /// so becomes a part of the executable's address space despite being defined by a different
        /// DSO, we need to know its alignment requirement so that we don't break other code. This
        /// isn't actually stored on the symbol---instead we compute a maximum alignment from the
        /// alignment of the section containing the symbol, and the symbol's offset within the
        /// section. I know this sounds like a terrible hack, but it is *genuinely* how you're
        /// supposed to do this. Copy relocations suck.
        alignment: Alignment,

        const Key = struct {
            name: []const u8,
            version: ?[]const u8,
        };

        const Adapter = struct {
            dso_globals: *const DsoGlobals,
            pub fn eql(ctx: Adapter, lhs_key: Key, rhs_symbol: DsoGlobals.Symbol, _: usize) bool {
                const dso_globals = ctx.dso_globals;
                const rhs_name = rhs_symbol.name.slice(dso_globals);
                if (!std.mem.eql(u8, lhs_key.name, rhs_name)) return false;
                if (lhs_key.version) |lhs_version| {
                    const rhs_version = rhs_symbol.version.unwrap() orelse return false;
                    if (!std.mem.eql(u8, lhs_version, rhs_version.slice(dso_globals))) return false;
                } else {
                    if (rhs_symbol.version != .none) return false;
                }
                return true;
            }

            pub fn hash(ctx: Adapter, key: Key) u32 {
                _ = ctx;
                var h: std.hash.Wyhash = .init(key.name.len);
                h.update(key.name);
                if (key.version) |version| {
                    h.update(&.{1});
                    h.update(version);
                } else {
                    h.update(&.{0});
                }
                return @truncate(h.final());
            }
        };

        const DefaultVersionAdapter = struct {
            dso_globals: *const DsoGlobals,
            pub fn eql(ctx: DefaultVersionAdapter, lhs_name: []const u8, rhs_symbol_index: u32, _: usize) bool {
                const rhs_symbol = &ctx.dso_globals.symbols.keys()[rhs_symbol_index];
                return std.mem.eql(u8, lhs_name, rhs_symbol.name.slice(ctx.dso_globals));
            }
            pub fn hash(ctx: DefaultVersionAdapter, name: []const u8) u32 {
                _ = ctx;
                return std.array_hash_map.hashString(name);
            }
        };
    };

    fn find(dso_globals: *const DsoGlobals, name: []const u8, version: ?[]const u8) ?*const DsoGlobals.Symbol {
        if (dso_globals.symbols.getIndexAdapted(@as(DsoGlobals.Symbol.Key, .{
            .name = name,
            .version = version,
        }), @as(DsoGlobals.Symbol.Adapter, .{
            .dso_globals = dso_globals,
        }))) |index| {
            return &dso_globals.symbols.keys()[index];
        } else {
            return null;
        }
    }

    fn findDefaultVersion(dso_globals: *const DsoGlobals, name: []const u8) ?*const DsoGlobals.Symbol {
        const adapter: DsoGlobals.Symbol.DefaultVersionAdapter = .{
            .dso_globals = dso_globals,
        };
        if (dso_globals.default_sym_vers.getIndexAdapted(name, adapter)) |default_index| {
            const symbol_index = dso_globals.default_sym_vers.keys()[default_index];
            return &dso_globals.symbols.keys()[symbol_index];
        } else {
            return null;
        }
    }
};

fn ensureDynsymHashCapacity(elf: *Elf, max_dynsym_count: u32) Error!void {
    const gpa = elf.base.comp.gpa;

    const min_buckets = max_dynsym_count / 2;

    const cur_dynsym_count: u32 = switch (elf.shdrPtr(elf.shndx.dynsym)) {
        inline else => |shdr, class| @intCast(@divExact(
            elf.targetLoad(&shdr.size),
            @sizeOf(class.ElfN().Sym),
        )),
    };

    switch (elf.targetDynsymHashInfo()) {
        inline else => |info| {
            {
                const section_slice: []align(@sizeOf(info.Int())) u8 = @alignCast(elf.shndx.hash.get(elf).ni.slice(&elf.mf));
                const header: *info.Header() = @ptrCast(section_slice[0..@sizeOf(info.Header())]);
                assert(elf.targetLoad(&header.nchain) == cur_dynsym_count);
                const nbucket = elf.targetLoad(&header.nbucket);
                if (nbucket >= min_buckets) {
                    // We don't need to add any buckets, but we still need to make sure the section is large
                    // enough to fit `max_dynsym_count` chains.
                    const need_size = @sizeOf(info.Header()) + (nbucket + max_dynsym_count) * 4;
                    try elf.shndx.hash.get(elf).ni.ensureMinimumSize(gpa, &elf.mf, need_size);
                    return;
                }
                // We need more buckets, so we'll have to rebuild the hash table.
            }

            // Rebuilding the hash table is quite expensive, so to avoid doing it too often we use a large
            // growth factor (* 2) for `nbucket`.
            const new_nbucket = min_buckets * 2;

            {
                const need_size = @sizeOf(info.Header()) + (new_nbucket + max_dynsym_count) * 4;
                try elf.shndx.hash.get(elf).ni.ensureMinimumSize(gpa, &elf.mf, need_size);
            }

            elf.mf.nodes_lock.lock();
            defer elf.mf.nodes_lock.unlock();

            const section_slice: []align(@sizeOf(info.Int())) u8 = @alignCast(elf.shndx.hash.get(elf).ni.slice(&elf.mf));
            const header: *info.Header() = @ptrCast(section_slice[0..@sizeOf(info.Header())]);
            const trailing: []info.Int() = @ptrCast(section_slice[@sizeOf(info.Header())..]);

            header.* = .{ .nbucket = new_nbucket, .nchain = cur_dynsym_count };
            if (elf.targetEndian() != std.lang.Endian.native) {
                std.mem.byteSwapAllFields(info.Header(), header);
            }
            const buckets: []info.Int() = trailing[0..@intCast(elf.targetLoad(&header.nbucket))];
            const chains: []info.Int() = trailing[@intCast(elf.targetLoad(&header.nbucket))..][0..@intCast(elf.targetLoad(&header.nchain))];

            @memset(buckets, 0);
            chains[0] = 0;
            for (1..cur_dynsym_count, chains[1..]) |dynsym_index_usize, *chain| {
                const dynsym_index: u32 = @intCast(dynsym_index_usize);
                const dynsym_name: String(.dynstr) = switch (elf.dynsymPtr(dynsym_index)) {
                    inline else => |sym| @fromBackingInt(elf.targetLoad(&sym.name)),
                };
                const b = std.elf.hash.calculate(dynsym_name.slice(elf)) % buckets.len;
                // Make this symbol the head of that bucket, and chain to the old head.
                chain.* = buckets[b];
                elf.targetStore(&buckets[b], dynsym_index);
            }
        },
    }
}

fn appendDynsymHashEntry(elf: *Elf, dynsym_index: u32) void {
    switch (elf.targetDynsymHashInfo()) {
        inline else => |info| {
            const section_slice: []align(@sizeOf(info.Int())) u8 = @alignCast(elf.shndx.hash.get(elf).ni.slice(&elf.mf));
            const header: *info.Header() = @ptrCast(section_slice[0..@sizeOf(info.Header())]);
            assert(elf.targetLoad(&header.nchain) == dynsym_index);
            elf.targetStore(&header.nchain, dynsym_index + 1);

            switch (elf.shdrPtr(elf.shndx.hash)) {
                inline else => |shdr| elf.targetStore(&shdr.size, elf.targetLoad(&shdr.size) + @sizeOf(info.Int())),
            }
        },
    }

    elf.populateDynsymHashEntry(dynsym_index);
}
fn populateDynsymHashEntry(elf: *Elf, dynsym_index: u32) void {
    elf.mf.nodes_lock.lock();
    defer elf.mf.nodes_lock.unlock();

    assert(dynsym_index != 0);

    switch (elf.targetDynsymHashInfo()) {
        inline else => |info| {
            const section_slice: []align(@sizeOf(info.Int())) u8 = @alignCast(elf.shndx.hash.get(elf).ni.slice(&elf.mf));
            const header: *info.Header() = @ptrCast(section_slice[0..@sizeOf(info.Header())]);
            const trailing: []info.Int() = @ptrCast(section_slice[@sizeOf(info.Header())..]);

            const buckets: []info.Int() = trailing[0..@intCast(elf.targetLoad(&header.nbucket))];
            const chains: []info.Int() = trailing[@intCast(elf.targetLoad(&header.nbucket))..][0..@intCast(elf.targetLoad(&header.nchain))];

            const dynsym_name: String(.dynstr) = switch (elf.dynsymPtr(dynsym_index)) {
                inline else => |sym| @fromBackingInt(elf.targetLoad(&sym.name)),
            };
            const b = std.elf.hash.calculate(dynsym_name.slice(elf)) % buckets.len;
            // Make this symbol the head of that bucket, and chain to the old head.
            chains[dynsym_index] = buckets[b];
            elf.targetStore(&buckets[b], dynsym_index);
        },
    }
}
fn popDynsymHashEntry(elf: *Elf, dynsym_index: u32) void {
    elf.clearDynsymHashEntry(dynsym_index);

    switch (elf.targetDynsymHashInfo()) {
        inline else => |info| {
            const section_slice: []align(@sizeOf(info.Int())) u8 = @alignCast(elf.shndx.hash.get(elf).ni.slice(&elf.mf));
            const header: *info.Header() = @ptrCast(section_slice[0..@sizeOf(info.Header())]);
            assert(elf.targetLoad(&header.nchain) == dynsym_index + 1);
            elf.targetStore(&header.nchain, dynsym_index);

            switch (elf.shdrPtr(elf.shndx.hash)) {
                inline else => |shdr| elf.targetStore(&shdr.size, elf.targetLoad(&shdr.size) - @sizeOf(info.Int())),
            }
        },
    }
}
fn clearDynsymHashEntry(elf: *Elf, dynsym_index: u32) void {
    elf.mf.nodes_lock.lock();
    defer elf.mf.nodes_lock.unlock();

    assert(dynsym_index != 0);

    switch (elf.targetDynsymHashInfo()) {
        inline else => |info| {
            const section_slice: []align(@sizeOf(info.Int())) u8 = @alignCast(elf.shndx.hash.get(elf).ni.slice(&elf.mf));
            const header: *info.Header() = @ptrCast(section_slice[0..@sizeOf(info.Header())]);
            const trailing: []info.Int() = @ptrCast(section_slice[@sizeOf(info.Header())..]);

            const buckets: []info.Int() = trailing[0..@intCast(elf.targetLoad(&header.nbucket))];
            const chains: []info.Int() = trailing[@intCast(elf.targetLoad(&header.nbucket))..][0..@intCast(elf.targetLoad(&header.nchain))];

            const dynsym_name: String(.dynstr) = switch (elf.dynsymPtr(dynsym_index)) {
                inline else => |sym| @fromBackingInt(elf.targetLoad(&sym.name)),
            };
            const b = std.elf.hash.calculate(dynsym_name.slice(elf)) % buckets.len;

            const next_dynsym_index = elf.targetLoad(&chains[dynsym_index]);
            elf.targetStore(&chains[dynsym_index], 0);

            // To remove `dynsym_index` from the singly-linked list, we need to iterate the chain to find
            // and replace it. But since this is, well, a hash table, that's actually fine.
            if (elf.targetLoad(&buckets[b]) == dynsym_index) {
                elf.targetStore(&buckets[b], next_dynsym_index);
            } else {
                var cur: usize = @intCast(elf.targetLoad(&buckets[b]));
                while (true) {
                    assert(cur != 0); // `dynsym_index` is definitely somewhere in the chain
                    if (elf.targetLoad(&chains[cur]) == dynsym_index) break;
                    cur = @intCast(elf.targetLoad(&chains[cur]));
                }
                // We found `dynsym_index`; replace it with `next_dynsym_index`.
                elf.targetStore(&chains[cur], next_dynsym_index);
            }
        },
    }
}

/// Given an index into the PLT, returns whether that PLT entry is dead, meaning it may be reused at
/// any time and must not be targeted by relocations. See also the doc comment on `Elf.plt`.
fn pltEntryIsDead(elf: *Elf, plt_index: usize) bool {
    assert(elf.shndx.plt != .UNDEF);
    assert(plt_index <= elf.plt.count());
    // We track which PLT entries are alive based on the relocation entries, since there is a 1-1
    // mapping between PLT entries and `.rela.plt` entries and the relocation entries already have
    // a free-list mechanism.
    switch (elf.shdrPtr(elf.shndx.rela_plt)) {
        inline else => |rela_shdr, class| {
            const size = elf.targetLoad(&rela_shdr.size);
            const relas: []class.ElfN().Rela = @ptrCast(@alignCast(
                elf.shndx.rela_plt.get(elf).ni.slice(&elf.mf)[0..@intCast(size)],
            ));
            const rel_type = elf.targetLoad(&relas[plt_index].info).type;
            return rel_type == MachineRelocType.none(elf).unwrap(elf);
        },
    }
}

const AddLocalSymbolOptions = struct {
    node: MappedFile.Node.Index.Optional,
    name: []const u8,
    value: u64,
    size: u64,
    type: std.elf.STT,
    shndx: Section.Index,
};
fn addLocalSymbol(elf: *Elf, opts: AddLocalSymbolOptions) Error!Symbol.LocalIndex {
    const gpa = elf.base.comp.gpa;

    try elf.symtab.ensureUnusedCapacity(gpa, 1);
    try elf.changed_symtab_index.ensureUnusedCapacity(gpa, 1);
    try Section.Index.symtab.get(elf).ni.ensureMinimumSize(gpa, &elf.mf, switch (elf.shdrPtr(.symtab)) {
        inline else => |shdr, class| elf.targetLoad(&shdr.size) + @sizeOf(class.ElfN().Sym),
    });

    const name = try elf.string(.strtab, opts.name);

    switch (elf.shdrPtr(.symtab)) {
        inline else => |shdr, class| {
            const ent_size = @sizeOf(class.ElfN().Sym);

            // `shdr.info` stores the index of the first global symbol. We will replace it with our
            // new local symbol, and move the global symbol to a new index at the end of the symtab.
            const target_index: Symbol.Index = @fromBackingInt(elf.targetLoad(&shdr.info));

            const old_size = elf.targetLoad(&shdr.size);
            const new_size = old_size + ent_size;

            assert(elf.symtab.items.len == @divExact(old_size, ent_size));

            elf.targetStore(&shdr.info, @backingInt(target_index) + 1);
            elf.targetStore(&shdr.size, new_size);

            const new_index: Symbol.Index = @fromBackingInt(@intCast(elf.symtab.items.len));
            elf.symtab.appendAssumeCapacity(undefined);

            const target_sym = @field(elf.symPtr(target_index), @tagName(class));

            if (target_index != new_index) {
                // Move the global at `target_index` to `new_index`. First the symtab entry...
                const new_sym = @field(elf.symPtr(new_index), @tagName(class));
                new_sym.* = target_sym.*;
                // ...then the `elf.symtab` metadata...
                new_index.ptr(elf).* = target_index.ptr(elf).*;
                // ...then update the `elf.globals` tracking.
                const gsi = elf.globalBySym(new_index);
                gsi.ptr(elf).symtab_index = new_index;

                if (elf.ehdrType() == .REL and target_index.ptr(elf).first_target_reloc != .none) {
                    // This symbol's index is changing, so queue an update of relocs targeting it.
                    elf.changed_symtab_index.putAssumeCapacity(gsi, {});
                }
            }

            target_index.ptr(elf).* = .{
                .node = opts.node,
                .first_target_reloc = .none,
            };

            target_sym.* = .{
                .name = @backingInt(name),
                .value = @intCast(opts.value),
                .size = @intCast(opts.size),
                .info = .{ .type = opts.type, .bind = .LOCAL },
                .other = .{ .visibility = .DEFAULT },
                .shndx = opts.shndx.toSection().?,
            };
            if (elf.targetEndian() != std.lang.Endian.native) {
                std.mem.byteSwapAllFields(class.ElfN().Sym, target_sym);
            }

            return @fromBackingInt(@backingInt(target_index));
        },
    }
}

fn addGlobalSymbol(elf: *Elf, opts: Symbol.Global.AddOptions) (error{
    MultipleDefinitions,
    MultipleDefaultVersions,
    UndefinedDefaultVersion,
} || Error)!Symbol.Global.Index {
    _ = opts.lib_name; // TODO

    const gpa = elf.base.comp.gpa;

    try elf.symtab.ensureUnusedCapacity(gpa, 1);
    try elf.globals.ensureUnusedCapacity(gpa, 1);
    try elf.globals_by_name.ensureUnusedCapacity(gpa, 1);
    try elf.node_global_symbols.ensureUnusedCapacity(gpa, 1);

    try Section.Index.symtab.get(elf).ni.ensureMinimumSize(gpa, &elf.mf, switch (elf.shdrPtr(.symtab)) {
        inline else => |shdr, class| elf.targetLoad(&shdr.size) + @sizeOf(class.ElfN().Sym),
    });

    const name = parseVersionedSymbolName(opts.name);

    if (name.version != null and name.is_default_version) {
        if (opts.shndx == .UNDEF) {
            return error.UndefinedDefaultVersion;
        }
    }

    const gop = elf.globals_by_name.getOrPutAssumeCapacityAdapted(@as(Symbol.Global.VersionedName, .{
        .name = name.name,
        .version = name.version,
    }), @as(Symbol.Global.VersionedName.Adapter, .{ .elf = elf }));
    const gsi: Symbol.Global.Index = @fromBackingInt(@intCast(gop.index));

    if (gop.found_existing) {
        var force_apply_relocs: bool = false;

        if (name.version != null and name.is_default_version) {
            const default_gop = try elf.default_version_globals.getOrPutAdapted(
                elf.base.comp.gpa,
                name.name,
                @as(Symbol.Global.DefaultVersionAdapter, .{ .elf = elf }),
            );
            if (!default_gop.found_existing) {
                // This symbol is becoming a default version.
                default_gop.key_ptr.* = gsi;
                const new_strtab_name = try elf.string(.strtab, opts.name);
                switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
                    inline else => |sym| elf.targetStore(&sym.name, @backingInt(new_strtab_name)),
                }
                force_apply_relocs = true;
            } else if (default_gop.key_ptr.* != gsi) {
                return error.MultipleDefaultVersions;
            }
        }

        if (elf.mergeGlobalSymbolVisibility(gsi, opts.visibility)) {
            force_apply_relocs = true;
        }

        // There's already a symbol with this name, we just need to potentially set its value, and
        // to update its bind and visibility.
        if (opts.shndx == .UNDEF) {
            // We are not defining the symbol, so we won't set its value and we should only
            // update its bind if it is undefined and we won't weaken it.
            if (!gsi.defined(elf) and
                gsi.ptr(elf).status.bind == .weak and
                opts.bind == .strong)
            {
                elf.setGlobalSymbolBind(gsi, .strong);
            }
        } else if (!gsi.defined(elf)) {
            elf.setGlobalSymbolBind(gsi, opts.bind);
            try elf.setGlobalSymbolValue(gsi, .{
                .node = opts.node,
                .value = opts.value,
                .size = opts.size,
                .type = opts.type,
                .shndx = opts.shndx,
            });
            force_apply_relocs = true;
        } else switch (opts.bind) {
            .strong => switch (gsi.ptr(elf).status.bind) {
                .strong => return error.MultipleDefinitions,
                .weak => {
                    elf.setGlobalSymbolBind(gsi, .strong);
                    try elf.setGlobalSymbolValue(gsi, .{
                        .node = opts.node,
                        .value = opts.value,
                        .size = opts.size,
                        .type = opts.type,
                        .shndx = opts.shndx,
                    });
                    force_apply_relocs = true;
                },
            },
            .weak => {},
        }

        try elf.updateGlobalDynamic(gsi, force_apply_relocs);

        return gsi;
    }

    assert(elf.globals.items.len == @backingInt(gsi));
    assert(elf.globals.addOneAssumeCapacity() == gsi.ptr(elf)); // populated later

    const strtab_name = try elf.string(.strtab, opts.name);

    const force_local_bind: bool = switch (opts.visibility) {
        .HIDDEN, .INTERNAL => elf.ehdrType() != .REL,
        .PROTECTED, .DEFAULT => false,
    };

    const bind: std.elf.STB = if (force_local_bind) b: {
        break :b .LOCAL;
    } else switch (opts.bind) {
        .strong => .GLOBAL,
        .weak => .WEAK,
    };

    const sym_index: Symbol.Index = @fromBackingInt(@intCast(elf.symtab.items.len));
    elf.symtab.appendAssumeCapacity(.{
        .node = opts.node,
        .first_target_reloc = .none,
    });
    switch (elf.shdrPtr(.symtab)) {
        inline else => |shdr, class| {
            const Sym = class.ElfN().Sym;
            // Increase the symtab size...
            const old_size = elf.targetLoad(&shdr.size);
            assert(old_size == @backingInt(sym_index) * @sizeOf(Sym));
            elf.targetStore(&shdr.size, old_size + @sizeOf(Sym));
            // ...then populate the newly-valid symbol pointer
            const sym = @field(elf.symPtr(sym_index), @tagName(class));
            sym.* = .{
                .name = @backingInt(strtab_name),
                .value = @intCast(opts.value),
                .size = @intCast(opts.size),
                .info = .{ .type = opts.type, .bind = bind },
                .other = .{ .visibility = opts.visibility },
                .shndx = opts.shndx.toSection().?,
            };
            if (elf.targetEndian() != std.lang.Endian.native) {
                std.mem.byteSwapAllFields(Sym, sym);
            }
        },
    }

    const old_head: Symbol.Global.Index.Optional = old_head: {
        const node = opts.node.unwrap() orelse break :old_head .none;
        const node_globals_gop = elf.node_global_symbols.getOrPutAssumeCapacity(node);
        const old_head: Symbol.Global.Index.Optional = if (node_globals_gop.found_existing)
            .wrap(node_globals_gop.value_ptr.*)
        else
            .none;
        node_globals_gop.value_ptr.* = gsi;
        break :old_head old_head;
    };

    gsi.ptr(elf).* = .{
        .status = .{
            .bind = opts.bind,
            .want_static_value = false,
            .owns_dynsym = false,
            .any_static_target_relocs = false,
            .any_static_relative_target_relocs = false,
            .any_dynamic_target_relocs = false,
        },
        .dynsym_index = 0,
        .symtab_index = sym_index,
        .prev_in_node = .none,
        .next_in_node = old_head,
    };

    if (old_head.unwrap()) |old_head_gsi| {
        const old_head_ptr = old_head_gsi.ptr(elf);
        assert(old_head_ptr.symtab_index.ptr(elf).node == opts.node);
        assert(old_head_ptr.prev_in_node == .none);
        old_head_ptr.prev_in_node = .wrap(gsi);
    }

    if (force_local_bind) {
        elf.moveDemotedGlobal(gsi);
    }

    if (name.version != null and name.is_default_version) {
        const default_gop = try elf.default_version_globals.getOrPutAdapted(
            elf.base.comp.gpa,
            name.name,
            @as(Symbol.Global.DefaultVersionAdapter, .{ .elf = elf }),
        );
        if (!default_gop.found_existing) {
            // This symbol is becoming a default version.
            default_gop.key_ptr.* = gsi;
        } else if (default_gop.key_ptr.* != gsi) {
            return error.MultipleDefaultVersions;
        }
    }

    // We must pass `force_apply_relocs` as `true` here, because even though `gsi` itself was only
    // just created (so there are no relocations targeting it yet), it is possible that another
    // symbol is now an alias of it, in which case that symbol *does* need target relocations to be
    // re-applied.
    try elf.updateGlobalDynamic(gsi, true);

    return gsi;
}

/// Updates the dynsym index associated with the given global symbol; populates that dynsym entry if
/// necessary; removes unnecessary dynamic relocations; creates/removes PLT entries as needed; and
/// creates/removes copy relocations as needed. Put simply, updates all state related to dynamic
/// linking for the given symbol.
///
/// As well as on creation, this function should be called whenever any of the following properties
/// of a global symbol changes:
///
/// * visibility
/// * type
/// * whether a copy relocation is required
/// * whether a PLT entry is required
/// * whether it is defined
/// * whether there is a matching symbol in `dso_globals`
/// * for versioned symbols, whether it is the default symbol version
/// * for unversioned symbols, this symbol's default version in `dso_globals`
///
/// This function may re-apply relocations targeting the global. If the caller has itself performed
/// some operation which requires relocations to be re-applied, it can pass `force_apply_relocs` as
/// `true` to guarantee this function to do so.
fn updateGlobalDynamic(elf: *Elf, orig_gsi: Symbol.Global.Index, force_apply_relocs: bool) Error!void {
    const gpa = elf.base.comp.gpa;

    const gsi = orig_gsi.resolveAlias(elf);

    const maybe_alias_gsi = gsi.findAliaser(elf);

    var apply_relocs: bool = force_apply_relocs;

    _ = elf.defined_alias_globals.swapRemove(gsi); // not an alias

    if (maybe_alias_gsi) |alias_gsi| {
        if (gsi.defined(elf) and
            alias_gsi.defined(elf) and
            gsi.ptr(elf).status.bind == .strong and
            alias_gsi.ptr(elf).status.bind == .strong)
        {
            // `gsi` and `alias_gsi` both have strong definitions. For now we've arbitrarily decided
            // that `gsi`'s definition wins, but this needs to cause a "multiple definitions" error!
            try elf.defined_alias_globals.put(gpa, alias_gsi, {});
        } else {
            _ = elf.defined_alias_globals.swapRemove(alias_gsi);
        }

        _ = elf.unknown_globals.swapRemove(alias_gsi);
        if (try elf.deleteOwnedDynsym(alias_gsi)) {
            apply_relocs = true;
        }
    }

    const maybe_dso_global: ?*const DsoGlobals.Symbol = dso_global: {
        if (gsi.defined(elf)) {
            break :dso_global null;
        }
        if (elf.dso_globals.find(gsi.name(elf), gsi.version(elf))) |dso_global| {
            break :dso_global dso_global;
        }
        if (gsi.version(elf) == null) {
            if (elf.dso_globals.findDefaultVersion(gsi.name(elf))) |dso_global| {
                break :dso_global dso_global;
            }
        }
        break :dso_global null;
    };

    const visibility: std.elf.STV, const sym_type: std.elf.STT = switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
        inline else => |sym| info: {
            // If the symbol is undefined with type `STT_NOTYPE`, but there is a type available in
            // an input DSO, then use that symbol type!
            if (maybe_dso_global) |dso_global| {
                if (elf.targetLoad(&sym.shndx) == std.elf.SHN_UNDEF and
                    elf.targetLoad(&sym.info).type == .NOTYPE and
                    dso_global.type != .NOTYPE)
                {
                    elf.targetStore(&sym.info, .{
                        .type = dso_global.type,
                        .bind = elf.targetLoad(&sym.info).bind,
                    });
                }
            }

            break :info .{
                elf.targetLoad(&sym.other).visibility,
                elf.targetLoad(&sym.info).type,
            };
        },
    };

    const omit_dynsym: bool = omit: {
        if (elf.shndx.dynsym == .UNDEF) break :omit true;
        break :omit switch (visibility) {
            .HIDDEN, .INTERNAL => true,
            .DEFAULT, .PROTECTED => false,
        };
    };
    if (omit_dynsym) {
        if (try elf.deleteOwnedDynsym(gsi)) {
            apply_relocs = true;
        }
        if (gsi.ptr(elf).dynsym_index != 0) {
            gsi.ptr(elf).dynsym_index = 0;
            try elf.changed_symtab_index.put(gpa, gsi, {});
        }
        if (maybe_alias_gsi) |alias_gsi| {
            if (alias_gsi.ptr(elf).dynsym_index != 0) {
                alias_gsi.ptr(elf).dynsym_index = 0;
                try elf.changed_symtab_index.put(gpa, alias_gsi, {});
            }
        }

        if (!gsi.defined(elf) and
            gsi.ptr(elf).status.bind == .strong and
            maybe_dso_global == null)
        {
            try elf.unknown_globals.put(gpa, gsi, {});
        } else {
            _ = elf.unknown_globals.swapRemove(gsi);
        }

        if (elf.ehdrType() != .REL) {
            assert(elf.classifySymbolValue(.global(gsi)) != .dynamic);
        }
        gsi.deleteDynamicTargetRelocs(elf);
        if (maybe_alias_gsi) |alias_gsi| {
            alias_gsi.deleteDynamicTargetRelocs(elf);
        }

        if (apply_relocs) {
            Symbol.Id.global(gsi).applyTargetRelocs(elf);
            if (maybe_alias_gsi) |alias_gsi| {
                Symbol.Id.global(alias_gsi).applyTargetRelocs(elf);
            }
        }
        return;
    }

    // We need an owned dynsym entry. We can either reuse an existing one or create a new one.
    const new_dynsym_index: u32 = owned_dynsym_index: {
        if (gsi.ptr(elf).status.owns_dynsym) {
            break :owned_dynsym_index gsi.ptr(elf).dynsym_index;
        }

        // We're going to add a new entry to dynsym; ensure there's space in the relevant sections.
        const dynsym_size: u64, const dynsym_ent_size: u32 = switch (elf.shdrPtr(elf.shndx.dynsym)) {
            inline else => |shdr, class| .{
                elf.targetLoad(&shdr.size),
                @sizeOf(class.ElfN().Sym),
            },
        };
        const need_dynsym_len: u32 = @intCast(@divExact(dynsym_size, dynsym_ent_size) + 1);

        try elf.shndx.dynsym.get(elf).ni.ensureMinimumSize(gpa, &elf.mf, need_dynsym_len * dynsym_ent_size);
        try elf.shndx.gnu_version.get(elf).ni.ensureMinimumSize(gpa, &elf.mf, need_dynsym_len * 2);
        try elf.ensureDynsymHashCapacity(need_dynsym_len);

        const dynstr_name: String(.dynstr) = name: {
            // Explicitly reserve space first because that reservation could invalidate the name slice.
            try elf.ensureAdditionalStringCapacity(.dynstr, gsi.name(elf).len);
            break :name elf.stringAssumeCapacity(.dynstr, gsi.name(elf));
        };

        const dynsym_index: u32 = switch (elf.shdrPtr(elf.shndx.dynsym)) {
            inline else => |shdr, class| new_dynsym_index: {
                const Sym = class.ElfN().Sym;

                // Increase the dynamic symbol table size...
                const old_size = elf.targetLoad(&shdr.size);
                elf.targetStore(&shdr.size, old_size + @sizeOf(Sym));
                const dynsym_index: u32 = @intCast(@divExact(old_size, @sizeOf(Sym)));

                // ...and the same for the symbol version table.
                const versym_shdr = @field(elf.shdrPtr(elf.shndx.gnu_version), @tagName(class));
                assert(elf.targetLoad(&versym_shdr.size) == dynsym_index * 2);
                elf.targetStore(&versym_shdr.size, (dynsym_index + 1) * 2);

                // Populate the symbol name (the other fields are set later).
                const sym = @field(elf.dynsymPtr(dynsym_index), @tagName(class));
                elf.targetStore(&sym.name, @backingInt(dynstr_name));

                break :new_dynsym_index dynsym_index;
            },
        };

        elf.appendDynsymHashEntry(dynsym_index);

        if (gsi.version(elf) != null) {
            try elf.versioned_dynsym_owners.putNoClobber(gpa, dynsym_index, gsi);
        }

        break :owned_dynsym_index dynsym_index;
    };

    gsi.ptr(elf).status.owns_dynsym = true;

    if (gsi.ptr(elf).dynsym_index != new_dynsym_index) {
        gsi.ptr(elf).dynsym_index = new_dynsym_index;
        try elf.changed_symtab_index.put(gpa, gsi, {});
    }
    if (maybe_alias_gsi) |alias_gsi| {
        if (alias_gsi.ptr(elf).dynsym_index != new_dynsym_index) {
            alias_gsi.ptr(elf).dynsym_index = new_dynsym_index;
            try elf.changed_symtab_index.put(gpa, alias_gsi, {});
        }
    }

    {
        const dynsym_versym: std.elf.Versym = versym: {
            // If there's a definition, just use the version in the symbol name.
            if (gsi.defined(elf)) {
                if (gsi.version(elf)) |orig_version_slice| {
                    // We want to add the version to dynstr, but we need to explicitly reserve space
                    // first because that reservation could invalidate the version slice.
                    try elf.ensureAdditionalStringCapacity(.dynstr, orig_version_slice.len);
                    const version_dynstr = elf.stringAssumeCapacity(.dynstr, gsi.version(elf).?);
                    break :versym .{
                        .VERSION = try elf.verdefId(version_dynstr),
                        .HIDDEN = !gsi.isDefaultDefinition(elf),
                    };
                }
                break :versym .GLOBAL;
            }

            // ...but if it's undefined, we need to consult the matching input DSO global.
            if (maybe_dso_global) |dso_global| {
                if (dso_global.version.unwrap()) |version| {
                    const version_dynstr = try elf.string(.dynstr, version.slice(&elf.dso_globals));
                    break :versym .{
                        .VERSION = try elf.verneedId(dso_global.soname, version_dynstr),
                        .HIDDEN = false,
                    };
                }
            }

            break :versym .LOCAL;
        };

        elf.targetStore(&elf.versymSlice()[new_dynsym_index], dynsym_versym);
    }

    // Populate the dynsym info from the symtab.
    switch (elf.dynsymPtr(new_dynsym_index)) {
        inline else => |dynsym, class| {
            const sym = @field(elf.symPtr(gsi.ptr(elf).symtab_index), @tagName(class));
            // No byte swaps needed here because src values are already in target endian.
            dynsym.* = .{
                .name = dynsym.name,
                .value = sym.value,
                .size = sym.size,
                .info = sym.info,
                .other = sym.other,
                .shndx = sym.shndx,
            };
        },
    }

    const need_plt_entry: bool = plt: {
        switch (sym_type) {
            .FUNC, .GNU_IFUNC => {},
            else => {
                // Only functions can have PLT entries.
                break :plt false;
            },
        }
        if (visibility == .PROTECTED) {
            // No need for a PLT entry, since any definition is guaranteed to be defined locally.
            break :plt false;
        }
        if (gsi.defined(elf) and elf.base.comp.config.output_mode == .Exe) {
            // No need for a PLT entry, since there's already a non-preemptible local definition.
            break :plt false;
        }
        break :plt true;
    };
    if (need_plt_entry) {
        if (try elf.ensurePltEntry(gsi)) {
            apply_relocs = true;
        }
    } else {
        if (elf.plt.getIndex(gsi)) |plt_index| {
            if (!elf.pltEntryIsDead(plt_index)) {
                elf.shndx.rela_plt.relaDeleteOne(elf, @fromBackingInt(@intCast(plt_index)));
                assert(elf.pltEntryIsDead(plt_index));
                apply_relocs = true;
            }
        }
    }

    const want_copy_rel: bool = copy: {
        if (elf.base.comp.config.output_mode != .Exe) {
            // Only executables can contain copy relocations.
            break :copy false;
        }
        if (gsi.defined(elf)) {
            // No need for a copy relocation when there's a local definition (which we know is not
            // preemptible because this is the executable).
            break :copy false;
        }
        if (!gsi.ptr(elf).status.want_static_value) {
            // Nobody's actually requesting a copy relocation, so no need to make one... unless the
            // alias is actually the one who needs it!
            const alias_gsi = maybe_alias_gsi orelse break :copy false;
            if (!alias_gsi.ptr(elf).status.want_static_value) break :copy false;
        }
        const dso_global = maybe_dso_global orelse {
            // We cannot create a copy relocation until we see the symbol in an input DSO.
            break :copy false;
        };
        if (dso_global.type != .OBJECT) {
            // We can only create copy relocations for `STT_OBJECT` symbols.
            break :copy false;
        }
        // A copy relocation was requested and is possible!
        break :copy true;
    };
    if (want_copy_rel) {
        const dso_global = maybe_dso_global.?;

        const shndx: Section.Index = .data;

        try shndx.ensureAligned(elf, dso_global.alignment);

        switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
            inline else => |sym| elf.targetStore(&sym.size, @intCast(dso_global.size)),
        }
        switch (elf.dynsymPtr(new_dynsym_index)) {
            inline else => |dynsym| elf.targetStore(&dynsym.size, @intCast(dso_global.size)),
        }

        const gop = try elf.copied_globals.getOrPut(gpa, gsi);
        if (gop.found_existing) {
            if (dso_global.alignment.compare(.gt, gop.value_ptr.node.alignment(&elf.mf))) {
                try gop.value_ptr.node.realign(gpa, &elf.mf, dso_global.alignment);
            }
            try gop.value_ptr.node.ensureMinimumSize(gpa, &elf.mf, dso_global.size);
        } else {
            errdefer assert(elf.copied_globals.pop().?.key == gsi);
            try elf.nodes.ensureUnusedCapacity(gpa, 1);
            const node = elf.addNodeAssumeCapacity(
                try shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{
                    .size = dso_global.alignment.forward(dso_global.size),
                    .alignment = dso_global.alignment,
                }),
                .{ .copied_global = gsi },
            );
            const vaddr = elf.computeNodeVAddr(node);
            const rela_index = elf.shndx.rela_dyn.relaAddOneAssumeCapacity(elf, .{
                .type = .copy(elf),
                .offset = vaddr,
                .raw_sym_index = new_dynsym_index,
                .addend = 0,
            });
            gop.value_ptr.* = .{
                .node = node,
                .rela_index = rela_index,
            };

            // We have added a copy relocation so we now own the canonical address of the symbol.
            switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
                inline else => |sym| elf.targetStore(&sym.value, @intCast(vaddr)),
            }
            switch (elf.dynsymPtr(new_dynsym_index)) {
                inline else => |sym| elf.targetStore(&sym.value, @intCast(vaddr)),
            }
            apply_relocs = true;
        }
    } else {
        if (elf.copied_globals.fetchSwapRemove(gsi)) |copied_global_kv| {
            // We previously made a copy relocation, but now have deemed it unnecessary.
            elf.shndx.rela_dyn.relaDeleteOne(elf, copied_global_kv.value.rela_index);
            // TODO: once `MappedFile` has a way to delete a node (so it can re-use the
            // space), we should delete `copied_global_kv.value.node`, which is an
            // "orphaned" `copied_global` node.
            apply_relocs = true;
        }
    }

    if (!gsi.defined(elf) and
        gsi.ptr(elf).status.bind == .strong and
        maybe_dso_global == null)
    {
        try elf.unknown_globals.put(gpa, gsi, {});
    } else {
        _ = elf.unknown_globals.swapRemove(gsi);
    }

    if (elf.classifySymbolValue(.global(gsi)) != .dynamic) {
        gsi.deleteDynamicTargetRelocs(elf);
        if (maybe_alias_gsi) |alias_gsi| {
            alias_gsi.deleteDynamicTargetRelocs(elf);
        }
    }

    if (apply_relocs) {
        Symbol.Id.global(gsi).applyTargetRelocs(elf);
        if (maybe_alias_gsi) |alias_gsi| {
            Symbol.Id.global(alias_gsi).applyTargetRelocs(elf);
        }
    }
}
/// Every call to this function *must* be followed by a call to `updateGlobalDynamic` with the
/// `force_apply_relocs` argument set to `true`. This ensures that the dynamic symbol table is kept
/// up-to-date, unnecessary PLT entries or copy relocations are removed, and that relocations are
/// applied when necessary.
fn setGlobalSymbolValue(elf: *Elf, gsi: Symbol.Global.Index, new: struct {
    node: MappedFile.Node.Index.Optional,
    value: u64,
    size: u64,
    type: std.elf.STT,
    shndx: Section.Index,
}) Error!void {
    assert(new.shndx != .UNDEF);

    const global_ptr = gsi.ptr(elf);

    if (global_ptr.symtab_index.ptr(elf).node.unwrap()) |old_node| {
        if (global_ptr.next_in_node.unwrap()) |next_gsi| {
            assert(next_gsi.ptr(elf).prev_in_node == Symbol.Global.Index.Optional.wrap(gsi));
            next_gsi.ptr(elf).prev_in_node = global_ptr.prev_in_node;
        }
        if (global_ptr.prev_in_node.unwrap()) |prev_gsi| {
            assert(prev_gsi.ptr(elf).next_in_node == Symbol.Global.Index.Optional.wrap(gsi));
            prev_gsi.ptr(elf).next_in_node = global_ptr.next_in_node;
        } else {
            // We're the start of the linked list, so we need to change the head.
            if (global_ptr.next_in_node.unwrap()) |next_gsi| {
                elf.node_global_symbols.getPtr(old_node).?.* = next_gsi;
            } else {
                assert(elf.node_global_symbols.fetchSwapRemove(old_node).?.value == gsi);
            }
        }
    } else {
        assert(global_ptr.next_in_node == .none);
        assert(global_ptr.prev_in_node == .none);
    }

    global_ptr.symtab_index.ptr(elf).node = new.node;

    const old_head: Symbol.Global.Index.Optional = old_head: {
        const new_node = new.node.unwrap() orelse break :old_head .none;
        const gop = elf.node_global_symbols.getOrPutAssumeCapacity(new_node);
        const old_head: Symbol.Global.Index.Optional = if (gop.found_existing) .wrap(gop.value_ptr.*) else .none;
        gop.value_ptr.* = gsi;
        break :old_head old_head;
    };

    global_ptr.prev_in_node = .none;
    global_ptr.next_in_node = old_head;

    if (old_head.unwrap()) |old_head_gsi| {
        assert(old_head_gsi.ptr(elf).prev_in_node == .none);
        old_head_gsi.ptr(elf).prev_in_node = .wrap(gsi);
    }

    // Now for the easy bit where we actually update the symtab entry.
    switch (elf.symPtr(global_ptr.symtab_index)) {
        inline else => |sym| {
            elf.targetStore(&sym.value, @intCast(new.value));
            elf.targetStore(&sym.size, @intCast(new.size));
            elf.targetStore(&sym.shndx, new.shndx.toSection().?);
            const old_bind = elf.targetLoad(&sym.info).bind;
            elf.targetStore(&sym.info, .{
                .type = new.type,
                .bind = old_bind,
            });
        },
    }
}

fn setGlobalSymbolBind(
    elf: *Elf,
    gsi: Symbol.Global.Index,
    bind: Symbol.Global.Bind,
) void {
    gsi.ptr(elf).status.bind = bind;
    switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
        inline else => |sym| {
            const old_info = elf.targetLoad(&sym.info);
            elf.targetStore(&sym.info, .{
                .type = old_info.type,
                .bind = switch (old_info.bind) {
                    .LOCAL => .LOCAL, // demoted global
                    else => switch (bind) {
                        .strong => .GLOBAL,
                        .weak => .WEAK,
                    },
                },
            });
        },
    }
}

/// When the same global symbol appears in two inputs---even if one symbol is defined and the other
/// undefined---their visibility values are combined to determine the resulting visibility.
///
/// Every call to this function *must* be followed by a call to `updateGlobalDynamic` since changes
/// to symbol visibility can affect the dynamic symbol table entry. If this function returns `true`,
/// then that `updateGlobalDynamic` call must have the `force_apply_relocs` argument set to `true`,
/// because this symbol's classification has changed (it is no longer a dynamic symbol) so
/// relocations targeting the symbol must be re-applied.
fn mergeGlobalSymbolVisibility(
    elf: *Elf,
    gsi: Symbol.Global.Index,
    other_visibility: std.elf.STV,
) bool {
    const old_visibility: std.elf.STV = switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
        inline else => |sym| elf.targetLoad(&sym.other).visibility,
    };

    // The new visibility is basically whichever of the old and new is "stricter", with the most
    // strict being INTERNAL, followed by HIDDEN, PROTECTED, DEFAULT.
    const new_visibility: std.elf.STV = switch (old_visibility) {
        .INTERNAL => .INTERNAL,
        .HIDDEN => switch (other_visibility) {
            .INTERNAL => .INTERNAL,
            .HIDDEN, .PROTECTED, .DEFAULT => .HIDDEN,
        },
        .PROTECTED => switch (other_visibility) {
            .INTERNAL => .INTERNAL,
            .HIDDEN => .HIDDEN,
            .PROTECTED, .DEFAULT => .PROTECTED,
        },
        .DEFAULT => switch (other_visibility) {
            .INTERNAL => .INTERNAL,
            .HIDDEN => .HIDDEN,
            .PROTECTED => .PROTECTED,
            .DEFAULT => .DEFAULT,
        },
    };
    switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
        inline else => |sym| elf.targetStore(
            &sym.other,
            .{ .visibility = new_visibility },
        ),
    }

    if (elf.ehdrType() == .REL) {
        return false;
    }

    // We also need to deal with a concept I call "symbol demotion". If a symbol is `STV_HIDDEN` or
    // `STV_INTERNAL`, and we're emitting an ELF module (executable or shared object, as opposed to
    // a relocatable), then the symbol should be "demoted" to binding `STB_LOCAL` in the output.
    const old_demoted: bool = switch (old_visibility) {
        .INTERNAL, .HIDDEN => true,
        .DEFAULT, .PROTECTED => false,
    };
    const new_demoted: bool = switch (new_visibility) {
        .INTERNAL, .HIDDEN => true,
        .DEFAULT, .PROTECTED => false,
    };
    if (!old_demoted and new_demoted) {
        switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
            inline else => |sym| elf.targetStore(&sym.info, .{
                .type = elf.targetLoad(&sym.info).type,
                .bind = .LOCAL,
            }),
        }
        elf.moveDemotedGlobal(gsi);
    }

    const old_protected: bool = switch (old_visibility) {
        .PROTECTED, .INTERNAL, .HIDDEN => true,
        .DEFAULT => false,
    };
    const new_protected: bool = switch (new_visibility) {
        .PROTECTED, .INTERNAL, .HIDDEN => true,
        .DEFAULT => false,
    };
    return old_protected != new_protected;
}
/// If a symbol which was STB_GLOBAL/STB_WEAK becomes STB_LOCAL (see `mergeGlobalSymbolVisibility`),
/// the symbol must be moved from the "globals" part of the symtab to the "locals" part, because ELF
/// requires that all STB_LOCAL symbols in a symbol table appear before any global symbols.
fn moveDemotedGlobal(elf: *Elf, gsi: Symbol.Global.Index) void {
    assert(elf.ehdrType() != .REL); // demotion only happens when emitting an ELF module
    const global_ptr = gsi.ptr(elf);
    switch (elf.shdrPtr(.symtab)) {
        inline else => |shdr, class| {
            // `shdr.info` stores the index of the first global symbol. We are going to swap the
            // demoted symbol with that first global symbol, then increment that start index.
            const dest_index: Symbol.Index = @fromBackingInt(elf.targetLoad(&shdr.info));
            const src_index = global_ptr.symtab_index;

            // This global should currently be in the "global symbols" part of the symtab, since our
            // job is to move it *out* of that part:
            assert(@backingInt(src_index) >= @backingInt(dest_index));

            elf.targetStore(&shdr.info, @backingInt(dest_index) + 1);

            if (src_index != dest_index) {
                // The demoted global was not the first global in the symtab, so we need to swap it
                // to its new location.

                const src_sym_ptr = @field(elf.symPtr(src_index), @tagName(class));
                const dest_sym_ptr = @field(elf.symPtr(dest_index), @tagName(class));

                const other_gsi = elf.globalBySym(dest_index);
                assert(other_gsi.ptr(elf).symtab_index == dest_index);

                // First swap the symtab entries...
                std.mem.swap(class.ElfN().Sym, src_sym_ptr, dest_sym_ptr);
                // ...then the `elf.symtab` metadata...
                std.mem.swap(Symbol, src_index.ptr(elf), dest_index.ptr(elf));
                // ...then update the `elf.globals` tracking.
                global_ptr.symtab_index = dest_index;
                other_gsi.ptr(elf).symtab_index = src_index;
            }
        },
    }
}
/// If `gsi` owns a dynsym entry, deletes that entry from the `.dynsym` section and marks `gsi` as
/// no longer owning it. All state depending on the dynamic symbol is also deleted. To keep the
/// dynamic symbol table compact, the last entry in the table is swapped into the place of the
/// removed one (all associated bookkeeping is handled by this function).
///
/// Returns `true` if a PLT entry or copy relocation for the global symbol was removed, indicating
/// that the caller must at some point re-apply relocations targeting `gsi`.
fn deleteOwnedDynsym(elf: *Elf, gsi: Symbol.Global.Index) std.mem.Allocator.Error!bool {
    if (!gsi.ptr(elf).status.owns_dynsym) {
        return false;
    }

    const gpa = elf.base.comp.gpa;

    const dynsym_index = gsi.ptr(elf).dynsym_index;
    assert(dynsym_index != 0);
    gsi.ptr(elf).status.owns_dynsym = false;

    var apply_relocs: bool = false;
    if (elf.copied_globals.fetchSwapRemove(gsi)) |copied_global_kv| {
        elf.shndx.rela_dyn.relaDeleteOne(elf, copied_global_kv.value.rela_index);
        // TODO: once `MappedFile` has a way to delete a node (so it can re-use the
        // space), we should delete `copied_global_kv.value.node`, which is an
        // "orphaned" `copied_global` node.
        apply_relocs = true;
    }
    if (elf.plt.getIndex(gsi)) |plt_index| {
        if (!elf.pltEntryIsDead(plt_index)) {
            elf.shndx.rela_plt.relaDeleteOne(elf, @fromBackingInt(@intCast(plt_index)));
            assert(elf.pltEntryIsDead(plt_index));
            apply_relocs = true;
        }
    }

    switch (elf.shdrPtr(elf.shndx.dynsym)) {
        inline else => |dynsym_shdr, class| {
            const ent_size = @sizeOf(class.ElfN().Sym);
            assert(elf.targetLoad(&dynsym_shdr.entsize) == ent_size);

            // We're going to decrease the size of `.dynsym`, thereby removing its last index.
            const old_size = elf.targetLoad(&dynsym_shdr.size);
            const new_size = old_size - ent_size;
            const pop_dynsym_index: u32 = @intCast(@divExact(new_size, ent_size));

            elf.popDynsymHashEntry(pop_dynsym_index);

            _ = elf.versioned_dynsym_owners.swapRemove(dynsym_index);

            if (dynsym_index != pop_dynsym_index) {
                // The demoted global wasn't the last entry, so move whatever entry we just
                // truncated out of dynsym into its place.

                const moved_gsi = elf.globalByDynsym(pop_dynsym_index);

                elf.clearDynsymHashEntry(dynsym_index);

                const src_dynsym_ptr = @field(elf.dynsymPtr(pop_dynsym_index), @tagName(class));
                const dest_dynsym_ptr = @field(elf.dynsymPtr(dynsym_index), @tagName(class));

                const versym = elf.versymSlice();

                dest_dynsym_ptr.* = src_dynsym_ptr.*;
                versym[dynsym_index] = versym[pop_dynsym_index];

                moved_gsi.ptr(elf).dynsym_index = @intCast(dynsym_index);

                elf.populateDynsymHashEntry(dynsym_index);

                if (elf.versioned_dynsym_owners.getIndex(pop_dynsym_index)) |map_index| {
                    elf.versioned_dynsym_owners.setKey(map_index, dynsym_index);
                }

                // Since that symbol's dynsym index has changed, we'll have to update any
                // relocation entries targeting it.
                try elf.changed_symtab_index.put(gpa, moved_gsi, {});

                // If `moved_gsi` is a versioned symbol, then it may have an unversioned
                // alias with the same dynsym index.
                if (moved_gsi.version(elf) != null) {
                    if (elf.globalByName(.{
                        .name = moved_gsi.name(elf),
                        .version = null,
                    })) |unversioned_gsi| {
                        if (unversioned_gsi.ptr(elf).dynsym_index == pop_dynsym_index) {
                            assert(!unversioned_gsi.ptr(elf).status.owns_dynsym);
                            unversioned_gsi.ptr(elf).dynsym_index = @intCast(dynsym_index);
                            try elf.changed_symtab_index.put(gpa, unversioned_gsi, {});
                        }
                    }
                }
            }

            // Now that we've given that symbol a new home, actually decrease the section size.
            elf.targetStore(&dynsym_shdr.size, new_size);

            const versym_shdr = @field(elf.shdrPtr(elf.shndx.gnu_version), @tagName(class));
            assert(elf.targetLoad(&versym_shdr.size) == (pop_dynsym_index + 1) * 2);
            elf.targetStore(&versym_shdr.size, pop_dynsym_index * 2);
        },
    }

    return apply_relocs;
}

const Symbol = struct {
    /// The node which this symbol's value is defined relative to. Possible values are:
    /// * `.none` for a SHN_ABS or SHN_UNDEF symbol
    /// * A section (the symbol's value is some vaddr in that section)
    /// * An input section (the symbol's value is some vaddr in that input section)
    /// * A NAV, UAV, or lazy code/data (the symbol's value is exactly the vaddr of that node)
    node: MappedFile.Node.Index.Optional,

    /// The head of a linked list of relocations targeting this symbol.
    first_target_reloc: SymbolReloc.Index,

    const Global = struct {
        status: packed struct(u8) {
            /// We cannot reliably recover this information from the symbol table, because symbols
            /// with visibility `STV_HIDDEN` are "demoted" to `STB_LOCAL` binding, yet we must still
            /// consider them strong or weak for the purposes of the link.
            bind: Symbol.Global.Bind,
            /// Initially set to `false`. If a relocation is added which targets this symbol and
            /// wants it to have a statically-known value, this flag is set to signal to
            /// `updateGlobalDynamic` that it may need to create a copy relocation.
            want_static_value: bool,
            /// Every entry in `.dynsym` is "owned" by exactly one `Global`. If this flag is set,
            /// then `Global.dynsym_index` is non-zero and refers to a dynsym entry owned by this
            /// global. Otherwise, if `Global.dynsym_index` is non-zero, this `Global` is an "alias"
            /// of a default version of this symbol (e.g. 'foo' is an alias of 'foo@@version') and
            /// `Global.dynsym_index` refers to that symbol's dynsym entry
            owns_dynsym: bool,
            any_static_target_relocs: bool,
            any_static_relative_target_relocs: bool,
            any_dynamic_target_relocs: bool,
            _: u2 = 0,
        },

        dynsym_index: u32,

        /// The current index of the symtab entry for this global symbol.
        symtab_index: Symbol.Index,

        /// The next entry in a linked list of global symbols with the same `Symbol.node` value.
        next_in_node: Symbol.Global.Index.Optional,
        /// The previous entry in a linked list of global symbols with the same `Symbol.node` value.
        prev_in_node: Symbol.Global.Index.Optional,

        /// The logical key of `Elf.globals_by_name`. Although versioned symbol names are stored in
        /// the form "foo@version" or "foo@@version" in the symbol table, it is generally convenient
        /// to consider the name and version as separate values.
        const VersionedName = struct {
            name: []const u8,
            version: ?[]const u8,

            const Adapter = struct {
                elf: *Elf,
                pub fn eql(ctx: Adapter, lhs_key: VersionedName, _: void, rhs_index: usize) bool {
                    const elf = ctx.elf;
                    const rhs_gsi: Symbol.Global.Index = @fromBackingInt(@intCast(rhs_index));

                    if (!std.mem.eql(u8, lhs_key.name, rhs_gsi.name(elf))) return false;

                    if (lhs_key.version) |lhs_version| {
                        const rhs_version = rhs_gsi.version(elf) orelse return false;
                        return std.mem.eql(u8, lhs_version, rhs_version);
                    } else {
                        return rhs_gsi.version(elf) == null;
                    }
                }
                pub fn hash(ctx: Adapter, key: VersionedName) u32 {
                    _ = ctx;
                    var h: std.hash.Wyhash = .init(0);
                    h.update(key.name);
                    if (key.version) |version| {
                        h.update("@");
                        h.update(version);
                    }
                    return @truncate(h.final());
                }
            };
        };

        const DefaultVersionAdapter = struct {
            elf: *Elf,
            pub fn eql(ctx: DefaultVersionAdapter, lhs_name: []const u8, rhs_gsi: Symbol.Global.Index, _: usize) bool {
                const elf = ctx.elf;
                return std.mem.eql(u8, lhs_name, rhs_gsi.name(elf));
            }
            pub fn hash(ctx: DefaultVersionAdapter, name: []const u8) u32 {
                _ = ctx;
                return std.array_hash_map.hashString(name);
            }
        };

        const Index = enum(u32) {
            _,

            fn ptr(gsi: Symbol.Global.Index, elf: *Elf) *Global {
                return &elf.globals.items[@backingInt(gsi)];
            }

            /// Does not traverse aliases: if 'foo' is an alias for 'foo@@version', then `false`
            /// will be returned for 'foo', even thought 'foo@@version' is defined.
            fn defined(gsi: Symbol.Global.Index, elf: *Elf) bool {
                return switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
                    inline else => |sym| elf.targetLoad(&sym.shndx) != std.elf.SHN_UNDEF,
                };
            }

            fn rawName(gsi: Symbol.Global.Index, elf: *Elf) String(.strtab) {
                return switch (elf.symPtr(gsi.ptr(elf).symtab_index)) {
                    inline else => |sym| @fromBackingInt(elf.targetLoad(&sym.name)),
                };
            }
            fn name(gsi: Symbol.Global.Index, elf: *Elf) []const u8 {
                return parseVersionedSymbolName(gsi.rawName(elf).slice(elf)).name;
            }
            fn version(gsi: Symbol.Global.Index, elf: *Elf) ?[]const u8 {
                return parseVersionedSymbolName(gsi.rawName(elf).slice(elf)).version;
            }
            fn isDefaultDefinition(gsi: Symbol.Global.Index, elf: *Elf) bool {
                return parseVersionedSymbolName(gsi.rawName(elf).slice(elf)).is_default_version;
            }

            fn dynsymIndex(gsi: Symbol.Global.Index, elf: *Elf) ?u32 {
                const dynsym_index = gsi.ptr(elf).dynsym_index;
                if (dynsym_index == 0) {
                    return null;
                } else {
                    return dynsym_index;
                }
            }

            /// One global symbol may be an "alias" of another, which means that the global's value
            /// is defined to be equal to the value of the other. The original global's symbol table
            /// entry is effectively ignored while it is acting as an alias, and it will not own an
            /// entry in the dynamic symbol table.
            ///
            /// This function will return the symbol for which `gsi` is an alias. If `gsi` is not an
            /// alias, then `gsi` itself is returned.
            ///
            /// At the time of writing, there is only one situation in which one global may alias
            /// another: if an unversioned symbol "foo" is undefined, and a default-versioned symbol
            /// "foo@@version" is defined, then "foo" is considered an alias for "foo@@version". We
            /// cannot simply combine the globals, because it is possible that a future incremental
            /// update would cause the globals to become separate again (e.g. due to a definition of
            /// "foo@@version" being replaced with a definition for "foo@version", which is the same
            /// symbol but no longer acting as a default version).
            ///
            /// See also `findAliaser`, which maps in the opposite direction, i.e. given a global,
            /// returns all globals which alias it.
            fn resolveAlias(gsi: Symbol.Global.Index, elf: *Elf) Symbol.Global.Index {
                if (elf.ehdrType() == .REL) {
                    // It's too early to combine 'foo@@v' with 'foo'---let the final static linker
                    // invocation do that.
                    return gsi;
                }

                if (gsi.version(elf) != null) {
                    // An explicitly versioned symbol is not overriden by a default version.
                    return gsi;
                }

                const default_gsi: Symbol.Global.Index = default_gsi: {
                    if (elf.default_version_globals.getIndexAdapted(
                        gsi.name(elf),
                        @as(Symbol.Global.DefaultVersionAdapter, .{ .elf = elf }),
                    )) |index| {
                        break :default_gsi elf.default_version_globals.keys()[index];
                    }
                    if (elf.dso_globals.findDefaultVersion(gsi.name(elf))) |dso_global| {
                        if (elf.globalByName(.{
                            .name = gsi.name(elf),
                            .version = dso_global.version.unwrap().?.slice(&elf.dso_globals),
                        })) |default_gsi| {
                            break :default_gsi default_gsi;
                        }
                    }
                    return gsi; // no default symbol version exists
                };

                if (!gsi.defined(elf)) {
                    // Always use the default version if the unversioned symbol isn't defined.
                    return default_gsi;
                } else if (!default_gsi.defined(elf)) {
                    // If the unverisoned symbol is defined and the default version is undefined,
                    // then ignore the default version: local definitions always take priority.
                    return gsi;
                }

                // `gsi` and `default_gsi` are both defined: the winner depends on the symbol binds.
                // If both are strong, there will be an error, so the answer doesn't matter. In that
                // case, we arbitrarily choose to consider the versioned symbol the winner. This is
                // useful because it makes it easier for `updateGlobalDynamic` to detect this case
                // and track it in `elf.defined_alias_globals`.
                return switch (gsi.ptr(elf).status.bind) {
                    .strong => switch (default_gsi.ptr(elf).status.bind) {
                        .strong => default_gsi,
                        .weak => gsi,
                    },
                    .weak => default_gsi,
                };
            }

            /// Effectively the opposite of `resolveAlias`; given a global symbol, returns the index
            /// of the single other global which aliases it, if any. The signature of this function
            /// implies that each global may have at most one global which is an alias for it.
            fn findAliaser(gsi: Symbol.Global.Index, elf: *Elf) ?Symbol.Global.Index {
                if (gsi.version(elf) == null) {
                    return null;
                }
                const unversioned_gsi = elf.globalByName(.{
                    .name = gsi.name(elf),
                    .version = null,
                }) orelse {
                    return null;
                };
                assert(unversioned_gsi != gsi);
                if (unversioned_gsi.resolveAlias(elf) == gsi) {
                    return unversioned_gsi;
                }
                return null;
            }

            /// Like `dynsymIndex`, except also returns `null` if `gsi` is the unversioned alias for
            /// a default symbol version. This means that for each dynsym entry, there is exactly
            /// one `Global` which will return that dynsym index here.
            fn ownedDynsymIndex(gsi: Symbol.Global.Index, elf: *Elf) ?u32 {
                if (gsi.ptr(elf).status.owns_dynsym) {
                    const dynsym_index = gsi.ptr(elf).dynsym_index;
                    assert(dynsym_index != 0);
                    return dynsym_index;
                }
                return null;
            }

            /// Scans through all relocations targeting `gsi`, deletes their dynamic relocation
            /// entries, and adds `R_*_RELATIVE` relocation entries as needed.
            ///
            /// Asserts we are creating a DSO.
            fn deleteDynamicTargetRelocs(gsi: Symbol.Global.Index, elf: *Elf) void {
                if (elf.shndx.dynamic == .UNDEF) return;
                const sym_id: Symbol.Id = .global(gsi);
                switch (elf.classifySymbolValue(sym_id)) {
                    .static => {
                        if (!gsi.ptr(elf).status.any_dynamic_target_relocs and
                            !gsi.ptr(elf).status.any_static_relative_target_relocs)
                        {
                            return;
                        }
                        gsi.ptr(elf).status.any_dynamic_target_relocs = false;
                        gsi.ptr(elf).status.any_static_relative_target_relocs = false;
                        gsi.ptr(elf).status.any_static_target_relocs = true;
                    },
                    .static_relative => {
                        if (!gsi.ptr(elf).status.any_dynamic_target_relocs and
                            !gsi.ptr(elf).status.any_static_target_relocs)
                        {
                            return;
                        }
                        gsi.ptr(elf).status.any_dynamic_target_relocs = false;
                        gsi.ptr(elf).status.any_static_target_relocs = false;
                        gsi.ptr(elf).status.any_static_relative_target_relocs = true;
                    },
                    // TODO: this function needs to support a symbol becoming dynamic which wasn't
                    // previously dynamic (and when it does, the function should be renamed to
                    // `updateTargetOutputRelocs`). But for now, this isn't supported.
                    .dynamic => unreachable,
                }
                var ri = sym_id.index(elf).ptr(elf).first_target_reloc;
                while (ri != .none) {
                    const reloc = ri.get(elf);
                    assert(reloc.target == sym_id);
                    reloc.deleteOutputRel(elf);
                    ri = reloc.next;
                }
                switch (elf.classifySymbolValue(sym_id)) {
                    .static => return,
                    .static_relative => {},
                    .dynamic => unreachable,
                }
                // We removed the symbol relocations, now add R_*_RELATIVE relocations where needed.
                ri = sym_id.index(elf).ptr(elf).first_target_reloc;
                while (ri != .none) {
                    const reloc = ri.get(elf);
                    ri = reloc.next;
                    assert(reloc.target == sym_id);
                    switch (reloc.type.target) {
                        // Only relocations which resolve to absolute addresses require runtime
                        // `R_*_RELATIVE` relocations.
                        .special,
                        .pltrel,
                        .rel,
                        .dtpoff,
                        .tpoff,
                        .size,
                        => continue,

                        .abs, .pltabs => {},
                    }
                    if (!reloc.type.action.simple.dest.isAddr(elf)) continue;
                    const node = reloc.node.unwrap().?;
                    switch (elf.nodeWantsDsoRelocation(node)) {
                        .no => continue,
                        .yes_textrel => elf.textrel_count += 1,
                        .yes => {},
                    }
                    // There is capacity for a relocation because we just deleted one earlier.
                    reloc.rela_index = elf.shndx.rela_dyn.relaAddOneAssumeCapacity(elf, .{
                        .type = .relative(elf),
                        .offset = elf.getNodeVAddr(node) + reloc.offset,
                        .raw_sym_index = 0,
                        .addend = 0,
                    }).toOptional();
                }
            }

            const Optional = enum(u32) {
                none = std.math.maxInt(u32),
                _,

                fn wrap(gsi: Symbol.Global.Index) Symbol.Global.Index.Optional {
                    return @bitCast(gsi);
                }
                fn unwrap(ogsi: Symbol.Global.Index.Optional) ?Symbol.Global.Index {
                    return switch (ogsi) {
                        .none => null,
                        _ => @fromBackingInt(@backingInt(ogsi)),
                    };
                }
            };
        };

        const Bind = enum(u1) { strong, weak };

        const AddOptions = struct {
            node: MappedFile.Node.Index.Optional,
            name: []const u8,
            lib_name: ?[]const u8 = null,
            value: u64,
            size: u64,
            type: std.elf.STT,
            bind: Symbol.Global.Bind,
            visibility: std.elf.STV,
            shndx: Section.Index,
        };
    };

    /// An index directly into the symtab. These values are not stable (global symbols are sometimes
    /// moved to new locations in the symtab) and therefore should only be used ephemerally.
    ///
    /// Local symbols *do* have stable indices into the symtab; see `LocalIndex`.
    ///
    /// For a stable reference to an arbitrary symbol, see `Id`.
    const Index = enum(u32) {
        null = 0,
        _,

        fn ptr(si: Symbol.Index, elf: *Elf) *Symbol {
            return &elf.symtab.items[@backingInt(si)];
        }
    };

    /// A `LocalIndex` is a raw index into the symtab like `Index`, but it guarantees that the
    /// symbol in question has STB_LOCAL binding, which guarantees that its symtab index is stable
    /// so can be stored long-term without needing to be updated
    ///
    /// This is because symbols which have STB_LOCAL binding in the output file gain fixed symtab
    /// indices, thanks to a combination of a few factors:
    /// * We never remove STB_LOCAL symbols
    /// * There is no symbol ordering requirement *within* the leading range of STB_LOCAL symbols
    /// * A symbol visibility which demotes a global to STB_LOCAL binding can never be reverted by
    ///   a subsequent operation (different visibilities resolve to the "strictest" one)
    const LocalIndex = enum(u32) {
        null = 0,
        _,

        fn index(li: LocalIndex) Index {
            return @fromBackingInt(@backingInt(li));
        }
    };

    /// Opaque, stable identifier for a symbol. Does not necessarily equal the index into the symtab.
    const Id = packed struct(u32) {
        kind: enum(u1) { local, global },
        raw: u31,

        const @"null": Symbol.Id = .local(.null);

        fn local(lsi: Symbol.LocalIndex) Symbol.Id {
            return .{ .kind = .local, .raw = @intCast(@backingInt(lsi)) };
        }
        fn global(gsi: Symbol.Global.Index) Symbol.Id {
            return .{ .kind = .global, .raw = @intCast(@backingInt(gsi)) };
        }
        fn unwrap(s: Symbol.Id) union(enum) {
            local: Symbol.LocalIndex,
            global: Symbol.Global.Index,
        } {
            return switch (s.kind) {
                .local => .{ .local = @fromBackingInt(s.raw) },
                .global => .{ .global = @fromBackingInt(s.raw) },
            };
        }

        fn toTypeErased(s: Symbol.Id) link.File.SymbolId {
            return @bitCast(s);
        }
        fn fromTypeErased(s: link.File.SymbolId) Symbol.Id {
            return @bitCast(s);
        }

        fn index(s: Symbol.Id, elf: *Elf) Symbol.Index {
            return switch (s.unwrap()) {
                .local => |lsi| lsi.index(),
                .global => |gsi| gsi.ptr(elf).symtab_index,
            };
        }

        /// Returns the `Symbol.Id` which determines the resolved value, size, etc of `s`.
        ///
        /// See `Symbol.Global.Index.resolveAlias` for details.
        fn resolveAlias(s: Symbol.Id, elf: *Elf) Symbol.Id {
            return switch (s.unwrap()) {
                .local => |lsi| .local(lsi),
                .global => |gsi| .global(gsi.resolveAlias(elf)),
            };
        }

        /// Returns the value of this symbol, or 0 if it is undefined. If the symbol is an undefined
        /// global for which we have emitted a copy relocation, returns the virtual address of that
        /// copy relocation, which the symbol is guaranteed to resolve to at runtime.
        fn value(s: Symbol.Id, elf: *Elf) u64 {
            return switch (elf.symPtr(s.resolveAlias(elf).index(elf))) {
                inline else => |sym| elf.targetLoad(&sym.value),
            };
        }

        fn size(s: Symbol.Id, elf: *Elf) u64 {
            return switch (elf.symPtr(s.resolveAlias(elf).index(elf))) {
                inline else => |sym| elf.targetLoad(&sym.value),
            };
        }

        fn flushMoved(sym_id: Symbol.Id, elf: *Elf, new_value: u64) void {
            const sym_index = sym_id.index(elf);
            switch (elf.symPtr(sym_index)) {
                inline else => |sym| elf.targetStore(&sym.value, @intCast(new_value)),
            }

            switch (sym_id.unwrap()) {
                .local => {},
                .global => |gsi| if (gsi.ownedDynsymIndex(elf)) |dynsym_index| {
                    switch (elf.dynsymPtr(dynsym_index)) {
                        inline else => |sym| elf.targetStore(&sym.value, @intCast(new_value)),
                    }
                },
            }

            sym_id.applyTargetRelocs(elf);

            switch (sym_id.unwrap()) {
                .local => {},
                .global => |gsi| if (gsi.findAliaser(elf)) |alias_gsi| {
                    Symbol.Id.global(alias_gsi).applyTargetRelocs(elf);
                },
            }
        }

        fn applyTargetRelocs(sym_id: Symbol.Id, elf: *Elf) void {
            if (elf.ehdrType() != .REL) {
                var ri = sym_id.index(elf).ptr(elf).first_target_reloc;
                while (ri != .none) {
                    const reloc = ri.get(elf);
                    assert(reloc.target == sym_id);
                    reloc.apply(elf);
                    ri = reloc.next;
                }
            }

            if (elf.got.getIndex(.{ .symbol = sym_id })) |got_index| {
                elf.updateGotEntry(got_index);
            }
            if (elf.got.getIndex(.{ .tpoff = sym_id })) |got_index| {
                elf.updateGotEntry(got_index);
            }
            if (elf.got.getIndex(.{ .tlsgd0 = sym_id })) |got_index| {
                elf.updateGotEntry(got_index);
                elf.updateGotEntry(got_index + 1); // tlsgd1
            }
        }

        /// Returns `true` if the target of `s` has moved, meaning the symbol's value will change at
        /// some point due to a call to `flushMoved`.
        fn hasMoved(s: Symbol.Id, elf: *Elf) bool {
            const resolved = s.resolveAlias(elf);
            if (resolved.index(elf).ptr(elf).node.unwrap()) |node| {
                return node.hasMoved(&elf.mf);
            }
            switch (resolved.unwrap()) {
                .local => {},
                .global => |gsi| if (elf.copied_globals.getPtr(gsi)) |copied_global| {
                    return copied_global.node.hasMoved(&elf.mf);
                },
            }
            return false;
        }
    };
};

fn globalByName(elf: *Elf, name: Symbol.Global.VersionedName) ?Symbol.Global.Index {
    const adapter: Symbol.Global.VersionedName.Adapter = .{ .elf = elf };
    const index_raw = elf.globals_by_name.getIndexAdapted(name, adapter) orelse return null;
    return @fromBackingInt(@intCast(index_raw));
}
fn globalBySym(elf: *Elf, sym_index: Symbol.Index) Symbol.Global.Index {
    const raw_name: String(.strtab) = switch (elf.symPtr(sym_index)) {
        inline else => |sym| @fromBackingInt(elf.targetLoad(&sym.name)),
    };
    const name = parseVersionedSymbolName(raw_name.slice(elf));
    return elf.globalByName(.{ .name = name.name, .version = name.version }).?;
}
fn globalByDynsym(elf: *Elf, dynsym_index: u32) Symbol.Global.Index {
    // Versioned symbols have different names in the normal symbol table vs the dynamic symbol
    // table. In those cases, a hash map holds an "override":
    if (elf.versioned_dynsym_owners.get(dynsym_index)) |gsi| {
        assert(gsi.ownedDynsymIndex(elf).? == dynsym_index);
        return gsi;
    }
    // For all other symbols, we can just take the name of the dynamic symbol table entry, and look
    // up the global with that name.
    const name: String(.dynstr) = switch (elf.dynsymPtr(dynsym_index)) {
        inline else => |dynsym| @fromBackingInt(elf.targetLoad(&dynsym.name)),
    };
    const gsi = elf.globalByName(.{ .name = name.slice(elf), .version = null }).?;
    assert(gsi.ownedDynsymIndex(elf).? == dynsym_index);
    return gsi;
}

fn classifySymbolValue(elf: *Elf, sym: Symbol.Id) enum {
    /// This symbol's value is guaranteed to equal `sym.value(elf)`.
    static,
    /// This symbol's value is an offset of `sym.value(elf)` from the runtime-known load address of
    /// this DSO (which is position-independent).
    static_relative,
    /// This symbol's definition does not necessarily come from this DSO, so is not known until RTLD
    /// runs. Therefore, a dynamic (runtime) relocation is necessary.
    dynamic,
} {
    const comp = elf.base.comp;

    const runtime_load_addr = switch (elf.ehdrType()) {
        .REL => unreachable,
        .DYN => true,
        .EXEC => false,
    };

    if (elf.shndx.dynamic == .UNDEF) {
        // This is a static non-PIE executable---every symbol has a statically known value.
        return .static;
    }

    const resolved_sym = sym.resolveAlias(elf);

    const shndx: Section.Index, const visibility: std.elf.STV = switch (elf.symPtr(resolved_sym.index(elf))) {
        inline else => |sym_ptr| .{
            .fromSection(elf.targetLoad(&sym_ptr.shndx)),
            elf.targetLoad(&sym_ptr.other).visibility,
        },
    };

    switch (resolved_sym.unwrap()) {
        .local => {
            assert(shndx != .UNDEF);
            assert(visibility == .DEFAULT);
        },
        .global => |gsi| if (visibility == .DEFAULT and comp.config.output_mode != .Exe) {
            // An unprotected symbol in a DSO which is not an executable is subject to runtime
            // preemption, so a dynamic relocation is required for it even if we have a definition.
            return .dynamic;
        } else if (elf.copied_globals.contains(gsi)) {
            // This becomes a locally-defined symbol in `.data`.
            return if (runtime_load_addr) .static_relative else .static;
        },
    }

    return switch (shndx) {
        .UNDEF => switch (visibility) {
            .DEFAULT => if (comp.config.link_mode == .static and comp.config.output_mode == .Exe) {
                assert(comp.config.pie); // non-PIE static exe should not have a `.dynamic` section
                // This is a static PIE---we'll see a definition at some point, so there'd be no
                // point in adding an invalid dynamic relocation (the only valid dynamic relation
                // type in a static PIE is `R_*_RELATIVE`).
                return .static;
            } else .dynamic, // external symbol

            // If the symbol *cannot* be external, then there's no point making a dynamic relocation
            // now---if linking succeeds we won't need anything more than perhaps an `R_*_RELATIVE`.
            .INTERNAL, .HIDDEN, .PROTECTED => .static,
        },

        .ABS => .static,

        else => if (runtime_load_addr and
            shndx.flags(elf).ALLOC and
            !shndx.flags(elf).TLS)
        {
            return .static_relative;
        } else {
            return .static;
        },
    };
}

pub fn symbolForAtom(elf: *Elf, atom: link.File.AtomId) link.File.SymbolId {
    const lsi: Symbol.LocalIndex = switch (elf.getNode(Node.fromAtom(atom))) {
        .deleted,
        .archive,
        .archive_header,
        .archive_input_member,
        .archive_elf_member_header,
        .elf,
        .ehdr,
        .shdr,
        .segment,
        .section,
        .section_manual_size,
        .input_section,
        .copied_global,
        .debug_shared,
        .debug_addr,
        .eh_frame_footer,
        .debug_str_offsets,
        .unit_padding,
        .unit_frame,
        .unit_frame_cie,
        .unit_debug_info,
        .unit_debug_info_header,
        .unit_debug_info_footer,
        .unit_debug_line,
        .unit_debug_line_header,
        .unit_debug_rnglists,
        .const_debug_info,
        .global_debug_info,
        .func_frame_fde,
        .func_debug_info,
        .func_debug_line,
        .decl_debug_info,
        => unreachable,
        inline .nav,
        .uav,
        .lazy_code,
        .lazy_const_data,
        => |i| i.symbol(elf),
    };
    const s: Symbol.Id = .local(lsi);
    return s.toTypeErased();
}
pub fn lazySymbol(elf: *Elf, lazy: link.File.LazySymbol) link.Error!link.File.SymbolId {
    return elf.lazySymbolInner(lazy) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return elf.base.comp.link_diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
    };
}
fn lazySymbolInner(elf: *Elf, lazy: link.File.LazySymbol) Error!link.File.SymbolId {
    const gpa = elf.base.comp.gpa;

    try elf.nodes.ensureUnusedCapacity(gpa, 1);
    try elf.lazy.getPtr(lazy.kind).map.ensureUnusedCapacity(gpa, 1);

    const gop = elf.lazy.getPtr(lazy.kind).map.getOrPutAssumeCapacity(lazy.ty);
    if (!gop.found_existing) {
        const shndx: Section.Index, const sym_type: std.elf.STT = switch (lazy.kind) {
            .code => .{ .text, .FUNC },
            .const_data => .{ .rodata, .OBJECT },
        };
        const node = elf.addNodeAssumeCapacity(
            try shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{}),
            switch (lazy.kind) {
                .code => .{ .lazy_code = @fromBackingInt(@intCast(gop.index)) },
                .const_data => .{ .lazy_const_data = @fromBackingInt(@intCast(gop.index)) },
            },
        );
        var name_buf: [std.fmt.count("__lazy_const_data_{d}", .{std.math.maxInt(u32)})]u8 = undefined;
        const name = std.mem.print(&name_buf, "__lazy_{t}_{d}", .{ lazy.kind, gop.index }) catch
            unreachable;
        gop.value_ptr.* = .{
            .lsi = try elf.addLocalSymbol(.{
                .node = .wrap(node),
                .name = name,
                .value = 0,
                .size = 0,
                .type = sym_type,
                .shndx = shndx,
            }),
            .first_symbol_reloc = .none,
            .first_got_reloc = .none,
        };
        elf.base.comp.link_prog_node.increaseEstimatedTotalItems(1);
    }
    const s: Symbol.Id = .local(gop.value_ptr.lsi);
    return s.toTypeErased();
}
pub const ExternSymbolOpts = struct {
    name: []const u8,
    lib_name: ?[]const u8,
    type: std.elf.STT,
    linkage: std.lang.GlobalLinkage = .strong,
    visibility: std.lang.SymbolVisibility = .default,
};
pub fn externSymbol(elf: *Elf, opts: ExternSymbolOpts) link.Error!link.File.SymbolId {
    const diags = &elf.base.comp.link_diags;
    return (elf.externSymbolInner(opts) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
    }).toTypeErased();
}
fn externSymbolInner(elf: *Elf, opts: ExternSymbolOpts) Error!Symbol.Id {
    const gsi = elf.addGlobalSymbol(.{
        .node = .none,
        .name = opts.name,
        .lib_name = opts.lib_name,
        .value = 0,
        .size = 0,
        .type = opts.type,
        .bind = switch (opts.linkage) {
            .strong => .strong,
            .weak => .weak,
        },
        .visibility = switch (opts.visibility) {
            .default => .DEFAULT,
            .hidden => .HIDDEN,
            .protected => .PROTECTED,
        },
        .shndx = .UNDEF,
    }) catch |err| switch (err) {
        error.MultipleDefinitions => unreachable, // shndx is undef
        error.MultipleDefaultVersions => unreachable, // shndx is undef
        error.UndefinedDefaultVersion => return elf.base.comp.link_diags.fail(
            "symbol '{s}' specifies default version without defining it",
            .{opts.name},
        ),
        else => |e| return e,
    };
    return .global(gsi);
}
pub fn addReloc(
    elf: *Elf,
    atom: link.File.AtomId,
    offset: u64,
    target: link.File.SymbolId,
    addend: i64,
    @"type": MachineRelocType,
) link.Error!void {
    const node: MappedFile.Node.Index = Node.fromAtom(atom);
    const diags = &elf.base.comp.link_diags;
    elf.ensureUnusedRelocCapacity(node, 1) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
    };
    elf.addRelocAssumeCapacity(node, offset, .fromTypeErased(target), addend, @"type") catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
        error.UnknownRelocation => unreachable, // codegen bug
        error.NonStaticRelocation => unreachable, // codegen bug
        error.UnimplementedRelocation => unreachable, // codegen bug (asking Elf2 for a relocation it does not support)
    };
}
pub fn addNodeReloc(
    elf: *Elf,
    node: MappedFile.Node.Index,
    offset: u64,
    target: MappedFile.Node.Index,
    addend: i64,
    @"type": NodeReloc.Type,
) link.Error!void {
    const diags = &elf.base.comp.link_diags;
    elf.ensureUnusedRelocCapacity(node, 1) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
    };
    elf.addNodeRelocAssumeCapacity(node, offset, target, addend, @"type") catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
    };
}
pub fn navSymbol(elf: *Elf, nav_index: InternPool.Nav.Index) link.Error!link.File.SymbolId {
    const diags = &elf.base.comp.link_diags;
    const zcu = elf.base.comp.zcu.?;
    const ip = &zcu.intern_pool;
    const nav = ip.getNav(nav_index);
    if (nav.getExtern(ip)) |@"extern"| {
        return elf.externSymbol(.{
            .name = @"extern".name.toSlice(ip),
            .lib_name = @"extern".lib_name.toSlice(ip),
            .type = elf.navType(nav.resolved.?),
            .linkage = @"extern".linkage,
            .visibility = @"extern".visibility,
        });
    }
    const nmi = elf.navMapIndex(zcu, nav_index) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
    };
    const s: Symbol.Id = .local(nmi.symbol(elf));
    return s.toTypeErased();
}
pub fn relocSymAddr(elf: *Elf, reloc_info: link.File.RelocInfo) link.Error!void {
    try elf.addReloc(
        switch (reloc_info.parent) {
            .none => unreachable,
            .atom_index => |atom_id| atom_id,
            .debug_output => |debug_output| Node.toAtom(debug_output.dwarf2.info_writer.ni),
        },
        reloc_info.offset,
        reloc_info.target,
        reloc_info.addend,
        .absAddr(elf),
    );
}
pub fn uavSymbol(
    elf: *Elf,
    pt: Zcu.PerThread,
    uav_val: InternPool.Index,
    uav_align: InternPool.Alignment,
) link.Error!link.File.SymbolId {
    _ = pt;
    const diags = &elf.base.comp.link_diags;
    const umi = elf.uavMapIndex(uav_val, uav_align) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
    };
    const s: Symbol.Id = .local(umi.symbol(elf));
    return s.toTypeErased();
}

const StringSection = enum {
    shstrtab,
    strtab,
    dynstr,
    fn shndx(s: StringSection, elf: *const Elf) Section.Index {
        return switch (s) {
            .strtab => .strtab,
            .shstrtab => .shstrtab,
            .dynstr => elf.shndx.dynstr,
        };
    }
};
fn String(section: StringSection) type {
    return enum(u32) {
        empty = 0,
        _,

        fn slice(str: @This(), elf: *Elf) [:0]const u8 {
            const section_node = section.shndx(elf).get(elf).ni;
            const overlong = section_node.sliceConst(&elf.mf)[@backingInt(str)..];
            return overlong[0..std.mem.findScalar(u8, overlong, 0).? :0];
        }
    };
}
fn string(elf: *Elf, comptime section: StringSection, key: []const u8) Error!String(section) {
    if (key.len == 0) {
        // Special case to allow calls from `initHeaders` before the strtab is initialized.
        return .empty;
    }
    const st: *StringTable = &@field(elf, @tagName(section));
    try st.ensureAdditionalCapacity(elf, section.shndx(elf), key.len);
    return @fromBackingInt(st.getAssumeCapacity(elf, section.shndx(elf), key));
}
fn ensureAdditionalStringCapacity(elf: *Elf, comptime section: StringSection, string_len: usize) Error!void {
    const st: *StringTable = &@field(elf, @tagName(section));
    try st.ensureAdditionalCapacity(elf, section.shndx(elf), string_len);
}
fn stringAssumeCapacity(elf: *Elf, comptime section: StringSection, key: []const u8) String(section) {
    const st: *StringTable = &@field(elf, @tagName(section));
    return @fromBackingInt(st.getAssumeCapacity(elf, section.shndx(elf), key));
}
/// Like `string`, but asserts that the string is already in `section`.
fn stringExisting(elf: *Elf, comptime section: StringSection, key: []const u8) String(section) {
    const st: *StringTable = &@field(elf, @tagName(section));
    return @fromBackingInt(st.getExisting(elf, section.shndx(elf), key));
}

const StringTable = struct {
    map: std.HashMapUnmanaged(u32, void, StringTable.Context, std.hash_map.default_max_load_percentage),

    const Context = struct {
        slice: []const u8,

        pub fn eql(_: Context, lhs_key: u32, rhs_key: u32) bool {
            return lhs_key == rhs_key;
        }

        pub fn hash(ctx: Context, key: u32) u64 {
            return std.hash_map.hashString(std.mem.sliceTo(ctx.slice[key..], 0));
        }
    };

    const Adapter = struct {
        slice: []const u8,

        pub fn eql(adapter: Adapter, lhs_key: []const u8, rhs_key: u32) bool {
            return std.mem.startsWith(u8, adapter.slice[rhs_key..], lhs_key) and
                adapter.slice[rhs_key + lhs_key.len] == 0;
        }

        pub fn hash(_: Adapter, key: []const u8) u64 {
            assert(std.mem.findScalar(u8, key, 0) == null);
            return std.hash_map.hashString(key);
        }
    };

    fn getExisting(st: *StringTable, elf: *Elf, shndx: Section.Index, key: []const u8) u32 {
        if (key.len == 0) return 0;
        const slice_const = shndx.get(elf).ni.sliceConst(&elf.mf);
        const adapter: StringTable.Adapter = .{ .slice = slice_const };
        return st.map.getKeyAdapted(key, adapter).?;
    }

    fn ensureAdditionalCapacity(st: *StringTable, elf: *Elf, shndx: Section.Index, string_len: usize) Error!void {
        const gpa = elf.base.comp.gpa;
        const ni = shndx.get(elf).ni;
        const slice_const = ni.sliceConst(&elf.mf);
        try st.map.ensureUnusedCapacityContext(gpa, 1, .{ .slice = slice_const });
        const size: u64 = switch (elf.shdrPtr(shndx)) {
            inline else => |shdr| elf.targetLoad(&shdr.size),
        };
        try ni.ensureMinimumSize(gpa, &elf.mf, size + string_len + 1);
    }

    fn getAssumeCapacity(st: *StringTable, elf: *Elf, shndx: Section.Index, key: []const u8) u32 {
        // If we are in `initHeaders` the strtab might not be initalized yet, so we need to special
        // case the empty string.
        if (key.len == 0) return 0;

        const ni = shndx.get(elf).ni;
        const slice_const = ni.sliceConst(&elf.mf);
        const gop = st.map.getOrPutAssumeCapacityAdapted(
            key,
            StringTable.Adapter{ .slice = slice_const },
        );
        if (gop.found_existing) return gop.key_ptr.*;
        const offset: u32 = switch (elf.shdrPtr(shndx)) {
            inline else => |shdr| offset: {
                const old_size = elf.targetLoad(&shdr.size);
                elf.targetStore(&shdr.size, @intCast(old_size + key.len + 1));
                break :offset @intCast(old_size);
            },
        };
        const slice = ni.slice(&elf.mf)[offset..];
        @memcpy(slice[0..key.len], key);
        slice[key.len] = 0;
        gop.key_ptr.* = offset;
        return offset;
    }
};

pub fn open(
    arena: std.mem.Allocator,
    comp: *Compilation,
    path: std.Build.Cache.Path,
    options: link.File.OpenOptions,
) !*Elf {
    return create(arena, comp, path, options);
}
pub fn createEmpty(
    arena: std.mem.Allocator,
    comp: *Compilation,
    path: std.Build.Cache.Path,
    options: link.File.OpenOptions,
) !*Elf {
    return create(arena, comp, path, options);
}
fn create(
    arena: std.mem.Allocator,
    comp: *Compilation,
    path: std.Build.Cache.Path,
    options: link.File.OpenOptions,
) !*Elf {
    const io = comp.io;
    const target = &comp.root_mod.resolved_target.result;
    assert(target.ofmt == .elf);
    const class: std.elf.CLASS = switch (target.ptrBitWidth()) {
        0...32 => .@"32",
        33...64 => .@"64",
        else => return error.UnsupportedELFArchitecture,
    };
    const data: std.elf.DATA = switch (target.cpu.arch.endian()) {
        .little => .@"2LSB",
        .big => .@"2MSB",
    };
    const osabi: std.elf.OSABI = switch (target.os.tag) {
        else => .NONE, // might be changed to `.GNU` by `checkInputIdent`
        .freestanding, .other => .STANDALONE,
        .netbsd => .NETBSD,
        .illumos => .SOLARIS,
        .freebsd, .ps4 => .FREEBSD,
        .openbsd => .OPENBSD,
        .cuda => .CUDA,
        .amdhsa => .AMDGPU_HSA,
        .amdpal => .AMDGPU_PAL,
        .mesa3d => .AMDGPU_MESA3D,
    };
    const @"type": EhdrType = switch (comp.config.output_mode) {
        .Exe => if (comp.config.pie or target.os.tag == .haiku) .DYN else .EXEC,
        .Lib => switch (comp.config.link_mode) {
            .static => .REL,
            .dynamic => .DYN,
        },
        .Obj => .REL,
    };
    const machine = EhdrMachine.fromElf(target.toElfMachine()) orelse {
        std.debug.panic("TODO(Elf2): add support for target machine '{t}'", .{target.toElfMachine()});
    };
    const maybe_interp = switch (comp.config.link_mode) {
        .static => null,
        .dynamic => switch (comp.config.output_mode) {
            .Exe => target.dynamic_linker.get(),
            .Lib => if (comp.root_mod.resolved_target.is_explicit_dynamic_linker)
                target.dynamic_linker.get()
            else
                null,
            .Obj => null,
        },
    };

    const elf = try arena.create(Elf);
    const file = try path.root_dir.handle.createFile(io, path.sub_path, .{
        .read = true,
        .permissions = link.File.determinePermissions(comp.config.output_mode, comp.config.link_mode),
    });
    errdefer file.close(io);
    elf.* = .{
        .base = .{
            .tag = .elf2,

            .comp = comp,
            .emit = path,

            .file = file,
            .gc_sections = false,
            .print_gc_sections = false,
            .build_id = .none,
            .allow_shlib_undefined = false,
            .stack_size = 0,
        },
        .options = options,
        .mf = try .init(file, comp.gpa, io),
        .ni = .{
            .elf = undefined,
            .ehdr = undefined,
            .shdr = undefined,
            .rodata = undefined,
            .phdr = undefined,
            .text = undefined,
            .data = undefined,
            .data_rel_ro = undefined,
            .tls = .none,
            .gnu_eh_frame = .none,
        },
        .archive = null,
        .nodes = .empty,
        .shdrs = .empty,
        .phdrs = .empty,
        .shndx = .{
            .got = .UNDEF,
            .got_plt = .UNDEF,
            .plt = .UNDEF,
            .plt_sec = .UNDEF,
            .dynsym = .UNDEF,
            .dynstr = .UNDEF,
            .dynamic = .UNDEF,
            .hash = .UNDEF,
            .tdata = .UNDEF,
            .rela_dyn = .UNDEF,
            .rela_plt = .UNDEF,
            .gnu_version = .UNDEF,
            .gnu_version_d = .UNDEF,
            .gnu_version_r = .UNDEF,
            .debug_abbrev = .UNDEF,
            .debug_addr = .UNDEF,
            .eh_frame_hdr = .UNDEF,
            .eh_frame = .UNDEF,
            .debug_frame = .UNDEF,
            .debug_info = .UNDEF,
            .debug_line = .UNDEF,
            .debug_line_str = .UNDEF,
            .debug_rnglists = .UNDEF,
            .debug_str = .UNDEF,
            .debug_str_offsets = .UNDEF,
            .init_array = .UNDEF,
            .fini_array = .UNDEF,
            .preinit_array = .UNDEF,
        },
        .dynamic = .{
            .flags = 0,
            .flags_1 = 0,
            .rpath = .empty,
            .soname = .empty,
        },
        .symtab = .empty,
        .globals = .empty,
        .globals_by_name = .empty,
        .default_version_globals = .empty,
        .unknown_globals = .empty,
        .defined_alias_globals = .empty,
        .copied_globals = .empty,
        .versioned_dynsym_owners = .empty,
        .verdef = .empty,
        .verneed = .empty,
        .last_verneed_file_index = 0,
        // This is initially 2 because that is the first user-defined version index.
        // 0 and 1 are reserved for `VER_NDX_LOCAL` and `VER_NDX_GLOBAL`.
        .next_version_id = 2,
        .node_global_symbols = .empty,
        .dso_globals = .{
            .string_bytes = .empty,
            .symbols = .empty,
            .default_sym_vers = .empty,
        },
        .shstrtab = .{ .map = .empty },
        .strtab = .{ .map = .empty },
        .dynstr = .{ .map = .empty },
        .got = .empty,
        .plt = .empty,
        .plt_first_symbol_reloc = .none,
        .eh_frame_hdr_first_symbol_reloc = .none,
        .needed = .empty,
        .inputs = .empty,
        .input_pending_index = 0,
        .input_sections = .empty,
        .input_section_pending_index = 0,
        .one_shot_fixups = .empty,
        .navs = .empty,
        .uavs = .empty,
        .lazy = comptime .initFill(.{
            .map = .empty,
            .pending_index = 0,
        }),
        .pending_uavs = .empty,
        .symbol_relocs = .empty,
        .node_relocs = .empty,
        .got_relocs = .empty,
        .tls_size_symbol_relocs = .empty,
        .section_by_name = .empty,
        .changed_symtab_index = .empty,
        .textrel_count = 0,

        .dwarf = .init(&elf.base, switch (comp.config.debug_format) {
            .strip => .@"32", // for .eh_frame
            .dwarf => |v| v,
            .code_view => unreachable,
        }),
        .dwarf_shared = comptime .initFill(.{
            .first_target_reloc = .none,
        }),
        .dwarf_addr = .{
            .first_target_reloc = .none,
            .symbol_relocs = .empty,
        },
        .dwarf_str_offsets = .{
            .first_target_reloc = .none,
            .node_relocs = .empty,
        },
        .dwarf_units = &.{},
        .dwarf_consts = .empty,
        .dwarf_globals = .empty,
        .dwarf_funcs = .empty,
        .dwarf_decls = .empty,

        .overflowed_reloc_count = 0,
        .misaligned_reloc_count = 0,

        .const_prog_node = .none,
        .input_prog_node = .none,
    };
    errdefer elf.deinit();

    try elf.initHeaders(class, data, osabi, @"type", machine, maybe_interp);
    return elf;
}

pub fn deinit(elf: *Elf) void {
    const gpa = elf.base.comp.gpa;
    elf.mf.deinit(gpa);
    elf.nodes.deinit(gpa);
    elf.shdrs.deinit(gpa);
    elf.phdrs.deinit(gpa);
    elf.symtab.deinit(gpa);
    elf.globals.deinit(gpa);
    elf.globals_by_name.deinit(gpa);
    elf.default_version_globals.deinit(gpa);
    elf.unknown_globals.deinit(gpa);
    elf.defined_alias_globals.deinit(gpa);
    elf.copied_globals.deinit(gpa);
    elf.versioned_dynsym_owners.deinit(gpa);
    elf.verdef.deinit(gpa);
    elf.verneed.deinit(gpa);
    elf.node_global_symbols.deinit(gpa);
    elf.dso_globals.string_bytes.deinit(gpa);
    elf.dso_globals.symbols.deinit(gpa);
    elf.dso_globals.default_sym_vers.deinit(gpa);
    elf.shstrtab.map.deinit(gpa);
    elf.strtab.map.deinit(gpa);
    elf.dynstr.map.deinit(gpa);
    elf.got.deinit(gpa);
    elf.plt.deinit(gpa);
    elf.needed.deinit(gpa);
    for (elf.inputs.items) |input| if (input.member) |m| gpa.free(m);
    elf.inputs.deinit(gpa);
    elf.input_sections.deinit(gpa);
    elf.one_shot_fixups.deinit(gpa);
    elf.navs.deinit(gpa);
    elf.uavs.deinit(gpa);
    for (&elf.lazy.values) |*lazy| lazy.map.deinit(gpa);
    elf.pending_uavs.deinit(gpa);
    elf.symbol_relocs.deinit(gpa);
    elf.node_relocs.deinit(gpa);
    elf.got_relocs.deinit(gpa);
    elf.tls_size_symbol_relocs.deinit(gpa);
    elf.section_by_name.deinit(gpa);
    elf.changed_symtab_index.deinit(gpa);

    elf.dwarf.deinit();
    elf.dwarf_addr.symbol_relocs.deinit(gpa);
    elf.dwarf_str_offsets.node_relocs.deinit(gpa);
    for (elf.dwarf_units) |*dwarf_unit| dwarf_unit.debug_rnglists_symbol_relocs.deinit(gpa);
    gpa.free(elf.dwarf_units);
    elf.dwarf_consts.deinit(gpa);
    elf.dwarf_globals.deinit(gpa);
    elf.dwarf_funcs.deinit(gpa);
    elf.dwarf_decls.deinit(gpa);

    elf.* = undefined;
}

fn initHeaders(
    elf: *Elf,
    class: std.elf.CLASS,
    data: std.elf.DATA,
    osabi: std.elf.OSABI,
    @"type": EhdrType,
    machine: EhdrMachine,
    maybe_interp: ?[]const u8,
) Error!void {
    const comp = elf.base.comp;
    const gpa = comp.gpa;

    const is_archive = comp.config.output_mode == .Lib and comp.config.link_mode == .static;
    const have_dynamic = switch (@"type") {
        .REL => false,
        .EXEC => comp.config.link_mode == .dynamic,
        .DYN => true,
    };
    const have_eh_frame = machine == .X86_64 and comp.config.any_unwind_tables;
    const have_debug_frame = machine == .X86_64 and switch (comp.config.debug_format) {
        .strip => false,
        .dwarf => !comp.config.any_unwind_tables,
        .code_view => unreachable,
    };
    const addr_align: Alignment = switch (class) {
        .NONE, _ => unreachable,
        .@"32" => .@"4",
        .@"64" => .@"8",
    };

    // Minimum alignment for an arbitrarily-chosen set of "large" nodes in the file (e.g. common
    // sections), to allow `MappedFile` to perform operations more efficiently. The downside to
    // using `elf.mf.flags.block_size` is that it causes outputs to be potentially unreproducible
    // across host filesystems, so in the future we may want to set this to `.@"1"` when using a
    // build mode that requires reproducibility.
    //
    // It can be handy to temporarily set this to `.@"1"` when working on the linker, because it
    // prevents alignment bugs from being hidden by your filesystem's block alignment.
    const node_block_align = elf.mf.flags.block_size;

    const plt: PltInfo = .fromMachine(machine);

    const shnum: u32 = shnum: {
        var shnum: u32 = 1; // reserved ("null") shdr
        shnum += 1; // .symtab
        shnum += 1; // .shstrtab
        shnum += 1; // .strtab
        shnum += @intFromBool(maybe_interp != null); // .interp
        shnum += 1; // .rodata
        shnum += 1; // .text
        shnum += 1; // .data
        shnum += @intFromBool(comp.config.any_non_single_threaded); // .tdata
        shnum += 1; // .data.rel.ro
        if (have_dynamic) {
            shnum += 1; // .dynamic
            shnum += 1; // .dynstr
            shnum += 1; // .dynsym
            shnum += 1; // .hash
            shnum += 1; // .gnu.version
            shnum += 1; // .gnu.version_d
            shnum += 1; // .gnu.version_r
            shnum += 1; // .rela.dyn
            shnum += 1; // .rela.plt
        }
        if (have_eh_frame) {
            shnum += @intFromBool(@"type" != .REL); // .eh_frame_hdr
            shnum += 1; // .eh_frame
        }
        switch (comp.config.debug_format) {
            .strip => {},
            .dwarf => {
                shnum += 1; // .debug_abbrev
                shnum += 1; // .debug_addr
                shnum += @intFromBool(have_debug_frame); // .debug_frame
                shnum += 1; // .debug_info
                shnum += 1; // .debug_line
                shnum += 1; // .debug_line_str
                shnum += 1; // .debug_rnglists
                shnum += 1; // .debug_str
                shnum += 1; // .debug_str_offsets
            },
            .code_view => unreachable,
        }
        if (@"type" != .REL) {
            shnum += 1; // .got
            shnum += @intFromBool(plt.got_plt != null); // .got.plt
            shnum += 1; // .plt
            shnum += @intFromBool(plt.plt_sec != null); // .plt_sec
        }
        break :shnum shnum;
    };

    const phndx: struct {
        phdr: u32,
        interp: u32,
        rodata: u32,
        text: u32,
        /// On most targets this is `undefined`, but on machines where JUMP_SLOT relocations write
        /// directly to the PLT, we place the PLT in its own segment in order to avoid making the
        /// general data segment RWX.
        plt: u32,
        data: u32,
        tls: u32,
        dynamic: u32,
        relro: u32,
        gnu_eh_frame: u32,
        gnu_stack: u32,
    }, const phnum: u32 = ph: {
        switch (@"type") {
            .REL => break :ph .{ undefined, 0 },
            .EXEC, .DYN => {},
        }
        var phnum: u32 = 0;
        break :ph .{
            .{
                .phdr = phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                },
                .interp = if (maybe_interp) |_| phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                } else undefined,
                .rodata = phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                },
                .text = phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                },
                .plt = if (plt.got_plt == null) phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                } else undefined,
                // `data` must be assigned after all other loadable segments so that it has the greatest
                // phndx of any loadable segment. This is so that `targetSegmentLoadAddressRestrictions`
                // can be obeyed (specifically, the `.data_last` restriction, needed on SPARC).
                .data = phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                },
                .tls = if (comp.config.any_non_single_threaded) phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                } else undefined,
                .dynamic = if (have_dynamic) phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                } else undefined,
                .relro = phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                },
                .gnu_eh_frame = if (have_eh_frame) phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                } else undefined,
                .gnu_stack = phndx: {
                    defer phnum += 1;
                    break :phndx phnum;
                },
            },
            // (I don't actually want the trailing comma below, but a `zig fmt` bug forces it.)
            phnum,
        };
    };

    const expected_nodes_len = @as(usize, if (is_archive) 3 else 0) + // .archive, .archive_header, .archive_elf_member_header
        3 + // `.elf`, `.ehdr`, and `.shdr` nodes
        (shnum - 1) + // -1 because the SHN_UNDEF shdr does not have a `.section` node
        (phnum -| 1) + // -1 because the GNU_STACK phdr does not have a `.segment` node
        @intFromBool(have_eh_frame and @"type" != .REL); // eh_frame_footer

    try elf.nodes.ensureTotalCapacity(gpa, expected_nodes_len);
    try elf.shdrs.ensureTotalCapacity(gpa, shnum - 1); // -1 to exclude SHN_UNDEF
    try elf.section_by_name.ensureUnusedCapacity(gpa, shnum - 1); // -1 to exclude SHN_UNDEF
    try elf.phdrs.resize(gpa, phnum);
    try elf.symtab.ensureTotalCapacity(gpa, 1);

    if (is_archive) {
        const archive_ni = elf.addNodeAssumeCapacity(.root, .archive);

        const archive_header_ni = elf.addNodeAssumeCapacity(
            try archive_ni.addOnlyHeaderChild(gpa, &elf.mf, .{
                // We intentionally do not set `.alignment = .@"2"` here, because the string table data
                // in this node does not need to have an aligned length. (This node's offset is aligned
                // regardless by virtue of it being a header.)
                .size = std.elf.ARMAG.len + @sizeOf(std.elf.ar_hdr),
                // The archive header uses 'next_moved' events to resize the "//" member, so that it
                // absorbs all padding between `archive_header_ni` and the actual object file members.
                .enable_next_moved = true,
                .next_moved = true,
            }),
            .archive_header,
        );
        const archive_header_slice = archive_header_ni.slice(&elf.mf);
        @memcpy(archive_header_slice[0..std.elf.ARMAG.len], std.elf.ARMAG);
        const strtab_ar_hdr: *std.elf.ar_hdr = @ptrCast(archive_header_slice[std.elf.ARMAG.len..]);
        strtab_ar_hdr.* = .{
            .ar_name = std.elf.STRNAME.*,
            .ar_date = @splat(' '),
            .ar_uid = @splat(' '),
            .ar_gid = @splat(' '),
            .ar_mode = @splat(' '),
            .ar_size = undefined, // populated by `flushNextMoved` for `archive_header_ni`
            .ar_fmag = std.elf.ARFMAG.*,
        };

        elf.ni.elf = elf.addNodeAssumeCapacity(try archive_ni.addOnlyFooterChild(gpa, &elf.mf, .{
            .alignment = node_block_align.max(.@"2"),
            .bubbles_moved = false,
            .resized = true, // ensure that this node's `ar_hdr.ar_size` is updated at least once
        }), .elf);

        const elf_ar_hdr_ni = elf.addNodeAssumeCapacity(
            try archive_ni.addFooterChildBefore(gpa, &elf.mf, .wrap(elf.ni.elf), .{
                .alignment = .@"2",
                .size = @sizeOf(std.elf.ar_hdr),
            }),
            .archive_elf_member_header,
        );

        // Must be populated before we call `populateArchiveMemberName` below.
        elf.archive = .{
            .ni = archive_ni,
            .header_ni = archive_header_ni,
            .elf_member_header_ni = elf_ar_hdr_ni,

            .elf_member_too_big = false,
            .strtab_member_too_big = false,
        };

        const elf_ar_hdr: *std.elf.ar_hdr = @ptrCast(elf_ar_hdr_ni.slice(&elf.mf));
        elf_ar_hdr.* = .{
            .ar_name = undefined, // populated below
            .ar_date = "0           ".*,
            .ar_uid = "0     ".*,
            .ar_gid = "0     ".*,
            .ar_mode = "644     ".*,
            .ar_size = undefined, // populated by `flushResized` for the `.elf` node
            .ar_fmag = std.elf.ARFMAG.*,
        };
        const zcu_member_name = try std.fmt.allocPrint(gpa, "{s}_zcu.o", .{comp.root_name});
        defer gpa.free(zcu_member_name);
        // After this call returns, `elf_ar_hdr` is invalidated.
        try elf.populateArchiveMemberName(elf_ar_hdr, zcu_member_name);
    } else elf.ni.elf = elf.addNodeAssumeCapacity(.root, .elf);

    const entsize: struct { ph: u32, sh: u32 } = switch (class) {
        .NONE, _ => unreachable,
        inline else => |ct_class| .{
            .ph = @sizeOf(ct_class.ElfN().Phdr),
            .sh = @sizeOf(ct_class.ElfN().Shdr),
        },
    };

    // We want to create the segment nodes *before* the ehdr, because the ehdr should go inside of
    // the rodata segment. Although to my knowledge neither ELF nor any ELF-based OS strictly
    // requires this, it is highly conventional and therefore sometimes relied upon.
    if (@"type" != .REL) {
        // This node will contain the ehdr, which must be at the start of the ELF file, so this
        // node must itself be a header of the `.elf` node.
        elf.ni.rodata = elf.addNodeAssumeCapacity(try elf.ni.elf.addOnlyHeaderChild(gpa, &elf.mf, .{
            // Must be at least `addr_align` for `elf.ni.phdr` to be placed inside this node
            .alignment = node_block_align.max(addr_align),
            .moved = true,
            .bubbles_moved = false,
        }), .{ .segment = phndx.rodata });
        elf.phdrs.items[phndx.rodata] = .wrap(elf.ni.rodata);

        elf.ni.phdr = elf.addNodeAssumeCapacity(try elf.ni.rodata.addFloatingChild(gpa, &elf.mf, .{
            .size = @as(u64, phnum) * entsize.ph,
            .alignment = addr_align, // keep in sync with `elf.ni.rodata` alignment above
            .moved = true,
            .resized = true,
            .bubbles_moved = false,
        }), .{ .segment = phndx.phdr });
        elf.phdrs.items[phndx.phdr] = .wrap(elf.ni.phdr);

        elf.ni.text = elf.addNodeAssumeCapacity(try elf.ni.elf.addFloatingChild(gpa, &elf.mf, .{
            .alignment = node_block_align,
            .moved = true,
            .bubbles_moved = false,
        }), .{ .segment = phndx.text });
        elf.phdrs.items[phndx.text] = .wrap(elf.ni.text);

        elf.ni.data = elf.addNodeAssumeCapacity(try elf.ni.elf.addFloatingChild(gpa, &elf.mf, .{
            // Must be at least `addr_align` for `elf.ni.data_rel_ro` to be placed inside this node
            .alignment = node_block_align.max(addr_align),
            .moved = true,
            .bubbles_moved = false,
        }), .{ .segment = phndx.data });
        elf.phdrs.items[phndx.data] = .wrap(elf.ni.data);

        if (plt.got_plt == null) elf.phdrs.items[phndx.plt] = .wrap(elf.addNodeAssumeCapacity(
            try elf.ni.elf.addFloatingChild(gpa, &elf.mf, .{
                .alignment = node_block_align,
                .moved = true,
                .bubbles_moved = false,
            }),
            .{ .segment = phndx.plt },
        ));

        elf.ni.data_rel_ro = elf.addNodeAssumeCapacity(try elf.ni.data.addFloatingChild(gpa, &elf.mf, .{
            // Must be at least `addr_align` for the `PT_DYNAMIC` node to be placed inside this one
            // later (if `have_dynamic_section`). Keep in sync with `elf.ni.data` alignment above.
            .alignment = node_block_align.max(addr_align),
            .moved = true,
            .bubbles_moved = false,
        }), .{ .segment = phndx.relro });
        elf.phdrs.items[phndx.relro] = .wrap(elf.ni.data_rel_ro);

        if (comp.config.any_non_single_threaded) {
            elf.ni.tls = .wrap(elf.addNodeAssumeCapacity(
                try elf.ni.rodata.addFloatingChild(gpa, &elf.mf, .{
                    .alignment = node_block_align,
                    .moved = true,
                    .bubbles_moved = false,
                }),
                .{ .segment = phndx.tls },
            ));
            elf.phdrs.items[phndx.tls] = elf.ni.tls;
        }

        elf.phdrs.items[phndx.gnu_stack] = .none;
    } else {
        elf.ni.rodata = elf.ni.elf;
        elf.ni.text = elf.ni.elf;
        elf.ni.data = elf.ni.elf;
        elf.ni.data_rel_ro = elf.ni.elf;
        if (comp.config.any_non_single_threaded) {
            elf.ni.tls = .wrap(elf.ni.elf);
        }
    }

    switch (class) {
        .NONE, _ => unreachable,
        inline else => |ct_class| {
            const ElfN = ct_class.ElfN();
            // In loadable modules, the ehdr goes in the rodata segment, as described above.
            const parent_ni = switch (@"type") {
                .REL => elf.ni.elf,
                .DYN, .EXEC => elf.ni.rodata,
            };
            elf.ni.ehdr = elf.addNodeAssumeCapacity(try parent_ni.addOnlyHeaderChild(gpa, &elf.mf, .{
                .size = @sizeOf(ElfN.Ehdr),
                .alignment = addr_align,
            }), .ehdr);

            const ehdr: *ElfN.Ehdr = @ptrCast(@alignCast(elf.ni.ehdr.slice(&elf.mf)));
            ehdr.ident = .{
                .class = class,
                .data = data,
                .version = 1,
                .osabi = osabi,
                .abiversion = 0,
            };
            ehdr.type = @"type".toElf();
            ehdr.machine = machine.toElf();
            ehdr.version = 1;
            ehdr.entry = 0;
            ehdr.phoff = 0;
            ehdr.shoff = 0;
            ehdr.flags = switch (machine) {
                .LOONGARCH => .{ .loongarch = .{
                    .base_abi_modifier = mod: {
                        const cpu = comp.getTarget().cpu;
                        if (cpu.has(.loongarch, .d)) break :mod .d;
                        if (cpu.has(.loongarch, .f)) break :mod .f;
                        break :mod .s;
                    },
                    .abi_extension = .base,
                    .abi_version = 1,
                } },
                .SPARCV9 => .{ .sparc = .{
                    .mm = .rmo,
                    .ext = .{
                        .@"32plus" = false,
                        .sun_us1 = false,
                        .hal_r1 = false,
                        .sun_us3 = false,
                        .le_data = false,
                    },
                } },
                .X86_64 => .{ .int = 0 },
                .AARCH64, .PPC64, .RISCV => @panic(@tagName(machine)),
            };
            ehdr.ehsize = @sizeOf(ElfN.Ehdr);
            ehdr.phentsize = @sizeOf(ElfN.Phdr);
            ehdr.phnum = @min(phnum, std.elf.PN_XNUM);
            ehdr.shentsize = @sizeOf(ElfN.Shdr);
            ehdr.shnum = 1; // Only the SHN_UNDEF shdr initially---will be incremented by `addSection`
            ehdr.shstrndx = std.elf.SHN_UNDEF;
            if (elf.targetEndian() != std.lang.Endian.native) std.mem.byteSwapAllFields(ElfN.Ehdr, ehdr);
        },
    }

    elf.ni.shdr = elf.addNodeAssumeCapacity(try elf.ni.elf.addFloatingChild(gpa, &elf.mf, .{
        .size = 1 * entsize.sh, // as above, only the null shdr initially
        .alignment = addr_align,
        .moved = true,
        .resized = true,
    }), .shdr);

    switch (class) {
        .NONE, _ => unreachable,
        inline else => |ct_class| {
            const ElfN = ct_class.ElfN();
            const target_endian = elf.targetEndian();

            populate_phdrs: {
                // Initially we will give every `PT_LOAD` segment this address. When we re-allocate
                // segments in the virtual address space in `flushMoved` and `flushResized`, we will
                // move some segments to higher addresses to prevent overlap. This address therefore
                // becomes the image's "base address"; i.e. the first `PT_LOAD` segment will start
                // at this address. The base address could eventually end up higher than this due to
                // how we re-allocate the address space, but never lower.
                const base_vaddr: u64 = switch (@"type") {
                    .REL => break :populate_phdrs,
                    .DYN => 0,
                    .EXEC => switch (machine) {
                        .AARCH64 => 0x200000,
                        .LOONGARCH => 0x10000,
                        .PPC64 => 0x10000000,
                        .RISCV => 0x10000,
                        .SPARCV9 => 0x100000,
                        .X86_64 => 0x200000,
                    },
                };

                // All `PT_LOAD` segments are given this `.@"align"`. However, to avoid bloating the
                // binary, their *nodes* are not aligned to this boundary---ELF only requires that
                // ecah segment's address equals its file offset modulo this alignment, not that its
                // file offset is actually aligned to this boundary. This property is maintained by
                // the segment virtual address space allocation logic.
                const page_align = elf.targetPageAlign();

                // We will populate elements in this slice (by index). The `PT_LOAD` segments are
                // actually `PT_NULL` for now, because we initialize `filesz` and `memsz` to zero.
                // Any which end up non-empty will have their size populated (and their type set to
                // `PT_LOAD`) by the segment virtual address space allocation logic.
                const phdr: []ElfN.Phdr = @ptrCast(@alignCast(
                    elf.ni.phdr.slice(&elf.mf)[0 .. phnum * @sizeOf(ElfN.Phdr)],
                ));

                phdr[phndx.phdr] = .{
                    .type = .PHDR,
                    .offset = 0,
                    .vaddr = 0,
                    .paddr = 0,
                    .filesz = 0,
                    .memsz = 0,
                    .flags = .{ .R = true },
                    .@"align" = @intCast(elf.ni.phdr.alignment(&elf.mf).toByteUnits()),
                };

                if (maybe_interp) |_| phdr[phndx.interp] = .{
                    .type = .INTERP,
                    .offset = 0,
                    .vaddr = 0,
                    .paddr = 0,
                    .filesz = 0,
                    .memsz = 0,
                    .flags = .{ .R = true },
                    .@"align" = 1,
                };

                phdr[phndx.rodata] = .{
                    .type = .NULL,
                    .offset = 0,
                    .vaddr = @intCast(base_vaddr),
                    .paddr = @intCast(base_vaddr),
                    .filesz = 0,
                    .memsz = 0,
                    .flags = .{ .R = true },
                    .@"align" = @intCast(page_align.toByteUnits()),
                };

                phdr[phndx.text] = .{
                    .type = .NULL,
                    .offset = 0,
                    .vaddr = @intCast(base_vaddr),
                    .paddr = @intCast(base_vaddr),
                    .filesz = 0,
                    .memsz = 0,
                    .flags = .{ .R = true, .X = true },
                    .@"align" = @intCast(page_align.toByteUnits()),
                };

                phdr[phndx.data] = .{
                    .type = .NULL,
                    .offset = 0,
                    .vaddr = @intCast(base_vaddr),
                    .paddr = @intCast(base_vaddr),
                    .filesz = 0,
                    .memsz = 0,
                    .flags = .{ .R = true, .W = true },
                    .@"align" = @intCast(page_align.toByteUnits()),
                };

                if (plt.got_plt == null) phdr[phndx.plt] = .{
                    .type = .NULL,
                    .offset = 0,
                    .vaddr = @intCast(base_vaddr),
                    .paddr = @intCast(base_vaddr),
                    .filesz = 0,
                    .memsz = 0,
                    .flags = .{ .R = true, .W = true, .X = true },
                    .@"align" = @intCast(page_align.toByteUnits()),
                };

                if (elf.ni.tls.unwrap()) |tls_segment_ni| phdr[phndx.tls] = .{
                    .type = .TLS,
                    .offset = 0,
                    .vaddr = 0,
                    .paddr = 0,
                    .filesz = 0,
                    .memsz = 0,
                    .flags = .{ .R = true },
                    .@"align" = @intCast(tls_segment_ni.alignment(&elf.mf).toByteUnits()),
                };

                if (have_dynamic) phdr[phndx.dynamic] = .{
                    .type = .DYNAMIC,
                    .offset = 0,
                    .vaddr = 0,
                    .paddr = 0,
                    .filesz = 0,
                    .memsz = 0,
                    .flags = .{ .R = true, .W = true },
                    .@"align" = @intCast(addr_align.toByteUnits()),
                };

                phdr[phndx.relro] = .{
                    .type = .GNU_RELRO,
                    .offset = 0,
                    .vaddr = 0,
                    .paddr = 0,
                    .filesz = 0,
                    .memsz = 0,
                    .flags = .{ .R = true },
                    .@"align" = @intCast(elf.ni.data_rel_ro.alignment(&elf.mf).toByteUnits()),
                };

                if (have_eh_frame) phdr[phndx.gnu_eh_frame] = .{
                    .type = .GNU_EH_FRAME,
                    .offset = 0,
                    .vaddr = 0,
                    .paddr = 0,
                    .filesz = @sizeOf(Dwarf.EhFrameHdr),
                    .memsz = @sizeOf(Dwarf.EhFrameHdr),
                    .flags = .{ .R = true },
                    .@"align" = 4,
                };

                phdr[phndx.gnu_stack] = .{
                    .type = .GNU_STACK,
                    .offset = 0,
                    .vaddr = 0,
                    .paddr = 0,
                    .filesz = 0,
                    .memsz = @intCast(elf.options.stack_size orelse 0),
                    .flags = .{ .R = true, .W = true },
                    .@"align" = 1,
                };

                if (target_endian != std.lang.Endian.native) {
                    std.mem.byteSwapAllElements(ElfN.Phdr, phdr);
                }
            }

            const sh_undef: *ElfN.Shdr = @ptrCast(@alignCast(elf.ni.shdr.slice(&elf.mf)));
            sh_undef.* = .{
                .name = @backingInt(String(.shstrtab).empty),
                .type = .NULL,
                .flags = .{ .shf = .{} },
                .addr = 0,
                .offset = 0,
                .size = if (shnum < std.elf.SHN_LORESERVE) 0 else shnum,
                .link = 0,
                .info = if (phnum < std.elf.PN_XNUM) 0 else phnum,
                .addralign = 0,
                .entsize = 0,
            };
            if (target_endian != std.lang.Endian.native) std.mem.byteSwapAllFields(ElfN.Shdr, sh_undef);

            elf.symtab.addOneAssumeCapacity().* = .{
                .node = .none,
                .first_target_reloc = .none,
            };
            assert(.symtab == try elf.addSection(elf.ni.elf, .{
                .type = .SYMTAB,
                .size = @sizeOf(ElfN.Sym) * 1,
                .addralign = addr_align,
                .entsize = @sizeOf(ElfN.Sym),
                .node_align = node_block_align,
                .info = 1, // index of first non-local symbol
                .manual_size = true,
            }));
            const symtab_null = @field(elf.symPtr(.null), @tagName(ct_class));
            symtab_null.* = .{
                .name = @backingInt(String(.strtab).empty),
                .value = 0,
                .size = 0,
                .info = .{ .type = .NOTYPE, .bind = .LOCAL },
                .other = .{ .visibility = .DEFAULT },
                .shndx = std.elf.SHN_UNDEF,
            };
            if (target_endian != std.lang.Endian.native) std.mem.byteSwapAllFields(ElfN.Sym, symtab_null);

            const ehdr = @field(elf.ehdrPtr(), @tagName(ct_class));
            ehdr.shstrndx = ehdr.shnum;
        },
    }
    assert(.shstrtab == try elf.addSection(elf.ni.elf, .{
        .type = .STRTAB,
        .size = 1,
        .entsize = 1,
        .node_align = node_block_align,
        .manual_size = true,
    }));
    Section.Index.get(.shstrtab, elf).ni.slice(&elf.mf)[0] = 0;

    try Section.Index.symtab.rename(elf, ".symtab");
    try Section.Index.shstrtab.rename(elf, ".shstrtab");

    assert(.strtab == try elf.addSection(elf.ni.elf, .{
        .name = ".strtab",
        .type = .STRTAB,
        .size = 1,
        .entsize = 1,
        .node_align = node_block_align,
        .manual_size = true,
    }));
    Section.Index.get(.strtab, elf).ni.slice(&elf.mf)[0] = 0;
    switch (elf.shdrPtr(.symtab)) {
        inline else => |shdr| elf.targetStore(&shdr.link, @backingInt(Section.Index.strtab)),
    }

    assert(.rodata == try elf.addSection(elf.ni.rodata, .{
        .name = ".rodata",
        .flags = .{ .ALLOC = true },
        .node_align = node_block_align,
    }));
    assert(.text == try elf.addSection(elf.ni.text, .{
        .name = ".text",
        .flags = .{ .ALLOC = true, .EXECINSTR = true },
        .node_align = node_block_align,
    }));
    assert(.data == try elf.addSection(elf.ni.data, .{
        .name = ".data",
        .flags = .{ .WRITE = true, .ALLOC = true },
        .node_align = node_block_align,
    }));
    assert(.data_rel_ro == try elf.addSection(elf.ni.data_rel_ro, .{
        .name = ".data.rel.ro",
        .flags = .{ .WRITE = true, .ALLOC = true },
        .node_align = node_block_align,
    }));
    if (@"type" != .REL) {
        elf.shndx.got = try elf.addSection(elf.ni.data_rel_ro, .{
            .name = ".got",
            .type = .PROGBITS,
            // Reserve space for the reserved words, populated later.
            .size = switch (machine) {
                .AARCH64, .PPC64, .RISCV => @panic(@tagName(machine)),
                .X86_64 => 3 * elf.targetPtrSize(),
                .LOONGARCH, .SPARCV9 => elf.targetPtrSize(),
            },
            .flags = .{ .WRITE = true, .ALLOC = true },
            .addralign = addr_align,
            .entsize = @intCast(addr_align.toByteUnits()),
            .manual_size = true,
        });
        {
            const init_plt_size = plt.entry_size * plt.header_entries;
            if (plt.got_plt) |got_plt| {
                const got_plt_segment_ni = if (elf.options.z_now) elf.ni.data_rel_ro else elf.ni.data;
                elf.shndx.got_plt = try elf.addSection(got_plt_segment_ni, .{
                    .name = ".got.plt",
                    .type = .PROGBITS,
                    .flags = .{ .WRITE = true, .ALLOC = true },
                    .size = got_plt.header_entries * elf.targetPtrSize(),
                    .addralign = addr_align,
                    .entsize = @intCast(addr_align.toByteUnits()),
                    .manual_size = true,
                });
                elf.shndx.plt = try elf.addSection(elf.ni.text, .{
                    .name = ".plt",
                    .type = .PROGBITS,
                    .flags = .{ .ALLOC = true, .EXECINSTR = true },
                    .size = plt.@"align".forward(init_plt_size),
                    .addralign = plt.@"align",
                    .node_align = node_block_align,
                    .manual_size = true,
                });
            } else {
                elf.shndx.plt = try elf.addSection(elf.phdrs.items[phndx.plt].unwrap().?, .{
                    .name = ".plt",
                    .type = .PROGBITS,
                    .flags = .{ .ALLOC = true, .WRITE = true, .EXECINSTR = true },
                    .size = plt.@"align".forward(init_plt_size),
                    .addralign = plt.@"align",
                    .node_align = node_block_align,
                    .manual_size = true,
                });
            }
            // And the award for most annoying PLT requirement goes to SPARC, which decided that the
            // whole table should have a greater alignment than the size of the individual entries,
            // hence this bullshit:
            if (plt.@"align".forward(init_plt_size) != init_plt_size) {
                switch (elf.shdrPtr(elf.shndx.plt)) {
                    inline else => |shdr| elf.targetStore(&shdr.size, init_plt_size),
                }
            }
        }
        if (plt.plt_sec != null) elf.shndx.plt_sec = try elf.addSection(elf.ni.text, .{
            .name = ".plt.sec",
            .flags = .{ .ALLOC = true, .EXECINSTR = true },
            .addralign = plt.@"align",
            .node_align = node_block_align,
        });
        if (maybe_interp) |interp| {
            const interp_ni = elf.addNodeAssumeCapacity(
                try elf.ni.rodata.addFloatingChild(gpa, &elf.mf, .{
                    .size = interp.len + 1,
                    .moved = true,
                    .resized = true,
                    .bubbles_moved = false,
                }),
                .{ .segment = phndx.interp },
            );
            elf.phdrs.items[phndx.interp] = .wrap(interp_ni);

            const sec_interp_shndx = try elf.addSection(interp_ni, .{
                .name = ".interp",
                .type = .PROGBITS,
                .flags = .{ .ALLOC = true },
                .size = @intCast(interp.len + 1),
            });
            const sec_interp = sec_interp_shndx.get(elf).ni.slice(&elf.mf);
            @memcpy(sec_interp[0..interp.len], interp);
            sec_interp[interp.len] = 0;
        }
        if (have_dynamic) {
            assert(elf.ni.data_rel_ro.alignment(&elf.mf).compare(.gte, addr_align));
            const dynamic_ni = elf.addNodeAssumeCapacity(
                try elf.ni.data_rel_ro.addFloatingChild(gpa, &elf.mf, .{
                    .alignment = addr_align,
                    .moved = true,
                    .bubbles_moved = false,
                }),
                .{ .segment = phndx.dynamic },
            );
            elf.phdrs.items[phndx.dynamic] = .wrap(dynamic_ni);

            const dynstr_shndx = try elf.addSection(elf.ni.rodata, .{
                .name = ".dynstr",
                .type = .STRTAB,
                .flags = .{ .ALLOC = true },
                .size = 1,
                .entsize = 1,
                .node_align = node_block_align,
                .manual_size = true,
            });
            dynstr_shndx.get(elf).ni.slice(&elf.mf)[0] = 0;
            elf.shndx.dynstr = dynstr_shndx;

            switch (class) {
                .NONE, _ => unreachable,
                inline else => |ct_class| {
                    const Sym = ct_class.ElfN().Sym;
                    elf.shndx.dynsym = try elf.addSection(elf.ni.rodata, .{
                        .name = ".dynsym",
                        .type = .DYNSYM,
                        .flags = .{ .ALLOC = true },
                        .size = @sizeOf(Sym) * 1,
                        .link = dynstr_shndx.toSection().?,
                        .info = 1,
                        .addralign = addr_align,
                        .entsize = @sizeOf(Sym),
                        .node_align = node_block_align,
                        .manual_size = true,
                    });
                    const dynsym_null = @field(elf.dynsymPtr(0), @tagName(ct_class));
                    dynsym_null.* = .{
                        .name = @backingInt(String(.dynstr).empty),
                        .value = 0,
                        .size = 0,
                        .info = .{ .type = .NOTYPE, .bind = .LOCAL },
                        .other = .{ .visibility = .DEFAULT },
                        .shndx = std.elf.SHN_UNDEF,
                    };
                    if (elf.targetEndian() != std.lang.Endian.native) std.mem.byteSwapAllFields(
                        Sym,
                        dynsym_null,
                    );
                },
            }
            const rela_size: std.elf.Word = switch (class) {
                .NONE, _ => unreachable,
                inline else => |ct_class| @sizeOf(ct_class.ElfN().Rela),
            };
            elf.shndx.rela_dyn = try elf.addSection(elf.ni.rodata, .{
                .name = ".rela.dyn",
                .type = .RELA,
                .flags = .{ .ALLOC = true },
                .link = elf.shndx.dynsym.toSection().?,
                .addralign = addr_align,
                .entsize = rela_size,
                .node_align = node_block_align,
                .manual_size = true,
            });
            elf.shndx.rela_plt = try elf.addSection(elf.ni.rodata, .{
                .name = ".rela.plt",
                .type = .RELA,
                .flags = .{ .ALLOC = true, .INFO_LINK = true },
                .link = elf.shndx.dynsym.toSection().?,
                .info = (if (plt.got_plt != null) elf.shndx.got_plt else elf.shndx.plt).toSection().?,
                .addralign = addr_align,
                .entsize = rela_size,
                .node_align = node_block_align,
                .manual_size = true,
            });
            elf.shndx.dynamic = try elf.addSection(dynamic_ni, .{
                .name = ".dynamic",
                .type = .DYNAMIC,
                .flags = .{ .ALLOC = true, .WRITE = true },
                .link = dynstr_shndx.toSection().?,
                .entsize = @intCast(addr_align.toByteUnits() * 2),
                .addralign = addr_align,
                .manual_size = true,
            });

            elf.shndx.gnu_version = try elf.addSection(elf.ni.rodata, .{
                .name = ".gnu.version",
                .type = .GNU_VERSYM,
                .flags = .{ .ALLOC = true },
                .size = @sizeOf(std.elf.Versym), // "null" dynsym entry
                .link = elf.shndx.dynsym.toSection().?,
                .addralign = .@"2",
                .entsize = @sizeOf(std.elf.Versym),
                .manual_size = true,
            });
            elf.targetStore(&elf.versymSlice()[0], .LOCAL); // "null" dynsym entry

            elf.shndx.gnu_version_d = try elf.addSection(elf.ni.rodata, .{
                .name = ".gnu.version_d",
                .type = .GNU_VERDEF,
                .flags = .{ .ALLOC = true },
                .link = elf.shndx.dynstr.toSection().?,
                // It's completely undocumented outside of source code, but `sh_info` for this
                // section must hold the number of `Verdef` entries.
                .info = 1,
                .addralign = .@"4",
                .size = @sizeOf(VerdefEntry), // "file" entry
                .manual_size = true,
            });
            {
                const base_version_name: []const u8 = elf.options.soname orelse comp.root_name;
                const entry_ptr = &elf.verdefSlice()[0];
                entry_ptr.* = .{
                    .def = .{
                        .version = 1,
                        .flags = std.elf.VER_FLG_BASE,
                        .ndx = .GLOBAL,
                        .cnt = 1,
                        .hash = std.elf.hash.calculate(base_version_name),
                        .aux = @offsetOf(VerdefEntry, "aux"),
                        .next = 0,
                    },
                    .aux = .{
                        .name = @backingInt(try elf.string(.dynstr, base_version_name)),
                        .next = 0,
                    },
                };
                if (elf.targetEndian() != std.lang.Endian.native) {
                    std.mem.byteSwapAllFields(VerdefEntry, entry_ptr);
                }
            }

            elf.shndx.gnu_version_r = try elf.addSection(elf.ni.rodata, .{
                .name = ".gnu.version_r",
                .type = .GNU_VERNEED,
                .flags = .{ .ALLOC = true },
                .link = elf.shndx.dynstr.toSection().?,
                // It's completely undocumented outside of source code, but `sh_info` for this
                // section must hold the number of `Verneed` entries.
                .info = 0,
                .addralign = .@"4",
                .size = 0,
                .manual_size = true,
            });

            switch (elf.targetDynsymHashInfo()) {
                inline else => |info| {
                    elf.shndx.hash = try elf.addSection(elf.ni.rodata, .{
                        .name = ".hash",
                        .type = .HASH,
                        .flags = .{ .ALLOC = true },
                        .link = elf.shndx.dynsym.toSection().?,
                        // It's unclear what value is correct for the alignment. binutils uses 8 everywhere,
                        // while lld uses 4 everywhere (but lld lacks support for the alpha/s390x special
                        // case). Matching the hash word (= entry) size seems like the actually sane choice,
                        // and is what mold does too.
                        .addralign = .fromByteUnits(@sizeOf(info.Int())),
                        // initially: nbucket = 8 + nchain = 1
                        .size = @sizeOf(info.Header()) + @sizeOf(info.Int()) * (8 + 1),
                        .manual_size = true,
                    });
                    const hash_slice: []align(@sizeOf(info.Int())) u8 = @alignCast(elf.shndx.hash.get(elf).ni.slice(&elf.mf));
                    const header: *info.Header() = @ptrCast(hash_slice[0..@sizeOf(info.Header())]);
                    header.* = .{ .nbucket = 8, .nchain = 1 };
                    if (elf.targetEndian() != std.lang.Endian.native) {
                        std.mem.byteSwapAllFields(info.Header(), header);
                    }
                    // The initial bucket and chain values are all 0.
                    @memset(hash_slice[@sizeOf(info.Header())..], 0);
                },
            }

            switch (machine) {
                .AARCH64, .PPC64, .RISCV => @panic(@tagName(machine)),
                .X86_64 => {
                    const plt_ni = elf.shndx.plt.get(elf).ni;
                    const got_plt_sym: Symbol.Id = .local(elf.shndx.got_plt.get(elf).lsi);
                    @memcpy(plt_ni.slice(&elf.mf)[0..16], &[16]u8{
                        0xff, 0x35, 0x00, 0x00, 0x00, 0x00, // push 0x0(%rip)
                        0xff, 0x25, 0x00, 0x00, 0x00, 0x00, // jmp *0x0(%rip)
                        0x0f, 0x1f, 0x40, 0x00, // nopl 0x0(%rax)
                    });
                    elf.plt_first_symbol_reloc = @fromBackingInt(@intCast(elf.symbol_relocs.items.len));
                    try elf.ensureUnusedRelocCapacity(plt_ni, 2);
                    try elf.addSymbolRelocAssumeCapacity(
                        plt_ni,
                        2,
                        got_plt_sym,
                        8 * 1 - 4,
                        .simple(.rel, .{ .dest = .@"32", .cast = .signed, .shift = .@"0" }),
                    );
                    try elf.addSymbolRelocAssumeCapacity(
                        plt_ni,
                        8,
                        got_plt_sym,
                        8 * 2 - 4,
                        .simple(.rel, .{ .dest = .@"32", .cast = .signed, .shift = .@"0" }),
                    );
                },
                .LOONGARCH => {
                    const plt_ni = elf.shndx.plt.get(elf).ni;
                    const got_plt_sym: Symbol.Id = .local(elf.shndx.got_plt.get(elf).lsi);
                    @memcpy(plt_ni.slice(&elf.mf)[0..32], switch (class) {
                        .NONE, _ => unreachable,
                        .@"32" => &[32]u8{
                            0x1a, 0x00, 0x00, 0x0e, // pcalau12i $t2, %pc_hi20(.got.plt)
                            0x00, 0x11, 0x3d, 0xad, // sub.w     $t1, $t1, $t3
                            0x28, 0x80, 0x01, 0xcf, // ld.w      $t3, $t2, %lo12(.got.plt) # _dl_runtime_resolve
                            0x02, 0xbf, 0x51, 0xad, // addi.w    $t1, $t1, -44             # .plt entry
                            0x02, 0x80, 0x01, 0xcc, // addi.w    $t0, $t2, %lo12(.got.plt) # &.got.plt
                            0x00, 0x44, 0x89, 0xad, // srli.w    $t1, $t1, 2               # .plt entry offset
                            0x28, 0x80, 0x11, 0x8c, // ld.w      $t0, $t0, 4               # link map
                            0x4c, 0x00, 0x01, 0xe0, // jr        $t3
                        },
                        .@"64" => &[32]u8{
                            0x1a, 0x00, 0x00, 0x0e, // pcalau12i $t2, %pc_hi20(.got.plt)
                            0x00, 0x11, 0xbd, 0xad, // sub.d     $t1, $t1, $t3
                            0x28, 0xc0, 0x01, 0xcf, // ld.d      $t3, $t2, %lo12(.got.plt) # _dl_runtime_resolve
                            0x02, 0xff, 0x51, 0xad, // addi.d    $t1, $t1, -44             # .plt entry
                            0x02, 0xc0, 0x01, 0xcc, // addi.d    $t0, $t2, %lo12(.got.plt) # &.got.plt
                            0x00, 0x45, 0x05, 0xad, // srli.d    $t1, $t1, 1               # .plt entry offset
                            0x28, 0xc0, 0x21, 0x8c, // ld.d      $t0, $t0, 8               # link map
                            0x4c, 0x00, 0x01, 0xe0, // jr        $t3
                        },
                    });
                    elf.plt_first_symbol_reloc = @fromBackingInt(@intCast(elf.symbol_relocs.items.len));
                    try elf.ensureUnusedRelocCapacity(plt_ni, 3);
                    elf.addRelocAssumeCapacity(plt_ni, 0, got_plt_sym, 0, .{ .LARCH = .PCALA_HI20 }) catch |err| switch (err) {
                        else => |e| return e,
                        error.UnknownRelocation => unreachable,
                        error.NonStaticRelocation => unreachable,
                        error.UnimplementedRelocation => unreachable,
                    };
                    elf.addRelocAssumeCapacity(plt_ni, 8, got_plt_sym, 0, .{ .LARCH = .PCALA_LO12 }) catch |err| switch (err) {
                        else => |e| return e,
                        error.UnknownRelocation => unreachable,
                        error.NonStaticRelocation => unreachable,
                        error.UnimplementedRelocation => unreachable,
                    };
                    elf.addRelocAssumeCapacity(plt_ni, 16, got_plt_sym, 0, .{ .LARCH = .PCALA_LO12 }) catch |err| switch (err) {
                        else => |e| return e,
                        error.UnknownRelocation => unreachable,
                        error.NonStaticRelocation => unreachable,
                        error.UnimplementedRelocation => unreachable,
                    };
                },
                .SPARCV9 => {},
            }
        }
        if (have_eh_frame) {
            const gnu_eh_frame = elf.addNodeAssumeCapacity(
                try elf.ni.rodata.addFloatingChild(gpa, &elf.mf, .{
                    .size = @sizeOf(Dwarf.EhFrameHdr),
                    .alignment = .@"4",
                    .moved = true,
                    .bubbles_moved = false,
                }),
                .{ .segment = phndx.gnu_eh_frame },
            );
            elf.ni.gnu_eh_frame = .wrap(gnu_eh_frame);
            elf.phdrs.items[phndx.gnu_eh_frame] = elf.ni.gnu_eh_frame;

            elf.shndx.eh_frame_hdr = try elf.addSection(gnu_eh_frame, .{
                .name = ".eh_frame_hdr",
                .type = .PROGBITS,
                .flags = .{ .ALLOC = true },
                .size = @sizeOf(Dwarf.EhFrameHdr),
                .addralign = .@"4",
            });
            elf.shndx.eh_frame = try elf.addSection(elf.ni.rodata, .{
                .name = ".eh_frame",
                .flags = .{ .ALLOC = true },
                .addralign = addr_align,
                .node_align = elf.mf.flags.block_size,
                .manual_size = true,
            });

            const eh_frame_hdr_ni = elf.shndx.eh_frame_hdr.get(elf).ni;
            elf.eh_frame_hdr_first_symbol_reloc =
                @fromBackingInt(@intCast(elf.symbol_relocs.items.len));
            try elf.dwarf.genEhFrameHdr(
                Node.toAtom(eh_frame_hdr_ni),
                @ptrCast(@alignCast(eh_frame_hdr_ni.slice(&elf.mf))),
                Symbol.Id.local(elf.shndx.eh_frame.get(elf).lsi).toTypeErased(),
            );
            _ = elf.addNodeAssumeCapacity(
                try elf.shndx.eh_frame.get(elf).ni.addOnlyFooterChild(gpa, &elf.mf, .{
                    .size = addr_align.forward(4),
                    .alignment = addr_align,
                }),
                .eh_frame_footer,
            );
        }

        // Populate reserved GOT words.
        switch (machine) {
            .AARCH64, .PPC64, .RISCV => @panic(@tagName(machine)),
            .X86_64 => {
                try elf.got.ensureUnusedCapacity(gpa, 3);
                elf.got.putAssumeCapacityNoClobber(switch (have_dynamic) {
                    true => .{ .symbol = .local(elf.shndx.dynamic.get(elf).lsi) },
                    false => .{ .reserved = 0 },
                }, .none);
                elf.got.putAssumeCapacityNoClobber(.{ .reserved = 1 }, .none);
                elf.got.putAssumeCapacityNoClobber(.{ .reserved = 2 }, .none);
            },
            .LOONGARCH, .SPARCV9 => {
                try elf.got.ensureUnusedCapacity(gpa, 1);
                elf.got.putAssumeCapacityNoClobber(switch (have_dynamic) {
                    true => .{ .symbol = .local(elf.shndx.dynamic.get(elf).lsi) },
                    false => .{ .reserved = 0 },
                }, .none);
            },
        }
        switch (elf.shdrPtr(elf.shndx.got)) {
            inline else => |shdr, ct_class| {
                const Addr = ct_class.ElfN().Addr;
                assert(elf.targetLoad(&shdr.size) == elf.got.count() * @sizeOf(Addr));
            },
        }
        if (elf.shndx.dynamic != .UNDEF) {
            try elf.shndx.rela_dyn.relaEnsureAdditionalCapacity(elf, elf.got.count());
        }
        for (0..elf.got.count()) |got_index| {
            elf.updateGotEntry(got_index);
        }

        // Create any always-provided linker-defined symbols. The symbols marking the `INIT_ARRAY`/
        // `FINI_ARRAY`/`PREINIT_ARRAY` sections are instead created by `createInitFiniArraySection`
        // when needed (it seems to be legal to leave those undefined if the section doesn't exist).

        // Despite the name, `__dso_handle` is necessary even in static binaries.
        _ = elf.addGlobalSymbol(.{
            .node = .wrap(Section.Index.text.get(elf).ni),
            .name = "__dso_handle",
            .value = Section.Index.text.vaddr(elf),
            .size = 0,
            .type = .NOTYPE,
            .bind = .weak,
            .visibility = .HIDDEN,
            .shndx = .text,
        }) catch |err| switch (err) {
            error.MultipleDefinitions => unreachable, // no inputs are processed yet
            error.MultipleDefaultVersions => unreachable, // not a versioned symbol
            error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
            else => |e| return e,
        };
        _ = elf.addGlobalSymbol(.{
            .node = .wrap(elf.shndx.plt.get(elf).ni),
            .name = "_PROCEDURE_LINKAGE_TABLE_",
            .value = elf.shndx.plt.vaddr(elf),
            .size = 0,
            .type = .NOTYPE,
            .bind = .strong,
            .visibility = .HIDDEN,
            .shndx = elf.shndx.plt,
        }) catch |err| switch (err) {
            error.MultipleDefinitions => unreachable, // no inputs are processed yet
            error.MultipleDefaultVersions => unreachable, // not a versioned symbol
            error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
            else => |e| return e,
        };
        _ = elf.addGlobalSymbol(.{
            .node = .wrap(elf.shndx.got.get(elf).ni),
            .name = "_GLOBAL_OFFSET_TABLE_",
            .value = switch (machine) {
                .AARCH64,
                .LOONGARCH,
                .PPC64,
                .RISCV,
                .SPARCV9,
                => elf.shndx.got.vaddr(elf),

                //.QDSP6,
                //.@"386",
                .X86_64,
                => elf.shndx.got_plt.vaddr(elf),
            },
            .size = 0,
            .type = .NOTYPE,
            .bind = .strong,
            .visibility = .HIDDEN,
            .shndx = elf.shndx.got,
        }) catch |err| switch (err) {
            error.MultipleDefinitions => unreachable, // no inputs are processed yet
            error.MultipleDefaultVersions => unreachable, // not a versioned symbol
            error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
            else => |e| return e,
        };
        _ = elf.addGlobalSymbol(.{
            .node = .none,
            .name = "__init_array_start",
            .value = 0,
            .size = 0,
            .type = .NOTYPE,
            .bind = .strong,
            .visibility = .HIDDEN,
            .shndx = .ABS,
        }) catch |err| switch (err) {
            error.MultipleDefinitions => unreachable, // no inputs are processed yet
            error.MultipleDefaultVersions => unreachable, // not a versioned symbol
            error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
            else => |e| return e,
        };
        _ = elf.addGlobalSymbol(.{
            .node = .none,
            .name = "__init_array_end",
            .value = 0,
            .size = 0,
            .type = .NOTYPE,
            .bind = .strong,
            .visibility = .HIDDEN,
            .shndx = .ABS,
        }) catch |err| switch (err) {
            error.MultipleDefinitions => unreachable, // no inputs are processed yet
            error.MultipleDefaultVersions => unreachable, // not a versioned symbol
            error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
            else => |e| return e,
        };
        _ = elf.addGlobalSymbol(.{
            .node = .none,
            .name = "__fini_array_start",
            .value = 0,
            .size = 0,
            .type = .NOTYPE,
            .bind = .strong,
            .visibility = .HIDDEN,
            .shndx = .ABS,
        }) catch |err| switch (err) {
            error.MultipleDefinitions => unreachable, // no inputs are processed yet
            error.MultipleDefaultVersions => unreachable, // not a versioned symbol
            error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
            else => |e| return e,
        };
        _ = elf.addGlobalSymbol(.{
            .node = .none,
            .name = "__fini_array_end",
            .value = 0,
            .size = 0,
            .type = .NOTYPE,
            .bind = .strong,
            .visibility = .HIDDEN,
            .shndx = .ABS,
        }) catch |err| switch (err) {
            error.MultipleDefinitions => unreachable, // no inputs are processed yet
            error.MultipleDefaultVersions => unreachable, // not a versioned symbol
            error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
            else => |e| return e,
        };
        _ = elf.addGlobalSymbol(.{
            .node = .none,
            .name = "__preinit_array_start",
            .value = 0,
            .size = 0,
            .type = .NOTYPE,
            .bind = .strong,
            .visibility = .HIDDEN,
            .shndx = .ABS,
        }) catch |err| switch (err) {
            error.MultipleDefinitions => unreachable, // no inputs are processed yet
            error.MultipleDefaultVersions => unreachable, // not a versioned symbol
            error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
            else => |e| return e,
        };
        _ = elf.addGlobalSymbol(.{
            .node = .none,
            .name = "__preinit_array_end",
            .value = 0,
            .size = 0,
            .type = .NOTYPE,
            .bind = .strong,
            .visibility = .HIDDEN,
            .shndx = .ABS,
        }) catch |err| switch (err) {
            error.MultipleDefinitions => unreachable, // no inputs are processed yet
            error.MultipleDefaultVersions => unreachable, // not a versioned symbol
            error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
            else => |e| return e,
        };
        if (have_dynamic) {
            _ = elf.addGlobalSymbol(.{
                .node = .wrap(elf.shndx.dynamic.get(elf).ni),
                .name = "_DYNAMIC",
                .value = elf.shndx.dynamic.vaddr(elf),
                .size = 0,
                .type = .NOTYPE,
                .bind = .strong,
                .visibility = .HIDDEN,
                .shndx = elf.shndx.dynamic,
            }) catch |err| switch (err) {
                error.MultipleDefinitions => unreachable, // no inputs are processed yet
                error.MultipleDefaultVersions => unreachable, // not a versioned symbol
                error.UndefinedDefaultVersion => unreachable, // not a versioned symbol
                else => |e| return e,
            };
        }
    } else {
        assert(maybe_interp == null);
        assert(!have_dynamic);
        if (have_eh_frame) elf.shndx.eh_frame = try elf.addSection(elf.ni.rodata, .{
            .name = ".eh_frame",
            .type = if (machine == .X86_64) .X86_64_UNWIND else .NULL,
            .flags = .{ .ALLOC = true },
            .addralign = addr_align,
            .node_align = elf.mf.flags.block_size,
            .manual_size = true,
        });
    }
    if (elf.ni.tls.unwrap()) |tls_segment_ni| elf.shndx.tdata = try elf.addSection(tls_segment_ni, .{
        .name = ".tdata",
        .flags = .{ .WRITE = true, .ALLOC = true, .TLS = true },
        .node_align = node_block_align,
    });
    switch (comp.config.debug_format) {
        .strip => {},
        .dwarf => {
            elf.shndx.debug_abbrev = try elf.addSection(elf.ni.elf, .{ .name = ".debug_abbrev" });
            elf.shndx.debug_addr = try elf.addSection(elf.ni.elf, .{
                .name = ".debug_addr",
                .addralign = addr_align,
                .node_align = elf.mf.flags.block_size,
            });
            if (have_debug_frame) elf.shndx.debug_frame = try elf.addSection(elf.ni.elf, .{
                .name = ".debug_frame",
                .addralign = addr_align,
                .node_align = elf.mf.flags.block_size,
                .manual_size = true,
            });
            elf.shndx.debug_info = try elf.addSection(elf.ni.elf, .{
                .name = ".debug_info",
                .node_align = elf.mf.flags.block_size,
            });
            elf.shndx.debug_line = try elf.addSection(elf.ni.elf, .{
                .name = ".debug_line",
                .node_align = elf.mf.flags.block_size,
            });
            elf.shndx.debug_line_str = try elf.addSection(elf.ni.elf, .{
                .name = ".debug_line_str",
                .flags = .{ .MERGE = true, .STRINGS = true },
            });
            elf.shndx.debug_rnglists = try elf.addSection(elf.ni.elf, .{
                .name = ".debug_rnglists",
                .node_align = elf.mf.flags.block_size,
            });
            elf.shndx.debug_str = try elf.addSection(elf.ni.elf, .{
                .name = ".debug_str",
                .flags = .{ .MERGE = true, .STRINGS = true },
            });
            elf.shndx.debug_str_offsets = try elf.addSection(elf.ni.elf, .{
                .name = ".debug_str_offsets",
                .addralign = switch (elf.dwarf.format) {
                    .@"32" => .@"4",
                    .@"64" => .@"8",
                },
                .node_align = elf.mf.flags.block_size,
            });
        },
        .code_view => unreachable,
    }

    assert(elf.nodes.len == expected_nodes_len);
    assert(elf.shdrs.items.len == shnum - 1); // -1 to exclude SHN_UNDEF

    for (1..shnum) |shndx_raw| { // start at 1 to exclude SHN_UNDEF
        const shndx: Section.Index = @fromBackingInt(@intCast(shndx_raw));
        elf.section_by_name.putAssumeCapacityNoClobber(shndx.name(elf), {});
    }

    if (have_dynamic) elf.dynamic = .{
        .flags = if (elf.options.z_now) std.elf.DF_BIND_NOW else 0,
        .flags_1 = f: {
            var f: u32 = 0;
            if (elf.options.z_now) f |= std.elf.DF_1_NOW;
            if (comp.config.output_mode == .Exe and comp.config.pie) f |= std.elf.DF_1_PIE;
            break :f f;
        },
        .rpath = str: {
            var buf: std.ArrayList(u8) = .empty;
            defer buf.deinit(gpa);
            for (elf.options.rpath_list, 0..) |path, i| {
                if (i > 0) try buf.append(gpa, ':');
                try buf.appendSlice(gpa, path);
            }
            break :str try elf.string(.dynstr, buf.items);
        },
        .soname = str: {
            const slice = elf.options.soname orelse break :str .empty;
            break :str try elf.string(.dynstr, slice);
        },
    };

    if (@"type" != .REL) switch (elf.targetSegmentLoadAddressRestrictions()) {
        .none => {},
        .data_last => switch (elf.phdrSlice()) {
            inline else => |phdr| {
                // Ensure that the segment after `.data` (if any) is not a loadable segment.
                const next_phndx = phndx.data + 1;
                if (next_phndx < phdr.len) {
                    switch (elf.targetLoad(&phdr[next_phndx].type)) {
                        .NULL, .LOAD => unreachable, // data segment should be the last loadable segment
                        else => {},
                    }
                }
            },
        },
    };
}

pub fn startProgress(elf: *Elf, prog_node: std.Progress.Node) void {
    prog_node.increaseEstimatedTotalItems(4);
    elf.const_prog_node = prog_node.start("Constants", elf.pending_uavs.items.len);
    elf.mf.update_prog_node = prog_node.start("Relocations", elf.mf.updates.items.len);
    elf.input_prog_node = prog_node.start("Inputs", (elf.inputs.items.len - elf.input_pending_index) +
        (elf.input_sections.items.len - elf.input_section_pending_index));
}

pub fn endProgress(elf: *Elf) void {
    elf.input_prog_node.end();
    elf.input_prog_node = .none;
    elf.mf.update_prog_node.end();
    elf.mf.update_prog_node = .none;
    elf.const_prog_node.end();
    elf.const_prog_node = .none;
}

fn getNode(elf: *const Elf, ni: MappedFile.Node.Index) Node {
    return elf.nodes.get(@backingInt(ni));
}
/// Asserts that `ni` is a section, input section, copied global, NAV, UAV, or lazy code/data.
fn getNodeShndx(elf: *const Elf, ni: MappedFile.Node.Index) Section.Index {
    return switch (elf.getNode(ni)) {
        .deleted,
        .archive,
        .archive_header,
        .archive_input_member,
        .archive_elf_member_header,
        .elf,
        .ehdr,
        .shdr,
        .segment,
        => unreachable,
        .section, .section_manual_size => |shndx| shndx,
        .input_section,
        .copied_global,
        .nav,
        .uav,
        .lazy_code,
        .lazy_const_data,
        .debug_shared,
        .debug_addr,
        .eh_frame_footer,
        .debug_str_offsets,
        .unit_padding,
        .unit_frame,
        .unit_debug_info,
        .unit_debug_line,
        .unit_debug_rnglists,
        => switch (elf.getNode(ni.parent(&elf.mf).unwrap().?)) {
            else => unreachable,
            .section, .section_manual_size => |shndx| shndx,
        },
        .unit_frame_cie,
        .unit_debug_info_header,
        .unit_debug_info_footer,
        .unit_debug_line_header,
        .const_debug_info,
        .global_debug_info,
        .func_frame_fde,
        .func_debug_info,
        .func_debug_line,
        .decl_debug_info,
        => switch (elf.getNode(ni.parent(&elf.mf).unwrap().?.parent(&elf.mf).unwrap().?)) {
            else => unreachable,
            .section, .section_manual_size => |shndx| shndx,
        },
    };
}
fn getNodeVAddr(elf: *Elf, ni: MappedFile.Node.Index) u64 {
    return switch (elf.getNode(ni)) {
        .deleted,
        .archive,
        .archive_header,
        .archive_input_member,
        .archive_elf_member_header,
        .elf,
        .ehdr,
        .shdr,
        .segment,
        .copied_global,
        => unreachable,
        .section, .section_manual_size => |shndx| shndx.vaddr(elf),
        .input_section => |isi| isi.ptrConst(elf).vaddr,
        inline .nav,
        .uav,
        .lazy_code,
        .lazy_const_data,
        => |i| Symbol.Id.local(i.symbol(elf)).value(elf),
        .debug_shared,
        .debug_addr,
        .eh_frame_footer,
        .debug_str_offsets,
        .unit_padding,
        .unit_frame,
        .unit_frame_cie,
        .unit_debug_info,
        .unit_debug_info_header,
        .unit_debug_info_footer,
        .unit_debug_line,
        .unit_debug_line_header,
        .unit_debug_rnglists,
        .const_debug_info,
        .global_debug_info,
        .func_frame_fde,
        .func_debug_info,
        .func_debug_line,
        .decl_debug_info,
        => elf.computeNodeVAddr(ni),
    };
}
fn computeNodeVAddr(elf: *Elf, ni: MappedFile.Node.Index) u64 {
    const parent_ni = ni.parent(&elf.mf).unwrap().?;
    const parent_vaddr = parent_vaddr: switch (elf.getNode(parent_ni)) {
        .deleted,
        .archive,
        .archive_header,
        .archive_input_member,
        .archive_elf_member_header,
        => unreachable,
        .elf => return 0,
        .ehdr, .shdr => unreachable,
        .segment => |phndx| switch (elf.phdrSlice()) {
            inline else => |phdr| elf.targetLoad(&phdr[phndx].vaddr),
        },
        .section, .section_manual_size => |shndx| if (shndx == elf.shndx.tdata) 0 else shndx.vaddr(elf),
        .input_section, .copied_global => unreachable,
        inline .nav,
        .uav,
        .lazy_code,
        .lazy_const_data,
        => |i| Symbol.Id.local(i.symbol(elf)).value(elf),
        .debug_shared, .debug_addr, .eh_frame_footer, .debug_str_offsets, .unit_padding => unreachable,
        .unit_frame, .unit_debug_info, .unit_debug_line => {
            const section_offset, _ = parent_ni.location(&elf.mf).resolve(&elf.mf);
            break :parent_vaddr elf.getNodeShndx(parent_ni).vaddr(elf) + section_offset;
        },
        .unit_frame_cie,
        .unit_debug_info_header,
        .unit_debug_info_footer,
        .unit_debug_line_header,
        .unit_debug_rnglists,
        .const_debug_info,
        .global_debug_info,
        .func_frame_fde,
        .func_debug_info,
        .func_debug_line,
        .decl_debug_info,
        => unreachable,
    };
    const offset, _ = ni.location(&elf.mf).resolve(&elf.mf);
    return parent_vaddr + offset;
}
fn computeNodeSectionOffset(elf: *Elf, ni: MappedFile.Node.Index) u64 {
    const parent_ni = ni.parent(&elf.mf).unwrap().?;
    const parent_section_offset = parent_section_offset: switch (elf.getNode(parent_ni)) {
        .deleted,
        .archive,
        .archive_header,
        .archive_input_member,
        .archive_elf_member_header,
        .elf,
        .ehdr,
        .shdr,
        .segment,
        => unreachable,
        .section, .section_manual_size => 0,
        .input_section, .copied_global => unreachable,
        .nav, .uav, .lazy_code, .lazy_const_data => unreachable,
        .debug_shared, .debug_addr, .eh_frame_footer, .debug_str_offsets, .unit_padding => unreachable,
        .unit_frame, .unit_debug_info, .unit_debug_line => {
            const parent_section_offset, _ = parent_ni.location(&elf.mf).resolve(&elf.mf);
            break :parent_section_offset parent_section_offset;
        },
        .unit_frame_cie,
        .unit_debug_info_header,
        .unit_debug_info_footer,
        .unit_debug_line_header,
        .unit_debug_rnglists,
        .const_debug_info,
        .global_debug_info,
        .func_frame_fde,
        .func_debug_info,
        .func_debug_line,
        .decl_debug_info,
        => unreachable,
    };
    const offset, _ = ni.location(&elf.mf).resolve(&elf.mf);
    return parent_section_offset + offset;
}
fn computeNodeElfOffset(elf: *Elf, ni: MappedFile.Node.Index) u64 {
    return ni.fileLocation(&elf.mf, false).offset - elf.ni.elf.fileLocation(&elf.mf, false).offset;
}

/// Deletes any existing relocations in the given node, and marks the start of the node's contiguous
/// sequence of relocations, so that the caller may append the node's updated relocations.
///
/// Asserts that `ni` must be a node which supports relocations (see `Elf.Node`). Does not support
/// the special-case sections '.plt', '.dynamic', and '.eh_frame_hdr'.
pub fn resetNodeRelocs(elf: *Elf, ni: MappedFile.Node.Index) void {
    const opts: struct {
        first_symbol_reloc: ?*SymbolReloc.Index = null,
        skip_symbol_relocs: MappedFile.Node.Index.Optional = .none,
        first_node_reloc: ?*NodeReloc.Index = null,
        skip_node_relocs: MappedFile.Node.Index.Optional = .none,
        first_got_reloc: ?*GotReloc.Index = null,
    } = switch (elf.getNode(ni)) {
        .deleted,
        .archive,
        .archive_header,
        .archive_input_member,
        .archive_elf_member_header,
        .elf,
        .ehdr,
        .shdr,
        .segment,
        .copied_global,
        .debug_shared,
        .debug_addr,
        .eh_frame_footer,
        .debug_str_offsets,
        .unit_padding,
        .unit_frame,
        .unit_frame_cie,
        .unit_debug_info,
        .unit_debug_line,
        => unreachable, // cannot contain relocs
        .section,
        .section_manual_size,
        => unreachable, // cannot contain relocs (.plt, .dynamic, and .eh_frame_hdr unsupported)
        .input_section => |isi| .{
            .first_symbol_reloc = &elf.input_sections.items[@backingInt(isi)].first_symbol_reloc,
            .first_got_reloc = &elf.input_sections.items[@backingInt(isi)].first_got_reloc,
        },
        .nav => |nmi| .{
            .first_symbol_reloc = &elf.navs.values()[@backingInt(nmi)].first_symbol_reloc,
            .first_got_reloc = &elf.navs.values()[@backingInt(nmi)].first_got_reloc,
        },
        .uav => |umi| .{
            .first_symbol_reloc = &elf.uavs.values()[@backingInt(umi)].first_symbol_reloc,
        },
        inline .lazy_code, .lazy_const_data => |lmi| .{
            .first_symbol_reloc = &elf.lazy.getPtr(lmi.ref().kind).map.values()[lmi.ref().index].first_symbol_reloc,
            .first_got_reloc = &elf.lazy.getPtr(lmi.ref().kind).map.values()[lmi.ref().index].first_got_reloc,
        },
        .unit_debug_info_header => |ui| .{
            .first_node_reloc = &elf.dwarf_units[@backingInt(ui)].debug_info_header_first_node_reloc,
        },
        .unit_debug_info_footer => unreachable, // cannot contain relocs
        .unit_debug_line_header => |ui| .{
            .first_node_reloc = &elf.dwarf_units[@backingInt(ui)].debug_line_header_first_node_reloc,
        },
        .unit_debug_rnglists => unreachable, // cannot contain relocs
        .const_debug_info => |cpi| .{
            .first_symbol_reloc = &elf.dwarf_consts.getPtr(cpi).?.debug_info_first_symbol_reloc,
            .first_node_reloc = &elf.dwarf_consts.getPtr(cpi).?.debug_info_first_node_reloc,
        },
        .global_debug_info => |gi| .{
            .first_symbol_reloc = &elf.dwarf_globals.items[@backingInt(gi)].debug_info_first_symbol_reloc,
            .first_node_reloc = &elf.dwarf_globals.items[@backingInt(gi)].debug_info_first_node_reloc,
        },
        .func_frame_fde => |fi| .{
            .first_symbol_reloc = &elf.dwarf_funcs.items[@backingInt(fi)].frame_fde_first_symbol_reloc,
            .first_node_reloc = &elf.dwarf_funcs.items[@backingInt(fi)].frame_fde_first_node_reloc,
        },
        .func_debug_info => |fi| .{
            .first_symbol_reloc = &elf.dwarf_funcs.items[@backingInt(fi)].debug_info_first_symbol_reloc,
            .skip_symbol_relocs = if (elf.navs.getPtr(fi.nav(&elf.dwarf))) |nav|
                nav.lsi.index().ptr(elf).node
            else
                .none,
            .first_node_reloc = &elf.dwarf_funcs.items[@backingInt(fi)].debug_info_first_node_reloc,
            .skip_node_relocs = fi.get(&elf.dwarf).debug_line_ni,
        },
        .func_debug_line => |fi| .{
            .first_symbol_reloc = &elf.dwarf_funcs.items[@backingInt(fi)].debug_line_first_symbol_reloc,
            .first_node_reloc = &elf.dwarf_funcs.items[@backingInt(fi)].debug_line_first_node_reloc,
            .skip_node_relocs = fi.get(&elf.dwarf).debug_info_ni,
        },
        .decl_debug_info => |di| .{
            .first_node_reloc = &elf.dwarf_decls.getPtr(di).?.debug_info_first_node_reloc,
        },
    };

    if (opts.first_symbol_reloc) |ptr| {
        if (ptr.* != .none) {
            for (elf.symbol_relocs.items[@backingInt(ptr.*)..], @backingInt(ptr.*)..) |*reloc, index| {
                if (reloc.node != ni.toOptional()) {
                    if (reloc.node == .none) continue;
                    if (reloc.node == opts.skip_symbol_relocs) continue;
                    break;
                }
                reloc.delete(elf, @fromBackingInt(@intCast(index)));
            }
        }
        ptr.* = @fromBackingInt(@intCast(elf.symbol_relocs.items.len));
    }

    if (opts.first_node_reloc) |ptr| {
        if (ptr.* != .none) {
            for (elf.node_relocs.items[@backingInt(ptr.*)..]) |*reloc| {
                if (reloc.node != ni.toOptional()) {
                    if (reloc.node == .none) continue;
                    if (reloc.node == opts.skip_node_relocs) continue;
                    break;
                }
                reloc.delete(elf);
            }
        }
        ptr.* = @fromBackingInt(@intCast(elf.node_relocs.items.len));
    }

    if (opts.first_got_reloc) |ptr| {
        if (ptr.* != .none) {
            for (elf.got_relocs.items[@backingInt(ptr.*)..]) |*reloc| {
                if (reloc.node != ni.toOptional()) {
                    if (reloc.node == .none) continue;
                    break;
                }
                reloc.delete(elf);
            }
        }
        ptr.* = @fromBackingInt(@intCast(elf.got_relocs.items.len));
    }
}

/// Given that `node` has moved, updates all relocations in `node` as needed. In relocatables, this
/// means updating the relocations' offsets. In ELF modules, this means applying the relocations.
fn flushMovedNodeRelocs(
    elf: *Elf,
    node: MappedFile.Node.Index,
    node_vaddr: u64,
    opts: struct {
        first_symbol_reloc: SymbolReloc.Index = .none,
        skip_symbol_relocs: MappedFile.Node.Index.Optional = .none,
        first_node_reloc: NodeReloc.Index = .none,
        skip_node_relocs: MappedFile.Node.Index.Optional = .none,
        first_got_reloc: GotReloc.Index = .none,
    },
) void {
    if (opts.first_symbol_reloc != .none) {
        for (elf.symbol_relocs.items[@backingInt(opts.first_symbol_reloc)..]) |*reloc| {
            if (reloc.node != node.toOptional()) {
                if (reloc.node == .none) continue;
                if (reloc.node == opts.skip_symbol_relocs) continue;
                break;
            }
            reloc.flushMovedNode(elf, node_vaddr);
        }
    }

    if (opts.first_node_reloc != .none) {
        for (elf.node_relocs.items[@backingInt(opts.first_node_reloc)..]) |*reloc| {
            if (reloc.node != node.toOptional()) {
                if (reloc.node == .none) continue;
                if (reloc.node == opts.skip_node_relocs) continue;
                break;
            }
            reloc.flushMovedNode(elf, node_vaddr);
        }
    }

    if (opts.first_got_reloc != .none) {
        for (elf.got_relocs.items[@backingInt(opts.first_got_reloc)..]) |*reloc| {
            if (reloc.node != node.toOptional()) {
                if (reloc.node == .none) continue;
                break;
            }
            reloc.apply(elf);
        }
    }
}

fn identClass(elf: *const Elf) std.elf.CLASS {
    return @fromBackingInt(elf.ni.elf.sliceConst(&elf.mf)[std.elf.EI.CLASS]);
}

/// Like `std.elf.ET`, but only includes the ELF machine architectures we support, so that we can
/// use exhaustive `switch` statements in the linker implementation.
const EhdrMachine = enum(u16) {
    AARCH64 = @backingInt(std.elf.EM.AARCH64),
    LOONGARCH = @backingInt(std.elf.EM.LOONGARCH),
    PPC64 = @backingInt(std.elf.EM.PPC64),
    RISCV = @backingInt(std.elf.EM.RISCV),
    SPARCV9 = @backingInt(std.elf.EM.SPARCV9),
    X86_64 = @backingInt(std.elf.EM.X86_64),

    fn toElf(m: EhdrMachine) std.elf.EM {
        return @bitCast(m);
    }
    /// Returns `null` if `m` is not a supported ELF machine architecture.
    fn fromElf(m: std.elf.EM) ?EhdrMachine {
        return std.enums.fromInt(EhdrMachine, @backingInt(m));
    }
};
/// Like `std.elf.ET`, but only includes the types of ELF file we can produce, so that we can use
/// exhaustive `switch` statements in the linker implementation.
const EhdrType = enum(u16) {
    REL = @backingInt(std.elf.ET.REL),
    EXEC = @backingInt(std.elf.ET.EXEC),
    DYN = @backingInt(std.elf.ET.DYN),
    fn toElf(t: EhdrType) std.elf.ET {
        return @bitCast(t);
    }
};
fn ehdrMachine(elf: *const Elf) EhdrMachine {
    const ehdr_slice = elf.ni.ehdr.sliceConst(&elf.mf);
    switch (elf.identClass()) {
        .NONE, _ => unreachable,
        inline else => |class| {
            const ehdr: *const class.ElfN().Ehdr = @ptrCast(@alignCast(ehdr_slice));
            return @bitCast(elf.targetLoad(&ehdr.machine));
        },
    }
}
fn ehdrType(elf: *const Elf) EhdrType {
    const ehdr_slice = elf.ni.ehdr.sliceConst(&elf.mf);
    switch (elf.identClass()) {
        .NONE, _ => unreachable,
        inline else => |class| {
            const ehdr: *const class.ElfN().Ehdr = @ptrCast(@alignCast(ehdr_slice));
            return @bitCast(elf.targetLoad(&ehdr.type));
        },
    }
}

fn targetPtrSize(elf: *const Elf) u8 {
    return elf.identClass().size();
}
/// Page alignment for the target platform.
/// Usually this returns the maximum page size supported on the
/// target to maximize compatibility but there can be exceptions.
fn targetPageAlign(elf: *const Elf) Alignment {
    return .fromByteUnits(switch (elf.ehdrMachine()) {
        .AARCH64 => 0x10000,
        .LOONGARCH => 0x10000,
        .PPC64 => 0x10000,
        .RISCV => 0x1000,
        .SPARCV9 => 0x100000,
        .X86_64 => 0x1000,

        //.@"68K" => 0x2000,
        //.AMDGPU => 0x10000,
        //.ARC_COMPACT2 => 0x2000,
        //.AVR => 0x1,
        //.BPF => 0x100000,
        //.MIPS => 0x10000,
        //.MSP430 => 0x4,
        //.PPC => 0x10000,
        //.QDSP6 => 0x10000,
        //.SPARC => 0x10000,
        //.SPARC32PLUS => 0x10000,
    });
}
fn targetEndian(elf: *const Elf) std.lang.Endian {
    const ident_data: std.elf.DATA = @fromBackingInt(elf.ni.elf.sliceConst(&elf.mf)[std.elf.EI.DATA]);
    return ident_data.endian();
}
fn targetTlsVariant(elf: *const Elf) union(enum) {
    /// TP points to the start of the TCB, which immediately precedes the executable's TLS block.
    I_original: struct { tcb_size: u8 },
    /// TP points at a fixed offset from the start of the executable's TLS block.
    I_modified: struct { tp_off: u32 },
    /// TP points to the TCB, which immediately *succeeds* the executable's TLS block. (In other
    /// words, TP points to the *end* of the executable's TLS block.)
    II,
} {
    return switch (elf.ehdrMachine()) {
        .AARCH64 => .{ .I_original = .{ .tcb_size = 2 * elf.targetPtrSize() } },
        .LOONGARCH => .{ .I_original = .{ .tcb_size = elf.targetPtrSize() } },
        .PPC64 => .{ .I_modified = .{ .tp_off = 0x7000 } },
        .RISCV => .{ .I_modified = .{ .tp_off = 0 } },
        .SPARCV9 => .II,
        .X86_64 => .II,
    };
}
const PltInfo = struct {
    /// If not `null`, there is a `.got.plt` section containing the target addresses, and the PLT
    /// itself is immutable. If `false`, JUMP_SLOT relocations write directly to the `.plt` section,
    /// which must therefore be mutable.
    got_plt: ?struct { header_entries: u8 },
    /// If not `null`, there is a `.plt.sec` section, and every function in the PLT has both a
    /// `.plt` entry and a `.plt.sec` entry. Jumps targeting the PLT should jump to the `.plt.sec`
    /// entry, not the `.plt` entry. The `.plt.sec` section has no header entries, and is aligned to
    /// the same boundary as the `.plt` section.
    plt_sec: ?struct { entry_size: u8 },
    @"align": Alignment,
    entry_size: u8,
    header_entries: u8,

    fn fromMachine(machine: EhdrMachine) PltInfo {
        return switch (machine) {
            .AARCH64, .PPC64, .RISCV => @panic(@tagName(machine)),
            .LOONGARCH => .{
                .got_plt = .{ .header_entries = 2 },
                .plt_sec = null,
                .@"align" = .@"4",
                .entry_size = 16,
                .header_entries = 2,
            },
            .SPARCV9 => .{
                .got_plt = null,
                .plt_sec = null,
                .@"align" = .fromByteUnits(256),
                .entry_size = 32,
                .header_entries = 4,
            },
            .X86_64 => .{
                .got_plt = .{ .header_entries = 3 },
                .plt_sec = .{ .entry_size = 16 },
                .@"align" = .@"16",
                .entry_size = 16,
                .header_entries = 1,
            },
        };
    }
};
fn targetPltInfo(elf: *const Elf) PltInfo {
    return .fromMachine(elf.ehdrMachine());
}
const DynsymHashInfo = enum(u32) {
    @"4" = 4,
    @"8" = 8,

    fn Int(comptime self: DynsymHashInfo) type {
        return switch (self) {
            .@"4" => u32,
            .@"8" => u64,
        };
    }

    fn Header(comptime self: DynsymHashInfo) type {
        return switch (self) {
            .@"4" => std.elf.hash.Header32,
            .@"8" => std.elf.hash.Header64,
        };
    }
};
fn targetDynsymHashInfo(elf: *const Elf) DynsymHashInfo {
    return switch (elf.ehdrMachine()) {
        else => .@"4",
        // TODO: Alpha and S390x will need to use either `."@4"` or `.@"8"` depending on `elf.identClass()`.
    };
}
/// Specifies any restrictions the current target has regarding how segments are ordered in the
/// virtual address space. Most targets do not have any such restrictions.
fn targetSegmentLoadAddressRestrictions(elf: *const Elf) enum {
    none,
    /// The "mutable data" segment must be the last loadable segment in the virtual address space.
    data_last,
} {
    return switch (elf.ehdrMachine()) {
        .AARCH64,
        .PPC64,
        .RISCV,
        .X86_64,
        .LOONGARCH,
        => .none,

        // SPARC uses `R_SPARC_PC{10,22}` relocations to construct pointers to the GOT, but these
        // relocations write an *unsigned* PC-relative offset. This cannot even be worked around by
        // using a larger code model, because the crt `_start` assembly always uses these specific
        // relocations. Therefore, to avoid relocation errors, all code must appear before the GOT
        // in the virtual address space. The easiest way for us to do that is to ensure that the
        // "mutable data" segment, containing the GOT, is the last segment in the address space.
        .SPARCV9 => .data_last,
    };
}
fn targetLoad(elf: *const Elf, ptr: anytype) @typeInfo(@TypeOf(ptr)).pointer.child {
    const pointer_ty = @typeInfo(@TypeOf(ptr)).pointer;
    const Child = pointer_ty.child;
    const alignment = pointer_ty.attrs.@"align" orelse @alignOf(Child);
    return switch (@typeInfo(Child)) {
        else => @compileError(@typeName(Child)),
        .int => std.mem.toNative(Child, ptr.*, elf.targetEndian()),
        .@"enum" => |@"enum"| @fromBackingInt(elf.targetLoad(@as(*align(alignment) const @"enum".tag_type, @ptrCast(ptr)))),
        .@"struct" => |@"struct"| @bitCast(
            elf.targetLoad(@as(*align(alignment) @"struct".backing_integer.?, @ptrCast(ptr))),
        ),
    };
}
fn targetStore(elf: *const Elf, ptr: anytype, val: @typeInfo(@TypeOf(ptr)).pointer.child) void {
    const pointer_ty = @typeInfo(@TypeOf(ptr)).pointer;
    const Child = pointer_ty.child;
    const alignment = pointer_ty.attrs.@"align" orelse @alignOf(Child);
    return switch (@typeInfo(Child)) {
        else => @compileError(@typeName(Child)),
        .int => ptr.* = std.mem.nativeTo(Child, val, elf.targetEndian()),
        .@"enum" => |@"enum"| elf.targetStore(
            @as(*align(alignment) @"enum".tag_type, @ptrCast(ptr)),
            @backingInt(val),
        ),
        .@"struct" => |@"struct"| elf.targetStore(
            @as(*align(alignment) @"struct".backing_integer.?, @ptrCast(ptr)),
            @bitCast(val),
        ),
    };
}

const EhdrPtr = union(std.elf.CLASS) {
    NONE: noreturn,
    @"32": *std.elf.Elf32.Ehdr,
    @"64": *std.elf.Elf64.Ehdr,
};
fn ehdrPtr(elf: *Elf) EhdrPtr {
    const slice = elf.ni.ehdr.slice(&elf.mf);
    return switch (elf.identClass()) {
        .NONE, _ => unreachable,
        inline else => |class| @unionInit(
            EhdrPtr,
            @tagName(class),
            @ptrCast(@alignCast(slice)),
        ),
    };
}

const PhdrSlice = union(std.elf.CLASS) {
    NONE: noreturn,
    @"32": []std.elf.Elf32.Phdr,
    @"64": []std.elf.Elf64.Phdr,
};
fn phdrSlice(elf: *Elf) PhdrSlice {
    assert(elf.ehdrType() != .REL);
    return switch (elf.identClass()) {
        .NONE, _ => unreachable,
        inline else => |class| @unionInit(PhdrSlice, @tagName(class), @ptrCast(@alignCast(
            elf.ni.phdr.slice(&elf.mf)[0 .. elf.phdrs.items.len * @sizeOf(class.ElfN().Phdr)],
        ))),
    };
}

const ShdrPtr = union(std.elf.CLASS) {
    NONE: noreturn,
    @"32": *std.elf.Elf32.Shdr,
    @"64": *std.elf.Elf64.Shdr,
};
fn shdrPtr(elf: *Elf, shndx: Section.Index) ShdrPtr {
    const slice = elf.ni.shdr.slice(&elf.mf);
    switch (elf.identClass()) {
        .NONE, _ => unreachable,
        inline else => |class| {
            const shdr_slice: []class.ElfN().Shdr = @ptrCast(@alignCast(
                slice[0 .. @sizeOf(class.ElfN().Shdr) * (1 + elf.shdrs.items.len)],
            ));
            const shdr_ptr = &shdr_slice[@backingInt(shndx)];
            return @unionInit(ShdrPtr, @tagName(class), shdr_ptr);
        },
    }
}

const SymPtr = union(std.elf.CLASS) {
    NONE: noreturn,
    @"32": *std.elf.Elf32.Sym,
    @"64": *std.elf.Elf64.Sym,
};
fn symPtr(elf: *Elf, index: Symbol.Index) SymPtr {
    const raw_slice = Section.Index.symtab.get(elf).ni.slice(&elf.mf);
    switch (elf.shdrPtr(.symtab)) {
        inline else => |shdr, class| {
            const size = elf.targetLoad(&shdr.size);
            const slice: []class.ElfN().Sym = @ptrCast(@alignCast(raw_slice[0..@intCast(size)]));
            return @unionInit(SymPtr, @tagName(class), &slice[@backingInt(index)]);
        },
    }
}
fn dynsymPtr(elf: *Elf, index: u32) SymPtr {
    const raw_slice = elf.shndx.dynsym.get(elf).ni.slice(&elf.mf);
    switch (elf.shdrPtr(elf.shndx.dynsym)) {
        inline else => |shdr, class| {
            const size = elf.targetLoad(&shdr.size);
            const slice: []class.ElfN().Sym = @ptrCast(@alignCast(raw_slice[0..@intCast(size)]));
            return @unionInit(SymPtr, @tagName(class), &slice[index]);
        },
    }
}

fn versymSlice(elf: *Elf) []std.elf.Versym {
    const raw_slice = elf.shndx.gnu_version.get(elf).ni.slice(&elf.mf);
    const size = elf.shndx.gnu_version.size(elf);
    return @ptrCast(@alignCast(raw_slice[0..@intCast(size)]));
}

/// Although `.gnu.version_d` (the `SHT_VERDEF` section) can have essentially any layout, we always
/// organize it as an array of contiguous entries where each entry is a single `std.elf.Verdef` with
/// a single `std.elf.Verdaux`. This type represents a single such entry; the section contents is
/// then effectively an array of these.
const VerdefEntry = extern struct {
    def: std.elf.Verdef,
    aux: std.elf.Verdaux,
};
fn verdefSlice(elf: *Elf) []VerdefEntry {
    const raw_slice = elf.shndx.gnu_version_d.get(elf).ni.slice(&elf.mf);
    const size = elf.shndx.gnu_version_d.size(elf);
    return @ptrCast(@alignCast(raw_slice[0..@intCast(size)]));
}

const VerneedEntry = extern union {
    verneed: std.elf.Verneed,
    vernaux: std.elf.Vernaux,
    comptime {
        assert(@sizeOf(std.elf.Verneed) == @sizeOf(std.elf.Vernaux));
    }
};
fn verneedSlice(elf: *Elf) []VerneedEntry {
    const raw_slice = elf.shndx.gnu_version_r.get(elf).ni.slice(&elf.mf);
    const size = elf.shndx.gnu_version_r.size(elf);
    return @ptrCast(@alignCast(raw_slice[0..@intCast(size)]));
}

fn navType(elf: *const Elf, nav_resolved: InternPool.Nav.Resolved) std.elf.STT {
    const comp = elf.base.comp;
    const any_non_single_threaded = comp.config.any_non_single_threaded;
    return if (any_non_single_threaded and nav_resolved.@"threadlocal")
        .TLS
    else if (comp.zcu.?.intern_pool.isFunctionType(nav_resolved.type))
        .FUNC
    else
        .OBJECT;
}
fn mapInputSection(elf: *Elf, opts: struct {
    name: []const u8,
    flags: std.elf.SHF,
    entsize: std.elf.Xword,
}) (Error || error{
    UnsupportedSectionFlags,
    TlsSectionUnavailable,
    StripSection,
    SectionFlagsConflict,
    SectionTypeConflict,
})!Section.Index {
    const gpa = elf.base.comp.gpa;
    if (opts.flags.INFO_LINK or
        opts.flags.LINK_ORDER or
        opts.flags.OS_NONCONFORMING or
        (opts.flags.EXECINSTR and opts.flags.WRITE) or
        (opts.flags.EXECINSTR and opts.flags.TLS))
    {
        return error.UnsupportedSectionFlags;
    }
    if (opts.flags.TLS and elf.ni.tls == .none) {
        assert(!elf.base.comp.config.any_non_single_threaded);
        return error.TlsSectionUnavailable;
    }

    if (elf.base.comp.config.debug_format == .strip and
        std.mem.startsWith(u8, opts.name, ".debug_") and
        !opts.flags.ALLOC)
    {
        return error.StripSection;
    }

    const name: []const u8 = switch (elf.ehdrType()) {
        .REL => opts.name,
        .EXEC, .DYN => name: {
            if (std.mem.startsWith(u8, opts.name, ".text.")) break :name ".text";
            if (std.mem.startsWith(u8, opts.name, ".rodata.")) break :name ".rodata";
            if (std.mem.startsWith(u8, opts.name, ".data.rel.ro.")) break :name ".data.rel.ro";
            if (std.mem.startsWith(u8, opts.name, ".data.")) break :name ".data";
            if (std.mem.startsWith(u8, opts.name, ".tdata.")) break :name ".tdata";
            if (std.mem.startsWith(u8, opts.name, ".gcc_except_table.")) break :name ".gcc_except_table";
            // TODO: actually generate a bss section!
            if (std.mem.eql(u8, opts.name, ".bss")) break :name ".data";
            if (std.mem.startsWith(u8, opts.name, ".bss.")) break :name ".data";
            // TODO: actually generate a tbss section!
            if (std.mem.eql(u8, opts.name, ".tbss")) break :name ".tdata";
            if (std.mem.startsWith(u8, opts.name, ".tbss.")) break :name ".tdata";
            break :name opts.name;
        },
    };
    const existing_shndx: Section.Index = existing: {
        const name_shstrtab = try elf.string(.shstrtab, name);
        const gop = try elf.section_by_name.getOrPut(gpa, name_shstrtab);
        if (gop.found_existing) {
            break :existing @fromBackingInt(@intCast(gop.index + 1)); // +1 to account for SHN_UDNEF
        }
        errdefer assert(elf.section_by_name.pop().?.key == name_shstrtab);
        const parent_node: MappedFile.Node.Index = parent: {
            if (!opts.flags.ALLOC) break :parent elf.ni.elf;
            if (opts.flags.EXECINSTR) break :parent elf.ni.text;
            if (opts.flags.TLS) break :parent elf.ni.tls.unwrap().?;
            if (opts.flags.WRITE) break :parent elf.ni.data;
            break :parent elf.ni.rodata;
        };
        assert(gop.index == elf.shdrs.items.len);
        return elf.addSection(parent_node, .{
            .name = name,
            .type = .NULL, // because initial size is 0
            .flags = flags: {
                // We need to decompress the section for linking.
                var flags = opts.flags;
                flags.COMPRESSED = false;
                break :flags flags;
            },
            .entsize = std.math.lossyCast(u32, opts.entsize),
        });
    };
    switch (elf.shdrPtr(existing_shndx)) {
        inline else => |shdr| {
            // Validate that the input is compatible with this section
            const cur_flags = elf.targetLoad(&shdr.flags).shf;
            if (cur_flags.EXECINSTR != opts.flags.EXECINSTR or
                cur_flags.WRITE != opts.flags.WRITE or
                cur_flags.TLS != opts.flags.TLS)
            {
                return error.SectionFlagsConflict;
            }

            switch (elf.targetLoad(&shdr.type)) {
                .NULL, .PROGBITS, .X86_64_UNWIND => {},
                else => return error.SectionTypeConflict,
            }

            // All okay, combine the section flags
            elf.targetStore(&shdr.flags, .{ .shf = .{
                .EXECINSTR = cur_flags.EXECINSTR,
                .WRITE = cur_flags.WRITE,
                .TLS = cur_flags.TLS,
                .ALLOC = cur_flags.ALLOC or opts.flags.ALLOC,
                .STRINGS = cur_flags.STRINGS and opts.flags.STRINGS,
                .MERGE = cur_flags.MERGE and opts.flags.MERGE,
            } });
        },
    }
    return existing_shndx;
}
fn navMapIndex(elf: *Elf, zcu: *Zcu, nav_index: InternPool.Nav.Index) Error!Node.NavMapIndex {
    const gpa = zcu.gpa;
    const ip = &zcu.intern_pool;
    const nav = ip.getNav(nav_index);

    try elf.nodes.ensureUnusedCapacity(gpa, 1);
    try elf.navs.ensureUnusedCapacity(gpa, 1);

    const nav_gop = elf.navs.getOrPutAssumeCapacity(nav_index);
    const nmi: Node.NavMapIndex = @fromBackingInt(@intCast(nav_gop.index));
    if (!nav_gop.found_existing) {
        const shndx: Section.Index = section: {
            if (nav.resolved.?.@"linksection".toSlice(ip)) |@"linksection"| {
                if (elf.mapInputSection(.{
                    .name = @"linksection",
                    .flags = .{
                        .ALLOC = true,
                        .EXECINSTR = ip.isFunctionType(nav.resolved.?.type),
                        .WRITE = !nav.resolved.?.@"const",
                        .TLS = elf.base.comp.config.any_non_single_threaded and
                            nav.resolved.?.@"threadlocal",
                    },
                    .entsize = 0,
                })) |shndx| {
                    break :section shndx;
                } else |err| switch (err) {
                    else => |e| return e,
                    error.StripSection,
                    error.TlsSectionUnavailable,
                    error.UnsupportedSectionFlags,
                    error.SectionTypeConflict,
                    error.SectionFlagsConflict,
                    => {}, // fall back to default behavior below

                }
            }
            if (elf.base.comp.config.any_non_single_threaded and nav.resolved.?.@"threadlocal") {
                break :section elf.shndx.tdata;
            } else if (!nav.resolved.?.@"const") {
                break :section .data;
            } else if (ip.isFunctionType(nav.resolved.?.type)) {
                break :section .text;
            } else {
                break :section .data_rel_ro; // TODO: it would be better to use `.rodata` if the NAV value doesn't have relocs
            }
        };
        const alignment: Alignment = switch (Type.fromInterned(nav.resolved.?.type).zigTypeTag(zcu)) {
            .@"fn" => a: {
                const mod = zcu.navFileScope(nav_index).mod.?;
                const target = &mod.resolved_target.result;
                break :a .fromIp(switch (nav.resolved.?.@"align") {
                    else => |a| a.maxStrict(target_util.minFunctionAlignment(target)),
                    .none => switch (mod.optimize_mode) {
                        .debug, .safe, .fast => target_util.defaultFunctionAlignment(target),
                        .small => target_util.minFunctionAlignment(target),
                    }.maxStrict(Type.fromInterned(nav.resolved.?.type).abiAlignment(zcu)),
                });
            },
            else => switch (nav.resolved.?.@"align") {
                .none => .fromIp(Type.fromInterned(nav.resolved.?.type).abiAlignment(zcu)),
                else => |a| .fromIp(a),
            },
        };
        try shndx.ensureAligned(elf, alignment);
        const node = elf.addNodeAssumeCapacity(try shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{
            .alignment = alignment,
        }), .{ .nav = nmi });
        nav_gop.value_ptr.* = .{
            .lsi = try elf.addLocalSymbol(.{
                .node = .wrap(node),
                .name = nav.fqn.toSlice(ip),
                .value = 0,
                .size = 0,
                .type = elf.navType(nav.resolved.?),
                .shndx = shndx,
            }),
            .first_symbol_reloc = .none,
            .first_got_reloc = .none,
        };
    }
    return nmi;
}

fn uavMapIndex(
    elf: *Elf,
    uav_val: InternPool.Index,
    uav_align: InternPool.Alignment,
) Error!Node.UavMapIndex {
    const gpa = elf.base.comp.gpa;
    const zcu = elf.base.comp.zcu.?;

    try elf.nodes.ensureUnusedCapacity(gpa, 1);
    try elf.uavs.ensureUnusedCapacity(gpa, 1);
    try elf.pending_uavs.ensureUnusedCapacity(gpa, 1);

    const abi_align = Value.fromInterned(uav_val).typeOf(zcu).abiAlignment(zcu);
    const resolved_align: Alignment = switch (uav_align) {
        .none => .fromIp(abi_align),
        else => |a| .fromIp(a.maxStrict(abi_align)),
    };

    const uav_gop = elf.uavs.getOrPutAssumeCapacity(uav_val);
    const umi: Node.UavMapIndex = @fromBackingInt(@intCast(uav_gop.index));
    if (!uav_gop.found_existing) {
        const shndx: Section.Index = .data_rel_ro; // TODO: it would be better to use `.rodata` if the UAV value doesn't have relocs
        try shndx.ensureAligned(elf, resolved_align);
        const node = elf.addNodeAssumeCapacity(try shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{
            .moved = true, // see assert at end of `genUav`
            .alignment = resolved_align,
        }), .{ .uav = umi });
        var name_buf: [std.fmt.count("__anon_{d}", .{std.math.maxInt(u32)})]u8 = undefined;
        const name = std.mem.print(&name_buf, "__anon_{d}", .{umi}) catch unreachable;
        uav_gop.value_ptr.* = .{
            .lsi = try elf.addLocalSymbol(.{
                .node = .wrap(node),
                .name = name,
                .value = 0,
                .size = 0,
                .type = .OBJECT,
                .shndx = shndx,
            }),
            .first_symbol_reloc = .none,
        };
        elf.const_prog_node.increaseEstimatedTotalItems(1);
        elf.pending_uavs.appendAssumeCapacity(umi);
    } else {
        const node = uav_gop.value_ptr.lsi.index().ptr(elf).node.unwrap().?;
        const shndx = elf.getNodeShndx(node);
        try shndx.ensureAligned(elf, resolved_align);
        if (resolved_align.order(node.alignment(&elf.mf)).compare(.gt)) {
            try node.realign(gpa, &elf.mf, resolved_align);
        }
    }
    return umi;
}

/// Internal error set used by input parsing functions `loadObject`, `loadArchive`, `loadDso`.
const LoadParseInputError = Error || Io.File.SeekError || Io.Reader.Error;

/// Returns `error.BadMagic` if a DSO or static archive has an incorrect magic number, which
/// indicates to the frontend that the input could be a GNU ld script instead.
pub fn loadInput(elf: *Elf, input: link.Input) (link.Error || error{BadMagic})!void {
    const diags = &elf.base.comp.link_diags;
    elf.loadInputInner(input) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return diags.fail(
            "failed to write output file: {t}",
            .{elf.mf.io_err.?},
        ),
    };
}
fn loadInputInner(elf: *Elf, input: link.Input) (Error || error{BadMagic})!void {
    const comp = elf.base.comp;
    const diags = &comp.link_diags;
    const io = comp.io;
    var buf: [4096]u8 = undefined;
    switch (input) {
        .object => |object| {
            var fr = object.file.reader(io, &buf);
            elf.loadObject(object.path, null, &fr, .{
                .offset = fr.logicalPos(),
                .size = fr.getSize() catch |err| switch (err) {
                    error.Canceled => |e| return e,
                    else => |e| return diags.fail(
                        "failed to stat \"{f}\": {t}",
                        .{ object.path.fmtEscapeString(), e },
                    ),
                },
            }) catch |err| switch (err) {
                else => |e| return e,
                error.EndOfStream => return diags.failParse(
                    object.path,
                    "unexpected eof",
                    .{},
                ),
                error.AccessDenied, error.Unexpected, error.Unseekable => |e| return diags.fail(
                    "failed to read \"{f}\": {t}",
                    .{ object.path.fmtEscapeString(), e },
                ),
                error.ReadFailed => switch (fr.err.?) {
                    error.Canceled => |e| return e,
                    else => |e| return diags.fail(
                        "failed to read \"{f}\": {t}",
                        .{ object.path.fmtEscapeString(), e },
                    ),
                },
            };
        },
        .archive => |archive| {
            var fr = archive.file.reader(io, &buf);
            elf.loadArchive(archive.path, &fr) catch |err| switch (err) {
                else => |e| return e,
                error.EndOfStream => return diags.failParse(
                    archive.path,
                    "unexpected eof",
                    .{},
                ),
                error.AccessDenied, error.Unexpected, error.Unseekable => |e| return diags.fail(
                    "failed to read \"{f}\": {t}",
                    .{ archive.path.fmtEscapeString(), e },
                ),
                error.ReadFailed => switch (fr.err.?) {
                    error.Canceled => |e| return e,
                    else => |e| return diags.fail(
                        "failed to read \"{f}\": {t}",
                        .{ archive.path.fmtEscapeString(), e },
                    ),
                },
            };
        },
        .dso => |dso| {
            try elf.needed.ensureUnusedCapacity(elf.base.comp.gpa, 1);
            var fr = dso.file.reader(io, &buf);
            elf.loadDso(dso.path, dso.fallback_soname, &fr) catch |err| switch (err) {
                else => |e| return e,
                error.EndOfStream => return diags.failParse(dso.path, "unexpected eof", .{}),
                error.AccessDenied, error.Unexpected, error.Unseekable => |e| return diags.fail(
                    "failed to read {qf}: {t}",
                    .{ dso.path, e },
                ),
                error.ReadFailed => switch (fr.err.?) {
                    error.Canceled => |e| return e,
                    else => |e| return diags.fail("failed to read {qf}: {t}", .{ dso.path, e }),
                },
            };
        },
        .res => unreachable,
        .tbd => unreachable,
    }
}
fn loadArchive(elf: *Elf, path: std.Build.Cache.Path, fr: *Io.File.Reader) (LoadParseInputError || error{BadMagic})!void {
    const comp = elf.base.comp;
    const gpa = comp.gpa;
    const diags = &comp.link_diags;
    const r = &fr.interface;

    log.debug("loadArchive({f})", .{path.fmtEscapeString()});

    if (elf.ehdrType() == .REL) return; // this input does not affect the output artifact

    {
        const magic = r.take(std.elf.ARMAG.len) catch |err| switch (err) {
            error.ReadFailed => |e| return e,
            error.EndOfStream => return error.BadMagic,
        };
        if (!std.mem.eql(u8, magic, std.elf.ARMAG)) {
            return error.BadMagic;
        }
    }
    var strtab: Io.Writer.Allocating = .init(gpa);
    defer strtab.deinit();
    while (r.takeStruct(std.elf.ar_hdr, .native)) |header| {
        if (!std.mem.eql(u8, &header.ar_fmag, std.elf.ARFMAG))
            return diags.failParse(path, "bad file magic", .{});
        const offset = fr.logicalPos();
        const size = header.size() catch
            return diags.failParse(path, "bad member size", .{});
        if (std.mem.eql(u8, &header.ar_name, std.elf.STRNAME)) {
            strtab.clearRetainingCapacity();
            try strtab.ensureTotalCapacityPrecise(size);
            r.streamExact(&strtab.writer, size) catch |err| switch (err) {
                else => |e| return e,
                error.WriteFailed => return error.OutOfMemory,
            };
            continue;
        }
        load_object: {
            if (std.mem.eql(u8, &header.ar_name, std.elf.SYMNAME) or
                std.mem.eql(u8, &header.ar_name, std.elf.SYM64NAME) or
                std.mem.eql(u8, &header.ar_name, std.elf.SYMDEFNAME) or
                std.mem.eql(u8, &header.ar_name, std.elf.SYMDEFSORTEDNAME))
            {
                break :load_object;
            }
            const member = header.name() orelse member: {
                const strtab_offset = header.nameOffset() catch |err| switch (err) {
                    error.Overflow => break :member error.Overflow,
                    error.InvalidCharacter => break :load_object,
                } orelse break :load_object;
                const strtab_written = strtab.written();
                if (strtab_offset > strtab_written.len) break :member error.Overflow;
                const member = std.mem.sliceTo(strtab_written[strtab_offset..], '\n');
                break :member if (std.mem.endsWith(u8, member, "/"))
                    member[0 .. member.len - "/".len]
                else
                    member;
            } catch |err| switch (err) {
                error.Overflow => return diags.failParse(path, "bad member name offset", .{}),
            };
            try elf.loadObject(path, member, fr, .{ .offset = offset, .size = size });
        }
        try fr.seekTo(std.mem.alignForward(u64, offset + size, 2));
    } else |err| switch (err) {
        else => |e| return e,
        error.EndOfStream => if (!fr.atEnd()) return error.EndOfStream,
    }
}
fn fmtMemberString(member: ?[]const u8) std.fmt.Alt(?[]const u8, memberStringEscape) {
    return .{ .data = member };
}
fn memberStringEscape(member: ?[]const u8, w: *Io.Writer) Io.Writer.Error!void {
    try w.print("({f})", .{std.zig.fmtString(member orelse return)});
}
fn loadObject(
    elf: *Elf,
    path: std.Build.Cache.Path,
    member: ?[]const u8,
    fr: *Io.File.Reader,
    fl: MappedFile.Node.FileLocation,
) LoadParseInputError!void {
    const comp = elf.base.comp;
    const gpa = comp.gpa;
    const diags = &comp.link_diags;
    const r = &fr.interface;

    const input_index: Node.InputIndex = @fromBackingInt(@intCast(elf.inputs.items.len));
    log.debug("loadObject({f}{f})", .{ path.fmtEscapeString(), fmtMemberString(member) });
    elf.checkInputIdent(path, r) catch |err| switch (err) {
        else => |e| return e,
        error.BadMagic => return diags.failParse(
            path,
            "bad ELF magic",
            .{},
        ),
    };

    const input = try elf.inputs.addOne(gpa);
    input.* = .{
        .path = path,
        .member = if (member) |m| try gpa.dupe(u8, m) else null,
        .extra = undefined,
    };
    if (elf.archive) |*archive| {
        // We're creating a static library, so just add this input as an archive member.
        assert(member == null); // don't try to put static library members into other static libraries

        const first_member_oni = archive.header_ni.next(&elf.mf);

        if (first_member_oni.unwrap()) |first_member_ni| switch (elf.getNode(first_member_ni)) {
            .archive_input_member, .archive_elf_member_header => {},
            .elf => unreachable, // always preceded by `.archive_elf_member_header`
            else => unreachable, // never a child of `.archive`
        };

        try elf.nodes.ensureUnusedCapacity(gpa, 1);
        const new_member_ni = elf.addNodeAssumeCapacity(
            try archive.ni.addFooterChildBefore(gpa, &elf.mf, first_member_oni, .{
                .size = Alignment.@"2".forward(@sizeOf(std.elf.ar_hdr) + fl.size),
                .alignment = .@"2",
            }),
            .{ .archive_input_member = input_index },
        );
        input.extra = .{ .node = new_member_ni };
        elf.input_prog_node.increaseEstimatedTotalItems(1);

        // The contents of the input will be written to the file by an idle task (`flushInput`), but
        // we do need to write the input's archive member header (`ar_hdr`) now, for two reasons:
        //
        // * If the input file has a long name, we need to add it to the archive member name string
        //   table, which must happen deterministically (i.e. not in an idle task).
        //
        // * `flushInput` needs to know the actual file size (before padding to the alignment).
        const member_ar_hdr: *std.elf.ar_hdr = @ptrCast(
            new_member_ni.slice(&elf.mf)[0..@sizeOf(std.elf.ar_hdr)],
        );
        member_ar_hdr.* = .{
            .ar_name = undefined, // populated below
            .ar_date = "0           ".*,
            .ar_uid = "0     ".*,
            .ar_gid = "0     ".*,
            .ar_mode = "644     ".*,
            .ar_size = undefined, // populated below
            .ar_fmag = std.elf.ARFMAG.*,
        };

        if (std.mem.print(&member_ar_hdr.ar_size, "{d}", .{fl.size})) |size_str| {
            @memset(member_ar_hdr.ar_size[size_str.len..], ' ');
        } else |err| switch (err) {
            error.NoSpaceLeft => return diags.failParse(
                path,
                "file size of {Bi} exceeds maximum size of archive member",
                .{fl.size},
            ),
        }

        const member_name = std.fs.path.basename(path.sub_path);
        // After this call returns, `member_ar_hdr` is invalidated.
        try elf.populateArchiveMemberName(member_ar_hdr, member_name);

        // Since we are not emitting the archive symbol table (yet?) we do not need to parse
        // the symbols in this input.
        return;
    }

    elf.input_pending_index += 1;
    input.extra = .{ .file_symbol = try elf.addLocalSymbol(.{
        .node = .none,
        .name = std.fs.path.stem(member orelse path.sub_path),
        .value = 0,
        .size = 0,
        .type = .FILE,
        .shndx = .ABS,
    }) };
    const target_endian = elf.targetEndian();
    switch (elf.identClass()) {
        .NONE, _ => unreachable,
        inline else => |class| {
            const ElfN = class.ElfN();
            const ehdr = try r.peekStruct(ElfN.Ehdr, target_endian);
            if (ehdr.type != .REL) return diags.failParse(path, "unsupported object type", .{});
            if (ehdr.machine != elf.ehdrMachine().toElf())
                return diags.failParse(path, "bad machine", .{});
            if (ehdr.shoff == 0 or ehdr.shnum <= 1) return;
            if (ehdr.shoff + @as(u64, ehdr.shentsize) * @as(u64, ehdr.shnum) > fl.size)
                return diags.failParse(path, "bad section header location", .{});
            if (ehdr.shentsize < @sizeOf(ElfN.Shdr))
                return diags.failParse(path, "unsupported shentsize", .{});
            const sections = try gpa.alloc(struct { shdr: ElfN.Shdr, isi: ?InputSection.Index }, ehdr.shnum);
            defer gpa.free(sections);
            try fr.seekTo(fl.offset + ehdr.shoff);
            for (sections) |*section| {
                section.* = .{
                    .shdr = try r.peekStruct(ElfN.Shdr, target_endian),
                    .isi = null,
                };
                try r.discardAll(ehdr.shentsize);
                switch (section.shdr.type) {
                    .NULL, .NOBITS => {},
                    else => if (section.shdr.offset + section.shdr.size > fl.size)
                        return diags.failParse(path, "bad section location", .{}),
                }
            }
            const shstrtab = shstrtab: {
                if (ehdr.shstrndx == std.elf.SHN_UNDEF or ehdr.shstrndx >= ehdr.shnum)
                    return diags.failParse(path, "missing section names", .{});
                const shdr = &sections[ehdr.shstrndx].shdr;
                if (shdr.type != .STRTAB) return diags.failParse(path, "invalid shstrtab type", .{});
                const shstrtab = try gpa.alloc(u8, @intCast(shdr.size));
                errdefer gpa.free(shstrtab);
                try fr.seekTo(fl.offset + shdr.offset);
                try r.readSliceAll(shstrtab);
                break :shstrtab shstrtab;
            };
            defer gpa.free(shstrtab);
            try elf.nodes.ensureUnusedCapacity(gpa, ehdr.shnum - 1);
            try elf.input_sections.ensureUnusedCapacity(gpa, ehdr.shnum - 1);
            for (sections[1..]) |*section| {
                if (section.shdr.name >= shstrtab.len) continue;
                const name = std.mem.sliceTo(shstrtab[section.shdr.name..], 0);
                if (!comp.config.any_unwind_tables and std.mem.eql(u8, name, ".eh_frame")) continue;
                const opts: struct {
                    shndx: Section.Index,
                    node_fixed: bool,
                } = switch (section.shdr.type) {
                    else => continue,
                    .PROGBITS, .NOBITS, .X86_64_UNWIND => opts: {
                        const shndx = elf.mapInputSection(.{
                            .name = name,
                            .flags = section.shdr.flags.shf,
                            .entsize = section.shdr.entsize,
                        }) catch |err| switch (err) {
                            else => |e| return e,
                            error.StripSection => continue,
                            error.TlsSectionUnavailable => return diags.failParse(
                                path,
                                "thread-local storage section '{s}' is incompatible with '-fsingle-threaded'",
                                .{name},
                            ),
                            error.UnsupportedSectionFlags => if (!section.shdr.flags.shf.ALLOC) {
                                // It probably doesn't matter, just skip this section.
                                continue;
                            } else return diags.failParse(
                                path,
                                "unsupported flags for section '{s}'",
                                .{name},
                            ),
                            error.SectionTypeConflict => if (!section.shdr.flags.shf.ALLOC) {
                                // It probably doesn't matter, just skip this section.
                                continue;
                            } else return diags.failParse(
                                path,
                                "type of section '{s}' conflicts with other inputs",
                                .{name},
                            ),
                            error.SectionFlagsConflict => if (!section.shdr.flags.shf.ALLOC) {
                                // It probably doesn't matter, just skip this section.
                                continue;
                            } else return diags.failParse(
                                path,
                                "flags of section '{s}' conflict with other inputs",
                                .{name},
                            ),
                        };
                        if (section.shdr.flags.shf.COMPRESSED) {
                            // SHF_COMPRESSED is only allowed on non-alloc sections.
                            if (section.shdr.flags.shf.ALLOC) return diags.failParse(
                                path,
                                "section '{s}' has conflicting flags SHF_ALLOC and SHF_COMPRESSED",
                                .{name},
                            );
                            // TODO: handle compressed input sections. We'll need to set a flag to
                            // indicate that `flushInputSection` needs to decompress the section.
                            // But because this section isn't SHF_ALLOC, it's probably okay to just
                            // skip it for now.
                            continue;
                        }
                        break :opts .{
                            .shndx = shndx,
                            // For well-known sections, we know that it's fine to have e.g. random
                            // padding, so there's no need to make the sections fixed. For custom
                            // sections, however, we do want fixed nodes to avoid padding.
                            .node_fixed = shndx != .text and
                                shndx != .rodata and
                                shndx != .data and
                                shndx != .data_rel_ro and
                                shndx != elf.shndx.tdata,
                        };
                    },
                    inline .INIT_ARRAY, .FINI_ARRAY, .PREINIT_ARRAY => |@"type"| .{
                        .shndx = shndx: {
                            // TODO: the input section name may include a "priority" value between 1
                            // and 65535 which should affect the order we assemble input sections in
                            const init_fini_section_name: []const u8 = switch (@"type") {
                                .INIT_ARRAY => "init_array",
                                .FINI_ARRAY => "fini_array",
                                .PREINIT_ARRAY => "preinit_array",
                                else => comptime unreachable,
                            };
                            const shndx: *Section.Index = &@field(elf.shndx, init_fini_section_name);
                            const need_addralign: u8 = switch (class) {
                                .NONE, _ => unreachable,
                                .@"32" => 4,
                                .@"64" => 8,
                            };
                            if (section.shdr.addralign != need_addralign) {
                                return diags.failParse(path, "bad addralign on {t} shdr", .{@"type"});
                            }
                            if (shndx.* == .UNDEF) {
                                try elf.createInitFiniArraySection(shndx, init_fini_section_name, @"type");
                            }
                            switch (elf.shdrPtr(shndx.*)) {
                                inline else => |shdr| {
                                    const old_size = elf.targetLoad(&shdr.size);
                                    const new_size = old_size + section.shdr.size;
                                    elf.targetStore(&shdr.size, @intCast(new_size));
                                    elf.updateInitFiniArraySectionSize(shndx.*, init_fini_section_name);
                                },
                            }
                            break :shndx shndx.*;
                        },
                        // This node must be fixed to prevent padding from being added between different
                        // INIT_ARRAY/FINI_ARRAY/PREINIT_ARRAY input sections.
                        .node_fixed = true,
                    },
                };
                const need_align: Alignment = .fromByteUnits(
                    std.math.ceilPowerOfTwoAssert(usize, @intCast(@max(section.shdr.addralign, 1))),
                );
                try opts.shndx.ensureAligned(elf, need_align);
                const add_node_opts: MappedFile.Node.AddOptions = .{
                    .size = need_align.forward(section.shdr.size),
                    .alignment = need_align,
                    .moved = true, // see assert at end of `flushInputSection`
                };
                const ni = elf.addNodeAssumeCapacity(
                    if (opts.node_fixed) ni: {
                        const shndx_ni = opts.shndx.get(elf).ni;
                        const after_oni: MappedFile.Node.Index.Optional = after: {
                            const last_ni = shndx_ni.last(&elf.mf).unwrap() orelse break :after .none;
                            break :after switch (last_ni.position(&elf.mf)) {
                                .header => .wrap(last_ni),
                                .footer, .floating => .none,
                            };
                        };
                        break :ni try shndx_ni.addHeaderChildAfter(gpa, &elf.mf, after_oni, add_node_opts);
                    } else try opts.shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, add_node_opts),
                    .{ .input_section = @fromBackingInt(@intCast(elf.input_sections.items.len)) },
                );
                section.isi = @fromBackingInt(@intCast(elf.input_sections.items.len));
                elf.input_sections.addOneAssumeCapacity().* = .{
                    .input = input_index,
                    .file_location = .{
                        .offset = fl.offset + section.shdr.offset,
                        .size = if (section.shdr.type == .NOBITS) 0 else section.shdr.size,
                    },
                    // The section vaddr is initially 0, because the symbol addresses are
                    // zero-based. This will eventually be updated by `flushMoved`.
                    .vaddr = 0,
                    .node = ni,
                    .first_symbol_reloc = .none,
                    .first_got_reloc = .none,
                };
                elf.input_prog_node.increaseEstimatedTotalItems(1);
            }
            var symmap: std.ArrayList(Symbol.Id) = .empty;
            defer symmap.deinit(gpa);
            for (sections[1..], 1..) |*symtab, symtab_shndx| switch (symtab.shdr.type) {
                else => {},
                .SYMTAB => {
                    if (symtab.shdr.entsize < @sizeOf(ElfN.Sym))
                        return diags.failParse(path, "unsupported symtab entsize", .{});
                    const strtab = strtab: {
                        if (symtab.shdr.link == std.elf.SHN_UNDEF or symtab.shdr.link >= ehdr.shnum)
                            return diags.failParse(path, "missing symbol names", .{});
                        const shdr = &sections[symtab.shdr.link].shdr;
                        if (shdr.type != .STRTAB)
                            return diags.failParse(path, "invalid strtab type", .{});
                        const strtab = try gpa.alloc(u8, @intCast(shdr.size));
                        errdefer gpa.free(strtab);
                        try fr.seekTo(fl.offset + shdr.offset);
                        try r.readSliceAll(strtab);
                        break :strtab strtab;
                    };
                    defer gpa.free(strtab);
                    const symnum = std.math.sub(u32, std.math.divExact(
                        u32,
                        @intCast(symtab.shdr.size),
                        @intCast(symtab.shdr.entsize),
                    ) catch return diags.failParse(
                        path,
                        "symtab section size (0x{x}) is not a multiple of entsize (0x{x})",
                        .{ symtab.shdr.size, symtab.shdr.entsize },
                    ), 1) catch continue;
                    symmap.clearRetainingCapacity();
                    try symmap.resize(gpa, symnum);
                    try fr.seekTo(fl.offset + symtab.shdr.offset + symtab.shdr.entsize);
                    for (symmap.items) |*si| {
                        si.* = .null;
                        const input_sym = try r.peekStruct(ElfN.Sym, target_endian);
                        try r.discardAll64(symtab.shdr.entsize);
                        if (input_sym.name >= strtab.len or input_sym.shndx >= ehdr.shnum) continue;

                        const name = std.mem.sliceTo(strtab[input_sym.name..], 0);

                        const sym_type: std.elf.STT = switch (input_sym.info.type) {
                            .NOTYPE, .OBJECT, .FUNC, .TLS => |t| t,
                            .SECTION => .NOTYPE,
                            .FILE, .COMMON, _ => continue,
                        };

                        if (input_sym.shndx == std.elf.SHN_UNDEF) switch (input_sym.info.bind) {
                            else => |bind| return diags.failParse(
                                path,
                                "symbol '{s}' has unsupported binding (0x{x})",
                                .{ name, bind },
                            ),
                            .LOCAL => continue,
                            .GLOBAL, .WEAK, .GNU_UNIQUE => |bind| {
                                si.* = .global(elf.addGlobalSymbol(.{
                                    .node = .none,
                                    .name = name,
                                    .value = input_sym.value,
                                    .size = input_sym.size,
                                    .type = sym_type,
                                    .bind = switch (bind) {
                                        .WEAK, .GNU_UNIQUE => .weak,
                                        .GLOBAL => .strong,
                                        else => unreachable,
                                    },
                                    .visibility = input_sym.other.visibility,
                                    .shndx = .UNDEF,
                                }) catch |err| switch (err) {
                                    error.MultipleDefinitions => unreachable, // shndx is .UNDEF
                                    error.MultipleDefaultVersions => unreachable, // shndx is .UNDEF
                                    error.UndefinedDefaultVersion => return diags.failParse(
                                        path,
                                        "symbol '{s}' specifies default version without defining it",
                                        .{name},
                                    ),
                                    else => |e| return e,
                                });
                                continue;
                            },
                        };

                        const input_section_node = (sections[input_sym.shndx].isi orelse continue).node(elf);

                        switch (input_sym.info.bind) {
                            else => |bind| return diags.failParse(
                                path,
                                "symbol '{s}' has unsupported binding (0x{x})",
                                .{ name, bind },
                            ),
                            .LOCAL => {
                                const lsi = try elf.addLocalSymbol(.{
                                    .node = .wrap(input_section_node),
                                    .name = name,
                                    .value = input_sym.value,
                                    .size = input_sym.size,
                                    .type = sym_type,
                                    .shndx = elf.getNodeShndx(input_section_node),
                                });
                                si.* = .local(lsi);
                            },
                            .GLOBAL, .WEAK, .GNU_UNIQUE => |bind| {
                                si.* = .global(elf.addGlobalSymbol(.{
                                    .node = .wrap(input_section_node),
                                    .name = name,
                                    .value = input_sym.value,
                                    .size = input_sym.size,
                                    .type = sym_type,
                                    .bind = switch (bind) {
                                        .WEAK, .GNU_UNIQUE => .weak,
                                        .GLOBAL => .strong,
                                        else => unreachable,
                                    },
                                    .visibility = input_sym.other.visibility,
                                    .shndx = elf.getNodeShndx(input_section_node),
                                }) catch |err| switch (err) {
                                    error.MultipleDefinitions => return diags.failParse(
                                        path,
                                        "multiple definitions of '{s}'",
                                        .{name},
                                    ),
                                    error.MultipleDefaultVersions => return diags.failParse(
                                        path,
                                        "multiple default versions of '{s}'",
                                        .{parseVersionedSymbolName(name).name},
                                    ),
                                    error.UndefinedDefaultVersion => unreachable, // shndx is not `.UNDEF` (i.e. the symbol is defined)
                                    else => |e| return e,
                                });
                            },
                        }
                    }
                    for (sections[1..]) |*rel_sec| switch (rel_sec.shdr.type) {
                        else => {},
                        inline .REL, .RELA => |sht| {
                            if (rel_sec.shdr.link != symtab_shndx or rel_sec.shdr.info == std.elf.SHN_UNDEF or
                                rel_sec.shdr.info >= ehdr.shnum) continue;
                            const Rel = switch (sht) {
                                else => comptime unreachable,
                                .REL => ElfN.Rel,
                                .RELA => ElfN.Rela,
                            };
                            if (rel_sec.shdr.entsize < @sizeOf(Rel))
                                return diags.failParse(path, "unsupported rel entsize", .{});

                            const loc_sec = &sections[rel_sec.shdr.info];
                            const loc_node = (loc_sec.isi orelse continue).node(elf);
                            elf.resetNodeRelocs(loc_node);

                            const relnum = std.math.divExact(
                                u32,
                                @intCast(rel_sec.shdr.size),
                                @intCast(rel_sec.shdr.entsize),
                            ) catch return diags.failParse(
                                path,
                                "relocation section size (0x{x}) is not a multiple of entsize (0x{x})",
                                .{ rel_sec.shdr.size, rel_sec.shdr.entsize },
                            );
                            try elf.ensureUnusedRelocCapacity(loc_node, relnum);
                            try fr.seekTo(fl.offset + rel_sec.shdr.offset);
                            for (0..relnum) |_| {
                                const rel = try r.peekStruct(Rel, target_endian);
                                try r.discardAll64(rel_sec.shdr.entsize);
                                if (rel.info.sym == 0) continue;
                                if (rel.info.sym > symnum) return diags.failParse(
                                    path,
                                    "relocation target symbol index {d} exceeds symtab size",
                                    .{rel.info.sym},
                                );
                                const target = symmap.items[rel.info.sym - 1];
                                if (target == Symbol.Id.null) {
                                    // If this is not an SHF_ALLOC section, then let's not report
                                    // this for now, because it probably doesn't affect the final
                                    // binary's functionality for this section to be a bit broken.
                                    if (loc_sec.shdr.flags.shf.ALLOC) {
                                        diags.addParseError(
                                            path,
                                            "unsupported symbol at index {d} required for relocation",
                                            .{rel.info.sym},
                                        );
                                    }
                                    continue;
                                }
                                const rt: MachineRelocType = .wrap(rel.info.type, elf);
                                elf.addRelocAssumeCapacity(
                                    loc_node,
                                    rel.offset - loc_sec.shdr.addr,
                                    target,
                                    rel.addend,
                                    rt,
                                ) catch |err| switch (err) {
                                    else => |e| return e,
                                    error.UnknownRelocation => diags.addParseError(
                                        path,
                                        "unknown relocation type '{f}'",
                                        .{rt.fmt(elf)},
                                    ),
                                    error.NonStaticRelocation => diags.addParseError(
                                        path,
                                        "non-static relocation type '{f}'",
                                        .{rt.fmt(elf)},
                                    ),
                                    error.UnimplementedRelocation => diags.addParseError(
                                        path,
                                        "TODO(Elf2): unimplemented relocation type '{f}'",
                                        .{rt.fmt(elf)},
                                    ),
                                };
                            }
                        },
                    };
                },
            };
        },
    }
}
/// This function may resize the archive header, so therefore invalidates `member_ar_hdr`.
fn populateArchiveMemberName(elf: *Elf, member_ar_hdr: *std.elf.ar_hdr, member_name: []const u8) Error!void {
    if (std.mem.print(&member_ar_hdr.ar_name, "{s}/", .{member_name})) |name_str| {
        @memset(member_ar_hdr.ar_name[name_str.len..], ' ');
        return;
    } else |err| switch (err) {
        error.NoSpaceLeft => {}, // handled below
    }

    const gpa = elf.base.comp.gpa;
    const archive_header_ni = elf.archive.?.header_ni;

    // The member's name is too big to put directly in the `ar_name` field, so it needs to go in the
    // "long name" string table instead (in the special member named "//").

    _, const old_archive_header_size = archive_header_ni.location(&elf.mf).resolve(&elf.mf);

    // We're going to add a new string at the end of the table. Update `member_ar_hdr` first,
    // because resizing the string table will invalidate it.
    const string_table_offset = old_archive_header_size - (std.elf.ARMAG.len + @sizeOf(std.elf.ar_hdr));
    if (std.mem.print(&member_ar_hdr.ar_name, "/{d}", .{string_table_offset})) |name_str| {
        @memset(member_ar_hdr.ar_name[name_str.len..], ' ');
    } else |inner_err| switch (inner_err) {
        error.NoSpaceLeft => {
            // The string table offset is itself too big to represent. This means the string table's
            // *size* is definitely too big to represent (we only get 10 bytes for that whereas we
            // get 16 here!), so as long as we still add the string, we're guaranteed to get a link
            // error for that reason. Therefore, we can just ignore this error and carry on.
        },
    }

    // We set the size of the archive header node exactly, because we want padding bytes to go into
    // the root `.archive` node. That way, those bytes could still be used to grow the string table
    // if necessary, but they could also be used for new archive members.
    try archive_header_ni.resizeLeaf(gpa, &elf.mf, old_archive_header_size + member_name.len + 2);

    const dest_slice = archive_header_ni.slice(&elf.mf)[@intCast(old_archive_header_size)..];
    @memcpy(dest_slice[0 .. dest_slice.len - 2], member_name);
    @memcpy(dest_slice[dest_slice.len - 2 ..], "/\n"); // yes, the terminator is weird
}
fn loadDso(
    elf: *Elf,
    path: std.Build.Cache.Path,
    fallback_soname: link.Input.Dso.FallbackSoname,
    fr: *Io.File.Reader,
) (LoadParseInputError || error{BadMagic})!void {
    const comp = elf.base.comp;
    const gpa = comp.gpa;
    const diags = &comp.link_diags;
    const r = &fr.interface;

    log.debug("loadDso({f})", .{path.fmtEscapeString()});
    try elf.checkInputIdent(path, r);

    if (elf.ehdrType() == .REL) return; // this input does not affect the output artifact

    const target_endian = elf.targetEndian();
    switch (elf.identClass()) {
        .NONE, _ => unreachable,
        inline else => |class| {
            const ElfN = class.ElfN();
            const ehdr = try r.peekStruct(ElfN.Ehdr, target_endian);
            if (ehdr.type != .DYN) return diags.failParse(path, "unsupported dso type", .{});
            if (ehdr.machine != elf.ehdrMachine().toElf())
                return diags.failParse(path, "bad machine", .{});
            // We're going to need to know the alignment of every section later.
            const section_aligns = try gpa.alloc(Alignment, ehdr.shnum);
            defer gpa.free(section_aligns);
            const shdr: struct {
                dynamic: ElfN.Shdr,
                dynsym: ElfN.Shdr,
                dynstr: ElfN.Shdr,
                verdef: ?ElfN.Shdr,
                versym: ?ElfN.Shdr,
            } = shdr: {
                if (ehdr.shnum > 0) try fr.seekTo(ehdr.shoff);
                const InputSh = struct {
                    shndx: u32,
                    sh: ElfN.Shdr,
                };
                var opt_dynamic: ?InputSh = null;
                var opt_dynsym: ?InputSh = null;
                var opt_verdef: ?InputSh = null;
                var opt_versym: ?InputSh = null;
                for (section_aligns, 0..) |*section_align, shndx| {
                    const sh = try r.peekStruct(ElfN.Shdr, target_endian);
                    try r.discardAll(ehdr.shentsize);
                    section_align.* = .fromByteUnits(std.math.ceilPowerOfTwoAssert(
                        usize,
                        @intCast(@max(sh.addralign, 1)),
                    ));
                    const ptr: *?InputSh = switch (sh.type) {
                        else => continue,
                        .DYNAMIC => &opt_dynamic,
                        .DYNSYM => &opt_dynsym,
                        .GNU_VERDEF => &opt_verdef,
                        .GNU_VERSYM => &opt_versym,
                    };
                    if (ptr.* != null) {
                        return diags.failParse(path, "multiple SHT_{t} sections", .{sh.type});
                    }
                    ptr.* = .{ .shndx = @intCast(shndx), .sh = sh };
                }

                const dynamic = opt_dynamic orelse {
                    return diags.failParse(path, "missing SHT_DYNAMIC section", .{});
                };
                const dynsym = opt_dynsym orelse {
                    return diags.failParse(path, "missing SHT_DYNSYM section", .{});
                };

                const dynstr_shndx = dynamic.sh.link;
                if (dynstr_shndx >= ehdr.shnum) {
                    return diags.failParse(path, "SHT_DYNAMIC section does not link to a valid section", .{});
                }
                try fr.seekTo(ehdr.shoff + dynstr_shndx * ehdr.shentsize);
                const dynstr_sh = try r.peekStruct(ElfN.Shdr, target_endian);
                if (dynstr_sh.type != .STRTAB) {
                    return diags.failParse(path, "SHT_DYNAMIC section does not link to a SHT_STRTAB section", .{});
                }

                // Validate all the other shdr `link` fields. After this we won't need the shndx
                // values for anything else.
                if (dynsym.sh.link != dynstr_shndx) {
                    return diags.failParse(path, "SHT_DYNSYM section does not link to the dynamic string table section", .{});
                }
                if (opt_verdef) |verdef| if (verdef.sh.link != dynstr_shndx) {
                    return diags.failParse(path, "SHT_GNU_VERDEF section does not link to the dynamic string table section", .{});
                };
                if (opt_versym) |versym| if (versym.sh.link != dynsym.shndx) {
                    return diags.failParse(path, "SHT_GNU_VERSYM section does not link to the dynamic symbol table section", .{});
                };

                break :shdr .{
                    .dynamic = dynamic.sh,
                    .dynsym = dynsym.sh,
                    .dynstr = dynstr_sh,
                    .verdef = if (opt_verdef) |verdef| verdef.sh else null,
                    .versym = if (opt_versym) |versym| versym.sh else null,
                };
            };

            if (shdr.dynamic.entsize != @sizeOf(ElfN.Addr) * 2) {
                return diags.failParse(path, "bad SHT_DYNAMIC section entsize", .{});
            }
            const dynnum = std.math.divExact(
                u32,
                @intCast(shdr.dynamic.size),
                @sizeOf(ElfN.Addr) * 2,
            ) catch return diags.failParse(
                path,
                "SHT_DYNAMIC section size (0x{x}) is not a multiple of entsize (0x{x})",
                .{ shdr.dynamic.size, @sizeOf(ElfN.Addr) * 2 },
            );

            if (shdr.dynsym.entsize < @sizeOf(ElfN.Sym)) {
                return diags.failParse(path, "dynamic symbol table has invalid entsize", .{});
            }
            const symnum = std.math.divExact(
                u32,
                @intCast(shdr.dynsym.size),
                @intCast(shdr.dynsym.entsize),
            ) catch return diags.failParse(
                path,
                "dynamic symbol table size (0x{x}) is not a multiple of entsize (0x{x})",
                .{ shdr.dynsym.size, shdr.dynsym.entsize },
            );

            const dynstr = try gpa.alloc(u8, @intCast(shdr.dynstr.size));
            defer gpa.free(dynstr);
            try fr.seekTo(shdr.dynstr.offset);
            try r.readSliceAll(dynstr);

            const versym: []std.elf.Versym = versym: {
                const versym_sh = shdr.versym orelse break :versym &.{};
                if (versym_sh.size != symnum * 2) return diags.failParse(
                    path,
                    "SHT_GNU_VERSYM section has invalid size (expected 0x{x}, got 0x{x})",
                    .{ symnum * 2, versym_sh.size },
                );
                const versym = try gpa.alloc(std.elf.Versym, symnum);
                errdefer gpa.free(versym);
                try fr.seekTo(versym_sh.offset);
                try r.readSliceEndian(std.elf.Versym, versym, target_endian);
                break :versym versym;
            };
            defer gpa.free(versym);

            const verdef: []DsoGlobals.String.Optional = verdef: {
                const verdef_sh = shdr.verdef orelse break :verdef &.{};

                // This parsing is a bit of a mess because it appears that at this moment in history
                // nobody at GNU was yet aware of the concept of an "array".

                const num_versions = verdef_sh.info;
                if (num_versions > std.math.maxInt(u16)) {
                    return diags.failParse(path, "entry count of SHT_GNU_VERDEF section is too large", .{});
                }

                // First pass: find the largest version index, because they're not actually indices,
                // but rather arbitrary IDs. But it's okay to allocate memory proportional to the
                // largest ID, because (a) glibc does this which suggests that the values should be
                // dense in practice, and more importantly (b) the IDs are only 16 bits long anyway
                // so this will never allocate more than 65k entries.
                var max_ver_ndx: u16 = 0;
                var verdef_offset: u64 = 0;
                for (0..num_versions) |_| {
                    if (verdef_offset + @sizeOf(std.elf.Verdef) > verdef_sh.size) {
                        return diags.failParse(path, "verdef entry exceeds section bounds", .{});
                    }
                    try fr.seekTo(verdef_sh.offset + verdef_offset);
                    const def = try r.peekStruct(std.elf.Verdef, target_endian);
                    if (def.cnt != 0 and def.flags & std.elf.VER_FLG_BASE == 0) {
                        max_ver_ndx = @max(max_ver_ndx, @backingInt(def.ndx));
                    }
                    verdef_offset += def.next;
                }

                const verdef = try gpa.alloc(DsoGlobals.String.Optional, @intCast(max_ver_ndx + 1));
                errdefer gpa.free(verdef);

                @memset(verdef, .none);

                // Second pass: for each version definition, look at its first verdaux entry to get
                // the actual version name.
                verdef_offset = 0;
                for (0..num_versions) |_| {
                    try fr.seekTo(verdef_sh.offset + verdef_offset);
                    const def = try r.peekStruct(std.elf.Verdef, target_endian);
                    if (def.cnt != 0 and def.flags & std.elf.VER_FLG_BASE == 0) {
                        const ver_ndx = @backingInt(def.ndx);
                        if (verdef[ver_ndx] != .none) {
                            return diags.failParse(path, "multiple definitions for symbol version index '{d}'", .{ver_ndx});
                        }
                        if (verdef_offset + def.aux + @sizeOf(std.elf.Verdaux) > verdef_sh.size) {
                            return diags.failParse(path, "verdaux entry exceeds section bounds", .{});
                        }
                        try fr.seekTo(verdef_sh.offset + verdef_offset + def.aux);
                        const aux = try r.peekStruct(std.elf.Verdaux, target_endian);
                        if (aux.name >= shdr.dynstr.size) {
                            return diags.failParse(path, "bad verdaux entry name string", .{});
                        }
                        const version_name = std.mem.sliceTo(dynstr[aux.name..], 0);
                        verdef[ver_ndx] = @fromBackingInt(@intCast(elf.dso_globals.string_bytes.items.len));
                        try elf.dso_globals.string_bytes.ensureUnusedCapacity(gpa, version_name.len + 1);
                        elf.dso_globals.string_bytes.appendSliceAssumeCapacity(version_name);
                        elf.dso_globals.string_bytes.appendAssumeCapacity(0);
                    }
                    verdef_offset += def.next;
                }

                break :verdef verdef;
            };
            defer gpa.free(verdef);

            // Find the DT_SONAME dynamic entry so that it can become our DT_NEEDED entry.
            try fr.seekTo(shdr.dynamic.offset);
            const soname_slice: []const u8 = for (0..dynnum) |_| {
                const tag = try r.takeInt(ElfN.Addr, target_endian);
                const val = try r.takeInt(ElfN.Addr, target_endian);
                if (tag == std.elf.DT_SONAME) {
                    // val is a dynstr index
                    if (val >= dynstr.len) {
                        return diags.failParse(path, "bad soname string", .{});
                    }
                    break std.mem.sliceTo(dynstr[@intCast(val)..], 0);
                }
            } else switch (fallback_soname) {
                .basename => std.fs.path.basename(path.sub_path),
                .full_path => try path.toString(comp.arena),
            };
            const soname = try elf.string(.dynstr, soname_slice);
            try elf.needed.put(gpa, soname, {});

            // Scan the symbol table and populate `elf.dso_globals`.
            const first_global = @min(shdr.dynsym.info, symnum);
            try elf.dso_globals.symbols.ensureUnusedCapacity(gpa, symnum - first_global);
            try fr.seekTo(shdr.dynsym.offset + first_global * shdr.dynsym.entsize);
            for (first_global..symnum) |in_dynsym_index| {
                const sym = try r.peekStruct(ElfN.Sym, target_endian);
                try r.discardAll(@intCast(shdr.dynsym.entsize));

                switch (sym.info.bind) {
                    else => continue,
                    .GLOBAL, .WEAK, .GNU_UNIQUE => {},
                }
                // STV_HIDDEN/STV_INTERNAL symbols should be marked as STB_LOCAL and hence skipped
                // above, but we might as well double-check.
                switch (sym.other.visibility) {
                    .HIDDEN, .INTERNAL => continue,
                    .DEFAULT, .PROTECTED => {},
                }

                if (sym.shndx == std.elf.SHN_UNDEF) continue;
                if (sym.shndx >= ehdr.shnum) continue;

                if (sym.name >= dynstr.len) {
                    return diags.failParse(path, "bad symbol name string", .{});
                }

                const maybe_version: DsoGlobals.String.Optional, const is_default_version: bool = if (versym.len > 0) switch (versym[in_dynsym_index]) {
                    .LOCAL, .GLOBAL => .{ .none, false },
                    else => |v| version: {
                        if (v.VERSION >= verdef.len or verdef[v.VERSION] == .none) {
                            return diags.failParse(path, "bad symbol version index '{d}'", .{v.VERSION});
                        }
                        break :version .{ verdef[v.VERSION], !v.HIDDEN };
                    },
                } else .{ .none, false };

                // We need to guess the worst-case alignment of the symbol. Yes, I know this seems
                // insane---refer to the doc comment on `DsoGlobals.Symbol.alignment`.
                const sym_align: Alignment = switch (sym.value) {
                    0 => section_aligns[sym.shndx],
                    else => section_aligns[sym.shndx].min(@fromBackingInt(@intCast(@ctz(sym.value)))),
                };

                const name = std.mem.sliceTo(dynstr[sym.name..], 0);

                const gop = elf.dso_globals.symbols.getOrPutAssumeCapacityAdapted(@as(DsoGlobals.Symbol.Key, .{
                    .name = name,
                    .version = if (maybe_version.unwrap()) |version| slice: {
                        break :slice version.slice(&elf.dso_globals);
                    } else null,
                }), @as(DsoGlobals.Symbol.Adapter, .{ .dso_globals = &elf.dso_globals }));

                if (gop.found_existing and gop.key_ptr.type != .NOTYPE) {
                    if (sym.size > gop.key_ptr.size or
                        sym_align.compare(.gt, gop.key_ptr.alignment))
                    {
                        gop.key_ptr.size = @max(gop.key_ptr.size, sym.size);
                        gop.key_ptr.alignment = gop.key_ptr.alignment.max(sym_align);
                    }
                } else {
                    gop.key_ptr.* = .{
                        .name = name: {
                            if (gop.found_existing) break :name gop.key_ptr.name;
                            const name_str_off = elf.dso_globals.string_bytes.items.len;
                            try elf.dso_globals.string_bytes.ensureUnusedCapacity(gpa, name.len + 1);
                            elf.dso_globals.string_bytes.appendSliceAssumeCapacity(name);
                            elf.dso_globals.string_bytes.appendAssumeCapacity(0);
                            break :name @fromBackingInt(@intCast(name_str_off));
                        },
                        .version = maybe_version,
                        .soname = soname,
                        .type = sym.info.type,
                        .size = sym.size,
                        .alignment = sym_align,
                    };
                }

                if (is_default_version) {
                    const adapter: DsoGlobals.Symbol.DefaultVersionAdapter = .{
                        .dso_globals = &elf.dso_globals,
                    };
                    const default_gop = try elf.dso_globals.default_sym_vers.getOrPutAdapted(gpa, name, adapter);
                    if (!default_gop.found_existing) {
                        default_gop.key_ptr.* = @intCast(gop.index);
                    }
                }

                // Update any global(s) which may care about this input DSO global.
                if (maybe_version.unwrap()) |version| {
                    if (elf.globalByName(.{ .name = name, .version = version.slice(&elf.dso_globals) })) |gsi| {
                        try elf.updateGlobalDynamic(gsi, false);
                    }
                }
                if (maybe_version == .none or is_default_version) {
                    if (elf.globalByName(.{ .name = name, .version = null })) |gsi| {
                        try elf.updateGlobalDynamic(gsi, false);
                    }
                }
            }
        },
    }
}

/// Validates that the `std.elf.Ident` present at the start of `r` is a compatible link input.
///
/// Returns an error if it is incompatible, or if the ident is broken or missing---usually
/// `error.AlreadyReported`, but if the magic number is missing or incorrect, returns
/// `error.BadMagic` instead.
///
/// If necessary, modifies our own ident to use `ELF_OSABI_GNU` instead of `ELF_OSABI_NONE`.
///
/// Does not advance the position of `r`. Requires `r` to have a 16-byte buffer.
fn checkInputIdent(
    elf: *Elf,
    path: std.Build.Cache.Path,
    r: *Io.Reader,
) error{ BadMagic, EndOfStream, AlreadyReported, ReadFailed }!void {
    const diags = &elf.base.comp.link_diags;

    const magic = r.peek(std.elf.MAGIC.len) catch |err| switch (err) {
        error.ReadFailed => |e| return e,
        error.EndOfStream => return error.BadMagic,
    };
    if (!std.mem.eql(u8, magic, std.elf.MAGIC)) {
        return error.BadMagic;
    }

    const ident = try r.peekStructPointer(std.elf.Ident);
    const target: *std.elf.Ident = @ptrCast(
        elf.ni.elf.slice(&elf.mf)[0..@sizeOf(std.elf.Ident)],
    );

    if (ident.class != target.class) return diags.failParse(
        path,
        "bad ELF class ({?s})",
        .{std.enums.tagName(std.elf.CLASS, ident.class)},
    );
    if (ident.data != target.data) return diags.failParse(
        path,
        "bad ELF data encoding ({?s})",
        .{std.enums.tagName(std.elf.DATA, ident.data)},
    );
    if (ident.version != target.version) return diags.failParse(
        path,
        "bad ELF version ({d})",
        .{ident.version},
    );
    // OSABI is a bit more complex.
    const expect_abiversion: u8 = switch (ident.osabi) {
        .NONE => 0,
        .GNU => abiversion: {
            // If we're currently emitting `ELF_OSABI_NONE` then prefer `ELF_OSABI_GNU` to signify
            // the GNU-specific features, but if we're already emitting a target-specific osabi then
            // just leave it alone.
            if (target.osabi == .NONE) target.osabi = .GNU;
            // Either way, allow the `ELF_OSABI_GNU` input through.
            break :abiversion 0;
        },
        else => if (ident.osabi == target.osabi) abiversion: {
            break :abiversion target.abiversion;
        } else return diags.failParse(
            path,
            "bad ELF OS/ABI ({?s})",
            .{std.enums.tagName(std.elf.OSABI, ident.osabi)},
        ),
    };
    if (ident.abiversion != expect_abiversion) return diags.failParse(
        path,
        "bad ELF ABI version ({d})",
        .{ident.abiversion},
    );
}

fn createInitFiniArraySection(
    elf: *Elf,
    shndx: *Section.Index,
    comptime name: []const u8,
    @"type": std.elf.SHT,
) Error!void {
    assert(shndx.* == .UNDEF);
    const gpa = elf.base.comp.gpa;
    const addr_align: Alignment = switch (elf.identClass()) {
        .NONE, _ => unreachable,
        .@"32" => .@"4",
        .@"64" => .@"8",
    };
    assert(elf.section_by_name.count() == elf.shdrs.items.len);
    try elf.section_by_name.ensureUnusedCapacity(gpa, 1);
    shndx.* = try elf.addSection(elf.ni.data_rel_ro, .{
        .name = "." ++ name,
        .type = @"type",
        .flags = .{ .WRITE = true, .ALLOC = true },
        .node_align = addr_align,
        .manual_size = true,
    });
    elf.section_by_name.putAssumeCapacityNoClobber(shndx.name(elf), {});
    // These symbols definitely already exist with strong definitions, because we added them
    // alongside the other linker-defined symbols, all the way back in `initHeaders`.
    const start_gsi = elf.globalByName(.{
        .name = "__" ++ name ++ "_start",
        .version = null,
    }).?;
    const end_gsi = elf.globalByName(.{
        .name = "__" ++ name ++ "_end",
        .version = null,
    }).?;
    try elf.setGlobalSymbolValue(start_gsi, .{
        .node = .wrap(shndx.get(elf).ni),
        .value = shndx.vaddr(elf),
        .size = 0,
        .type = .NOTYPE,
        .shndx = shndx.*,
    });
    try elf.updateGlobalDynamic(start_gsi, true);
    try elf.setGlobalSymbolValue(end_gsi, .{
        .node = .wrap(shndx.get(elf).ni),
        .value = shndx.vaddr(elf),
        .size = 0,
        .type = .NOTYPE,
        .shndx = shndx.*,
    });
    try elf.updateGlobalDynamic(end_gsi, true);
}
fn updateInitFiniArraySectionSize(
    elf: *Elf,
    shndx: Section.Index,
    comptime name: []const u8,
) void {
    const end_vaddr: u64 = switch (elf.shdrPtr(shndx)) {
        inline else => |shdr| shndx.vaddr(elf) + elf.targetLoad(&shdr.size),
    };
    const end_sym_gsi = elf.globalByName(.{
        .name = "__" ++ name ++ "_end",
        .version = null,
    }).?;
    Symbol.Id.global(end_sym_gsi).flushMoved(elf, end_vaddr);
}

pub fn prelink(elf: *Elf, prog_node: std.Progress.Node) link.Error!void {
    const prelink_prog_node = prog_node.start("ELF Prelink", 0);
    defer prelink_prog_node.end();

    const diags = &elf.base.comp.link_diags;
    elf.prelinkInner() catch |err| switch (err) {
        error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
        else => |e| return e,
    };
}
fn prelinkInner(elf: *Elf) Error!void {
    const comp = elf.base.comp;
    const gpa = comp.gpa;

    const addr_align: Alignment = switch (elf.identClass()) {
        .NONE, _ => unreachable,
        .@"32" => .@"4",
        .@"64" => .@"8",
    };
    try elf.nodes.ensureUnusedCapacity(gpa, 7 + 5);
    for ([7]Section.Index{
        elf.shndx.debug_addr,
        elf.shndx.eh_frame,
        elf.shndx.debug_frame,
        elf.shndx.debug_info,
        elf.shndx.debug_line,
        elf.shndx.debug_rnglists,
        elf.shndx.debug_str_offsets,
    }) |debug_shndx| {
        if (debug_shndx == .UNDEF) continue;
        const debug_ni = debug_shndx.get(elf).ni;
        const frame_format = debug_shndx.debugFrameFormat(elf);
        const unit_padding_ni = elf.addNodeAssumeCapacity(
            try debug_ni.addHeaderChildAfter(gpa, &elf.mf, last_header_oni: {
                var last_header_oni = debug_ni.last(&elf.mf);
                while (last_header_oni.unwrap()) |last_header_ni|
                    switch (last_header_ni.position(&elf.mf)) {
                        .header => break,
                        .footer => last_header_oni = last_header_ni.prev(&elf.mf),
                        .floating => unreachable,
                    };
                break :last_header_oni last_header_oni;
            }, .{
                .alignment = if (frame_format) |_| addr_align else .@"1",
                .next_moved = true,
                .enable_next_moved = true,
            }),
            .unit_padding,
        );
        var debug_nw: MappedFile.Node.Writer = undefined;
        unit_padding_ni.writer(gpa, &elf.mf, &debug_nw);
        defer debug_nw.deinit();
        (if (frame_format) |format|
            elf.dwarf.genDebugFrameCie(&debug_nw.interface, null, format)
        else
            elf.dwarf.genUnitPadding(&debug_nw.interface)) catch |err| switch (err) {
            error.WriteFailed => return debug_nw.err.?,
        };
    }

    if (comp.zcu) |_| self_hosted_codegen: {
        if (comp.config.use_llvm) break :self_hosted_codegen;

        // We're using self-hosted codegen---add an input representing the Zig "object".
        try elf.inputs.ensureUnusedCapacity(gpa, 1);
        const zcu_name = try std.fmt.allocPrint(gpa, "{s}_zcu", .{comp.root_name});
        defer gpa.free(zcu_name);
        const zcu_file_symbol = try elf.addLocalSymbol(.{
            .node = .none,
            .name = zcu_name,
            .value = 0,
            .size = 0,
            .type = .FILE,
            .shndx = .ABS,
        });
        elf.inputs.addOneAssumeCapacity().* = .{
            .path = elf.base.emit,
            .member = null,
            .extra = .{ .file_symbol = zcu_file_symbol },
        };
        elf.input_pending_index += 1;

        switch (elf.shndx.debug_addr) {
            .UNDEF => {},
            else => |debug_addr_shndx| {
                const debug_addr_ni = elf.addNodeAssumeCapacity(
                    try debug_addr_shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{
                        .alignment = addr_align,
                        .next_moved = true,
                        .enable_next_moved = true,
                    }),
                    .debug_addr,
                );
                elf.dwarf.debug_addr.ni = .wrap(debug_addr_ni);

                var dah_nw: link.MappedFile.Node.Writer = undefined;
                debug_addr_ni.writer(gpa, &elf.mf, &dah_nw);
                defer dah_nw.deinit();
                elf.dwarf.genDebugAddrHeader(&dah_nw.interface) catch |err| switch (err) {
                    else => |e| return e,
                    error.WriteFailed => return dah_nw.err.?,
                };
            },
        }
        switch (elf.shndx.debug_abbrev) {
            .UNDEF => {},
            else => |debug_abbrev_shndx| elf.dwarf.debug_abbrev.ni = .wrap(elf.addNodeAssumeCapacity(
                try debug_abbrev_shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{}),
                .{ .debug_shared = .debug_abbrev },
            )),
        }
        switch (elf.shndx.debug_line_str) {
            .UNDEF => {},
            else => |debug_line_str_shndx| elf.dwarf.debug_line_str.ni =
                .wrap(elf.addNodeAssumeCapacity(
                    try debug_line_str_shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{}),
                    .{ .debug_shared = .debug_line_str },
                )),
        }
        switch (elf.shndx.debug_str) {
            .UNDEF => {},
            else => |debug_str_shndx| elf.dwarf.debug_str.ni = .wrap(elf.addNodeAssumeCapacity(
                try debug_str_shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{}),
                .{ .debug_shared = .debug_str },
            )),
        }
        switch (elf.shndx.debug_str_offsets) {
            .UNDEF => {},
            else => |debug_str_offsets_shndx| {
                const debug_str_offsets_ni = elf.addNodeAssumeCapacity(
                    try debug_str_offsets_shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{
                        .alignment = switch (elf.dwarf.format) {
                            .@"32" => .@"4",
                            .@"64" => .@"8",
                        },
                        .next_moved = true,
                        .enable_next_moved = true,
                    }),
                    .debug_str_offsets,
                );
                elf.dwarf.debug_str_offsets.ni = .wrap(debug_str_offsets_ni);

                var dsoh_nw: link.MappedFile.Node.Writer = undefined;
                debug_str_offsets_ni.writer(gpa, &elf.mf, &dsoh_nw);
                defer dsoh_nw.deinit();
                elf.dwarf.genDebugStrOffsetsHeader(&dsoh_nw.interface) catch |err| switch (err) {
                    else => |e| return e,
                    error.WriteFailed => return dsoh_nw.err.?,
                };
            },
        }
    }
}

pub fn zcuFilesReady(elf: *Elf, zcu: *Zcu) link.Error!void {
    elf.zcuFilesReadyInner(zcu) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return elf.base.comp.link_diags.fail(
            "failed to write output file: {t}",
            .{elf.mf.io_err.?},
        ),
    };
}
fn zcuFilesReadyInner(elf: *Elf, zcu: *Zcu) Error!void {
    const gpa = zcu.gpa;
    const units_len = zcu.module_roots.count();
    if (elf.dwarf_units.len == 0) {
        @branchHint(.unlikely);
        try elf.dwarf.initUnits(gpa, units_len);
        elf.dwarf_units = try gpa.alloc(dwarf_relocs.Unit, zcu.module_roots.count());
        @memset(elf.dwarf_units, .{
            .frame_cie_first_target_reloc = .none,
            .debug_info_header_first_target_reloc = .none,
            .debug_info_header_first_node_reloc = .none,
            .debug_line_header_first_target_reloc = .none,
            .debug_line_header_first_node_reloc = .none,
            .debug_rnglists_first_target_reloc = .none,
            .debug_rnglists_symbol_relocs = .empty,
        });
    }
    if (!try elf.dwarf.updateUnits(zcu)) return;
    try elf.nodes.ensureUnusedCapacity(gpa, 6 * units_len);
    for (0..units_len) |unit_index| {
        const ui: Dwarf.Unit.Index = @fromBackingInt(@intCast(unit_index));
        const unit = ui.get(&elf.dwarf);
        if (!unit.alive) continue;
        switch (elf.shndx.debug_info) {
            .UNDEF => {},
            else => |debug_info_shndx| {
                const debug_info_ni = unit.debug_info_ni.unwrap() orelse debug_info_ni: {
                    const debug_info_ni = elf.addNodeAssumeCapacity(
                        try debug_info_shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{
                            .alignment = elf.mf.flags.block_size,
                            .enable_next_moved = true,
                        }),
                        .{ .unit_debug_info = ui },
                    );
                    unit.debug_info_ni = .wrap(debug_info_ni);
                    break :debug_info_ni debug_info_ni;
                };
                if (unit.debug_info_header_ni == .none) unit.debug_info_header_ni = .wrap(
                    elf.addNodeAssumeCapacity(try debug_info_ni.addOnlyHeaderChild(gpa, &elf.mf, .{
                        .next_moved = true,
                        .enable_next_moved = true,
                    }), .{ .unit_debug_info_header = ui }),
                );
                if (unit.debug_info_footer_ni == .none) unit.debug_info_footer_ni = .wrap(
                    elf.addNodeAssumeCapacity(try debug_info_ni.addOnlyFooterChild(gpa, &elf.mf, .{
                        .size = comptime Dwarf.uleb128Size(@backingInt(Dwarf.AbbrevCode.null)) * 2,
                    }), .{ .unit_debug_info_footer = ui }),
                );
            },
        }
        switch (elf.shndx.debug_line) {
            .UNDEF => {},
            else => |debug_line_shndx| {
                const debug_line_ni = unit.debug_line_ni.unwrap() orelse debug_line_ni: {
                    const debug_line_ni = elf.addNodeAssumeCapacity(
                        try debug_line_shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{
                            .alignment = elf.mf.flags.block_size,
                            .enable_next_moved = true,
                        }),
                        .{ .unit_debug_line = ui },
                    );
                    unit.debug_line_ni = .wrap(debug_line_ni);
                    break :debug_line_ni debug_line_ni;
                };
                if (unit.debug_line_header_ni == .none) unit.debug_line_header_ni = .wrap(
                    elf.addNodeAssumeCapacity(try debug_line_ni.addOnlyHeaderChild(gpa, &elf.mf, .{
                        // Idle tasks are going to try to keep this up to date before we are able to
                        // write out the full header, so just reserve space for them to do so.
                        .size = elf.dwarf.unitLengthSize(),
                        .enable_next_moved = true,
                    }), .{ .unit_debug_line_header = ui }),
                );
            },
        }
        switch (elf.shndx.debug_rnglists) {
            .UNDEF => {},
            else => |debug_rnglists_shndx| {
                const debug_rnglists_ni = unit.debug_rnglists_ni.unwrap() orelse debug_rnglists_ni: {
                    const debug_rnglists_ni = elf.addNodeAssumeCapacity(
                        try debug_rnglists_shndx.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{
                            .next_moved = true,
                            .enable_next_moved = true,
                        }),
                        .{ .unit_debug_rnglists = ui },
                    );
                    unit.debug_rnglists_ni = .wrap(debug_rnglists_ni);
                    break :debug_rnglists_ni debug_rnglists_ni;
                };

                var drh_nw: MappedFile.Node.Writer = undefined;
                debug_rnglists_ni.writer(gpa, &elf.mf, &drh_nw);
                defer drh_nw.deinit();
                elf.dwarf.genDebugRnglistsHeader(unit, &drh_nw) catch |err| switch (err) {
                    else => |e| return e,
                    error.WriteFailed => return drh_nw.err.?,
                };
            },
        }
    }
    for (0..units_len) |unit_index| {
        const ui: Dwarf.Unit.Index = @fromBackingInt(@intCast(unit_index));
        const unit = ui.get(&elf.dwarf);
        if (unit.debug_info_header_ni == .none) continue;
        var dih_nw: MappedFile.Node.Writer = undefined;
        const debug_info_header_ni = unit.debug_info_header_ni.unwrap().?;
        debug_info_header_ni.writer(gpa, &elf.mf, &dih_nw);
        defer dih_nw.deinit();
        elf.resetNodeRelocs(debug_info_header_ni);
        elf.dwarf.genDebugInfoHeader(zcu, ui.mod(&elf.dwarf), unit, &dih_nw) catch |err| switch (err) {
            else => |e| return e,
            error.WriteFailed => return dih_nw.err.?,
        };
    }
    try elf.genPendingDebug(gpa);
}

fn flushFiles(elf: *Elf) Error!void {
    const gpa = elf.base.comp.gpa;
    if (elf.shndx.debug_line != .UNDEF) for (elf.dwarf.units) |*unit| {
        if (!unit.cleanDebugLineHeaderChanged()) continue;
        assert(unit.alive);
        const debug_line_header_ni = unit.debug_line_header_ni.unwrap().?;
        try debug_line_header_ni.parent(&elf.mf).unwrap().?.nextMoved(gpa, &elf.mf);
        try debug_line_header_ni.moved(gpa, &elf.mf);
        var dlh_nw: MappedFile.Node.Writer = undefined;
        debug_line_header_ni.writer(gpa, &elf.mf, &dlh_nw);
        defer dlh_nw.deinit();
        elf.resetNodeRelocs(debug_line_header_ni);
        elf.dwarf.genDebugLineHeader(unit, &dlh_nw, elf.base.comp.zcu.?) catch |err| switch (err) {
            else => |e| return e,
            error.WriteFailed => return dlh_nw.err.?,
        };
    };
}

fn prepareDynamic(elf: *Elf) Error!void {
    const comp = elf.base.comp;

    if (elf.shndx.dynamic == .UNDEF) return;

    // Static PIEs don't need a PLT, so we shouldn't emit the associated dynamic entries.
    const use_plt = !(comp.config.output_mode == .Exe and
        comp.config.link_mode == .static and
        comp.config.pie);

    const dynamic_len: u64 = elf.needed.count() + @intFromBool(elf.dynamic.soname != .empty) +
        @intFromBool(elf.dynamic.rpath != .empty) +
        @intFromBool(elf.dynamic.flags != 0) + @intFromBool(elf.dynamic.flags_1 != 0) +
        @as(usize, @intFromBool(elf.shndx.init_array != .UNDEF)) * 2 +
        @as(usize, @intFromBool(elf.shndx.fini_array != .UNDEF)) * 2 +
        @as(usize, @intFromBool(elf.shndx.preinit_array != .UNDEF)) * 2 +
        @as(usize, @intFromBool(use_plt)) * 4 +
        @as(usize, @intFromBool(elf.verneed.count() > 0)) * 2 +
        @as(usize, @intFromBool(elf.verdef.count() > 0)) * 2 +
        @intFromBool(comp.config.output_mode == .Exe) +
        @intFromBool(elf.textrel_count > 0) + 10;

    const dynamic_size = dynamic_len * 2 * elf.targetPtrSize();

    try elf.shndx.dynamic.get(elf).ni.resizeLeaf(comp.gpa, &elf.mf, dynamic_size);
    switch (elf.shdrPtr(elf.shndx.dynamic)) {
        inline else => |shdr| elf.targetStore(&shdr.size, @intCast(dynamic_size)),
    }
}

fn flushDynamic(elf: *Elf) void {
    const comp = elf.base.comp;

    if (elf.shndx.dynamic == .UNDEF) return;

    switch (elf.identClass()) {
        .NONE, _ => unreachable,
        inline else => |class| {
            const ElfN = class.ElfN();

            // Static PIEs don't need a PLT, so we shouldn't emit the associated dynamic entries.
            const use_plt = !(comp.config.output_mode == .Exe and
                comp.config.link_mode == .static and
                comp.config.pie);

            const dynamic_size = elf.targetLoad(&@field(elf.shdrPtr(elf.shndx.dynamic), @tagName(class)).size);
            const dynamic_slice = elf.shndx.dynamic.get(elf).ni.slice(&elf.mf)[0..@intCast(dynamic_size)];
            const dynamic_entries: [][2]ElfN.Addr = @ptrCast(@alignCast(dynamic_slice));

            var dynamic_index: usize = 0;

            for (
                dynamic_entries[dynamic_index..][0..elf.needed.count()],
                elf.needed.keys(),
            ) |*dynamic_entry, needed| {
                dynamic_entry.* = .{ std.elf.DT_NEEDED, @backingInt(needed) };
            }
            dynamic_index += elf.needed.count();

            if (elf.dynamic.soname != .empty) {
                dynamic_entries[dynamic_index] = .{ std.elf.DT_SONAME, @backingInt(elf.dynamic.soname) };
                dynamic_index += 1;
            }
            if (elf.dynamic.rpath != .empty) {
                dynamic_entries[dynamic_index] = .{ std.elf.DT_RUNPATH, @backingInt(elf.dynamic.rpath) };
                dynamic_index += 1;
            }
            if (elf.dynamic.flags != 0) {
                dynamic_entries[dynamic_index] = .{ std.elf.DT_FLAGS, elf.dynamic.flags };
                dynamic_index += 1;
            }
            if (elf.dynamic.flags_1 != 0) {
                dynamic_entries[dynamic_index] = .{ std.elf.DT_FLAGS_1, elf.dynamic.flags_1 };
                dynamic_index += 1;
            }
            if (comp.config.output_mode == .Exe) {
                dynamic_entries[dynamic_index] = .{ std.elf.DT_DEBUG, 0 };
                dynamic_index += 1;
            }
            if (elf.textrel_count > 0) {
                dynamic_entries[dynamic_index] = .{ std.elf.DT_TEXTREL, 0 };
                dynamic_index += 1;
            }
            if (elf.shndx.init_array != .UNDEF) {
                dynamic_entries[dynamic_index..][0..2].* = .{
                    .{ std.elf.DT_INIT_ARRAY, @intCast(elf.shndx.init_array.vaddr(elf)) },
                    .{ std.elf.DT_INIT_ARRAYSZ, @intCast(elf.shndx.init_array.size(elf)) },
                };
                dynamic_index += 2;
            }
            if (elf.shndx.fini_array != .UNDEF) {
                dynamic_entries[dynamic_index..][0..2].* = .{
                    .{ std.elf.DT_FINI_ARRAY, @intCast(elf.shndx.fini_array.vaddr(elf)) },
                    .{ std.elf.DT_FINI_ARRAYSZ, @intCast(elf.shndx.fini_array.size(elf)) },
                };
                dynamic_index += 2;
            }
            if (elf.shndx.preinit_array != .UNDEF) {
                dynamic_entries[dynamic_index..][0..2].* = .{
                    .{ std.elf.DT_PREINIT_ARRAY, @intCast(elf.shndx.preinit_array.vaddr(elf)) },
                    .{ std.elf.DT_PREINIT_ARRAYSZ, @intCast(elf.shndx.preinit_array.size(elf)) },
                };
                dynamic_index += 2;
            }
            if (use_plt) {
                // The `DT_PLTGOT` entry usually points to `.got.plt`, but on targets where that
                // section does not exist it instead points to `.plt`.
                const pltgot_shndx: Section.Index = switch (elf.targetPltInfo().got_plt != null) {
                    true => elf.shndx.got_plt,
                    false => elf.shndx.plt,
                };
                dynamic_entries[dynamic_index..][0..4].* = .{
                    .{ std.elf.DT_JMPREL, @intCast(elf.shndx.rela_plt.vaddr(elf)) },
                    .{ std.elf.DT_PLTGOT, @intCast(pltgot_shndx.vaddr(elf)) },
                    .{ std.elf.DT_PLTRELSZ, @intCast(elf.shndx.rela_plt.size(elf)) },
                    .{ std.elf.DT_PLTREL, std.elf.DT_RELA },
                };
                dynamic_index += 4;
            }
            if (elf.verneed.count() > 0) {
                dynamic_entries[dynamic_index..][0..2].* = .{
                    .{ std.elf.DT_VERNEED, @intCast(elf.shndx.gnu_version_r.vaddr(elf)) },
                    .{ std.elf.DT_VERNEEDNUM, verneed_num: {
                        const shdr = @field(elf.shdrPtr(elf.shndx.gnu_version_r), @tagName(class));
                        break :verneed_num elf.targetLoad(&shdr.info);
                    } },
                };
                dynamic_index += 2;
            }
            if (elf.verdef.count() > 0) {
                dynamic_entries[dynamic_index..][0..2].* = .{
                    .{ std.elf.DT_VERDEF, @intCast(elf.shndx.gnu_version_d.vaddr(elf)) },
                    .{ std.elf.DT_VERDEFNUM, @intCast(elf.verdef.count() + 1) },
                };
                dynamic_index += 2;
            }

            dynamic_entries[dynamic_index..][0..10].* = .{
                .{ std.elf.DT_RELA, @intCast(elf.shndx.rela_dyn.vaddr(elf)) },
                .{ std.elf.DT_RELASZ, @intCast(elf.shndx.rela_dyn.size(elf)) },
                .{ std.elf.DT_RELAENT, @sizeOf(ElfN.Rela) },
                .{ std.elf.DT_SYMTAB, @intCast(elf.shndx.dynsym.vaddr(elf)) },
                .{ std.elf.DT_SYMENT, @sizeOf(ElfN.Sym) },
                .{ std.elf.DT_STRTAB, @intCast(elf.shndx.dynstr.vaddr(elf)) },
                .{ std.elf.DT_STRSZ, @intCast(elf.shndx.dynstr.size(elf)) },
                .{ std.elf.DT_VERSYM, @intCast(elf.shndx.gnu_version.vaddr(elf)) },
                .{ std.elf.DT_HASH, @intCast(elf.shndx.hash.vaddr(elf)) },
                .{ std.elf.DT_NULL, 0 },
            };
            dynamic_index += 10;

            assert(dynamic_index == dynamic_entries.len);
            if (elf.targetEndian() != std.lang.Endian.native) for (dynamic_entries) |*dynamic_entry|
                std.mem.byteSwapAllFields(@TypeOf(dynamic_entry.*), dynamic_entry);
        },
    }
}

fn addSection(elf: *Elf, segment_ni: MappedFile.Node.Index, opts: struct {
    name: []const u8 = "",
    type: std.elf.SHT = .NULL,
    flags: std.elf.SHF = .{},
    size: std.elf.Xword = 0,
    link: std.elf.Word = 0,
    info: std.elf.Word = 0,
    addralign: Alignment = .@"1",
    entsize: std.elf.Word = 0,
    node_align: Alignment = .@"1",
    manual_size: bool = false,
}) Error!Section.Index {
    switch (opts.type) {
        .NULL => assert(opts.size == 0),
        .PROGBITS => assert(opts.size > 0),
        else => {},
    }
    if (opts.flags.ALLOC and elf.ehdrType() != .REL) {
        const phndx = elf.getNode(segment_ni).segment;
        try elf.ensureSegmentAligned(phndx, opts.addralign);
    }
    const gpa = elf.base.comp.gpa;
    try elf.nodes.ensureUnusedCapacity(gpa, 1);
    try elf.shdrs.ensureUnusedCapacity(gpa, 1);
    const want_symbol = opts.flags.ALLOC or switch (opts.type) {
        .NULL, .PROGBITS, .NOBITS, .X86_64_UNWIND => elf.ehdrType() == .REL,
        else => false,
    };

    const shstrtab_entry = try elf.string(.shstrtab, opts.name);
    const shndx: Section.Index, const new_shdr_size = shndx: switch (elf.ehdrPtr()) {
        inline else => |ehdr, class| {
            const shndx, const shnum = alloc_shndx: switch (elf.targetLoad(&ehdr.shnum)) {
                1...std.elf.SHN_LORESERVE - 2 => |shndx| {
                    const shnum = shndx + 1;
                    elf.targetStore(&ehdr.shnum, shnum);
                    break :alloc_shndx .{ shndx, shnum };
                },
                std.elf.SHN_LORESERVE - 1 => |shndx| {
                    const shnum = shndx + 1;
                    elf.targetStore(&ehdr.shnum, 0);
                    elf.targetStore(&@field(elf.shdrPtr(.UNDEF), @tagName(class)).size, shnum);
                    break :alloc_shndx .{ shndx, shnum };
                },
                std.elf.SHN_LORESERVE...std.elf.SHN_HIRESERVE => unreachable,
                0 => {
                    const shnum_ptr = &@field(elf.shdrPtr(.UNDEF), @tagName(class)).size;
                    const shndx: u32 = @intCast(elf.targetLoad(shnum_ptr));
                    const shnum = shndx + 1;
                    elf.targetStore(shnum_ptr, shnum);
                    break :alloc_shndx .{ shndx, shnum };
                },
            };
            assert(shndx < @backingInt(Section.Index.LORESERVE));
            break :shndx .{ @fromBackingInt(shndx), @as(u64, elf.targetLoad(&ehdr.shentsize)) * @as(u64, shnum) };
        },
    };
    try elf.ni.shdr.ensureMinimumSize(gpa, &elf.mf, new_shdr_size);
    const parent_ni = switch (elf.ehdrType()) {
        .REL => elf.ni.elf,
        .EXEC, .DYN => segment_ni,
    };
    assert(opts.addralign.check(opts.size));
    const ni = elf.addNodeAssumeCapacity(try parent_ni.addFloatingChild(gpa, &elf.mf, .{
        .size = opts.node_align.forward(opts.size),
        .alignment = opts.addralign.max(opts.node_align),
        .resized = opts.size > 0,
        .bubbles_moved = opts.flags.ALLOC,
    }), switch (opts.manual_size) {
        false => .{ .section = shndx },
        true => .{ .section_manual_size = shndx },
    });
    const addr = elf.computeNodeVAddr(ni);
    elf.shdrs.appendAssumeCapacity(.{
        .lsi = if (want_symbol) try elf.addLocalSymbol(.{
            .node = ni.toOptional(),
            .name = "",
            .value = addr,
            .size = 0,
            .type = .SECTION,
            .shndx = shndx,
        }) else .null,
        .ni = ni,
        .rela = switch (opts.type) {
            .REL => unreachable,
            .RELA => .{ .free_head = .none },
            else => .{ .shndx = .UNDEF },
        },
    });
    switch (elf.shdrPtr(shndx)) {
        inline else => |shdr, class| {
            shdr.* = .{
                .name = @backingInt(shstrtab_entry),
                .type = opts.type,
                .flags = .{ .shf = opts.flags },
                .addr = @intCast(addr),
                .offset = @intCast(elf.computeNodeElfOffset(ni)),
                .size = @intCast(opts.size),
                .link = opts.link,
                .info = opts.info,
                .addralign = @intCast(opts.addralign.toByteUnits()),
                .entsize = opts.entsize,
            };
            if (elf.targetEndian() != std.lang.Endian.native) std.mem.byteSwapAllFields(class.ElfN().Shdr, shdr);
        },
    }
    return shndx;
}

fn ensureUnusedRelocCapacity(elf: *Elf, node: MappedFile.Node.Index, len: usize) Error!void {
    if (len == 0) return;
    const gpa = elf.base.comp.gpa;
    try elf.symbol_relocs.ensureUnusedCapacity(gpa, len);
    try elf.node_relocs.ensureUnusedCapacity(gpa, len);
    try elf.got_relocs.ensureUnusedCapacity(gpa, len);
    const class = elf.identClass();
    switch (elf.ehdrType()) {
        .REL => {
            const shndx = elf.getNodeShndx(node);
            if (shndx.get(elf).rela.shndx == .UNDEF) {
                var bfa_buf: [32]u8 = undefined;
                var bfa: std.heap.BufferFirstAllocator = .init(&bfa_buf, gpa);
                const allocator = bfa.allocator();

                const rela_name = try std.fmt.allocPrint(allocator, ".rela{s}", .{shndx.name(elf).slice(elf)});
                defer allocator.free(rela_name);

                assert(elf.section_by_name.count() == elf.shdrs.items.len);
                try elf.section_by_name.ensureUnusedCapacity(gpa, 1);
                const rela_shndx = try elf.addSection(elf.ni.elf, .{
                    .name = rela_name,
                    .type = .RELA,
                    .link = @backingInt(Section.Index.symtab),
                    .info = shndx.toSection().?,
                    .addralign = switch (class) {
                        .NONE, _ => unreachable,
                        .@"32" => .@"4",
                        .@"64" => .@"8",
                    },
                    .entsize = switch (class) {
                        .NONE, _ => unreachable,
                        inline else => |ct_class| @sizeOf(ct_class.ElfN().Rela),
                    },
                    .node_align = elf.mf.flags.block_size,
                    .manual_size = true,
                });
                elf.section_by_name.putAssumeCapacityNoClobber(rela_shndx.name(elf), {});
                shndx.get(elf).rela.shndx = rela_shndx;
            }
            try shndx.get(elf).rela.shndx.relaEnsureAdditionalCapacity(elf, len);
        },
        .EXEC, .DYN => {
            try elf.tls_size_symbol_relocs.ensureUnusedCapacity(gpa, len);
            const new_got_entries = len * 2; // at worst, every reloc is a new TLSGD
            try elf.got.ensureUnusedCapacity(gpa, new_got_entries);
            const need_got_size = switch (class) {
                .NONE, _ => unreachable,
                inline else => |ct_class| (elf.got.count() + new_got_entries) * @sizeOf(ct_class.ElfN().Addr),
            };
            try elf.shndx.got.get(elf).ni.ensureMinimumSize(gpa, &elf.mf, need_got_size);

            if (elf.shndx.dynamic != .UNDEF) {
                try elf.shndx.rela_dyn.relaEnsureAdditionalCapacity(elf, new_got_entries);
            }
        },
    }
}
/// Although this function requires a preceding call to `ensureUnusedRelocCapacity`, it is still
/// fallible, because there are some rare cases for which we cannot reserve capacity upfront.
fn addRelocAssumeCapacity(
    elf: *Elf,
    node: MappedFile.Node.Index,
    offset: u64,
    target: Symbol.Id,
    addend: i64,
    @"type": MachineRelocType,
) (Error || error{ UnknownRelocation, NonStaticRelocation, UnimplementedRelocation })!void {
    switch (elf.ehdrType()) {
        .REL => {
            const rela_shndx = elf.getNodeShndx(node).get(elf).rela.shndx;
            const rela_index = rela_shndx.relaAddOneAssumeCapacity(elf, .{
                .type = @"type",
                // This field needs to equal the offset into the section, which is *not* necessarily
                // the same thing as our `offset`, which is the offset into `node`. We could compute
                // the section offset now, but there's no point, because `flushMovedNodeRelocs` will
                // eventually do it for us anyway, so just init to 0.
                .offset = 0,
                .raw_sym_index = @backingInt(target.index(elf)),
                .addend = addend,
            });
            const ri: SymbolReloc.Index = @fromBackingInt(@intCast(elf.symbol_relocs.items.len));
            const first_target_reloc = &target.index(elf).ptr(elf).first_target_reloc;
            const next = first_target_reloc.*;
            first_target_reloc.* = ri;
            if (next != .none) next.get(elf).prev = ri;
            elf.symbol_relocs.appendAssumeCapacity(.{
                .node = node.toOptional(),
                .offset = offset,
                .type = undefined,
                .target = target,
                .addend = addend,
                .next = next,
                .prev = .none,
                .rela_index = rela_index.toOptional(),
                .result = .ok,
            });
        },
        .DYN, .EXEC => switch (elf.ehdrMachine()) {
            .AARCH64 => switch (@"type".AARCH64) {
                .NONE => {},
                _ => return error.UnknownRelocation,
                else => return error.UnimplementedRelocation,
            },
            .LOONGARCH => rel_type: switch (@"type".LARCH) {
                .NONE => {},
                _ => return error.UnknownRelocation,

                .COPY,
                .JUMP_SLOT,
                .RELATIVE,
                .IRELATIVE,
                => return error.NonStaticRelocation,

                else => return error.UnimplementedRelocation,

                // These relocations signal that certain relaxations are legal, but this linker does
                // not yet implement relaxation, so these are ignored.
                .RELAX, .TLS_LE_ADD_R => {},

                // Relaxable versions of other relocations. Since we don't yet implement relaxation,
                // just use the handling for the non-relaxable versions.
                .TLS_LE_LO12_R => continue :rel_type .TLS_LE_LO12,
                .TLS_LE_HI20_R => continue :rel_type .TLS_LE_HI20,

                // zig fmt: off
                .@"32"        => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32",        .cast = .unsigned, .shift = .@"0" })),
                .@"64"        => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"64",        .cast = .unsigned, .shift = .@"0" })),
                .@"32_PCREL"  => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel, .{ .dest = .@"32",        .cast = .signed,   .shift = .@"0" })),
                .@"64_PCREL"  => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel, .{ .dest = .@"64",        .cast = .signed,   .shift = .@"0" })),
                .ABS_LO12     => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[21:10]", .cast = .trunc,    .shift = .@"0" })),
                .ABS_HI20     => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[24:5]",  .cast = .trunc,    .shift = .@"12" })),
                .ABS64_LO20   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[24:5]",  .cast = .trunc,    .shift = .@"32" })),
                .ABS64_HI12   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[21:10]", .cast = .unsigned, .shift = .@"52" })),
                .PCALA_LO12   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[21:10]", .cast = .trunc,    .shift = .@"0" })),
                .PCALA_HI20   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .special(.larch_pcala_hi20)),
                .PCALA64_LO20 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .special(.larch_pcala64_lo20)),
                .PCALA64_HI12 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .special(.larch_pcala64_hi12)),

                .B16    => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.pltrel, .{ .dest = .@"32[25:10]", .cast = .signed, .shift = .@"2_exact" })),
                .B21    => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .special(.larch_b21)),
                .B26    => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .special(.larch_b26)),
                .CALL36 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .special(.larch_call36)),

                .TLS_LE_LO12   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.tpoff, .{ .dest = .@"32[21:10]", .cast = .trunc,    .shift = .@"0" })),
                .TLS_LE_HI20   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.tpoff, .{ .dest = .@"32[24:5]",  .cast = .trunc,    .shift = .@"12" })),
                .TLS_LE64_LO20 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.tpoff, .{ .dest = .@"32[24:5]",  .cast = .trunc,    .shift = .@"32" })),
                .TLS_LE64_HI12 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.tpoff, .{ .dest = .@"32[21:10]", .cast = .unsigned, .shift = .@"52" })),

                .GOT_PC_LO12   => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.abs, .{ .dest = .@"32[21:10]", .cast = .trunc, .shift = .@"0" })),
                .GOT_PC_HI20   => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .special(.larch_pcala_hi20)),
                .GOT64_PC_LO20 => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .special(.larch_pcala64_lo20)),
                .GOT64_PC_HI12 => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .special(.larch_pcala64_hi12)),
                .GOT_LO12      => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.abs, .{ .dest = .@"32[21:10]", .cast = .trunc,    .shift = .@"0" })),
                .GOT_HI20      => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.abs, .{ .dest = .@"32[24:5]",  .cast = .trunc,    .shift = .@"12" })),
                .GOT64_LO20    => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.abs, .{ .dest = .@"32[24:5]",  .cast = .trunc,    .shift = .@"32" })),
                .GOT64_HI12    => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.abs, .{ .dest = .@"32[21:10]", .cast = .unsigned, .shift = .@"52" })),
                // zig fmt: on
            },
            .PPC64 => switch (@"type".PPC64) {
                .NONE => {},
                _ => return error.UnknownRelocation,
                else => return error.UnimplementedRelocation,
            },
            .RISCV => switch (@"type".RISCV) {
                .NONE => {},
                _ => return error.UnknownRelocation,
                else => return error.UnimplementedRelocation,
            },
            .SPARCV9 => switch (@"type".SPARC) {
                .NONE => {},
                _ => return error.UnknownRelocation,

                .COPY,
                .GLOB_DAT,
                .JMP_SLOT,
                .RELATIVE,
                .IRELATIVE,
                => return error.NonStaticRelocation,

                .WDISP22,
                .HI22,
                .LO10,
                .HIPLT22,
                .LOPLT10,
                .PCPLT22,
                .PCPLT10,
                .OLO10,
                .HH22,
                .HM10,
                .LM22,
                .PC_HH22,
                .PC_HM10,
                .PC_LM22,
                .WDISP16,
                .WDISP19,
                .HIX22,
                .LOX10,
                .REGISTER,
                .TLS_IE_HI22,
                .TLS_IE_LO10,
                .TLS_DTPMOD32,
                .TLS_DTPMOD64,
                .H34,
                .WDISP10,
                => return error.UnimplementedRelocation,

                // These need similar handling to `R_X86_64_GOTOFF64`. No compiler seems to emit them though.
                .GOTDATA_HIX22 => return error.UnimplementedRelocation,
                .GOTDATA_LOX10 => return error.UnimplementedRelocation,

                // These relocations signal that certain relaxations are legal, but this linker does
                // not yet implement relaxation, so these are ignored.
                .GOTDATA_OP,
                .TLS_GD_ADD,
                .TLS_LDM_ADD,
                .TLS_LDO_ADD,
                .TLS_IE_LD,
                .TLS_IE_LDX,
                .TLS_IE_ADD,
                => {},

                // zig fmt: off
                .@"8"         => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"8",  .cast = .unsigned, .shift = .@"0" })),
                .@"16", .UA16 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"16", .cast = .unsigned, .shift = .@"0" })),
                .@"32", .UA32 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32", .cast = .unsigned, .shift = .@"0" })),
                .@"64", .UA64 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"64", .cast = .unsigned, .shift = .@"0" })),

                .@"5"         => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[4:0]", .cast = .unsigned, .shift = .@"0" })),
                .@"6"         => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[5:0]", .cast = .unsigned, .shift = .@"0" })),
                .@"7"         => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[6:0]", .cast = .unsigned, .shift = .@"0" })),
                .@"10"        => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[9:0]", .cast = .unsigned, .shift = .@"0" })),
                .@"11"        => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[10:0]", .cast = .unsigned, .shift = .@"0" })),
                .@"13"        => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[12:0]", .cast = .unsigned, .shift = .@"0" })),
                .@"22"        => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[21:0]", .cast = .unsigned, .shift = .@"0" })),

                .DISP8  => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel, .{ .dest = .@"8",  .cast = .signed, .shift = .@"0" })),
                .DISP16 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel, .{ .dest = .@"16", .cast = .signed, .shift = .@"0" })),
                .DISP32 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel, .{ .dest = .@"32", .cast = .signed, .shift = .@"0" })),
                .DISP64 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel, .{ .dest = .@"64", .cast = .signed, .shift = .@"0" })),

                .SIZE32   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.size, .{ .dest = .@"32", .cast = .unsigned, .shift = .@"0" })),
                .SIZE64   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.size, .{ .dest = .@"64", .cast = .unsigned, .shift = .@"0" })),

                .PCPLT32 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.pltrel, .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                .PLT32   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.pltabs, .{ .dest = .@"32", .cast = .unsigned, .shift = .@"0" })),
                .PLT64   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.pltabs, .{ .dest = .@"64", .cast = .unsigned, .shift = .@"0" })),

                .WDISP30 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel,    .{ .dest = .@"32[29:0]", .cast = .signed,   .shift = .@"2_exact" })),
                .WPLT30  => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.pltrel, .{ .dest = .@"32[29:0]", .cast = .signed,   .shift = .@"2_exact" })),
                .PC22    => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel,    .{ .dest = .@"32[21:0]", .cast = .unsigned, .shift = .@"10" })),
                .H44     => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs,    .{ .dest = .@"32[21:0]", .cast = .unsigned, .shift = .@"22" })),
                .M44     => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs,    .{ .dest = .@"32[9:0]",  .cast = .trunc,    .shift = .@"12" })),

                .TLS_LDO_HIX22 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.dtpoff, .{ .dest = .@"32[21:0]", .cast = .trunc, .shift = .@"10" })),
                .TLS_LE_HIX22  => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .special(.sparc_le_hix22)),
                .TLS_DTPOFF32  => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.dtpoff, .{ .dest = .@"32", .cast = .unsigned, .shift = .@"0" })),
                .TLS_DTPOFF64  => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.dtpoff, .{ .dest = .@"64", .cast = .unsigned, .shift = .@"0" })),
                .TLS_TPOFF32   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.tpoff,  .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                .TLS_TPOFF64   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.tpoff,  .{ .dest = .@"64", .cast = .signed,   .shift = .@"0" })),

                .GOT13            => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.offset, .{ .dest = .@"32[12:0]", .cast = .unsigned, .shift = .@"0" })),
                .GOT22            => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.offset, .{ .dest = .@"32[21:0]", .cast = .trunc,    .shift = .@"10" })),
                .GOTDATA_OP_LOX10 => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .special(.sparc_op_lox10)),
                .GOTDATA_OP_HIX22 => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .special(.sparc_op_hix22)),
                .TLS_GD_HI22      => elf.addGotRelocAssumeCapacity(node, offset, .{ .tlsgd0 = target }, addend, .simple(.offset, .{ .dest = .@"32[21:0]", .cast = .trunc, .shift = .@"10" })),
                .TLS_LDM_HI22     => elf.addGotRelocAssumeCapacity(node, offset, .tlsld0,               addend, .simple(.offset, .{ .dest = .@"32[21:0]", .cast = .trunc, .shift = .@"10" })),
                // zig fmt: on

                .TLS_GD_CALL, .TLS_LDM_CALL => {
                    const callee_sym = try elf.externSymbolInner(.{
                        .lib_name = null,
                        .name = "__tls_get_addr",
                        .type = .FUNC,
                    });
                    try elf.addSymbolRelocAssumeCapacity(node, offset, callee_sym, addend, .simple(.pltrel, .{ .dest = .@"32[29:0]", .cast = .signed, .shift = .@"2_exact" }));
                },

                // The following relocations are all represented by the ABI as writing to a 13 bit
                // field (32[12:0]), but masking out some bits of the value. To simplify our logic
                // for applying relocations, we split this action up: we create a relocation writing
                // to the 10--12 bit long field which is actually variable, and queue a one-shot
                // task to set the constant bits. We can't just write the bits now unfortunately
                // because they may be in an input section which has not yet been loaded.
                .PC10 => {
                    try elf.one_shot_fixups.append(elf.base.comp.gpa, .{ .node = node, .offset = offset, .action = .@"32[12:10] = 0b000" });
                    try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel, .{ .dest = .@"32[9:0]", .cast = .trunc, .shift = .@"0" }));
                },
                .L44 => {
                    try elf.one_shot_fixups.append(elf.base.comp.gpa, .{ .node = node, .offset = offset, .action = .@"32[12:12] = 0b0" });
                    try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs, .{ .dest = .@"32[11:0]", .cast = .trunc, .shift = .@"0" }));
                },
                .TLS_LDO_LOX10 => {
                    try elf.one_shot_fixups.append(elf.base.comp.gpa, .{ .node = node, .offset = offset, .action = .@"32[12:10] = 0b000" });
                    try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.dtpoff, .{ .dest = .@"32[9:0]", .cast = .trunc, .shift = .@"0" }));
                },
                .TLS_LE_LOX10 => {
                    try elf.one_shot_fixups.append(elf.base.comp.gpa, .{ .node = node, .offset = offset, .action = .@"32[12:10] = 0b111" });
                    try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.tpoff, .{ .dest = .@"32[9:0]", .cast = .trunc, .shift = .@"0" }));
                },
                .GOT10 => {
                    try elf.one_shot_fixups.append(elf.base.comp.gpa, .{ .node = node, .offset = offset, .action = .@"32[12:10] = 0b000" });
                    elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.offset, .{ .dest = .@"32[9:0]", .cast = .trunc, .shift = .@"0" }));
                },
                .TLS_GD_LO10 => {
                    try elf.one_shot_fixups.append(elf.base.comp.gpa, .{ .node = node, .offset = offset, .action = .@"32[12:10] = 0b000" });
                    elf.addGotRelocAssumeCapacity(node, offset, .{ .tlsgd0 = target }, addend, .simple(.offset, .{ .dest = .@"32[9:0]", .cast = .trunc, .shift = .@"0" }));
                },
                .TLS_LDM_LO10 => {
                    try elf.one_shot_fixups.append(elf.base.comp.gpa, .{ .node = node, .offset = offset, .action = .@"32[12:10] = 0b000" });
                    elf.addGotRelocAssumeCapacity(node, offset, .tlsld0, addend, .simple(.offset, .{ .dest = .@"32[9:0]", .cast = .trunc, .shift = .@"0" }));
                },
            },
            .X86_64 => rel_type: switch (@"type".X86_64) {
                .NONE => {},
                _ => return error.UnknownRelocation,

                .COPY,
                .GLOB_DAT,
                .JUMP_SLOT,
                .RELATIVE64,
                .RELATIVE,
                .IRELATIVE,
                .DTPMOD64,
                => return error.NonStaticRelocation,

                // TODO: the psABI links to https://www.fsfla.org/~lxoliva/writeups/TLS/RFC-TLSDESC-x86.txt
                .GOTPC32_TLSDESC => return error.UnimplementedRelocation,
                .TLSDESC_CALL => return error.UnimplementedRelocation,
                .TLSDESC => return error.UnimplementedRelocation,

                // TODO: these are the address of an arbitrary symbol (or PLT entry) relative to the
                // base of the GOT, which is quite annoying. Luckily, they seem to be rare, so I'm
                // probably just going to introduce a set (ArrayHashMap) of SymbolReloc.Index which
                // need to be re-applied whenever the GOT moves.
                .GOTOFF64 => return error.UnimplementedRelocation, // offset of symbol from GOT base
                .PLTOFF64 => return error.UnimplementedRelocation, // offset of PLT entry from GOT base (yes, I know, the name is stupid)

                // TODO: figure out how to do relaxations. Perhaps we want to remove a `GotReloc`
                // and replace it with a `SymbolReloc` when a relaxation becomes possible, but we'd
                // need to bear in mind whether incremental updates might make a relaxation
                // impossible again or something like that. Relaxations seem kind of hostile to
                // incremental compilation, so perhaps we just only support them in non-incremental
                // compilations and just apply them in flush or something.

                // Relaxable versions of other relocations. Since we don't yet implement relaxation,
                // just use the handling for the non-relaxable versions.
                .GOTPCRELX, .REX_GOTPCRELX => continue :rel_type .GOTPCREL,

                // This relocation was a historical attempt to help linkers optimize uses of symbols
                // which have both GOT entries and PLT entries, by encouraging the linker to create
                // a `.got.plt` entry instead of a `.got` entry. This makes no sense, because the
                // linker already has sufficient knowledge to do that optimization, while compilers
                // actually do *not* have sufficient knowledge (since the PLT and GOT relocations
                // may not be in the same compilation unit). This relocation has since been removed
                // from the psABI, but just in case it appears, we can easily support it by just
                // disregarding the PLT stuff and lowering to a normal GOT entry.
                //
                // More details: https://sourceware.org/pipermail/binutils/2014-November/086548.html
                .GOTPLT64 => continue :rel_type .GOT64,

                // zig fmt: off
                .@"8"     => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs,    .{ .dest = .@"8",  .cast = .unsigned, .shift = .@"0" })),
                .@"16"    => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs,    .{ .dest = .@"16", .cast = .unsigned, .shift = .@"0" })),
                .@"32"    => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs,    .{ .dest = .@"32", .cast = .unsigned, .shift = .@"0" })),
                .@"32S"   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs,    .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                .@"64"    => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.abs,    .{ .dest = .@"64", .cast = .unsigned, .shift = .@"0" })),
                .PC8      => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel,    .{ .dest = .@"8",  .cast = .signed,   .shift = .@"0" })),
                .PC16     => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel,    .{ .dest = .@"16", .cast = .signed,   .shift = .@"0" })),
                .PC32     => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel,    .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                .PC64     => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.rel,    .{ .dest = .@"64", .cast = .signed,   .shift = .@"0" })),
                .PLT32    => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.pltrel, .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                .SIZE32   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.size,   .{ .dest = .@"32", .cast = .unsigned, .shift = .@"0" })),
                .SIZE64   => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.size,   .{ .dest = .@"64", .cast = .unsigned, .shift = .@"0" })),
                .DTPOFF32 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.dtpoff, .{ .dest = .@"32", .cast = .unsigned, .shift = .@"0" })),
                .DTPOFF64 => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.dtpoff, .{ .dest = .@"64", .cast = .unsigned, .shift = .@"0" })),
                .TPOFF32  => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.tpoff,  .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                .TPOFF64  => try elf.addSymbolRelocAssumeCapacity(node, offset, target, addend, .simple(.tpoff,  .{ .dest = .@"64", .cast = .signed,   .shift = .@"0" })),

                .GOT32      => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.offset, .{ .dest = .@"32", .cast = .unsigned, .shift = .@"0" })),
                .GOT64      => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.offset, .{ .dest = .@"64", .cast = .unsigned, .shift = .@"0" })),
                .GOTPCREL   => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.rel,    .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                .GOTPCREL64 => elf.addGotRelocAssumeCapacity(node, offset, .{ .symbol = target }, addend, .simple(.rel,    .{ .dest = .@"64", .cast = .signed,   .shift = .@"0" })),
                .TLSGD      => elf.addGotRelocAssumeCapacity(node, offset, .{ .tlsgd0 = target }, addend, .simple(.rel,    .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                .TLSLD      => elf.addGotRelocAssumeCapacity(node, offset, .tlsld0,               addend, .simple(.rel,    .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                .GOTTPOFF   => elf.addGotRelocAssumeCapacity(node, offset, .{ .tpoff = target },  addend, .simple(.rel,    .{ .dest = .@"32", .cast = .signed,   .shift = .@"0" })),
                // zig fmt: on

                .GOTPC64 => {
                    const got_sym: Symbol.Id = .local(elf.shndx.got.get(elf).lsi);
                    try elf.addSymbolRelocAssumeCapacity(node, offset, got_sym, addend, .simple(.rel, .{ .dest = .@"64", .cast = .signed, .shift = .@"0" }));
                },
                .GOTPC32 => {
                    const got_sym: Symbol.Id = .local(elf.shndx.got.get(elf).lsi);
                    try elf.addSymbolRelocAssumeCapacity(node, offset, got_sym, addend, .simple(.rel, .{ .dest = .@"32", .cast = .signed, .shift = .@"0" }));
                },
            },
        },
    }
}
fn addSymbolRelocAssumeCapacity(
    elf: *Elf,
    node: MappedFile.Node.Index,
    offset: u64,
    target: Symbol.Id,
    addend: i64,
    @"type": SymbolReloc.Type,
) Error!void {
    assert(elf.ehdrType() != .REL);

    const rela_index: Section.RelaIndex.Optional = r: {
        if (elf.shndx.dynamic == .UNDEF) break :r .none;

        // If we emit a runtime relocation entry, its `offset` is a virtual address, so we need to
        // determine the vaddr of `node`.
        const node_vaddr = elf.getNodeVAddr(node);

        // If this is `true`, we will try to create a copy relocation for the target symbol if it is
        // not locally defined. If the relocation value is always computed from the target symbol's
        // value (even for an external target symbol), and if the target symbol might be of type
        // STT_OBJECT, this should probably be `true`.
        const try_copy_reloc: bool = switch (@"type".target) {
            .rel, .abs => true,

            .pltrel,
            .pltabs,
            .dtpoff,
            .tpoff,
            .size,
            => false,

            .special => switch (@"type".action.special) {
                .larch_pcala_hi20,
                .larch_pcala64_lo20,
                .larch_pcala64_hi12,
                => true,

                .larch_b21,
                .larch_b26,
                .larch_call36,
                .sparc_le_hix22,
                => false,
            },
        };
        if (try_copy_reloc) switch (target.unwrap()) {
            .local => {},
            .global => |gsi| if (!gsi.ptr(elf).status.want_static_value) {
                gsi.ptr(elf).status.want_static_value = true;
                try elf.updateGlobalDynamic(gsi, false);
            },
        };

        classify: switch (elf.classifySymbolValue(target)) {
            .static => {
                switch (target.unwrap()) {
                    .local => {},
                    .global => |gsi| gsi.ptr(elf).status.any_static_target_relocs = true,
                }
                break :r .none;
            },
            .static_relative => {
                switch (target.unwrap()) {
                    .local => {},
                    .global => |gsi| gsi.ptr(elf).status.any_static_relative_target_relocs = true,
                }
                switch (@"type".target) {
                    // Only relocations which resolve to absolute addresses require runtime
                    // `R_*_RELATIVE` relocations.
                    .special,
                    .pltrel,
                    .rel,
                    .dtpoff,
                    .tpoff,
                    .size,
                    => break :r .none,

                    .abs, .pltabs => {},
                }
                if (!@"type".action.simple.dest.isAddr(elf)) break :r .none;
                switch (elf.nodeWantsDsoRelocation(node)) {
                    .no => break :r .none,
                    .yes => {},
                    .yes_textrel => elf.textrel_count += 1,
                }
                break :r elf.shndx.rela_dyn.relaAddOneAssumeCapacity(elf, .{
                    .type = .relative(elf),
                    .offset = node_vaddr + offset,
                    .raw_sym_index = 0,
                    .addend = 0,
                }).toOptional();
            },
            .dynamic => {
                target.unwrap().global.ptr(elf).status.any_dynamic_target_relocs = true;
                const dynamic_reloc_type: MachineRelocType = switch (@"type".target) {
                    // PLT relocations targeting dynamic symbols actually target that symbol's PLT
                    // entry, so we should emit an `R_*_RELATIVE` relocation instead.
                    .pltabs => continue :classify .static_relative,
                    // ...although PC-relative PLT relocations don't even need that!
                    .pltrel => break :r .none,
                    // Weird sizes or computations are not supported as runtime relocations.
                    .special => break :r .none,
                    // Relative addresses are not supported as runtime relocations.
                    .rel => break :r .none,

                    // On the few targets supporting size relocations, they are valid at runtime.
                    .size => switch (@"type".action.simple.dest) {
                        .@"32" => MachineRelocType.size32(elf) orelse break :r .none,
                        .@"64" => MachineRelocType.size64(elf) orelse break :r .none,
                        else => break :r .none,
                    },
                    // Absolute addresses and TLS offsets can be lowered at runtime provided they
                    // are address-sized.
                    .dtpoff => if (@"type".action.simple.dest.isAddr(elf)) .dtpOff(elf) else break :r .none,
                    .tpoff => if (@"type".action.simple.dest.isAddr(elf)) .tpOff(elf) else break :r .none,
                    .abs => if (@"type".action.simple.dest.isAddr(elf)) .absAddr(elf) else break :r .none,
                };
                switch (elf.nodeWantsDsoRelocation(node)) {
                    .no => break :r .none,
                    .yes => {},
                    .yes_textrel => elf.textrel_count += 1,
                }
                break :r elf.shndx.rela_dyn.relaAddOneAssumeCapacity(elf, .{
                    .type = dynamic_reloc_type,
                    .offset = node_vaddr + offset,
                    .raw_sym_index = target.unwrap().global.dynsymIndex(elf).?,
                    .addend = addend,
                }).toOptional();
            },
        }
    };

    const ri: SymbolReloc.Index = @fromBackingInt(@intCast(elf.symbol_relocs.items.len));
    const first_target_reloc = &target.index(elf).ptr(elf).first_target_reloc;
    const next = first_target_reloc.*;
    first_target_reloc.* = ri;
    if (next != .none) next.get(elf).prev = ri;
    elf.symbol_relocs.appendAssumeCapacity(.{
        .node = node.toOptional(),
        .offset = offset,
        .target = target,
        .addend = addend,
        .type = @"type",
        .next = next,
        .prev = .none,
        .rela_index = rela_index,
        .result = .ok,
    });
    if (@"type".dependsOnTlsSize(elf)) {
        elf.tls_size_symbol_relocs.putAssumeCapacityNoClobber(ri, {});
    }

    // Actually apply the new relocation!
    ri.get(elf).apply(elf);
}
fn addNodeRelocAssumeCapacity(
    elf: *Elf,
    node: MappedFile.Node.Index,
    offset: u64,
    target: MappedFile.Node.Index,
    addend: i64,
    @"type": NodeReloc.Type,
) Error!void {
    const shndx = elf.getNodeShndx(target);
    assert(!shndx.flags(elf).ALLOC); // not yet needed so not implemented
    const first_target_reloc = switch (elf.getNode(target)) {
        else => unreachable,
        .debug_shared => |ss| &elf.dwarf_shared.getPtr(ss).first_target_reloc,
        .debug_addr => &elf.dwarf_addr.first_target_reloc,
        .debug_str_offsets => &elf.dwarf_str_offsets.first_target_reloc,
        .unit_frame_cie => |ui| &elf.dwarf_units[@backingInt(ui)].frame_cie_first_target_reloc,
        .unit_debug_info_header => |ui| &elf.dwarf_units[@backingInt(ui)].debug_info_header_first_target_reloc,
        .unit_debug_line_header => |ui| &elf.dwarf_units[@backingInt(ui)].debug_line_header_first_target_reloc,
        .unit_debug_rnglists => |ui| &elf.dwarf_units[@backingInt(ui)].debug_rnglists_first_target_reloc,
        .const_debug_info => |cpi| &elf.dwarf_consts.getPtr(cpi).?.debug_info_first_target_reloc,
        .global_debug_info => |gi| &elf.dwarf_globals.items[@backingInt(gi)].debug_info_first_target_reloc,
        .func_debug_info => |fi| &elf.dwarf_funcs.items[@backingInt(fi)].debug_info_first_target_reloc,
        .decl_debug_info => |di| &elf.dwarf_decls.getPtr(di).?.debug_info_first_target_reloc,
    };
    const next = first_target_reloc.*;
    const ri: NodeReloc.Index = @fromBackingInt(@intCast(elf.node_relocs.items.len));
    first_target_reloc.* = ri;
    if (next != .none) next.get(elf).prev = ri;
    switch (elf.ehdrType()) {
        .REL => {
            const rela_shndx = elf.getNodeShndx(node).get(elf).rela.shndx;
            const rela_index = rela_shndx.relaAddOneAssumeCapacity(elf, .{
                .type = switch (elf.ehdrMachine()) {
                    .AARCH64 => .{ .AARCH64 = switch (@"type") {
                        .abs32 => .ABS32,
                        .abs64 => .ABS64,
                    } },
                    .LOONGARCH => .{ .LARCH = switch (@"type") {
                        .abs32 => .@"32",
                        .abs64 => .@"64",
                    } },
                    .PPC64 => .{ .PPC64 = switch (@"type") {
                        .abs32 => .ADDR32,
                        .abs64 => .ADDR64,
                    } },
                    .RISCV => .{ .RISCV = switch (@"type") {
                        .abs32 => .@"32",
                        .abs64 => .@"64",
                    } },
                    .SPARCV9 => .{ .SPARC = switch (@"type") {
                        .abs32 => .UA32,
                        .abs64 => .UA64,
                    } },
                    .X86_64 => .{ .X86_64 = switch (@"type") {
                        .abs32 => .@"32",
                        .abs64 => .@"64",
                    } },
                },
                // This field needs to equal the offset into the section, which is *not* necessarily
                // the same thing as our `offset`, which is the offset into `node`. We could compute
                // the section offset now, but there's no point, because `flushMovedNodeRelocs` will
                // eventually do it for us anyway, so just init to 0.
                .offset = 0,
                .raw_sym_index = @backingInt(switch (shndx.get(elf).lsi) {
                    .null => unreachable,
                    else => |lsi| lsi.index(),
                }),
                .addend = 0,
            });
            elf.node_relocs.appendAssumeCapacity(.{
                .node = node.toOptional(),
                .offset = offset,
                .type = undefined,
                .target = target,
                .addend = addend,
                .next = next,
                .prev = .none,
                .rela_index = rela_index.toOptional(),
                .result = .ok,
            });
        },
        .DYN, .EXEC => {
            elf.node_relocs.appendAssumeCapacity(.{
                .node = node.toOptional(),
                .offset = offset,
                .target = target,
                .addend = addend,
                .type = @"type",
                .next = next,
                .prev = .none,
                .rela_index = .none,
                .result = .ok,
            });

            // Actually apply the new relocation!
            ri.get(elf).apply(elf);
        },
    }
}
fn addGotRelocAssumeCapacity(
    elf: *Elf,
    node: MappedFile.Node.Index,
    offset: u64,
    target: GotKey,
    addend: i64,
    @"type": GotReloc.Type,
) void {
    assert(elf.ehdrType() != .REL);
    switch (elf.getNode(node)) {
        .deleted,
        .archive,
        .archive_header,
        .archive_input_member,
        .archive_elf_member_header,
        .elf,
        .ehdr,
        .shdr,
        .segment,
        .copied_global,
        .debug_shared,
        .debug_addr,
        .eh_frame_footer,
        .debug_str_offsets,
        .unit_padding,
        .unit_frame,
        .unit_frame_cie,
        .unit_debug_info,
        .unit_debug_info_header,
        .unit_debug_info_footer,
        .unit_debug_line,
        .unit_debug_line_header,
        .unit_debug_rnglists,
        .const_debug_info,
        .global_debug_info,
        .func_frame_fde,
        .func_debug_info,
        .func_debug_line,
        .decl_debug_info,
        => unreachable, // cannot contain relocs,
        .section,
        .section_manual_size,
        .uav,
        => unreachable, // cannot contain GOT relocs
        .input_section,
        .nav,
        .lazy_code,
        .lazy_const_data,
        => {},
    }

    const gop = elf.got.getOrPutAssumeCapacity(target);
    if (!gop.found_existing) {
        gop.value_ptr.* = .none;
        const maybe_next_key: ?GotKey = switch (target) {
            .reserved => null,
            .tpoff => null,
            .symbol => null,
            .tlsld0 => .tlsld1,
            .tlsgd0 => |sym| .{ .tlsgd1 = sym },
            .tlsld1 => unreachable,
            .tlsgd1 => unreachable,
        };
        switch (elf.shdrPtr(elf.shndx.got)) {
            inline else => |got_shdr, class| {
                const Addr = class.ElfN().Addr;
                const old_size = elf.targetLoad(&got_shdr.size);
                const new_entry_count = @as(u32, 1) + @intFromBool(maybe_next_key != null);
                elf.targetStore(&got_shdr.size, @intCast(old_size + @sizeOf(Addr) * new_entry_count));
            },
        }
        if (maybe_next_key) |next_key| {
            elf.got.putAssumeCapacityNoClobber(next_key, .none);
            elf.updateGotEntry(gop.index);
            elf.updateGotEntry(gop.index + 1);
        } else {
            elf.updateGotEntry(gop.index);
        }
    }

    elf.got_relocs.appendAssumeCapacity(.{
        .node = .wrap(node),
        .offset = offset,
        .target = target,
        .addend = addend,
        .type = @"type",
        .result = .ok,
    });
}
fn updateGotEntry(elf: *Elf, got_index: usize) void {
    assert(elf.ehdrType() != .REL);
    const entry_value: union(enum) {
        unsigned: u64,
        signed: i64,
        reloc: struct {
            type: MachineRelocType,
            dynsym_index: u32,
            addend: i64,
        },
    } = switch (elf.got.keys()[got_index]) {
        .reserved => .{ .unsigned = 0 },
        .tpoff => |sym_id| val: {
            // Only the executable's per-module TLS block is at a known offset from the TLS pointer.
            if (elf.base.comp.config.output_mode == .Exe and elf.classifySymbolValue(sym_id) != .dynamic) {
                const tls_phndx = elf.getNode(elf.ni.tls.unwrap().?).segment;
                const tls_size: u64 = switch (elf.phdrSlice()) {
                    inline else => |phdr| tls_size: {
                        assert(elf.targetLoad(&phdr[tls_phndx].type) == .TLS);
                        break :tls_size elf.targetLoad(&phdr[tls_phndx].memsz);
                    },
                };
                const sym_value = sym_id.value(elf);
                break :val .{ .signed = @bitCast(sym_value -% tls_size) };
            }
            switch (sym_id.unwrap()) {
                .global => |gsi| if (gsi.dynsymIndex(elf)) |dynsym_index| {
                    // When there is a dynsym entry, just target that with no addend.
                    break :val .{ .reloc = .{
                        .type = .tpOff(elf),
                        .dynsym_index = dynsym_index,
                        .addend = 0,
                    } };
                },
                .local => {},
            }
            // Otherwise, we know the offset of the symbol into our own TLS block, so target the
            // null symbol (index 0) so we get the offset to the base of that block, and use the
            // addend to offset to the correct symbol.
            break :val .{ .reloc = .{
                .type = .tpOff(elf),
                .dynsym_index = 0,
                .addend = @intCast(sym_id.value(elf)),
            } };
        },
        .symbol => |sym| switch (elf.classifySymbolValue(sym)) {
            .static => .{ .unsigned = sym.value(elf) },
            .static_relative => .{ .reloc = .{
                .type = .relative(elf),
                .dynsym_index = 0,
                .addend = @bitCast(sym.value(elf)),
            } },
            .dynamic => .{ .reloc = .{
                .type = .globDat(elf),
                .dynsym_index = sym.unwrap().global.dynsymIndex(elf).?,
                .addend = 0,
            } },
        },
        .tlsgd1 => |sym| switch (elf.classifySymbolValue(sym)) {
            .static => .{ .unsigned = sym.value(elf) },
            .static_relative => unreachable, // TLS variables should be in TLS sections, which do not return `.static_relative`
            .dynamic => .{ .reloc = .{
                .type = .dtpOff(elf),
                .dynsym_index = sym.unwrap().global.dynsymIndex(elf).?,
                .addend = 0,
            } },
        },
        .tlsgd0 => |sym| switch (elf.base.comp.config.link_mode) {
            .static => val: {
                assert(elf.base.comp.config.output_mode == .Exe); // static libraries don't have GOTs
                break :val .{ .unsigned = 1 }; // TLS module ID for executable
            },
            .dynamic => .{ .reloc = .{
                .type = .dtpMod(elf),
                .dynsym_index = switch (elf.classifySymbolValue(sym)) {
                    .static, .static_relative => 0,
                    .dynamic => sym.unwrap().global.dynsymIndex(elf).?,
                },
                .addend = 0,
            } },
        },
        .tlsld0 => switch (elf.base.comp.config.link_mode) {
            .static => val: {
                assert(elf.base.comp.config.output_mode == .Exe); // static libraries don't have GOTs
                break :val .{ .unsigned = 1 }; // TLS module ID for executable
            },
            .dynamic => .{ .reloc = .{
                .type = .dtpMod(elf),
                .dynsym_index = 0,
                .addend = 0,
            } },
        },
        .tlsld1 => .{ .unsigned = 0 },
    };

    // First, write to the GOT itself. If we're planning to use a relocation, we'll just write zeroes.
    const got_entry_addr: u64 = switch (elf.shdrPtr(elf.shndx.got)) {
        inline else => |got_shdr, class| got_entry_addr: {
            const addr_size = @sizeOf(class.ElfN().Addr);
            const offset = got_index * addr_size;
            const entry_ptr: *class.ElfN().Addr = @ptrCast(@alignCast(
                elf.shndx.got.get(elf).ni.slice(&elf.mf)[offset..][0..addr_size],
            ));
            elf.targetStore(entry_ptr, switch (entry_value) {
                .unsigned => |x| @intCast(x),
                .signed => |x| switch (class) {
                    .NONE, _ => comptime unreachable,
                    .@"32" => @bitCast(@as(i32, @intCast(x))),
                    .@"64" => @bitCast(x),
                },
                .reloc => 0,
            });
            break :got_entry_addr elf.targetLoad(&got_shdr.addr) + offset;
        },
    };

    // Then, add or remove the relocation entry if needed.
    if (elf.shndx.dynamic == .UNDEF) {
        // There are no relocations in the output file, so there's no reloc to delete and we can't
        // add a reloc in any case. (If we *are* requesting a reloc, it'll be because the value of
        // this GOT entry is not yet known, e.g. because a symbol is currently undefined.)
        return;
    }
    if (elf.got.values()[got_index].unwrap()) |rela_index| {
        // Clear the old relocation entry (although we might immediately re-use it below).
        elf.shndx.rela_dyn.relaDeleteOne(elf, rela_index);
    }
    elf.got.values()[got_index] = switch (entry_value) {
        .unsigned, .signed => .none, // no relocation needed
        .reloc => |reloc| elf.shndx.rela_dyn.relaAddOneAssumeCapacity(elf, .{
            .type = reloc.type,
            .offset = got_entry_addr,
            .raw_sym_index = reloc.dynsym_index,
            .addend = reloc.addend,
        }).toOptional(),
    };
}

/// If `node` cannot contain runtime relocations, returns `.no`.
///
/// If `node` can contain runtime relocations, returns `.yes_textrel` if such a relocation requires
/// the presence of a `DT_TEXTREL` dynamic entry, or `.yes` otherwise.
fn nodeWantsDsoRelocation(elf: *Elf, node: MappedFile.Node.Index) enum { yes, yes_textrel, no } {
    const shndx = elf.getNodeShndx(node);
    const shf: std.elf.SHF = switch (elf.shdrPtr(shndx)) {
        inline else => |shdr| elf.targetLoad(&shdr.flags).shf,
    };
    if (!shf.ALLOC) return .no;
    if (!shf.WRITE) return .yes_textrel;
    return .yes;
}

pub fn updateNav(elf: *Elf, pt: Zcu.PerThread, nav_index: InternPool.Nav.Index) link.Error!void {
    elf.updateNavInner(pt, nav_index) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return elf.base.comp.link_diags.fail(
            "failed to write output file: {t}",
            .{elf.mf.io_err.?},
        ),
    };
}
fn updateNavInner(elf: *Elf, pt: Zcu.PerThread, nav_index: InternPool.Nav.Index) Error!void {
    const comp = elf.base.comp;
    const gpa = comp.gpa;
    const zcu = comp.zcu.?;
    const ip = &zcu.intern_pool;

    const nav = ip.getNav(nav_index);
    const mod = zcu.fileByIndex(nav.srcInst(ip).resolveFile(ip)).mod.?;
    switch (ip.indexToKey(nav.resolved.?.value)) {
        .@"extern" => |@"extern"| {
            if (mod.strip or elf.ehdrMachine() != .X86_64) return;
            const si = try elf.externSymbol(.{
                .name = @"extern".name.toSlice(ip),
                .lib_name = @"extern".lib_name.toSlice(ip),
                .type = elf.navType(nav.resolved.?),
                .linkage = @"extern".linkage,
                .visibility = @"extern".visibility,
            });
            const dwarf_gi = try elf.dwarf.getGlobal(nav_index);
            const dwarf_global = dwarf_gi.get(&elf.dwarf);
            const debug_info_ni = dwarf_global.debug_info_ni.unwrap().?;
            try debug_info_ni.moved(gpa, &elf.mf);
            var di_nw: MappedFile.Node.Writer = undefined;
            debug_info_ni.writer(gpa, &elf.mf, &di_nw);
            defer di_nw.deinit();
            elf.resetNodeRelocs(debug_info_ni);
            try elf.dwarf.updateExtern(pt, &di_nw, switch (elf.ehdrMachine()) {
                else => unreachable,
                .X86_64 => .x86_64,
            }, si, nav_index);
        },
        else => if (Type.fromInterned(nav.resolved.?.type).hasRuntimeBits(zcu)) {
            const nmi = try elf.navMapIndex(zcu, nav_index);
            const lsi = nmi.symbol(elf);
            const ni = lsi.index().ptr(elf).node.unwrap().?;

            // Ensure the NAV is marked as moved so that once we're done, `flushMoved`
            // will eventually be called to apply the NAV's new relocations.
            try ni.moved(gpa, &elf.mf);

            {
                var nw: MappedFile.Node.Writer = undefined;
                ni.writer(gpa, &elf.mf, &nw);
                defer nw.deinit();
                elf.resetNodeRelocs(ni);
                codegen.generateSymbol(
                    &elf.base,
                    pt,
                    .fromInterned(nav.resolved.?.value),
                    &nw.interface,
                    .{ .atom_index = Node.toAtom(ni) },
                ) catch |err| switch (err) {
                    else => |e| return e,
                    error.WriteFailed => return nw.err.?,
                };
                switch (elf.symPtr(nmi.symbol(elf).index())) {
                    inline else => |sym| elf.targetStore(&sym.size, @intCast(nw.interface.end)),
                }
            }

            if (!mod.strip and elf.ehdrMachine() == .X86_64) {
                const dwarf_gi = try elf.dwarf.getGlobal(nav_index);
                const dwarf_global = dwarf_gi.get(&elf.dwarf);
                const debug_info_ni = dwarf_global.debug_info_ni.unwrap().?;
                try debug_info_ni.moved(gpa, &elf.mf);
                var di_nw: MappedFile.Node.Writer = undefined;
                debug_info_ni.writer(gpa, &elf.mf, &di_nw);
                defer di_nw.deinit();
                elf.resetNodeRelocs(debug_info_ni);
                try elf.dwarf.updateGlobal(pt, &di_nw, switch (elf.ehdrMachine()) {
                    else => unreachable,
                    .X86_64 => .x86_64,
                }, Symbol.Id.local(lsi).toTypeErased(), nav_index);
            }
        } else {
            if (mod.strip or elf.ehdrMachine() != .X86_64) return;
            try elf.dwarf.updateComptimeGlobal(pt, nav_index);
        },
    }

    // The NAV's node is done---now generate any UAVs or lazy code/data which the NAV needs.
    try elf.genPending(pt);
    try elf.dwarf.const_pool.flushPending(pt, .{ .elf2 = elf });
}

pub fn updateContainerType(
    elf: *Elf,
    pt: Zcu.PerThread,
    ty: InternPool.Index,
    success: bool,
) link.Error!void {
    elf.updateContainerTypeInner(pt, ty, success) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return elf.base.comp.link_diags.fail(
            "failed to write output file: {t}",
            .{elf.mf.io_err.?},
        ),
    };
}
fn updateContainerTypeInner(
    elf: *Elf,
    pt: Zcu.PerThread,
    ty: InternPool.Index,
    success: bool,
) Error!void {
    switch (elf.base.comp.config.debug_format) {
        .strip => {},
        .dwarf => {
            try elf.dwarf.const_pool.updateContainerType(pt, .{ .elf2 = elf }, ty, success);
            try elf.dwarf.const_pool.flushPending(pt, .{ .elf2 = elf });
        },
        .code_view => unreachable,
    }
    if (!success) return;
    var lazy_it = elf.lazy.iterator();
    while (lazy_it.next()) |lazy| if (lazy.value.map.getIndex(ty)) |lmi| {
        if (lazy.value.pending_index <= lmi) continue;
        // This type has changed on this incremental update, so update the lazy code/data.
        try elf.genLazy(pt, .{ .kind = lazy.key, .index = @intCast(lmi) });
    };
}

pub fn addConst(
    elf: *Elf,
    _: Zcu.PerThread,
    cpi: link.ConstPool.Index,
    val: InternPool.Index,
) link.Error!void {
    switch (elf.base.comp.config.debug_format) {
        .strip => {},
        .dwarf => {
            const gpa = elf.base.comp.gpa;
            try elf.nodes.ensureUnusedCapacity(gpa, 1);
            try elf.dwarf.consts.ensureUnusedCapacity(gpa, 1);
            try elf.dwarf_consts.ensureUnusedCapacity(gpa, 1);
            try elf.dwarf.addConst(cpi, val, &addConstNode);
        },
        .code_view => unreachable,
    }
}
fn addConstNode(
    lf: *link.File,
    ui: Dwarf.Unit.Index,
    cpi: link.ConstPool.Index,
) link.Error!MappedFile.Node.Index {
    const elf = lf.cast(.elf2).?;
    const unit = ui.get(&elf.dwarf);
    const debug_info_ni = elf.addNodeAssumeCapacity(
        unit.debug_info_ni.unwrap().?.addFloatingChild(lf.comp.gpa, &elf.mf, .{
            .enable_next_moved = true,
        }) catch |err| switch (err) {
            else => |e| return e,
            error.MappedFileIo => return lf.comp.link_diags.fail("failed to write output file: {t}", .{
                elf.mf.io_err.?,
            }),
        },
        .{ .const_debug_info = cpi },
    );
    elf.dwarf_consts.putAssumeCapacityNoClobber(cpi, .{
        .debug_info_first_target_reloc = .none,
        .debug_info_first_symbol_reloc = .none,
        .debug_info_first_node_reloc = .none,
    });
    return debug_info_ni;
}

pub fn updateConst(
    elf: *Elf,
    pt: Zcu.PerThread,
    cpi: link.ConstPool.Index,
    val: InternPool.Index,
) link.Error!void {
    switch (val) {
        .anyerror_type => {}, // handled in `updateErrorData` instead
        else => try elf.updateConstInner(pt, cpi, val, .complete),
    }
}
fn updateConstInner(
    elf: *Elf,
    pt: Zcu.PerThread,
    cpi: link.ConstPool.Index,
    val: InternPool.Index,
    complete: enum { incomplete, complete },
) link.Error!void {
    switch (elf.base.comp.config.debug_format) {
        .strip => {},
        .dwarf => {
            switch (pt.zcu.intern_pool.indexToKey(val)) {
                else => {},
                .@"extern" => return,
                .func => |func| {
                    const fi = try elf.dwarf.getFunc(func.owner_nav);
                    switch (fi.get(&elf.dwarf).state) {
                        .unresolved => {},
                        .resolved => return,
                    }
                },
            }
            {
                const gpa = elf.base.comp.gpa;
                const debug_info_ni = Dwarf.Const.get(cpi, &elf.dwarf).debug_info_ni.unwrap().?;
                try debug_info_ni.moved(gpa, &elf.mf);
                var di_nw: MappedFile.Node.Writer = undefined;
                debug_info_ni.writer(gpa, &elf.mf, &di_nw);
                defer di_nw.deinit();
                elf.resetNodeRelocs(debug_info_ni);
                switch (complete) {
                    .incomplete => try elf.dwarf.updateConstIncomplete(pt, &di_nw, val),
                    .complete => try elf.dwarf.updateConst(pt, &di_nw, val),
                }
            }
            try elf.genPending(pt);
        },
        .code_view => unreachable,
    }
}

pub fn updateConstIncomplete(
    elf: *Elf,
    pt: Zcu.PerThread,
    cpi: link.ConstPool.Index,
    val: InternPool.Index,
) link.Error!void {
    return elf.updateConstInner(pt, cpi, val, .incomplete);
}

pub fn updateFunc(
    elf: *Elf,
    pt: Zcu.PerThread,
    func_index: InternPool.Index,
    mir: *const codegen.AnyMir,
) link.Error!void {
    elf.updateFuncInner(pt, func_index, mir) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return elf.base.comp.link_diags.fail(
            "failed to write output file: {t}",
            .{elf.mf.io_err.?},
        ),
    };
}
fn updateFuncInner(
    elf: *Elf,
    pt: Zcu.PerThread,
    func_index: InternPool.Index,
    mir: *const codegen.AnyMir,
) Error!void {
    const zcu = pt.zcu;
    const gpa = zcu.gpa;
    const ip = &zcu.intern_pool;
    const func = zcu.funcInfo(func_index);
    const nav = ip.getNav(func.owner_nav);

    const nmi = try elf.navMapIndex(zcu, func.owner_nav);
    log.debug("updateFunc({f}) = {d}", .{ nav.fqn.fmt(ip), nmi.symbol(elf) });
    const lsi = nmi.symbol(elf);
    const ni = lsi.index().ptr(elf).node.unwrap().?;

    // Ensure the NAV is marked as moved so that once we're done, `flushMoved` will eventually be
    // called to apply the NAV's new relocations.
    try ni.moved(gpa, &elf.mf);

    {
        var nw: MappedFile.Node.Writer = undefined;
        ni.writer(gpa, &elf.mf, &nw);
        defer nw.deinit();
        var debug_output_buf: Dwarf.WipFunc.Debug = undefined;
        const debug_output: link.File.DebugInfoOutput, const dwarf_func = debug_output: {
            if (elf.ehdrMachine() != .X86_64) break :debug_output .{ .none, undefined };
            const dwarf = &elf.dwarf;
            const mod = zcu.fileByIndex(nav.srcInst(ip).resolveFile(ip)).mod.?;
            if (mod.strip and mod.unwind_tables == .none) break :debug_output .{ .none, undefined };

            try elf.nodes.ensureUnusedCapacity(gpa, 4);
            const dwarf_fi = try dwarf.getFunc(func.owner_nav);

            const wip_func = &debug_output_buf.wip_func;
            wip_func.* = .{
                .dwarf = dwarf,
                .unit = dwarf.getUnit(mod),
                .func = func_index,
                .func_si = Symbol.Id.local(lsi).toTypeErased(),
                .cfi = .{
                    .loc = 0,
                    .cfa = dwarf.frame.header.initial_instructions[0].def_cfa,
                },
                .frame_format = switch (mod.unwind_tables) {
                    .none => .debug_frame,
                    .sync, .async => .eh_frame,
                },
                .fde_writer = undefined,
                .frame_func_length = undefined,
            };
            const unit = wip_func.unit.get(dwarf);

            const frame_align: Alignment = switch (elf.identClass()) {
                .NONE, _ => unreachable,
                .@"32" => .@"4",
                .@"64" => .@"8",
            };
            const frame_ni = unit.frame_ni.unwrap() orelse frame_ni: {
                const frame_ni = elf.addNodeAssumeCapacity(try switch (wip_func.frame_format) {
                    .debug_frame => elf.shndx.debug_frame,
                    .eh_frame => elf.shndx.eh_frame,
                }.get(elf).ni.addFloatingChild(gpa, &elf.mf, .{
                    .alignment = frame_align.max(elf.mf.flags.block_size),
                    .enable_next_moved = true,
                }), .{ .unit_frame = wip_func.unit });
                unit.frame_ni = .wrap(frame_ni);
                break :frame_ni frame_ni;
            };
            if (unit.cie_ni == .none) {
                const cie_ni = elf.addNodeAssumeCapacity(
                    try frame_ni.addOnlyHeaderChild(gpa, &elf.mf, .{
                        .alignment = frame_align,
                        .next_moved = true,
                        .enable_next_moved = true,
                    }),
                    .{ .unit_frame_cie = wip_func.unit },
                );
                unit.cie_ni = .wrap(cie_ni);
                var cie_nw: MappedFile.Node.Writer = undefined;
                cie_ni.writer(gpa, &elf.mf, &cie_nw);
                defer cie_nw.deinit();
                dwarf.genDebugFrameCie(&cie_nw.interface, switch (elf.ehdrMachine()) {
                    else => unreachable,
                    .X86_64 => .x86_64,
                }, wip_func.frame_format) catch |err| switch (err) {
                    error.WriteFailed => return cie_nw.err.?,
                };
            }
            const dwarf_func = dwarf_fi.get(dwarf);
            const fde_ni = if (dwarf_func.fde_ni.unwrap()) |fde_ni| fde_ni: {
                try fde_ni.moved(gpa, &elf.mf);
                break :fde_ni fde_ni;
            } else fde_ni: {
                const fde_ni = elf.addNodeAssumeCapacity(try frame_ni.addFloatingChild(gpa, &elf.mf, .{
                    .alignment = frame_align,
                    .moved = true,
                    .next_moved = true,
                    .enable_next_moved = true,
                }), .{ .func_frame_fde = dwarf_fi });
                dwarf_func.fde_ni = .wrap(fde_ni);
                break :fde_ni fde_ni;
            };
            fde_ni.writer(gpa, &elf.mf, &wip_func.fde_writer);

            if (mod.strip) break :debug_output .{ .{ .eh_frame = wip_func }, dwarf_func };

            const debug = &debug_output_buf;
            debug.init(pt);
            dwarf_func.state = .resolved;

            const debug_info_ni = dwarf_func.debug_info_ni.unwrap().?;
            try debug_info_ni.moved(gpa, &elf.mf);
            debug_info_ni.writer(gpa, &elf.mf, &debug.info_writer);

            const debug_line_ni = dwarf_func.debug_line_ni.unwrap() orelse debug_line_ni: {
                const debug_line_ni = elf.addNodeAssumeCapacity(
                    try unit.debug_line_ni.unwrap().?.addFloatingChild(gpa, &elf.mf, .{
                        .moved = true,
                        .next_moved = true,
                        .enable_next_moved = true,
                    }),
                    .{ .func_debug_line = dwarf_fi },
                );
                dwarf_func.debug_line_ni = .wrap(debug_line_ni);
                break :debug_line_ni debug_line_ni;
            };
            debug_line_ni.writer(gpa, &elf.mf, &debug.line_writer);

            break :debug_output .{ .{ .dwarf2 = debug }, dwarf_func };
        };
        defer switch (debug_output) {
            .dwarf => unreachable,
            inline .eh_frame, .dwarf2 => |dwarf| dwarf.deinit(),
            .none => {},
        };
        switch (debug_output) {
            .dwarf => unreachable,
            .eh_frame => |wip_func| {
                elf.resetNodeRelocs(dwarf_func.fde_ni.unwrap().?);
                try wip_func.genDebugFrameHeader();
            },
            .dwarf2 => |debug| {
                elf.resetNodeRelocs(dwarf_func.fde_ni.unwrap().?);
                try debug.wip_func.genDebugFrameHeader();
                elf.resetNodeRelocs(dwarf_func.debug_line_ni.unwrap().?);
                try debug.startDebugLine();
                elf.resetNodeRelocs(dwarf_func.debug_info_ni.unwrap().?);
                try debug.startDebugInfo();
            },
            .none => {},
        }
        elf.resetNodeRelocs(ni);
        codegen.emitFunction(
            &elf.base,
            pt,
            func_index,
            Node.toAtom(ni),
            mir,
            &nw.interface,
            debug_output,
        ) catch |err| switch (err) {
            else => |e| return e,
            error.WriteFailed => if (nw.err) |e| return e,
        };
        const func_length = nw.interface.end;
        switch (elf.symPtr(nmi.symbol(elf).index())) {
            inline else => |sym| elf.targetStore(&sym.size, @intCast(func_length)),
        }
        switch (debug_output) {
            .dwarf => unreachable,
            .eh_frame => |wip_func| wip_func.finishDebugFrameFde(func_length),
            .dwarf2 => |debug| {
                try debug.finish(func_length);
                const unit = debug.wip_func.unit.get(debug.wip_func.dwarf);
                {
                    var dr_nw: MappedFile.Node.Writer = undefined;
                    const debug_rnglists_ni = unit.debug_rnglists_ni.unwrap().?;
                    try debug_rnglists_ni.moved(gpa, &elf.mf);
                    debug_rnglists_ni.writer(gpa, &elf.mf, &dr_nw);
                    defer dr_nw.deinit();
                    const first_symbol_ri = elf.symbol_relocs.items.len;
                    debug.wip_func.dwarf.genDebugRnglistsRange(
                        unit,
                        &dr_nw,
                        debug.wip_func.func_si,
                        func_length,
                    ) catch |err| switch (err) {
                        else => |e| return e,
                        error.WriteFailed => return dr_nw.err.?,
                    };
                    const symbol_relocs = &elf.dwarf_units[@backingInt(debug.wip_func.unit)]
                        .debug_rnglists_symbol_relocs;
                    try symbol_relocs.ensureUnusedCapacity(gpa, elf.symbol_relocs.items.len -
                        first_symbol_ri);
                    for (first_symbol_ri..elf.symbol_relocs.items.len) |symbol_ri|
                        symbol_relocs.appendAssumeCapacity(@fromBackingInt(@intCast(symbol_ri)));
                }
                debug.wip_func.finishDebugFrameFde(func_length);
                if (func.analysisUnordered(ip).inferred_error_set) {
                    const ies = ip.getIfExists(.{ .inferred_error_set_type = func_index }).?;
                    if (elf.dwarf.const_pool.getIfExists(ies)) |cpi|
                        try elf.updateConstInner(pt, cpi, ies, .complete);
                }
            },
            .none => {},
        }
    }

    // The NAV's node is done---now generate any UAVs or lazy code/data which the NAV needs.
    try elf.genPending(pt);
    try elf.dwarf.const_pool.flushPending(pt, .{ .elf2 = elf });
}

pub fn updateLineNumber(
    elf: *Elf,
    _: Zcu.PerThread,
    inst: InternPool.TrackedInst.Index,
    line: u32,
) void {
    elf.dwarf.updateLineNumber(&elf.mf, inst, line);
}

pub fn lostTracking(
    elf: *Elf,
    _: Zcu.PerThread,
    inst: InternPool.TrackedInst.Index,
) link.Error!void {
    const di = elf.dwarf.getDeclIfExists(inst) orelse return;
    const decl_ni = di.get(&elf.dwarf).debug_info_ni.unwrap() orelse return;
    const comp = elf.base.comp;
    var di_nw: MappedFile.Node.Writer = undefined;
    decl_ni.writer(comp.gpa, &elf.mf, &di_nw);
    defer di_nw.deinit();
    elf.resetNodeRelocs(decl_ni);
    elf.dwarf.lostTracking(&di_nw) catch |err| switch (err) {
        else => |e| return e,
        error.WriteFailed => unreachable,
    };
    decl_ni.resizeLeaf(comp.gpa, &elf.mf, di_nw.interface.end) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return comp.link_diags.fail("failed to write output file: {t}", .{
            elf.mf.io_err.?,
        }),
    };
}

pub fn updateErrorData(elf: *Elf, pt: Zcu.PerThread) link.Error!void {
    if (elf.lazy.getPtr(.const_data).map.getIndex(.anyerror_type)) |lmi| try elf.genLazyInner(pt, .{
        .kind = .const_data,
        .index = @intCast(lmi),
    });
    if (elf.dwarf.const_pool.getIfExists(.anyerror_type)) |cpi|
        try elf.updateConstInner(pt, cpi, .anyerror_type, .complete);
}

pub fn flush(
    elf: *Elf,
    arena: std.mem.Allocator,
    tid: Zcu.PerThread.Id,
    prog_node: std.Progress.Node,
) link.Error!void {
    _ = tid;
    elf.flushInner(arena, prog_node) catch |err| switch (err) {
        else => |e| return e,
        error.MappedFileIo => return elf.base.comp.link_diags.fail(
            "failed to write output file: {t}",
            .{elf.mf.io_err.?},
        ),
    };
}
fn flushInner(
    elf: *Elf,
    arena: std.mem.Allocator,
    prog_node: std.Progress.Node,
) Error!void {
    const comp = elf.base.comp;
    const diags = &comp.link_diags;
    _ = arena;

    const flush_prog_node = prog_node.start("ELF Flush", 0);
    defer flush_prog_node.end();

    try elf.flushFiles();

    if (comp.config.output_mode == .Exe and elf.unknown_globals.count() > 0) {
        for (elf.unknown_globals.keys()) |gsi| {
            diags.addError("undefined global symbol '{s}'", .{gsi.rawName(elf).slice(elf)});
        }
        return error.AlreadyReported;
    }

    if (elf.defined_alias_globals.count() > 0) {
        for (elf.defined_alias_globals.keys()) |gsi| {
            diags.addError("multiple definitions of '{s}'", .{gsi.name(elf)});
        }
        return error.AlreadyReported;
    }

    try elf.prepareDynamic();

    while (try elf.idle()) {}

    assert(elf.input_pending_index == elf.inputs.items.len);
    assert(elf.input_section_pending_index == elf.input_sections.items.len);
    assert(elf.pending_uavs.items.len == 0);
    assert(elf.dwarf.const_pool.pending.items.len == 0);
    assert(!elf.dwarf.debug_addr.anyPending());
    assert(!elf.dwarf.debug_str_offsets.anyPending());

    // We've done the final `idle` loop, so everything is at its final place in the file. We have a
    // few more things to check and write now that addresses and offsets are finalized.
    elf.mf.nodes_lock.lock();
    defer elf.mf.nodes_lock.unlock();

    if (elf.overflowed_reloc_count > 0) {
        diags.addError("failed to apply {d} relocations: overflow", .{elf.overflowed_reloc_count});
    }
    if (elf.misaligned_reloc_count > 0) {
        diags.addError("failed to apply {d} relocations: misaligned value", .{elf.misaligned_reloc_count});
    }

    if (elf.archive) |*archive| {
        if (archive.elf_member_too_big) diags.addError(
            "file size of {Bi} exceeds maximum size of archive member",
            .{elf.ni.elf.location(&elf.mf).resolve(&elf.mf)[1]},
        );
        if (archive.strtab_member_too_big) diags.addError(
            "archive file name string table exceeds maximum size",
            .{},
        );
    }

    elf.flushDynamic();

    const entry_addr: u64 = entry: {
        const sym_name: []const u8 = name: switch (elf.options.entry) {
            .default => switch (comp.config.output_mode) {
                .Exe => continue :name .enabled,
                .Lib, .Obj => continue :name .disabled,
            },
            .disabled => break :entry 0,
            .enabled => "_start",
            .named => |named| named,
        };
        const gsi = elf.globalByName(.{
            .name = sym_name,
            .version = null,
        }) orelse break :entry 0;
        break :entry Symbol.Id.global(gsi).value(elf);
    };
    switch (elf.ehdrPtr()) {
        inline else => |ehdr| elf.targetStore(&ehdr.entry, @intCast(entry_addr)),
    }

    try elf.mf.flush();

    if (elf.options.enable_link_snapshots)
        elf.dumpStderr() catch |err|
            return diags.fail("dumping link snapshot failed: {t}", .{err});
}

pub fn idle(elf: *Elf) link.Error!bool {
    // This function is called non-deterministically, and so must not affect the layout of any nodes.
    elf.mf.nodes_lock.lock();
    defer elf.mf.nodes_lock.unlock();

    const comp = elf.base.comp;
    const diags = &comp.link_diags;

    assert(elf.pending_uavs.items.len == 0);
    assert(elf.dwarf.const_pool.pending.items.len == 0);

    task: {
        if (elf.inputs.items.len - elf.input_pending_index > 0) {
            const ii: Node.InputIndex = @fromBackingInt(elf.input_pending_index);
            elf.input_pending_index += 1;
            const idle_prog_node =
                elf.startIdleProgress(elf.input_prog_node, elf.getNode(ii.node(elf)));
            defer idle_prog_node.end();
            elf.flushInput(ii) catch |err| switch (err) {
                else => |e| return e,
                error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
            };
            break :task;
        }
        if (elf.input_sections.items.len - elf.input_section_pending_index > 0) {
            const isi: InputSection.Index = @fromBackingInt(elf.input_section_pending_index);
            elf.input_section_pending_index += 1;
            const idle_prog_node =
                elf.startIdleProgress(elf.input_prog_node, elf.getNode(isi.node(elf)));
            defer idle_prog_node.end();
            elf.flushInputSection(isi) catch |err| switch (err) {
                else => |e| return e,
                error.MappedFileIo => return diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
            };
            break :task;
        }
        if (elf.one_shot_fixups.items.len > 0) {
            // Each of these is very simple, so an unreasonable amount of overhead would be
            // introduced if we only did one per `idle` call. Also, there is no risk of this work
            // being invalidated. So let's just flush the entire queue at once.
            for (elf.one_shot_fixups.items) |isw| {
                const dest_slice = isw.node.slice(&elf.mf)[@intCast(isw.offset)..][0..4];
                const old: u32 = std.mem.readInt(u32, dest_slice, elf.targetEndian());
                const new: u32 = switch (isw.action) {
                    // zig fmt: off
                    .@"32[12:10] = 0b000" => old & 0b11111111_11111111_11100011_11111111,
                    .@"32[12:10] = 0b111" => old | 0b00000000_00000000_00011100_00000000,
                    .@"32[12:12] = 0b0"   => old & 0b11111111_11111111_11101111_11111111,
                    // zig fmt: on
                };
                std.mem.writeInt(u32, dest_slice, new, elf.targetEndian());
            }
            elf.one_shot_fixups.clearRetainingCapacity();
            break :task;
        }
        if (elf.changed_symtab_index.pop()) |kv| {
            const gsi = kv.key;

            const idle_prog_node = elf.mf.update_prog_node.start(gsi.rawName(elf).slice(elf), 0);
            defer idle_prog_node.end();

            const sym_id: Symbol.Id = .global(gsi);
            const symtab_index = gsi.ptr(elf).symtab_index;

            switch (elf.ehdrType()) {
                .REL => {
                    // Index in `.symtab` has changed. Relocatables are easy, we just need to update
                    // all of the output relocations.
                    var ri = symtab_index.ptr(elf).first_target_reloc;
                    while (ri != .none) {
                        const reloc = ri.get(elf);
                        assert(reloc.target == sym_id);
                        // In relocatables, every symbol relocation has an output relocation.
                        const rela_index = reloc.rela_index.unwrap().?;
                        reloc.relaSection(elf).relaUpdateSym(elf, rela_index, @backingInt(symtab_index));
                        ri = reloc.next;
                    }
                },
                // For other `ET_*` values, the index in `.dynsym` has changed. There are a few
                // places we might have emitted output relocations, depending on whether or not the
                // symbol's value is statically known.
                .EXEC, .DYN => switch (elf.classifySymbolValue(sym_id)) {
                    .static, .static_relative => {
                        // Since the symbol value is statically known, we definitely aren't emitting
                        // any relocation targeting it (we might have `R_*_RELATIVE` relocs but they
                        // don't care about the dynsym index). The only exception is a copy reloc
                        // could exist (and be the *reason* the symbol value is statically known).
                        if (elf.copied_globals.get(gsi)) |copied| {
                            const dynsym_index = gsi.dynsymIndex(elf).?;
                            elf.shndx.rela_dyn.relaUpdateSym(elf, copied.rela_index, dynsym_index);
                        }
                    },
                    .dynamic => {
                        assert(!elf.copied_globals.contains(gsi)); // value would be statically known

                        const dynsym_index = gsi.dynsymIndex(elf).?;

                        // Update symbol relocs:
                        var ri = symtab_index.ptr(elf).first_target_reloc;
                        while (ri != .none) {
                            const reloc = ri.get(elf);
                            assert(reloc.target == sym_id);
                            // There may or may not be a runtime relocation for this symbol reloc.
                            if (reloc.rela_index.unwrap()) |rela_index| {
                                elf.shndx.rela_dyn.relaUpdateSym(elf, rela_index, dynsym_index);
                            }
                            ri = reloc.next;
                        }

                        // Update the PLT entry's reloc if there is one:
                        if (elf.plt.getIndex(gsi)) |plt_index| {
                            // PLT indices exactly match `.rela.plt` relocation indices.
                            elf.shndx.rela_plt.relaUpdateSym(elf, @fromBackingInt(@intCast(plt_index)), dynsym_index);
                        }

                        // Update relocs for any relevant GOT entries:
                        if (elf.got.getIndex(.{ .symbol = sym_id })) |got_index| {
                            elf.updateGotEntry(got_index);
                        }
                        if (elf.got.getIndex(.{ .tpoff = sym_id })) |got_index| {
                            elf.updateGotEntry(got_index);
                        }
                        if (elf.got.getIndex(.{ .tlsgd0 = sym_id })) |got_index| {
                            elf.updateGotEntry(got_index);
                            elf.updateGotEntry(got_index + 1); // tlsgd1
                        }
                    },
                },
            }

            break :task;
        }
        while (elf.mf.updates.pop()) |ni| : (elf.mf.update_prog_node.completeOne()) {
            if (ni.pendingDelete(&elf.mf)) continue;
            const clean_moved = ni.cleanMoved(&elf.mf);
            const clean_resized = ni.cleanResized(&elf.mf);
            const clean_next_moved = ni.cleanNextMoved(&elf.mf);
            if (!clean_moved and !clean_resized and !clean_next_moved) continue;
            const idle_prog_node = elf.startIdleProgress(elf.mf.update_prog_node, elf.getNode(ni));
            defer idle_prog_node.end();
            if (clean_moved) try elf.flushMoved(ni);
            if (clean_resized) try elf.flushResized(ni);
            if (clean_moved or clean_resized or clean_next_moved) try elf.flushPadding(ni);
            break :task;
        }
    }
    if (elf.inputs.items.len - elf.input_pending_index > 0) return true;
    if (elf.input_sections.items.len - elf.input_section_pending_index > 0) return true;
    if (elf.one_shot_fixups.items.len > 0) return true;
    if (elf.changed_symtab_index.count() > 0) return true;
    if (elf.mf.updates.items.len > 0) return true;
    return false;
}

fn startIdleProgress(
    elf: *Elf,
    prog_node: std.Progress.Node,
    node: Node,
) std.Progress.Node {
    var name: [std.Progress.Node.max_name_len]u8 = undefined;
    return prog_node.start(name: switch (node) {
        else => |tag| @tagName(tag),
        .archive_input_member => |ii| std.mem.print(&name, "{f}{f}", .{
            ii.path(elf).fmtEscapeString(),
            fmtMemberString(ii.member(elf)),
        }) catch &name,
        .section, .section_manual_size => |shndx| shndx.name(elf).slice(elf),
        .input_section => |isi| {
            const ii = isi.input(elf);
            break :name std.mem.print(&name, "{f}{f} {s}", .{
                ii.path(elf).fmtEscapeString(),
                fmtMemberString(ii.member(elf)),
                elf.getNodeShndx(isi.node(elf)).name(elf).slice(elf),
            }) catch &name;
        },
        .nav => |nmi| {
            const ip = &elf.base.comp.zcu.?.intern_pool;
            break :name ip.getNav(nmi.nav(elf)).fqn.toSlice(ip);
        },
        .uav => |umi| std.mem.print(&name, "{f}", .{
            Value.fromInterned(umi.uavValue(elf)).fmtValue(elf.base.comp.zcu.?),
        }) catch &name,
        .debug_shared => |ss| switch (ss) {
            .debug_abbrev => "debug info abbrevs",
            .debug_str => "debug info strings",
            .debug_line_str => "line info strings",
        },
        .unit_frame,
        .unit_frame_cie,
        .unit_debug_info,
        .unit_debug_info_header,
        .unit_debug_info_footer,
        .unit_debug_line,
        .unit_debug_line_header,
        .unit_debug_rnglists,
        => |ui, tag| std.mem.print(&name, "{s} info for {s}", .{
            switch (tag) {
                else => unreachable,
                .unit_frame, .unit_frame_cie => "unwind",
                .unit_debug_info,
                .unit_debug_info_header,
                .unit_debug_info_footer,
                .unit_debug_rnglists,
                => "debug",
                .unit_debug_line, .unit_debug_line_header => "line",
            },
            ui.mod(&elf.dwarf).fully_qualified_name,
        }) catch &name,
        .const_debug_info => |cpi| switch (cpi.val(&elf.dwarf.const_pool)) {
            .generic_poison_type => "anytype",
            else => |val| std.mem.print(&name, "debug info for {f}", .{
                Value.fromInterned(val).fmtValue(elf.base.comp.zcu.?),
            }) catch &name,
        },
        .global_debug_info => |gi| {
            const ip = &elf.base.comp.zcu.?.intern_pool;
            break :name std.mem.print(&name, "debug info for {f}", .{
                ip.getNav(gi.nav(&elf.dwarf)).fqn.fmt(ip),
            }) catch &name;
        },
        .func_frame_fde, .func_debug_info, .func_debug_line => |fi, tag| {
            const ip = &elf.base.comp.zcu.?.intern_pool;
            break :name std.mem.print(&name, "{s} info for {f}", .{
                switch (tag) {
                    else => unreachable,
                    .func_frame_fde => "unwind",
                    .func_debug_info => "debug",
                    .func_debug_line => "line",
                },
                ip.getNav(fi.nav(&elf.dwarf)).fqn.fmt(ip),
            }) catch &name;
        },
        .decl_debug_info => |di| {
            const comp = elf.base.comp;
            const zcu = comp.zcu.?;
            break :name std.mem.print(&name, "debug info for {f}", .{
                zcu.fileByIndex(di.srcInst(&elf.dwarf).resolveFile(&zcu.intern_pool)).path.fmt(comp),
            }) catch &name;
        },
    }, 0);
}

fn genPending(elf: *Elf, pt: Zcu.PerThread) link.Error!void {
    const zcu = elf.base.comp.zcu.?;
    while (elf.pending_uavs.pop()) |umi| {
        var prog_name_buf: [std.Progress.Node.max_name_len]u8 = undefined;
        const prog_name = std.mem.print(&prog_name_buf, "{f}", .{
            Value.fromInterned(umi.uavValue(elf)).fmtValue(zcu),
        }) catch &prog_name_buf;
        const prog_node = elf.const_prog_node.start(prog_name, 0);
        defer prog_node.end();
        try elf.genUav(pt, umi);
    }
    var lazy_it = elf.lazy.iterator();
    while (lazy_it.next()) |lazy| while (lazy.value.map.count() - lazy.value.pending_index > 0) {
        try elf.genLazy(pt, .{ .kind = lazy.key, .index = lazy.value.pending_index });
        lazy.value.pending_index += 1;
    };
    const gpa = elf.base.comp.gpa;
    switch (elf.base.comp.config.debug_format) {
        .strip => {},
        .dwarf => while (true) {
            const pending = elf.dwarf.pending_decl;
            if (pending.instance == .none) break;
            elf.dwarf.pending_decl = .{ .di = undefined, .instance = .none };
            const debug_info_ni = pending.di.get(&elf.dwarf).debug_info_ni.unwrap().?;
            try debug_info_ni.moved(gpa, &elf.mf);
            var di_nw: MappedFile.Node.Writer = undefined;
            debug_info_ni.writer(gpa, &elf.mf, &di_nw);
            defer di_nw.deinit();
            elf.resetNodeRelocs(debug_info_ni);
            try elf.dwarf.genDecl(pt, &di_nw, pending.instance);
        },
        .code_view => unreachable,
    }
    try elf.genPendingDebug(gpa);
    assert(elf.pending_uavs.items.len == 0); // no UAVs added by lazy code/data or by debug info
}
fn genPendingDebug(elf: *Elf, gpa: std.mem.Allocator) link.Error!void {
    while (elf.dwarf.debug_addr.map.count() - elf.dwarf.debug_addr.pending_index > 0) {
        const debug_addr_ni = elf.dwarf.debug_addr.ni.unwrap().?;
        try debug_addr_ni.moved(gpa, &elf.mf);
        var da_nw: link.MappedFile.Node.Writer = undefined;
        debug_addr_ni.writer(gpa, &elf.mf, &da_nw);
        defer da_nw.deinit();
        const first_symbol_ri = elf.symbol_relocs.items.len;
        try elf.dwarf.genPendingDebugAddr(&da_nw);
        const symbol_relocs = &elf.dwarf_addr.symbol_relocs;
        try symbol_relocs.ensureUnusedCapacity(gpa, elf.symbol_relocs.items.len - first_symbol_ri);
        for (first_symbol_ri..elf.symbol_relocs.items.len) |symbol_ri|
            symbol_relocs.appendAssumeCapacity(@fromBackingInt(@intCast(symbol_ri)));
    }
    while (elf.dwarf.debug_str_offsets.map.count() - elf.dwarf.debug_str_offsets.pending_index > 0) {
        const debug_str_offsets_ni = elf.dwarf.debug_str_offsets.ni.unwrap().?;
        try debug_str_offsets_ni.moved(gpa, &elf.mf);
        var dso_nw: link.MappedFile.Node.Writer = undefined;
        debug_str_offsets_ni.writer(gpa, &elf.mf, &dso_nw);
        defer dso_nw.deinit();
        const first_node_ri = elf.node_relocs.items.len;
        try elf.dwarf.genPendingDebugStrOffsets(&dso_nw);
        const node_relocs = &elf.dwarf_str_offsets.node_relocs;
        try node_relocs.ensureUnusedCapacity(gpa, elf.node_relocs.items.len - first_node_ri);
        for (first_node_ri..elf.node_relocs.items.len) |node_ri|
            node_relocs.appendAssumeCapacity(@fromBackingInt(@intCast(node_ri)));
    }
}

fn genUav(
    elf: *Elf,
    pt: Zcu.PerThread,
    umi: Node.UavMapIndex,
) link.Error!void {
    const comp = elf.base.comp;
    const gpa = comp.gpa;

    const uav_val = umi.uavValue(elf);
    const ni = umi.symbol(elf).index().ptr(elf).node.unwrap().?;

    var nw: MappedFile.Node.Writer = undefined;
    ni.writer(gpa, &elf.mf, &nw);
    defer nw.deinit();
    elf.resetNodeRelocs(ni);
    codegen.generateSymbol(
        &elf.base,
        pt,
        .fromInterned(uav_val),
        &nw.interface,
        .{ .atom_index = Node.toAtom(ni) },
    ) catch |err| switch (err) {
        else => |e| return e,
        error.WriteFailed => switch (nw.err.?) {
            else => |e| return e,
            error.MappedFileIo => return comp.link_diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
        },
    };
    switch (elf.symPtr(umi.symbol(elf).index())) {
        inline else => |sym| elf.targetStore(&sym.size, @intCast(nw.interface.end)),
    }
    // The UAV should already be considered to have moved, because it is created as moved and
    // pending calls to `genUav` always happen before pending calls to `flushMoved`.
    assert(ni.hasMoved(&elf.mf));
}

fn genLazy(elf: *Elf, pt: Zcu.PerThread, lmr: Node.LazyMapRef) link.Error!void {
    const zcu = elf.base.comp.zcu.?;
    const lazy = lmr.lazySymbol(elf);
    if (lazy.ty == .anyerror_type) return;
    const lazy_ty: Type = .fromInterned(lazy.ty);
    var prog_name_buf: [std.Progress.Node.max_name_len]u8 = undefined;
    const prog_name: []const u8 = switch (lazy_ty.zigTypeTag(zcu)) {
        .@"enum" => std.mem.print(&prog_name_buf, "@tagName({f})", .{lazy_ty.fmt(zcu)}) catch &prog_name_buf,
        .error_set => switch (lmr.kind) {
            .code => std.mem.print(&prog_name_buf, "@errorCast({f})", .{lazy_ty.fmt(zcu)}) catch &prog_name_buf,
            .const_data => "@errorName(anyerror)",
        },
        else => unreachable,
    };
    const prog_node = elf.base.comp.link_prog_node.start(prog_name, 0);
    defer prog_node.end();
    try elf.genLazyInner(pt, lmr);
}
fn genLazyInner(elf: *Elf, pt: Zcu.PerThread, lmr: Node.LazyMapRef) link.Error!void {
    const zcu = pt.zcu;
    const gpa = zcu.gpa;

    const lazy = lmr.lazySymbol(elf);
    const ni = lmr.symbol(elf).index().ptr(elf).node.unwrap().?;

    // Ensure the lazy node is marked as moved so that once we're done, `flushMoved` will eventually
    // be called to apply the lazy node's new relocations.
    try ni.moved(gpa, &elf.mf);

    var required_alignment: InternPool.Alignment = .none;
    var nw: MappedFile.Node.Writer = undefined;
    ni.writer(gpa, &elf.mf, &nw);
    defer nw.deinit();
    elf.resetNodeRelocs(ni);
    codegen.generateLazySymbol(
        &elf.base,
        pt,
        lazy,
        &required_alignment,
        &nw.interface,
        .none,
        .{ .atom_index = Node.toAtom(ni) },
    ) catch |err| switch (err) {
        else => |e| return e,
        error.WriteFailed => return switch (nw.err.?) {
            else => |e| return e,
            error.MappedFileIo => return elf.base.comp.link_diags.fail(
                "failed to write output file: {t}",
                .{elf.mf.io_err.?},
            ),
        },
    };
    switch (elf.symPtr(lmr.symbol(elf).index())) {
        inline else => |sym| elf.targetStore(&sym.size, @intCast(nw.interface.end)),
    }
}

fn flushInput(elf: *Elf, ii: Node.InputIndex) Error!void {
    const comp = elf.base.comp;
    const io = comp.io;
    const diags = &comp.link_diags;
    const path = ii.path(elf);
    const file = path.root_dir.handle.openFile(io, path.sub_path, .{}) catch |err| switch (err) {
        error.Canceled => |e| return e,
        else => |e| return diags.fail("failed to open input file \"{f}\": {t}", .{ path.fmtEscapeString(), e }),
    };
    defer file.close(io);

    const slice = ii.node(elf).slice(&elf.mf);

    const member_ar_hdr: *const std.elf.ar_hdr = @ptrCast(slice[0..@sizeOf(std.elf.ar_hdr)]);
    const input_size: u32 = member_ar_hdr.size() catch |err| switch (err) {
        // We wrote the `ar_hdr` ourselves (in `loadObject`), so it is definitely valid.
        error.Overflow, error.InvalidCharacter => unreachable,
    };

    switch (slice.len - @sizeOf(std.elf.ar_hdr) - input_size) {
        0 => {},
        1 => {
            // Alignment added one padding byte, which the format requires to have value '\n'.
            slice[slice.len - 1] = '\n';
        },
        else => unreachable, // node size should agree with the value we wrote into `ar_hdr.ar_size`
    }

    var fr = file.reader(io, &.{});
    var w: Io.Writer = .fixed(slice[@sizeOf(std.elf.ar_hdr)..]);
    const n_bytes_read = w.sendFileAll(&fr, .limited(input_size)) catch |err| switch (err) {
        error.ReadFailed => return diags.fail("failed to read input \"{f}{f}\": {t}", .{
            path.fmtEscapeString(),
            fmtMemberString(ii.member(elf)),
            fr.err orelse (fr.seek_err orelse fr.size_err.?),
        }),
        error.WriteFailed => unreachable, // `.limited(input_size)` prevents us writing too many bytes
    };
    if (n_bytes_read != input_size) {
        return diags.fail("failed to load input \"{f}{f}\": file truncated during compilation", .{
            path.fmtEscapeString(),
            fmtMemberString(ii.member(elf)),
        });
    }
}

fn flushInputSection(elf: *Elf, isi: InputSection.Index) Error!void {
    const file_loc = isi.fileLocation(elf);
    if (file_loc.size == 0) return;
    const comp = elf.base.comp;
    const io = comp.io;
    const gpa = comp.gpa;
    const diags = &comp.link_diags;
    const ii = isi.input(elf);
    const path = ii.path(elf);
    const file = path.root_dir.handle.openFile(io, path.sub_path, .{}) catch |err| switch (err) {
        error.Canceled => |e| return e,
        else => |e| return diags.fail("failed to open input file \"{f}\": {t}", .{ path.fmtEscapeString(), e }),
    };
    defer file.close(io);
    var fr = file.reader(io, &.{});
    fr.seekTo(file_loc.offset) catch |err| switch (err) {
        error.Canceled => |e| return e,
        else => |e| return diags.fail("failed to read input section '{s}' from \"{f}{f}\": {t}", .{
            elf.getNodeShndx(isi.node(elf)).name(elf).slice(elf),
            path.fmtEscapeString(),
            fmtMemberString(ii.member(elf)),
            e,
        }),
    };
    var nw: MappedFile.Node.Writer = undefined;
    isi.node(elf).writer(gpa, &elf.mf, &nw);
    defer nw.deinit();
    const n_bytes = nw.interface.sendFileAll(&fr, .limited(@intCast(file_loc.size))) catch |err| switch (err) {
        error.ReadFailed => return diags.fail("failed to read input section '{s}' from \"{f}{f}\": {t}", .{
            elf.getNodeShndx(isi.node(elf)).name(elf).slice(elf),
            path.fmtEscapeString(),
            fmtMemberString(ii.member(elf)),
            fr.err orelse (fr.seek_err orelse fr.size_err.?),
        }),
        error.WriteFailed => return nw.err.?,
    };
    if (n_bytes != file_loc.size) return diags.fail("failed to read input section '{s}' from \"{f}{f}\": unexpected eof", .{
        elf.getNodeShndx(isi.node(elf)).name(elf).slice(elf),
        path.fmtEscapeString(),
        fmtMemberString(ii.member(elf)),
    });
    // The input section should already be considered to have moved, because it is created as moved
    // and pending calls to `flushInputSection` always happen before pending calls to `flushMoved`.
    assert(isi.node(elf).hasMoved(&elf.mf));
}

fn flushElfOffset(elf: *Elf, ni: MappedFile.Node.Index) void {
    const elf_offset = elf.computeNodeElfOffset(ni);
    switch (elf.getNode(ni)) {
        else => unreachable,
        .ehdr => assert(elf_offset == 0),
        .shdr => switch (elf.ehdrPtr()) {
            inline else => |ehdr| elf.targetStore(&ehdr.shoff, @intCast(elf_offset)),
        },
        .segment => |phndx| {
            switch (elf.phdrSlice()) {
                inline else => |phdr, class| {
                    const ph = &phdr[phndx];
                    elf.targetStore(&ph.offset, @intCast(elf_offset));
                    if (elf.targetLoad(&ph.type) == .PHDR) {
                        @field(elf.ehdrPtr(), @tagName(class)).phoff = ph.offset;
                    }
                },
            }
            var child_oni = ni.first(&elf.mf);
            while (child_oni.unwrap()) |child_ni| : (child_oni = child_ni.next(&elf.mf)) {
                elf.flushElfOffset(child_ni);
            }
        },
        .section, .section_manual_size => |shndx| switch (elf.shdrPtr(shndx)) {
            inline else => |shdr| elf.targetStore(&shdr.offset, @intCast(elf_offset)),
        },
    }
}

fn flushMoved(elf: *Elf, ni: MappedFile.Node.Index) std.mem.Allocator.Error!void {
    const trace = tracy.trace(@src());
    defer trace.end();

    switch (elf.getNode(ni)) {
        .deleted => unreachable,
        .archive, .archive_header => unreachable,
        .archive_input_member, .archive_elf_member_header, .elf => {
            assert(elf.archive != null);
            return;
        },
        .ehdr, .shdr => elf.flushElfOffset(ni),
        .segment => |phndx| {
            elf.flushElfOffset(ni);
            switch (elf.phdrSlice()) {
                inline else => |phdr| {
                    const ph = &phdr[phndx];
                    switch (elf.targetLoad(&ph.type)) {
                        else => unreachable,

                        .NULL, .LOAD => {
                            try elf.allocateSegmentLoadAddress(phndx);
                        },

                        .DYNAMIC,
                        .INTERP,
                        .PHDR,
                        .TLS,
                        .GNU_EH_FRAME,
                        .GNU_RELRO,
                        => {
                            const new_vaddr = elf.computeNodeVAddr(ni);
                            elf.targetStore(&ph.vaddr, @intCast(new_vaddr));
                            elf.targetStore(&ph.paddr, @intCast(new_vaddr));
                        },
                    }
                },
            }
        },
        .section, .section_manual_size => |shndx| {
            elf.flushElfOffset(ni);
            const addr = elf.computeNodeVAddr(ni);
            const old_addr: u64, const flags: std.elf.SHF = switch (elf.shdrPtr(shndx)) {
                inline else => |shdr| .{ elf.targetLoad(&shdr.addr), elf.targetLoad(&shdr.flags).shf },
            };

            if (flags.ALLOC) {
                switch (elf.shdrPtr(shndx)) {
                    inline else => |shdr| elf.targetStore(&shdr.addr, @intCast(addr)),
                }

                // Update global symbols targeting this section
                if (elf.node_global_symbols.get(ni)) |first_gsi| {
                    var opt_gsi: Symbol.Global.Index.Optional = .wrap(first_gsi);
                    while (opt_gsi.unwrap()) |gsi| : (opt_gsi = gsi.ptr(elf).next_in_node) {
                        const old_sym_addr = Symbol.Id.global(gsi).value(elf);
                        Symbol.Id.global(gsi).flushMoved(elf, old_sym_addr - old_addr + addr);
                    }
                }

                Symbol.Id.local(shndx.get(elf).lsi).flushMoved(elf, addr);
            }

            if (shndx == elf.shndx.got) {
                const rela_dyn_shndx = elf.shndx.rela_dyn;
                for (elf.got.values()) |opt_rela_index| {
                    const rela_index = opt_rela_index.unwrap() orelse continue;
                    rela_dyn_shndx.relaAdjustOffset(elf, rela_index, old_addr, addr);
                }
                for (elf.got_relocs.items) |*reloc| {
                    reloc.apply(elf);
                }
            } else if (shndx == elf.shndx.plt) {
                elf.flushMovedNodeRelocs(ni, addr, .{
                    .first_symbol_reloc = elf.plt_first_symbol_reloc,
                });
                elf.flushMovedPltSection(.plt, old_addr, addr);
            } else if (shndx == elf.shndx.got_plt) {
                elf.flushMovedPltSection(.got_plt, old_addr, addr);
            } else if (shndx == elf.shndx.plt_sec) {
                elf.flushMovedPltSection(.plt_sec, old_addr, addr);
            } else if (shndx == elf.shndx.eh_frame_hdr) {
                elf.flushMovedNodeRelocs(ni, addr, .{
                    .first_symbol_reloc = elf.eh_frame_hdr_first_symbol_reloc,
                });
            }
        },
        .input_section => |isi| {
            const old_section_addr = isi.ptr(elf).vaddr;
            const new_section_addr = elf.computeNodeVAddr(ni);
            isi.ptr(elf).vaddr = new_section_addr;

            // Update local symbols
            const ii = isi.input(elf);
            var lsi, const end_lsi = ii.localSymbolRange(elf);
            while (lsi != end_lsi) : (lsi = @fromBackingInt(@backingInt(lsi) + 1)) {
                if (lsi.index().ptr(elf).node != ni.toOptional()) continue;
                const visibility: std.elf.STV = switch (elf.symPtr(lsi.index())) {
                    inline else => |sym| elf.targetLoad(&sym.other).visibility,
                };
                switch (visibility) {
                    .HIDDEN, .INTERNAL => {
                        // This is actually a global symbol which got demoted to STB_LOCAL due
                        // to its visibility. It will be handled in the global symbols pass
                        // below; don't touch it now.
                        continue;
                    },
                    .PROTECTED => unreachable, // not allowed for an STB_LOCAL symbol
                    .DEFAULT => {},
                }
                const old_sym_addr = Symbol.Id.local(lsi).value(elf);
                Symbol.Id.local(lsi).flushMoved(
                    elf,
                    old_sym_addr - old_section_addr + new_section_addr,
                );
            }

            // Update global symbols
            if (elf.node_global_symbols.get(ni)) |first_gsi| {
                var opt_gsi: Symbol.Global.Index.Optional = .wrap(first_gsi);
                while (opt_gsi.unwrap()) |gsi| : (opt_gsi = gsi.ptr(elf).next_in_node) {
                    const old_sym_addr = Symbol.Id.global(gsi).value(elf);
                    Symbol.Id.global(gsi).flushMoved(
                        elf,
                        old_sym_addr - old_section_addr + new_section_addr,
                    );
                }
            }

            elf.flushMovedNodeRelocs(ni, new_section_addr, .{
                .first_symbol_reloc = isi.ptrConst(elf).first_symbol_reloc,
                .first_got_reloc = isi.ptrConst(elf).first_got_reloc,
            });
        },
        .copied_global => |global_name| {
            const copied_global = elf.copied_globals.getPtr(global_name) orelse {
                // TODO: this node is orphaned, which is possible because `MappedFile` does not yet
                // support deleting nodes. See logic in `updateGlobalDynamic`.
                return;
            };
            assert(copied_global.node == ni);

            const new_addr = elf.computeNodeVAddr(ni);
            elf.shndx.rela_dyn.relaSetOffset(elf, copied_global.rela_index, new_addr);

            Symbol.Id.global(global_name).flushMoved(elf, new_addr);
        },
        inline .nav, .uav, .lazy_code, .lazy_const_data => |mi, tag| {
            const new_addr = elf.computeNodeVAddr(ni);
            Symbol.Id.local(mi.symbol(elf)).flushMoved(elf, new_addr);
            if (elf.node_global_symbols.get(ni)) |first_gsi| {
                var opt_gsi: Symbol.Global.Index.Optional = .wrap(first_gsi);
                while (opt_gsi.unwrap()) |gsi| : (opt_gsi = gsi.ptr(elf).next_in_node) {
                    Symbol.Id.global(gsi).flushMoved(elf, new_addr);
                }
            }
            elf.flushMovedNodeRelocs(ni, new_addr, .{
                .first_symbol_reloc = mi.firstSymbolReloc(elf),
                .skip_symbol_relocs = switch (tag) {
                    else => comptime unreachable,
                    .nav => if (elf.dwarf.getFuncIfExists(mi.nav(elf))) |dwarf_fi|
                        dwarf_fi.get(&elf.dwarf).debug_info_ni
                    else
                        .none,
                    .uav, .lazy_code, .lazy_const_data => .none,
                },
                .first_got_reloc = mi.firstGotReloc(elf),
            });
        },
        .debug_shared => |ss| {
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = elf.dwarf_shared.getPtr(ss).first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
        },
        .eh_frame_footer,
        .unit_padding,
        .unit_frame,
        .unit_debug_info,
        .unit_debug_line,
        => {},
        .debug_addr => {
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = elf.dwarf_addr.first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
            const node_vaddr = elf.computeNodeVAddr(ni);
            for (elf.dwarf_addr.symbol_relocs.items) |symbol_ri| {
                const symbol_reloc = symbol_ri.get(elf);
                assert(symbol_reloc.node.unwrap().? == ni);
                symbol_reloc.flushMovedNode(elf, node_vaddr);
            }
        },
        .debug_str_offsets => {
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = elf.dwarf_str_offsets.first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
            const node_vaddr = elf.computeNodeVAddr(ni);
            for (elf.dwarf_str_offsets.node_relocs.items) |node_ri| {
                const node_reloc = node_ri.get(elf);
                assert(node_reloc.node.unwrap().? == ni);
                node_reloc.flushMovedNode(elf, node_vaddr);
            }
        },
        .unit_frame_cie => |ui| {
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = elf.dwarf_units[@backingInt(ui)].frame_cie_first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
        },
        .unit_debug_info_header => |ui| {
            const dwarf_unit = &elf.dwarf_units[@backingInt(ui)];
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = dwarf_unit.debug_info_header_first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
            elf.flushMovedNodeRelocs(ni, elf.computeNodeVAddr(ni), .{
                .first_node_reloc = dwarf_unit.debug_info_header_first_node_reloc,
            });
        },
        .unit_debug_info_footer => {},
        .unit_debug_line_header => |ui| {
            const dwarf_unit = &elf.dwarf_units[@backingInt(ui)];
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = dwarf_unit.debug_line_header_first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
            elf.flushMovedNodeRelocs(ni, elf.computeNodeVAddr(ni), .{
                .first_node_reloc = dwarf_unit.debug_line_header_first_node_reloc,
            });
        },
        .unit_debug_rnglists => |ui| {
            const dwarf_unit = &elf.dwarf_units[@backingInt(ui)];
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = dwarf_unit.debug_rnglists_first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
            const node_vaddr = elf.computeNodeVAddr(ni);
            for (dwarf_unit.debug_rnglists_symbol_relocs.items) |symbol_ri| {
                const symbol_reloc = symbol_ri.get(elf);
                assert(symbol_reloc.node.unwrap().? == ni);
                symbol_reloc.flushMovedNode(elf, node_vaddr);
            }
        },
        .const_debug_info => |cpi| {
            const dwarf_const = &elf.dwarf_consts.get(cpi).?;
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = dwarf_const.debug_info_first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
            elf.flushMovedNodeRelocs(ni, elf.computeNodeVAddr(ni), .{
                .first_symbol_reloc = dwarf_const.debug_info_first_symbol_reloc,
                .first_node_reloc = dwarf_const.debug_info_first_node_reloc,
            });
        },
        .global_debug_info => |gi| {
            const dwarf_global = &elf.dwarf_globals.items[@backingInt(gi)];
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = dwarf_global.debug_info_first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
            elf.flushMovedNodeRelocs(ni, elf.computeNodeVAddr(ni), .{
                .first_symbol_reloc = dwarf_global.debug_info_first_symbol_reloc,
                .first_node_reloc = dwarf_global.debug_info_first_node_reloc,
            });
        },
        .func_frame_fde => |fi| {
            const dwarf_func = &elf.dwarf_funcs.items[@backingInt(fi)];
            const zcu = elf.base.comp.zcu.?;
            const mod = zcu.navFileScope(fi.nav(&elf.dwarf)).mod.?;
            switch (mod.unwind_tables) {
                .none => {},
                .sync, .async => {
                    const offset, _ = ni.location(&elf.mf).resolve(&elf.mf);
                    elf.dwarf.updateEhFrameFde(ni.slice(&elf.mf), offset);
                },
            }
            elf.flushMovedNodeRelocs(ni, elf.computeNodeVAddr(ni), .{
                .first_symbol_reloc = dwarf_func.frame_fde_first_symbol_reloc,
                .first_node_reloc = dwarf_func.frame_fde_first_node_reloc,
            });
        },
        .func_debug_info => |fi| {
            const dwarf_func = &elf.dwarf_funcs.items[@backingInt(fi)];
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = dwarf_func.debug_info_first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
            elf.flushMovedNodeRelocs(ni, elf.computeNodeVAddr(ni), .{
                .first_symbol_reloc = dwarf_func.debug_info_first_symbol_reloc,
                .skip_symbol_relocs = if (elf.navs.getPtr(fi.nav(&elf.dwarf))) |nav|
                    nav.lsi.index().ptr(elf).node
                else
                    .none,
                .first_node_reloc = dwarf_func.debug_info_first_node_reloc,
                .skip_node_relocs = fi.get(&elf.dwarf).debug_line_ni,
            });
        },
        .func_debug_line => |fi| {
            const dwarf_func = &elf.dwarf_funcs.items[@backingInt(fi)];
            elf.flushMovedNodeRelocs(ni, elf.computeNodeVAddr(ni), .{
                .first_symbol_reloc = dwarf_func.debug_line_first_symbol_reloc,
                .first_node_reloc = dwarf_func.debug_line_first_node_reloc,
                .skip_node_relocs = fi.get(&elf.dwarf).debug_info_ni,
            });
        },
        .decl_debug_info => |di| {
            const dwarf_decl = &elf.dwarf_decls.get(di).?;
            const target_section_offset = elf.computeNodeSectionOffset(ni);
            var target_ri = dwarf_decl.debug_info_first_target_reloc;
            while (target_ri != .none) {
                const target_reloc = target_ri.get(elf);
                assert(target_reloc.target == ni);
                target_reloc.flushMovedTarget(elf, target_section_offset);
                target_ri = target_reloc.next;
            }
            elf.flushMovedNodeRelocs(ni, elf.computeNodeVAddr(ni), .{
                .first_node_reloc = dwarf_decl.debug_info_first_node_reloc,
            });
        },
    }
    try ni.childrenMoved(elf.base.comp.gpa, &elf.mf);
}

/// Given the index of a `PT_LOAD`/`PT_NULL` segment, assumes that the phdr's `offset` and `filesz`
/// have been updated as needed by the caller, and updates the `@"align"`, `vaddr`, `paddr`, and
/// `memsz` fields of the segment, in order to place it at a valid virtual address.
///
/// TODO: this function is currently a source of non-determinism in the linker, because handling the
/// moving or resizing of a segment could reorder them and thereby affect how we handle *future*
/// changes to segments.
fn allocateSegmentLoadAddress(elf: *Elf, orig_phndx: u32) std.mem.Allocator.Error!void {
    const segment_ni = elf.phdrs.items[orig_phndx].unwrap().?;
    assert(elf.getNode(segment_ni).segment == orig_phndx);
    const page_align = elf.targetPageAlign();
    const node_align = segment_ni.alignment(&elf.mf);
    const ph_align = page_align.max(node_align);

    // If we determine that the segment's virtual address needs to move, then it's a good idea to
    // make it less likely that it needs to move *again* in the future, because it is expensive to
    // change a segment's load address (a lot of re-flushing is necessary). To do that, we reserve
    // more virtual address space than we need (multiplying the actual size by this value). That
    // way, there will usually be padding between segments which they can grow into.
    //
    // TODO: we might want to decrease this multiplier, or even omit it entirely, in cases where
    // virtual address space is constrained. For instance, 32-bit targets, or targets where short
    // PC-relative relocations between segments are common.
    const reserve_size_multiplier = 4;

    switch (elf.phdrSlice()) {
        inline else => |phdr| {
            const offset = elf.targetLoad(&phdr[orig_phndx].offset);
            const size = elf.targetLoad(&phdr[orig_phndx].filesz);

            if (size == 0) {
                assert(elf.targetLoad(&phdr[orig_phndx].type) == .NULL);
            } else {
                assert(elf.targetLoad(&phdr[orig_phndx].type) == .LOAD);
            }

            elf.targetStore(&phdr[orig_phndx].memsz, size);
            elf.targetStore(&phdr[orig_phndx].@"align", @intCast(ph_align.toByteUnits()));

            const orig_vaddr = elf.targetLoad(&phdr[orig_phndx].vaddr);
            assert(elf.targetLoad(&phdr[orig_phndx].paddr) == orig_vaddr);

            var vaddr: u64 = orig_vaddr;

            // First, we will shift the virtual address as needed in order to maintain the required
            // property that vaddr is congruent to offset modulo the phdr alignment.
            {
                // Compute the candidate address by undoing the current offset and then re-offsetting
                vaddr = std.mem.alignBackward(u64, vaddr, ph_align.toByteUnits()) + offset % ph_align.toByteUnits();
                // If `node_align` is greater than `page_align`, the address we just set might be in
                // the previous segment. The first page we "own" is the one in which the old vaddr
                // resides, so check against that.
                const first_good_vaddr = std.mem.alignBackward(u64, orig_vaddr, page_align.toByteUnits());
                if (vaddr < first_good_vaddr) {
                    // Yep, we crossed into the previous segment's pages, so correct for that by
                    // offsetting our address by another `ph_align`.
                    vaddr += ph_align.toByteUnits();
                    assert(vaddr >= first_good_vaddr);
                }
            }

            // If our size has changed, or if the address shift above caused our "end" address to
            // cross a page boundary, then we might be overlapping with the next segment's pages. In
            // that case, we will jump past that segment and give ourselves a new address after it.
            // We'll need to repeat this for every loadable phdr after us, until we're no longer
            // overlapping anything.
            var phndx = orig_phndx;
            for (phdr[orig_phndx + 1 ..], orig_phndx + 1..) |*next_ph, next_phndx| {
                switch (elf.targetLoad(&next_ph.type)) {
                    .NULL, .LOAD => {},
                    else => {
                        // All loadable segments have contiguous indices, so this indicates we have
                        // become the last loadable segment, meaning we definitely don't overlap any
                        // other loadable segment.
                        break;
                    },
                }

                const next_vaddr = elf.targetLoad(&next_ph.vaddr);
                // Find the first virtual address which the next phdr "owns" by aligning its vaddr
                // backwards to the start of the page.
                const next_page_vaddr = std.mem.alignBackward(u64, next_vaddr, page_align.toByteUnits());

                // Check if the segment fits here. We apply `reserve_size_multiplier`, but only if
                // the segment is already known to be moving---making it easier to grow in-place is
                // the whole point of the multiplier!
                {
                    const target_size = if (vaddr == orig_vaddr) size else size * reserve_size_multiplier;
                    if (vaddr + target_size <= next_page_vaddr) {
                        break; // hooray, we fit here!
                    }
                }

                const next_ni = elf.phdrs.items[next_phndx].unwrap().?;

                // This segment don't fit here, but before deciding how to proceed, we need to
                // consider any target-specific restrictions we are subject to.
                switch (elf.targetSegmentLoadAddressRestrictions()) {
                    .none => {},
                    .data_last => if (next_ni == elf.ni.data) {
                        // We can't leapfrog over the data segment. Instead, that segment just needs
                        // to be shifted forwards to make space for us, and we'll then `break` with
                        // our current vaddr.

                        if (next_phndx + 1 < phdr.len) switch (elf.targetLoad(&phdr[next_phndx + 1].type)) {
                            .NULL, .LOAD => unreachable, // data segment should be the last loadable segment
                            else => {},
                        };

                        const free_vaddr = vaddr + size * reserve_size_multiplier;

                        const next_align = page_align.max(next_ni.alignment(&elf.mf));
                        const next_offset = elf.targetLoad(&next_ph.offset);
                        const next_new_vaddr = next_align.forward(free_vaddr) + next_offset % next_align.toByteUnits();

                        // This logic for updating the data segment's vaddr is identical to how we
                        // will update the vaddr of `phndx` when we break from the loop.
                        elf.targetStore(&next_ph.vaddr, @intCast(next_new_vaddr));
                        elf.targetStore(&next_ph.paddr, @intCast(next_new_vaddr));
                        try next_ni.childrenMoved(elf.base.comp.gpa, &elf.mf);

                        break;
                    },
                }

                // We don't fit here, so shift ourselves forward (i.e. swap with `next_phndx`). But
                // first we need to adjust `vaddr` to come after it.
                const next_size = elf.targetLoad(&next_ph.memsz);
                // Instead of putting ourselves right after `next_ph`, we'll go a bit later in the
                // address space so that `next_ph` has address space to grow into (like above).
                vaddr = ph_align.forward(@intCast(next_vaddr + next_size * 4)) + offset % ph_align.toByteUnits();

                // Now just swap the phdrs and update our `phndx`.
                std.mem.swap(@TypeOf(next_ph.*), &phdr[phndx], next_ph);
                elf.phdrs.items[phndx] = .wrap(next_ni);
                elf.nodes.items(.data)[@backingInt(next_ni)] = .{ .segment = phndx };
                elf.phdrs.items[next_phndx] = .wrap(segment_ni);
                elf.nodes.items(.data)[@backingInt(segment_ni)] = .{ .segment = @intCast(next_phndx) };
                phndx = @intCast(next_phndx);
            }

            if (vaddr != orig_vaddr) {
                elf.targetStore(&phdr[phndx].vaddr, @intCast(vaddr));
                elf.targetStore(&phdr[phndx].paddr, @intCast(vaddr));
                try segment_ni.childrenMoved(elf.base.comp.gpa, &elf.mf);
            }
        },
    }
}

fn flushResized(elf: *Elf, ni: MappedFile.Node.Index) std.mem.Allocator.Error!void {
    const trace = tracy.trace(@src());
    defer trace.end();

    _, const size = ni.location(&elf.mf).resolve(&elf.mf);
    switch (elf.getNode(ni)) {
        .deleted => unreachable,
        .archive, .archive_header => {},
        .archive_input_member => unreachable,
        .archive_elf_member_header => unreachable,
        .elf => if (elf.archive) |*archive| {
            const member_ar_hdr: *std.elf.ar_hdr = @ptrCast(
                archive.elf_member_header_ni.slice(&elf.mf),
            );
            if (std.mem.print(&member_ar_hdr.ar_size, "{d}", .{size})) |size_str| {
                @memset(member_ar_hdr.ar_size[size_str.len..], ' ');
                archive.elf_member_too_big = false;
            } else |err| switch (err) {
                error.NoSpaceLeft => archive.elf_member_too_big = true,
            }
        },
        .ehdr => unreachable,
        .shdr => {},
        .segment => |phndx| switch (elf.phdrSlice()) {
            inline else => |phdr| {
                assert(elf.phdrs.items[phndx].unwrap().? == ni);
                const ph = &phdr[phndx];
                elf.targetStore(&ph.filesz, @intCast(size));
                switch (elf.targetLoad(&ph.type)) {
                    else => unreachable,
                    .NULL, .LOAD => {
                        elf.targetStore(&ph.type, if (size > 0) .LOAD else .NULL);
                        try elf.allocateSegmentLoadAddress(phndx);
                    },
                    .DYNAMIC, .INTERP, .PHDR, .GNU_EH_FRAME, .GNU_RELRO => {
                        elf.targetStore(&ph.memsz, @intCast(size));
                    },
                    .TLS => {
                        elf.targetStore(&ph.memsz, @intCast(size));
                        // TPOFF relocations care about the size of the TLS segment. Re-apply
                        // those, and also update any GOT entries from GOTTPOFF relocations.
                        for (elf.tls_size_symbol_relocs.keys()) |reloc| {
                            reloc.get(elf).apply(elf);
                        }
                        for (elf.got.keys(), 0..) |got_key, got_index| {
                            switch (got_key) {
                                .reserved,
                                .symbol,
                                .tlsld0,
                                .tlsld1,
                                .tlsgd0,
                                .tlsgd1,
                                => {
                                    @branchHint(.likely);
                                    continue;
                                },

                                .tpoff => elf.updateGotEntry(got_index),
                            }
                        }
                        try ni.childrenMoved(elf.base.comp.gpa, &elf.mf);
                    },
                }
            },
        },
        .section => |shndx| switch (elf.shdrPtr(shndx)) {
            inline else => |shdr| {
                switch (elf.targetLoad(&shdr.type)) {
                    else => unreachable,
                    .NULL => if (size > 0) elf.targetStore(&shdr.type, .PROGBITS),
                    .PROGBITS => if (size == 0) elf.targetStore(&shdr.type, .NULL),
                    .X86_64_UNWIND => {},
                }
                elf.targetStore(&shdr.size, @intCast(size));
            },
        },
        .section_manual_size,
        .input_section,
        .copied_global,
        .nav,
        .uav,
        .lazy_code,
        .lazy_const_data,
        .debug_shared,
        .debug_addr,
        .eh_frame_footer,
        .debug_str_offsets,
        .unit_padding,
        .unit_frame,
        .unit_frame_cie,
        .unit_debug_info,
        .unit_debug_info_header,
        .unit_debug_info_footer,
        .unit_debug_line,
        .unit_debug_line_header,
        .unit_debug_rnglists,
        .const_debug_info,
        .global_debug_info,
        .func_frame_fde,
        .func_debug_info,
        .func_debug_line,
        .decl_debug_info,
        => {},
    }
}

fn flushPadding(elf: *Elf, ni: MappedFile.Node.Index) std.mem.Allocator.Error!void {
    const trace = tracy.trace(@src());
    defer trace.end();

    const node = elf.getNode(ni);
    switch (node) {
        .deleted => unreachable,
        .archive,
        .archive_input_member,
        .archive_elf_member_header,
        .elf,
        .ehdr,
        .shdr,
        .segment,
        .section,
        .section_manual_size,
        .input_section,
        .copied_global,
        .nav,
        .uav,
        .lazy_code,
        .lazy_const_data,
        .debug_shared,
        .eh_frame_footer,
        .unit_debug_info_footer,
        => {},

        .archive_header => {
            const archive = &elf.archive.?;

            // Because we can't just throw padding bytes in the middle of an archive file, we need
            // the member name string table (the "//" member) to absorb all the padding bytes
            // between it (in the `.archive_header` node) and the first actual member.
            const next_member_ni = ni.next(&elf.mf).unwrap() orelse {
                // I guess there are no link inputs yet? But there will be eventually!
                return;
            };
            const next_member_offset: u64, _ = next_member_ni.location(&elf.mf).resolve(&elf.mf);
            const strtab_member_offset = std.elf.ARMAG.len + @sizeOf(std.elf.ar_hdr);
            assert(Alignment.@"2".check(next_member_offset));
            assert(Alignment.@"2".check(strtab_member_offset));
            const strtab_size = next_member_offset - strtab_member_offset;

            const member_ar_hdr: *std.elf.ar_hdr = @ptrCast(
                archive.header_ni.slice(&elf.mf)[std.elf.ARMAG.len..][0..@sizeOf(std.elf.ar_hdr)],
            );
            if (std.mem.print(&member_ar_hdr.ar_size, "{d}", .{strtab_size})) |size_str| {
                @memset(member_ar_hdr.ar_size[size_str.len..], ' ');
                archive.strtab_member_too_big = false;
            } else |err| switch (err) {
                error.NoSpaceLeft => archive.strtab_member_too_big = true,
            }
        },
        .debug_addr,
        .debug_str_offsets,
        .unit_padding,
        .unit_frame_cie,
        .unit_debug_info_header,
        .unit_debug_line_header,
        .unit_debug_rnglists,
        .const_debug_info,
        .global_debug_info,
        .func_frame_fde,
        .func_debug_info,
        .func_debug_line,
        .decl_debug_info,
        => {
            const offset, const size = location: {
                const offset, const size = ni.location(&elf.mf).resolve(&elf.mf);
                break :location .{ offset, switch (node) {
                    else => unreachable,
                    .debug_addr => elf.dwarf.debug_addr.size(&elf.dwarf),
                    .debug_str_offsets => elf.dwarf.debug_str_offsets.size(&elf.dwarf),
                    .unit_padding,
                    .unit_frame_cie,
                    .unit_debug_info_header,
                    .unit_debug_line_header,
                    .const_debug_info,
                    .global_debug_info,
                    .func_frame_fde,
                    .func_debug_info,
                    .func_debug_line,
                    .decl_debug_info,
                    => size,
                    .unit_debug_rnglists => |ui| Dwarf.Rnglists.size(&elf.dwarf, ui),
                } };
            };
            const parent_ni = ni.parent(&elf.mf).unwrap().?;
            const slice = slice: {
                if (ni.next(&elf.mf).unwrap()) |next_ni| switch (next_ni.position(&elf.mf)) {
                    .header => unreachable,
                    .footer => {},
                    .floating => {
                        const parent_slice = parent_ni.slicePadding(&elf.mf);
                        const next_offset, _ = next_ni.location(&elf.mf).resolve(&elf.mf);
                        break :slice parent_slice[@intCast(offset)..@intCast(next_offset)];
                    },
                };
                switch (node) {
                    else => unreachable,
                    .debug_addr, .debug_str_offsets, .unit_padding, .unit_debug_rnglists => {
                        const parent_slice = parent_ni.slicePadding(&elf.mf);
                        const frame_shndx = elf.getNodeShndx(parent_ni);
                        const frame_format = frame_shndx.debugFrameFormat(elf) orelse
                            break :slice parent_slice[@intCast(offset)..];
                        const footer_size = elf.debugFrameFooterSize(frame_format);
                        @memset(parent_slice[@intCast(offset + size)..][0..footer_size], 0);
                        frame_shndx.setSize(elf, offset + size + footer_size);
                        break :slice parent_slice[@intCast(offset)..][0..@intCast(size)];
                    },
                    .unit_frame_cie, .func_frame_fde => {
                        const parent_offset, _ = parent_ni.location(&elf.mf).resolve(&elf.mf);
                        const frame_ni = parent_ni.parent(&elf.mf).unwrap().?;
                        const frame_slice = frame_ni.slicePadding(&elf.mf);
                        const frame_shndx = elf.getNode(frame_ni).section_manual_size;
                        const frame_format = frame_shndx.debugFrameFormat(elf).?;
                        if (parent_ni.next(&elf.mf).unwrap()) |parent_next_ni| {
                            switch (parent_next_ni.position(&elf.mf)) {
                                .header => unreachable,
                                .footer => {},
                                .floating => {
                                    const parent_next_offset, _ =
                                        parent_next_ni.location(&elf.mf).resolve(&elf.mf);
                                    const slice = frame_slice[@intCast(
                                        parent_offset + offset,
                                    )..@intCast(parent_next_offset)];
                                    var fw: Io.Writer = .fixed(slice[@intCast(size)..]);
                                    elf.dwarf.genDebugFrameCie(
                                        &fw,
                                        null,
                                        frame_format,
                                    ) catch |err| switch (err) {
                                        error.WriteFailed => break :slice slice,
                                    };
                                    elf.dwarf.updateUnitLength(fw.buffer, fw.buffer.len);
                                    break :slice slice[0..@intCast(size)];
                                },
                            }
                        }
                        const footer_size = elf.debugFrameFooterSize(frame_format);
                        @memset(
                            frame_slice[@intCast(parent_offset + offset + size)..][0..footer_size],
                            0,
                        );
                        frame_shndx.setSize(elf, parent_offset + offset + size + footer_size);
                        break :slice frame_slice[@intCast(parent_offset + offset)..][0..@intCast(size)];
                    },
                    .unit_debug_info_header,
                    .unit_debug_line_header,
                    .const_debug_info,
                    .global_debug_info,
                    .func_debug_info,
                    .func_debug_line,
                    .decl_debug_info,
                    => {
                        const parent_offset, _ = parent_ni.location(&elf.mf).resolve(&elf.mf);
                        const debug_ni = parent_ni.parent(&elf.mf).unwrap().?;
                        const debug_slice = debug_ni.slicePadding(&elf.mf);
                        var fw: Io.Writer = .fixed(buffer: {
                            if (parent_ni.next(&elf.mf).unwrap()) |parent_next_ni| {
                                switch (parent_next_ni.position(&elf.mf)) {
                                    .header => unreachable,
                                    .footer => {},
                                    .floating => {
                                        const parent_next_offset, _ =
                                            parent_next_ni.location(&elf.mf).resolve(&elf.mf);
                                        break :buffer debug_slice[@intCast(
                                            parent_offset,
                                        )..@intCast(parent_next_offset)];
                                    },
                                }
                            }
                            break :buffer debug_slice[@intCast(parent_offset)..];
                        });
                        fw.end = @intCast(offset + size);
                        switch (node) {
                            else => unreachable,
                            .unit_debug_info_header,
                            .const_debug_info,
                            .global_debug_info,
                            .func_debug_info,
                            .decl_debug_info,
                            => for (0..2) |_| fw.writeUleb128(@backingInt(Dwarf.AbbrevCode.null)) catch
                                unreachable,
                            .unit_debug_line_header, .func_debug_line => {},
                        }
                        const unit_padding_offset = fw.end;
                        const unit_padding = fw.unusedCapacitySlice();
                        elf.dwarf.genUnitPadding(&fw) catch |err| switch (err) {
                            error.WriteFailed => {
                                fw.end = unit_padding_offset;
                                elf.dwarf.updateUnitLength(fw.buffer, fw.buffer.len);
                                switch (node) {
                                    else => unreachable,
                                    .unit_debug_info_header,
                                    .const_debug_info,
                                    .global_debug_info,
                                    .func_debug_info,
                                    .decl_debug_info,
                                    => {
                                        comptime assert(
                                            Dwarf.uleb128Size(@backingInt(Dwarf.AbbrevCode.null)) == 1,
                                        );
                                        @memset(
                                            fw.unusedCapacitySlice(),
                                            @backingInt(Dwarf.AbbrevCode.null),
                                        );
                                    },
                                    .unit_debug_line_header,
                                    .func_debug_line,
                                    => Dwarf.genDebugLinePadding(&fw, fw.unusedCapacityLen()) catch
                                        unreachable,
                                }
                                return;
                            },
                        };
                        elf.dwarf.updateUnitLength(fw.buffer, unit_padding_offset);
                        elf.dwarf.updateUnitLength(unit_padding, unit_padding.len);
                        return;
                    },
                }
            };
            var fw: Io.Writer = .fixed(slice[@intCast(size)..]);
            switch (node) {
                else => unreachable,
                .debug_addr, .debug_str_offsets, .unit_debug_rnglists => {
                    elf.dwarf.genUnitPadding(&fw) catch |err| switch (err) {
                        error.WriteFailed => {
                            elf.dwarf.updateUnitLength(slice, slice.len);
                            @memset(fw.buffer, switch (node) {
                                else => unreachable,
                                .debug_addr, .debug_str_offsets => std.math.maxInt(u8),
                                .unit_debug_rnglists => std.dwarf.RLE.end_of_list,
                            });
                            return;
                        },
                    };
                    elf.dwarf.updateUnitLength(slice, size);
                    elf.dwarf.updateUnitLength(fw.buffer, fw.buffer.len);
                },
                .unit_padding => elf.dwarf.updateUnitLength(slice, slice.len),
                .unit_frame_cie, .func_frame_fde => {
                    elf.dwarf.updateUnitLength(slice, slice.len);
                    @memset(fw.buffer, std.dwarf.CFA.nop);
                },
                .unit_debug_info_header,
                .const_debug_info,
                .global_debug_info,
                .func_debug_info,
                .decl_debug_info,
                => elf.dwarf.genDebugInfoPadding(&fw, fw.buffer.len) catch unreachable,
                .unit_debug_line_header,
                .func_debug_line,
                => Dwarf.genDebugLinePadding(&fw, fw.buffer.len) catch unreachable,
            }
        },
        .unit_frame, .unit_debug_info, .unit_debug_line => {
            var last_ni = ni.last(&elf.mf).unwrap() orelse return;
            while (last_ni.position(&elf.mf) == .footer)
                last_ni = last_ni.prev(&elf.mf).unwrap() orelse return;
            try last_ni.nextMoved(elf.base.comp.gpa, &elf.mf);
        },
    }
}

/// If `gsi` does not have a PLT entry, adds one and returns `true`. If `gsi` already has a PLT
/// entry, does nothing and returns `false`.
///
/// Asserts that `gsi` owns a `.dynsym` entry.
fn ensurePltEntry(elf: *Elf, gsi: Symbol.Global.Index) Error!bool {
    const target_endian = elf.targetEndian();

    const maybe_dead_plt_index: ?u32 = dead_plt_index: {
        const old_plt_index = elf.plt.getIndex(gsi) orelse break :dead_plt_index null;
        if (!elf.pltEntryIsDead(old_plt_index)) return false;
        break :dead_plt_index @intCast(old_plt_index);
    };

    const plt = elf.targetPltInfo();

    const gpa = elf.base.comp.gpa;
    try elf.shndx.rela_plt.relaEnsureAdditionalCapacity(elf, 1);
    try elf.plt.ensureUnusedCapacity(gpa, 1);
    try elf.shndx.plt.get(elf).ni.ensureMinimumSize(
        gpa,
        &elf.mf,
        plt.entry_size * (plt.header_entries + elf.plt.count() + 1),
    );
    if (plt.got_plt) |got_plt| try elf.shndx.got_plt.get(elf).ni.ensureMinimumSize(
        gpa,
        &elf.mf,
        elf.targetPtrSize() * (got_plt.header_entries + elf.plt.count() + 1),
    );
    if (plt.plt_sec) |plt_sec| try elf.shndx.plt_sec.get(elf).ni.ensureMinimumSize(
        gpa,
        &elf.mf,
        plt_sec.entry_size * (elf.plt.count() + 1),
    );

    // We use the existing free-list tracking of the `.rela.plt` section to also behave as a
    // free-list for the PLT itself---see `pltEntryIsDead` for details.
    const plt_index: u32 = @backingInt(elf.shndx.rela_plt.relaAddOneAssumeCapacity(elf, .{
        .type = .jumpSlot(elf),
        .offset = 0, // populated later
        .raw_sym_index = gsi.ownedDynsymIndex(elf).?,
        .addend = 0,
    }));

    // On architectures without `.got.plt` (e.g. SPARC) these values actually refer to `.plt`.
    const got_plt_section: Section.Index, const got_plt_offset: u64 = if (plt.got_plt) |got_plt| .{
        elf.shndx.got_plt,
        elf.targetPtrSize() * (got_plt.header_entries + plt_index),
    } else .{
        elf.shndx.plt,
        plt.entry_size * (plt.header_entries + plt_index),
    };

    // Now that we know the index, we can set the relocation's offset.
    elf.shndx.rela_plt.relaSetOffset(elf, @fromBackingInt(plt_index), got_plt_section.vaddr(elf) + got_plt_offset);

    if (plt_index < elf.plt.count()) {
        // We reused a free entry, so we're already done! However, if this global has a dead PLT
        // entry, then `gsi` is already a key in `elf.plt`, so we'll need to swap it out for some
        // other dead key.
        if (maybe_dead_plt_index) |dead_plt_index| {
            const dead_gsi = elf.plt.keys()[plt_index];
            elf.plt.setKey(dead_plt_index, dead_gsi);
        }
        elf.plt.setKey(plt_index, gsi);
        return true;
    }

    assert(maybe_dead_plt_index == null); // if there were a dead entry we would have reused it

    // We added a new entry, so we now need to extend the PLT sections.
    assert(plt_index == elf.plt.count());
    elf.plt.putAssumeCapacityNoClobber(gsi, {});

    switch (elf.ehdrMachine()) {
        .AARCH64, .PPC64, .RISCV => |machine| @panic(@tagName(machine)),
        .X86_64 => {
            const plt_ni = elf.shndx.plt.get(elf).ni;
            const plt_addr = plt_addr: switch (elf.shdrPtr(elf.shndx.plt)) {
                inline else => |shdr| {
                    const old_size = 16 * (1 + plt_index);
                    assert(elf.targetLoad(&shdr.size) == old_size);
                    elf.targetStore(&shdr.size, old_size + 16);
                    const plt_slice = plt_ni.slice(&elf.mf)[old_size..][0..16];
                    @memcpy(plt_slice, &[16]u8{
                        0xf3, 0x0f, 0x1e, 0xfa, // endbr64
                        0x68, 0x00, 0x00, 0x00, 0x00, // push $0x0
                        0xe9, 0x00, 0x00, 0x00, 0x00, // jmp 0
                        0x66, 0x90, // xchg %ax,%ax
                    });
                    std.mem.writeInt(u32, plt_slice[5..][0..4], plt_index, target_endian);
                    std.mem.writeInt(
                        i32,
                        plt_slice[10..][0..4],
                        -@as(i32, @intCast(old_size + 14)),
                        target_endian,
                    );
                    break :plt_addr elf.targetLoad(&shdr.addr) + old_size;
                },
            };

            const got_plt_ni = elf.shndx.got_plt.get(elf).ni;
            switch (elf.shdrPtr(elf.shndx.got_plt)) {
                inline else => |shdr, class| {
                    assert(elf.targetLoad(&shdr.size) == got_plt_offset);
                    elf.targetStore(&shdr.size, @intCast(got_plt_offset + @sizeOf(class.ElfN().Addr)));
                    std.mem.writeInt(
                        class.ElfN().Addr,
                        got_plt_ni.slice(&elf.mf)[@intCast(got_plt_offset)..][0..@sizeOf(class.ElfN().Addr)],
                        @intCast(plt_addr),
                        target_endian,
                    );
                },
            }

            const plt_sec_ni = elf.shndx.plt_sec.get(elf).ni;
            switch (elf.shdrPtr(elf.shndx.plt_sec)) {
                inline else => |shdr| {
                    const old_size = 16 * plt_index;
                    elf.targetStore(&shdr.size, old_size + 16);
                    const plt_sec_slice = plt_sec_ni.slice(&elf.mf)[old_size..][0..16];
                    @memcpy(plt_sec_slice, &[16]u8{
                        0xf3, 0x0f, 0x1e, 0xfa, // endbr64
                        0xff, 0x25, 0x00, 0x00, 0x00, 0x00, // jmp *0x0(%rip)
                        0x66, 0x0f, 0x1f, 0x44, 0x00, 0x00, // nopw 0x0(%rax,%rax,1)
                    });
                    std.mem.writeInt(
                        i32,
                        plt_sec_slice[6..][0..4],
                        @intCast(@as(i64, @bitCast(
                            (got_plt_section.vaddr(elf) + got_plt_offset) -% (elf.targetLoad(&shdr.addr) + old_size + 10),
                        ))),
                        target_endian,
                    );
                },
            }
        },
        .LOONGARCH => {
            // add a .PLT entry, writing the template
            const plt_ni = elf.shndx.plt.get(elf).ni;
            const plt_addr, const plt_slice = plt_entry: switch (elf.shdrPtr(elf.shndx.plt)) {
                inline else => |shdr| {
                    const old_size = 16 * (1 + plt_index);
                    assert(elf.targetLoad(&shdr.size) == old_size);
                    elf.targetStore(&shdr.size, old_size + 16);
                    const plt_slice = plt_ni.slice(&elf.mf)[old_size..][0..16];
                    @memcpy(plt_slice, source: switch (elf.identClass()) {
                        .NONE, _ => unreachable,
                        inline .@"32", .@"64" => |elf_class| {
                            const ld_byte = if (elf_class == .@"64") 0xc0 else 0x80;
                            break :source &[16]u8{
                                0x1a, 0x00, 0x00, 0x0f, //    pcalau12i $t3, %pc_hi20(func@.got.plt)
                                0x28, ld_byte, 0x01, 0xef, // ld.w/d    $t3, $t3, %lo12(func@.got.plt)
                                0x4c, 0x00, 0x01, 0xed, //    jirl      $t1, $t3, 0
                                0x00, 0x2a, 0x00, 0x00, //    break
                            };
                        },
                    });
                    break :plt_entry .{ elf.targetLoad(&shdr.addr) + old_size, plt_slice };
                },
            };

            // add a .GOT.PLT entry, writing the address of the corresponding .PLT entry
            const got_plt_ni = elf.shndx.got_plt.get(elf).ni;
            switch (elf.shdrPtr(elf.shndx.got_plt)) {
                inline else => |shdr, class| {
                    assert(elf.targetLoad(&shdr.size) == got_plt_offset);
                    elf.targetStore(&shdr.size, @intCast(got_plt_offset + @sizeOf(class.ElfN().Addr)));
                    std.mem.writeInt(
                        class.ElfN().Addr,
                        got_plt_ni.slice(&elf.mf)[@intCast(got_plt_offset)..][0..@sizeOf(class.ElfN().Addr)],
                        @intCast(plt_addr),
                        target_endian,
                    );
                },
            }

            // relocate the PLT entry to point to the .GOT.PLT entry
            const got_plt_abs = got_plt_section.vaddr(elf) + got_plt_offset;
            // TODO: handle overflow gracefully
            const inst0: *align(1) link.loongarch.J20 = @ptrCast(plt_slice[0..4]);
            const inst1: *align(1) link.loongarch.K12 = @ptrCast(plt_slice[4..8]);
            elf.targetStore(inst0, .{
                .b0_4 = elf.targetLoad(inst0).b0_4,
                .j20 = link.loongarch.pcalaHi20(got_plt_abs, plt_addr),
                .b25_31 = elf.targetLoad(inst0).b25_31,
            });
            elf.targetStore(inst1, .{
                .b0_9 = elf.targetLoad(inst1).b0_9,
                .k12 = @truncate(got_plt_abs),
                .b22_31 = elf.targetLoad(inst1).b22_31,
            });
        },
        .SPARCV9 => {
            // add a .PLT entry, writing the template
            const plt_ni = elf.shndx.plt.get(elf).ni;
            switch (elf.shdrPtr(elf.shndx.plt)) {
                inline else => |shdr| {
                    assert(elf.targetLoad(&shdr.size) == got_plt_offset);
                    elf.targetStore(&shdr.size, @intCast(got_plt_offset + 32));
                    const Inst = packed union(u32) {
                        raw: u32,
                        imm22: packed struct { imm: u22, op: u10 },
                        disp19: packed struct { disp: u19, op: u13 },
                    };
                    const plt_slice: []Inst = @ptrCast(@alignCast(plt_ni.slice(&elf.mf)[@intCast(got_plt_offset)..][0..32]));
                    @memcpy(plt_slice, &[8]Inst{
                        // sethi (. - .plt[0]), %g1
                        .{ .imm22 = .{ .imm = @truncate(got_plt_offset), .op = 0b0000001100 } },
                        // ba,a %xcc, .plt[1]
                        .{ .disp19 = .{ .disp = @truncate((got_plt_offset + 4 - 32) >> 2), .op = 0b0011000001101 } },
                        // nop
                        .{ .raw = 0x0100_0000 },
                        // nop
                        .{ .raw = 0x0100_0000 },
                        // nop
                        .{ .raw = 0x0100_0000 },
                        // nop
                        .{ .raw = 0x0100_0000 },
                        // nop
                        .{ .raw = 0x0100_0000 },
                        // nop
                        .{ .raw = 0x0100_0000 },
                    });
                    if (elf.targetEndian() != std.lang.Endian.native) {
                        std.mem.byteSwapAllElements(Inst, plt_slice);
                    }
                },
            }
        },
    }

    return true;
}
fn flushMovedPltSection(elf: *Elf, which: enum { plt, plt_sec, got_plt }, old_addr: u64, addr: u64) void {
    const target_endian = elf.targetEndian();
    switch (elf.ehdrMachine()) {
        .AARCH64, .PPC64, .RISCV => |machine| @panic(@tagName(machine)),
        .X86_64 => {
            switch (which) {
                .plt => return,
                .plt_sec => {
                    // Re-apply all PLT relocations. If a symbol is in the PLT then the majority of
                    // its relocations are probably going through the PLT, so we don't bother with
                    // specific tracking for PLT relocations---instead just re-apply all relocations
                    // targeting symbols with PLT entries.
                    for (elf.plt.keys()) |gsi| {
                        Symbol.Id.global(gsi).applyTargetRelocs(elf);
                    }
                    // We also need to update all of the references from `.plt.sec` to `.got.plt`.
                    // However, if there's also a flush pending for `.got.plt`, don't bother doing
                    // this now, because we'll do it when `.got.plt` is flushed anyway.
                    if (elf.shndx.got_plt.get(elf).ni.hasMoved(&elf.mf)) {
                        return;
                    }
                    // Exit this `switch` to update those references.
                },
                .got_plt => {
                    // Update the offsets of the relocation entries in `.rela.plt`.
                    const rela_plt_shndx = elf.shndx.rela_plt;
                    for (0..elf.plt.count()) |plt_index| {
                        if (elf.pltEntryIsDead(plt_index)) continue;
                        rela_plt_shndx.relaAdjustOffset(elf, @fromBackingInt(@intCast(plt_index)), old_addr, addr);
                    }
                    // We also need to update all of the references from `.plt.sec` to `.got.plt`.
                    // However, if there's also a flush pending for `.plt.sec`, don't bother doing
                    // this now, because we'll do it when `.plt.sec` is flushed anyway.
                    if (elf.shndx.plt_sec.get(elf).ni.hasMoved(&elf.mf)) {
                        return;
                    }
                    // Exit this `switch` to update those references.
                },
            }
            // We are updating the references from `.plt.sec` to `.got.plt`.
            const got_plt_addr = elf.shndx.got_plt.vaddr(elf);
            const plt_sec_addr = elf.shndx.plt_sec.vaddr(elf);
            const plt_sec_slice = elf.shndx.plt_sec.get(elf).ni.slice(&elf.mf);
            switch (elf.identClass()) {
                .NONE, _ => unreachable,
                inline else => |class| {
                    const Addr = class.ElfN().Addr;
                    for (0..elf.plt.count()) |plt_index| {
                        const plt_sec_offset = 16 * plt_index;
                        const got_plt_offset = @sizeOf(Addr) * (3 + plt_index);
                        std.mem.writeInt(
                            i32,
                            plt_sec_slice[plt_sec_offset + 6 ..][0..4],
                            @intCast(@as(i64, @bitCast(
                                (got_plt_addr + got_plt_offset) -% (plt_sec_addr + plt_sec_offset + 10),
                            ))),
                            target_endian,
                        );
                    }
                },
            }
        },
        .LOONGARCH => {
            switch (which) {
                .plt => {
                    // Re-apply all PLT relocations. If a symbol is in the PLT then the majority of
                    // its relocations are probably going through the PLT, so we don't bother with
                    // specific tracking for PLT relocations---instead just re-apply all relocations
                    // targeting symbols with PLT entries.
                    for (elf.plt.keys()) |gsi| {
                        Symbol.Id.global(gsi).applyTargetRelocs(elf);
                    }
                    // We also need to update all of the references from `.plt` to `.got.plt`.
                    // However, if there's also a flush pending for `.got.plt`, don't bother doing
                    // this now, because we'll do it when `.got.plt` is flushed anyway.
                    if (elf.shndx.got_plt.get(elf).ni.hasMoved(&elf.mf)) {
                        return;
                    }
                    // Exit this `switch` to update those references.
                },
                .plt_sec => unreachable,
                .got_plt => {
                    // Update the offsets of the relocation entries in `.rela.plt`.
                    const rela_plt_shndx = elf.shndx.rela_plt;
                    for (0..elf.plt.count()) |plt_index| {
                        if (elf.pltEntryIsDead(plt_index)) continue;
                        rela_plt_shndx.relaAdjustOffset(elf, @fromBackingInt(@intCast(plt_index)), old_addr, addr);
                    }
                    // We also need to update all of the references from `.plt` to `.got.plt`.
                    // However, if there's also a flush pending for `.plt`, don't bother doing
                    // this now, because we'll do it when `.plt` is flushed anyway.
                    if (elf.shndx.plt.get(elf).ni.hasMoved(&elf.mf)) {
                        return;
                    }
                    // Exit this `switch` to update those references.
                },
            }
            // We are updating the references from `.plt` to `.got.plt`.
            const got_plt_addr = elf.shndx.got_plt.vaddr(elf);
            const plt_addr = elf.shndx.plt.vaddr(elf);
            const plt_slice = elf.shndx.plt.get(elf).ni.slice(&elf.mf);
            switch (elf.identClass()) {
                .NONE, _ => unreachable,
                inline else => |class| {
                    const Addr = class.ElfN().Addr;
                    for (0..elf.plt.count()) |plt_index| {
                        const plt_offset = 16 * plt_index;
                        const got_plt_offset = @sizeOf(Addr) * (2 + plt_index);
                        const target_slice = plt_slice[plt_offset..];

                        const got_plt_abs: u64 = got_plt_addr + got_plt_offset;
                        // TODO: handle overflow gracefully
                        const inst0: *align(1) link.loongarch.J20 = @ptrCast(target_slice[0..4]);
                        const inst1: *align(1) link.loongarch.K12 = @ptrCast(target_slice[4..8]);

                        elf.targetStore(inst0, .{
                            .b0_4 = elf.targetLoad(inst0).b0_4,
                            .j20 = link.loongarch.pcalaHi20(got_plt_abs, plt_addr + plt_offset),
                            .b25_31 = elf.targetLoad(inst0).b25_31,
                        });

                        elf.targetStore(inst1, .{
                            .b0_9 = elf.targetLoad(inst1).b0_9,
                            .k12 = @truncate(got_plt_abs),
                            .b22_31 = elf.targetLoad(inst1).b22_31,
                        });
                    }
                },
            }
        },
        .SPARCV9 => switch (which) {
            .plt => {
                // Re-apply all PLT relocations. If a symbol is in the PLT then the majority of
                // its relocations are probably going through the PLT, so we don't bother with
                // specific tracking for PLT relocations---instead just re-apply all relocations
                // targeting symbols with PLT entries.
                for (elf.plt.keys()) |gsi| {
                    Symbol.Id.global(gsi).applyTargetRelocs(elf);
                }
                // Update the offsets of the relocation entries in `.rela.plt`.
                const rela_plt_shndx = elf.shndx.rela_plt;
                for (0..elf.plt.count()) |plt_index| {
                    if (elf.pltEntryIsDead(plt_index)) continue;
                    rela_plt_shndx.relaAdjustOffset(elf, @fromBackingInt(@intCast(plt_index)), old_addr, addr);
                }
            },
            .plt_sec, .got_plt => unreachable,
        },
    }
}

pub fn updateExports(
    elf: *Elf,
    pt: Zcu.PerThread,
    export_indices: []const Zcu.Export.Index,
) link.Error!void {
    for (export_indices) |export_index| {
        elf.updateExportInner(export_index) catch |err| switch (err) {
            else => |e| return e,
            error.MappedFileIo => return elf.base.comp.link_diags.fail("failed to write output file: {t}", .{elf.mf.io_err.?}),
        };
    }
    try elf.genPending(pt);
}
fn updateExportInner(elf: *Elf, export_index: Zcu.Export.Index) Error!void {
    const zcu = elf.base.comp.zcu.?;
    const ip = &zcu.intern_pool;

    const @"export" = export_index.ptr(zcu);

    switch (@"export".exported) {
        .nav => |nav| log.debug("updateExports({f})", .{ip.getNav(nav).fqn.fmt(ip)}),
        .uav => |uav| log.debug("updateExports(@as({f}, {f}))", .{
            Type.fromInterned(ip.typeOf(uav)).fmt(zcu),
            Value.fromInterned(uav).fmtValue(zcu),
        }),
    }
    const exported_lsi: Symbol.LocalIndex = switch (@"export".exported) {
        .nav => |nav| (try elf.navMapIndex(zcu, nav)).symbol(elf),
        .uav => |uav| (try elf.uavMapIndex(uav, .none)).symbol(elf),
    };

    // Initialize the global symbol with the same values that the local one currently has. If the
    // NAV/UAV is updated, then `updateNavInner` or `genUav` will update the global symbol sizes,
    // and `flushMoved` will update their values.
    const cur_value: u64, const cur_size: u64, const @"type": std.elf.STT, const shndx: Section.Index = switch (elf.symPtr(exported_lsi.index())) {
        inline else => |exported_sym| .{
            elf.targetLoad(&exported_sym.value),
            elf.targetLoad(&exported_sym.size),
            elf.targetLoad(&exported_sym.info).type,
            .fromSection(elf.targetLoad(&exported_sym.shndx)),
        },
    };

    const name = @"export".opts.name.toSlice(ip);
    _ = elf.addGlobalSymbol(.{
        .node = exported_lsi.index().ptr(elf).node,
        .name = name,
        .value = cur_value,
        .size = cur_size,
        .type = @"type",
        .bind = switch (@"export".opts.linkage) {
            .strong => .strong,
            .weak => .weak,
        },
        .visibility = switch (@"export".opts.visibility) {
            .default => .DEFAULT,
            .hidden => .HIDDEN,
            .protected => .PROTECTED,
        },
        .shndx = shndx,
    }) catch |err| switch (err) {
        error.MultipleDefinitions => {
            // HACK: because we currently don't/can't delete these exports, we would typically
            // get these errors on every non-initial incremental update. Hack around that by
            // only emitting this error if the symbol we're conflicting with comes from an input
            // section (as opposed to the ZCU).
            const parsed = parseVersionedSymbolName(name);
            const conflicting_global = elf.globalByName(.{
                .name = parsed.name,
                .version = parsed.version,
            }).?;
            if (conflicting_global.ptr(elf).symtab_index.ptr(elf).node.unwrap()) |conflicting_node| {
                if (elf.getNode(conflicting_node) == .input_section) {
                    return elf.base.comp.link_diags.fail(
                        "multiple definitions of '{s}'",
                        .{name},
                    );
                }
            }
        },
        error.MultipleDefaultVersions => return elf.base.comp.link_diags.fail(
            "multiple default versions of '{s}'",
            .{parseVersionedSymbolName(name).name},
        ),
        error.UndefinedDefaultVersion => unreachable, // this is an export, so the symbol is defined
        else => |e| return e,
    };
}

fn dumpStderr(elf: *Elf) Io.File.Writer.Error!void {
    const comp = elf.base.comp;
    const io = comp.io;
    var buffer: [512]u8 = undefined;
    const stderr = try io.lockStderr(&buffer, null);
    defer io.unlockStderr();
    const w = &stderr.file_writer.interface;
    _ = elf.dump(w) catch |err| switch (err) {
        error.WriteFailed => return stderr.file_writer.err.?,
    };
}

pub fn dump(elf: *Elf, w: *Io.Writer) Io.Writer.Error!link.File.DumpResult {
    if (elf.options.enable_link_snapshots) {
        try elf.printNode(w, .root, 0);
        return .enabled;
    }
    return .disabled;
}

pub fn printNode(
    elf: *Elf,
    w: *Io.Writer,
    ni: MappedFile.Node.Index,
    indent: usize,
) Io.Writer.Error!void {
    const node = elf.getNode(ni);
    try w.splatByteAll(' ', indent);
    try w.writeAll(@tagName(node));
    switch (node) {
        else => {},
        .segment => |phndx| switch (elf.phdrSlice()) {
            inline else => |phdr| {
                const ph = &phdr[phndx];
                try w.writeByte('(');
                const pt = elf.targetLoad(&ph.type);
                if (std.enums.tagName(std.elf.PT, pt)) |pt_name|
                    try w.writeAll(pt_name)
                else inline for (@typeInfo(std.elf.PT).@"enum".decl_names) |decl_name| {
                    const decl_val = @field(std.elf.PT, decl_name);
                    if (@TypeOf(decl_val) != std.elf.PT) continue;
                    if (pt == @field(std.elf.PT, decl_name)) break try w.writeAll(decl_name);
                } else try w.print("0x{x}", .{pt});
                try w.writeAll(", ");
                const pf = elf.targetLoad(&ph.flags);
                if (pf.R) try w.writeByte('R');
                if (pf.W) try w.writeByte('W');
                if (pf.X) try w.writeByte('X');
                try w.writeByte(')');
            },
        },
        .section, .section_manual_size => |shndx| try w.print("({s})", .{shndx.name(elf).slice(elf)}),
        .input_section => |isi| {
            const ii = isi.input(elf);
            try w.print("({f}{f}, {s})", .{
                ii.path(elf).fmtEscapeString(),
                fmtMemberString(ii.member(elf)),
                elf.getNodeShndx(isi.node(elf)).name(elf).slice(elf),
            });
        },
        .copied_global => |gsi| try w.print("(copy:{s})", .{gsi.rawName(elf).slice(elf)}),
        .nav => |nmi| {
            const zcu = elf.base.comp.zcu.?;
            const ip = &zcu.intern_pool;
            const nav = ip.getNav(nmi.nav(elf));
            try w.print("({f}, {f})", .{
                Type.fromInterned(nav.resolved.?.type).fmt(zcu),
                nav.fqn.fmt(ip),
            });
        },
        .uav => |umi| {
            const zcu = elf.base.comp.zcu.?;
            const val: Value = .fromInterned(umi.uavValue(elf));
            try w.print("({f}, {f})", .{
                val.typeOf(zcu).fmt(zcu),
                val.fmtValue(zcu),
            });
        },
        inline .lazy_code, .lazy_const_data => |lmi| try w.print("({f})", .{
            Type.fromInterned(lmi.lazySymbol(elf).ty).fmt(elf.base.comp.zcu.?),
        }),
        .debug_shared => |ss| try w.print("({})", .{ss}),
        .unit_frame,
        .unit_frame_cie,
        .unit_debug_info,
        .unit_debug_info_header,
        .unit_debug_info_footer,
        .unit_debug_line,
        .unit_debug_line_header,
        .unit_debug_rnglists,
        => |ui| try w.print("({s})", .{ui.mod(&elf.dwarf).fully_qualified_name}),
        .const_debug_info => |cpi| switch (cpi.val(&elf.dwarf.const_pool)) {
            .generic_poison_type => try w.writeAll("(anytype)"),
            else => |val| try w.print("({f})", .{
                Value.fromInterned(val).fmtValue(elf.base.comp.zcu.?),
            }),
        },
        .global_debug_info => |gi| {
            const zcu = elf.base.comp.zcu.?;
            const ip = &zcu.intern_pool;
            const nav = ip.getNav(gi.nav(&elf.dwarf));
            try w.writeByte('(');
            if (nav.resolved) |resolved| try w.print("{f}, ", .{
                Type.fromInterned(resolved.type).fmt(zcu),
            });
            try w.print("{f}", .{nav.fqn.fmt(ip)});
            if (nav.resolved) |resolved| try w.print(", {f}", .{
                Value.fromInterned(resolved.value).fmtValue(zcu),
            });
            try w.writeByte(')');
        },
        .func_frame_fde, .func_debug_info, .func_debug_line => |fi| {
            const zcu = elf.base.comp.zcu.?;
            const ip = &zcu.intern_pool;
            const nav = ip.getNav(fi.nav(&elf.dwarf));
            try w.writeByte('(');
            if (nav.resolved) |resolved| try w.print("{f}, ", .{
                Type.fromInterned(resolved.type).fmt(zcu),
            });
            try w.print("{f})", .{nav.fqn.fmt(ip)});
        },
        .decl_debug_info => |di| {
            const comp = elf.base.comp;
            const zcu = comp.zcu.?;
            const ip = &zcu.intern_pool;
            const src_inst = di.srcInst(&elf.dwarf);
            try w.print("({f}, ", .{zcu.fileByIndex(src_inst.resolveFile(ip)).path.fmt(comp)});
            if (src_inst.resolve(ip)) |inst| try w.print("%{d}", .{inst}) else try w.writeAll("lost");
            try w.writeByte(')');
        },
    }
    {
        const mf_node = &elf.mf.nodes.items[@backingInt(ni)];
        const off, const size = mf_node.location().resolve(&elf.mf);
        try w.print(" index={d} offset=0x{x} size=0x{x} align=0x{x} {t}{s}{s}{s}{s}{s}{s}\n", .{
            @backingInt(ni),
            off,
            size,
            mf_node.flags.alignment.toByteUnits(),
            mf_node.flags.position,
            if (mf_node.flags.bubbles_moved) " bubbles_moved" else "",
            if (mf_node.flags.moved) " moved" else "",
            if (mf_node.flags.resized) " resized" else "",
            if (mf_node.flags.enable_next_moved) " enable_next_moved" else "",
            if (mf_node.flags.next_moved) " next_moved" else "",
            if (mf_node.flags.has_content) " has_content" else "",
        });
    }
    if (ni.first(&elf.mf).unwrap()) |first_ni| {
        // non-leaf, just print children
        var child_ni = first_ni;
        while (true) {
            try elf.printNode(w, child_ni, indent + 1);
            child_ni = child_ni.next(&elf.mf).unwrap() orelse break;
        }
        return;
    }
    const start_address: usize, const end_address: usize = file_loc: {
        const file_loc = ni.fileLocation(&elf.mf, false);
        break :file_loc .{ @intCast(file_loc.offset), @intCast(file_loc.offset + file_loc.size) };
    };
    var address = start_address;
    const line_len = 0x10;
    while (true) : (address = @min(std.mem.alignForward(usize, address + 1, line_len), end_address)) {
        try w.splatByteAll(' ', indent + 1);
        try w.print("{x:0>8}", .{address});
        if (address == end_address) break try w.writeByte('\n');
        try w.splatByteAll(' ', 2);
        const start_byte_address = std.mem.alignBackward(usize, address, line_len);
        const end_byte_address = start_byte_address + line_len;
        for (start_byte_address..end_byte_address) |byte_address|
            if (byte_address < start_address or byte_address >= end_address)
                try w.splatByteAll(' ', 3)
            else
                try w.print("{x:0>2} ", .{elf.mf.memory_map.memory[byte_address]});
        try w.writeByte(' ');
        for (start_byte_address..@min(end_address, end_byte_address)) |byte_address|
            try w.writeByte(if (byte_address < start_address or byte_address >= end_address) ' ' else char: {
                const byte = elf.mf.memory_map.memory[byte_address];
                break :char if (std.ascii.isPrint(byte)) byte else '.';
            });
        try w.writeByte('\n');
    }
}

fn ensureSegmentAligned(elf: *Elf, start_phndx: u32, min_align: Alignment) Error!void {
    const gpa = elf.base.comp.gpa;
    // We need to loop through parent nodes because segments may be nested (e.g. a PT_TLS segment
    // inside a PT_LOAD segment).
    var phndx = start_phndx;
    while (true) {
        // Align the actual node
        const seg_ni = elf.phdrs.items[phndx].unwrap().?;
        if (min_align.compare(.gt, seg_ni.alignment(&elf.mf))) {
            try seg_ni.realign(gpa, &elf.mf, min_align);
        }
        // Update the phdr `@"align"` field if necessary
        switch (elf.phdrSlice()) {
            inline else => |phdr| switch (elf.targetLoad(&phdr[phndx].type)) {
                .NULL, .LOAD => {
                    // The `@"align"` field is managed by `allocateSegmentLoadAddress`.
                    //
                    // It's very likely that the node was moved and/or resized when we realigned it
                    // just above, but it is possible that it was not moved *but* still has an
                    // unaligned virtual address. In that case, we need to ensure the segment's
                    // virtual address range will be recomputed.
                    if (!min_align.check(@intCast(elf.targetLoad(&phdr[phndx].vaddr)))) {
                        try seg_ni.moved(gpa, &elf.mf);
                    }
                },
                else => elf.targetStore(&phdr[phndx].@"align", @intCast(@max(
                    elf.targetLoad(&phdr[phndx].@"align"),
                    min_align.toByteUnits(),
                ))),
            },
        }
        // Continue on to the parent segment, if any
        switch (elf.getNode(seg_ni.parent(&elf.mf).unwrap().?)) {
            .segment => |parent_phndx| phndx = parent_phndx,
            .elf => return,
            else => unreachable,
        }
    }
}

pub fn addNodeAssumeCapacity(elf: *Elf, ni: MappedFile.Node.Index, node: Node) MappedFile.Node.Index {
    if (elf.nodes.len - @backingInt(ni) > 0) {
        assert(elf.getNode(ni) == .deleted);
        elf.nodes.set(@backingInt(ni), node);
    } else elf.nodes.appendAssumeCapacity(node);
    return ni;
}

fn deleteNode(elf: *Elf, node: *MappedFile.Node.Index.Optional) std.mem.Allocator.Error!void {
    const ni = node.unwrap().?;
    try ni.delete(elf.base.comp.gpa, &elf.mf);
    elf.nodes.set(@backingInt(ni), .deleted);
    node.* = .none;
}

/// If `sym` has a PLT entry, returns the address of that entry (specifically, the address which a
/// branch to the PLT should target). If `sym` does not have a PLT entry, returns `null`.
fn pltEntryTargetAddr(elf: *Elf, sym: Symbol.Id) ?u64 {
    const index = switch (sym.unwrap()) {
        .local => return null,
        .global => |gsi| elf.plt.getIndex(gsi.resolveAlias(elf)) orelse return null,
    };
    if (elf.pltEntryIsDead(index)) return null;
    const plt = elf.targetPltInfo();
    if (plt.plt_sec) |plt_sec| {
        return elf.shndx.plt_sec.vaddr(elf) +% index * plt_sec.entry_size;
    } else {
        return elf.shndx.plt.vaddr(elf) +% (plt.header_entries + index) * plt.entry_size;
    }
}

fn parseVersionedSymbolName(raw_name: []const u8) struct {
    name: []const u8,
    version: ?[]const u8,
    is_default_version: bool,
} {
    const split_index = std.mem.findScalar(u8, raw_name, '@') orelse {
        return .{ .name = raw_name, .version = null, .is_default_version = false };
    };
    if (std.mem.startsWith(u8, raw_name[split_index..], "@@")) {
        return .{
            .name = raw_name[0..split_index],
            .version = raw_name[split_index + 2 ..],
            .is_default_version = true,
        };
    } else {
        return .{
            .name = raw_name[0..split_index],
            .version = raw_name[split_index + 1 ..],
            .is_default_version = false,
        };
    }
}

const VerdefAdapter = struct {
    elf: *Elf,
    pub fn eql(ctx: VerdefAdapter, lhs_name: String(.dynstr), _: void, rhs_index: usize) bool {
        const elf = ctx.elf;
        const rhs_entry = &elf.verdefSlice()[rhs_index + 1];
        const rhs_name: String(.dynstr) = @fromBackingInt(
            elf.targetLoad(&rhs_entry.aux.name),
        );
        return lhs_name == rhs_name;
    }
    pub fn hash(ctx: VerdefAdapter, name: String(.dynstr)) u32 {
        _ = ctx;
        return @truncate(std.hash.int(@backingInt(name)));
    }
};

fn verdefId(elf: *Elf, version: String(.dynstr)) Error!u15 {
    const gpa = elf.base.comp.gpa;
    const adapter: VerdefAdapter = .{ .elf = elf };
    try elf.shndx.gnu_version_d.get(elf).ni.ensureMinimumSize(gpa, &elf.mf, (elf.verdef.count() + 2) * @sizeOf(VerdefEntry));
    const gop = try elf.verdef.getOrPutAdapted(gpa, version, adapter);

    if (gop.found_existing) {
        const verdef_ptr = &elf.verdefSlice()[gop.index + 1].def;
        return @intCast(@backingInt(elf.targetLoad(&verdef_ptr.ndx)));
    }

    const version_id = std.math.cast(u15, elf.next_version_id) orelse {
        return elf.base.comp.link_diags.fail(
            "symbol version identifier '{d}' out of range",
            .{elf.next_version_id},
        );
    };
    elf.next_version_id += 1;

    errdefer comptime unreachable;

    switch (elf.shdrPtr(elf.shndx.gnu_version_d)) {
        inline else => |shdr| {
            const old_size = elf.targetLoad(&shdr.size);
            const old_info = elf.targetLoad(&shdr.info);
            assert(old_info == gop.index + 1);
            assert(old_size == old_info * @sizeOf(VerdefEntry));
            elf.targetStore(&shdr.info, old_info + 1);
            elf.targetStore(&shdr.size, old_size + @sizeOf(VerdefEntry));
        },
    }
    const verdef_slice = elf.verdefSlice();
    elf.targetStore(&verdef_slice[gop.index].def.next, @sizeOf(VerdefEntry));
    const entry_ptr = &verdef_slice[gop.index + 1];
    entry_ptr.* = .{
        .def = .{
            .version = 1,
            .flags = 0,
            .ndx = @fromBackingInt(version_id),
            .cnt = 1,
            .hash = std.elf.hash.calculate(version.slice(elf)),
            .aux = @offsetOf(VerdefEntry, "aux"),
            .next = 0,
        },
        .aux = .{
            .name = @backingInt(version),
            .next = 0,
        },
    };
    if (elf.targetEndian() != std.lang.Endian.native) {
        std.mem.byteSwapAllFields(VerdefEntry, entry_ptr);
    }

    return version_id;
}

const VerneedAdapter = struct {
    elf: *Elf,
    const Key = struct {
        file: String(.dynstr),
        /// If this is `null`, key refers to a `std.elf.Verneed` entry; otherwise it refers to a
        /// `std.elf.Vernaux` entry.
        version: ?String(.dynstr),
    };
    const StoredKey = struct {
        kind: enum(u8) { verneed, vernaux },
        /// If `kind == .verneed`, this is the index in `.gnu.version_r` of the last
        /// `std.elf.Vernaux` in this `std.elf.Verneed`.
        ///
        /// If `kind == .vernaux`, this is the index in `.gnu.version_r` of the `std.elf.Verneed`
        /// associated with this `std.elf.Vernaux`.
        last_or_verneed_index: u32,
    };
    pub fn eql(ctx: VerneedAdapter, lhs: Key, rhs_stored: StoredKey, rhs_index: usize) bool {
        const elf = ctx.elf;

        const lhs_version = lhs.version orelse {
            if (rhs_stored.kind != .verneed) return false;
            const rhs_file: String(.dynstr) = @fromBackingInt(elf.targetLoad(
                &elf.verneedSlice()[rhs_index].verneed.file,
            ));
            return lhs.file == rhs_file;
        };

        if (rhs_stored.kind != .vernaux) return false;
        const rhs_file: String(.dynstr) = @fromBackingInt(elf.targetLoad(
            &elf.verneedSlice()[rhs_stored.last_or_verneed_index].verneed.file,
        ));
        const rhs_version: String(.dynstr) = @fromBackingInt(elf.targetLoad(
            &elf.verneedSlice()[rhs_index].vernaux.name,
        ));

        return lhs.file == rhs_file and lhs_version == rhs_version;
    }
    pub fn hash(ctx: VerneedAdapter, key: Key) u32 {
        _ = ctx;
        const packed_key: packed struct(u65) {
            file: String(.dynstr),
            have_version: bool,
            version: String(.dynstr),
        } = .{
            .file = key.file,
            .have_version = key.version != null,
            .version = key.version orelse .empty,
        };
        return @truncate(std.hash.int(@backingInt(packed_key)));
    }
};

fn verneedId(elf: *Elf, file: String(.dynstr), version: String(.dynstr)) Error!u15 {
    const gpa = elf.base.comp.gpa;
    const adapter: VerneedAdapter = .{ .elf = elf };
    try elf.shndx.gnu_version_r.get(elf).ni.ensureMinimumSize(gpa, &elf.mf, (elf.verneed.count() + 2) * @sizeOf(VerneedEntry));

    if (elf.verneed.getIndexAdapted(@as(VerneedAdapter.Key, .{
        .file = file,
        .version = version,
    }), adapter)) |index| {
        const vernaux_ptr = &elf.verneedSlice()[index].vernaux;
        return @intCast(elf.targetLoad(&vernaux_ptr.other));
    }

    const file_gop = try elf.verneed.getOrPutAdapted(gpa, @as(VerneedAdapter.Key, .{
        .file = file,
        .version = null,
    }), adapter);

    const predicted_vernaux_index: u32 = @intCast(elf.verneed.count());

    if (!file_gop.found_existing) {
        switch (elf.shdrPtr(elf.shndx.gnu_version_r)) {
            inline else => |shdr| {
                const old_size = elf.targetLoad(&shdr.size);
                const old_info = elf.targetLoad(&shdr.info);
                elf.targetStore(&shdr.info, old_info + 1);
                elf.targetStore(&shdr.size, old_size + @sizeOf(VerneedEntry));
            },
        }
        const new_verneed = &elf.verneedSlice()[file_gop.index].verneed;
        new_verneed.* = .{
            .version = 1,
            .cnt = 1,
            .file = @backingInt(file),
            .aux = @sizeOf(VerneedEntry),
            .next = 0,
        };
        if (elf.targetEndian() != std.lang.Endian.native) {
            std.mem.byteSwapAllFields(std.elf.Verneed, new_verneed);
        }
        file_gop.key_ptr.* = .{
            .kind = .verneed,
            .last_or_verneed_index = predicted_vernaux_index,
        };

        if (file_gop.index != 0) {
            const prev_verneed = &elf.verneedSlice()[elf.last_verneed_file_index].verneed;
            const off = (file_gop.index - elf.last_verneed_file_index) * @sizeOf(VerneedEntry);
            assert(off > 0);
            elf.targetStore(&prev_verneed.next, @intCast(off));
        }
        elf.last_verneed_file_index = file_gop.index;
    } else {
        const verneed_ptr = &elf.verneedSlice()[file_gop.index].verneed;
        const old_cnt = elf.targetLoad(&verneed_ptr.cnt);
        assert(old_cnt > 0);
        elf.targetStore(&verneed_ptr.cnt, old_cnt + 1);

        const last_vernaux_index = file_gop.key_ptr.last_or_verneed_index;
        const last_vernaux = &elf.verneedSlice()[last_vernaux_index].vernaux;
        assert(elf.targetLoad(&last_vernaux.next) == 0);
        const off = (predicted_vernaux_index - last_vernaux_index) * @sizeOf(VerneedEntry);
        assert(off > 0);
        elf.targetStore(&last_vernaux.next, @intCast(off));
        file_gop.key_ptr.last_or_verneed_index = predicted_vernaux_index;
    }

    const version_gop = try elf.verneed.getOrPutAdapted(gpa, @as(VerneedAdapter.Key, .{
        .file = file,
        .version = version,
    }), adapter);
    assert(!version_gop.found_existing); // already checked `contains` earlier
    assert(version_gop.index == predicted_vernaux_index);
    switch (elf.shdrPtr(elf.shndx.gnu_version_r)) {
        inline else => |shdr| {
            const old_size = elf.targetLoad(&shdr.size);
            elf.targetStore(&shdr.size, old_size + @sizeOf(VerneedEntry));
        },
    }
    version_gop.key_ptr.* = .{
        .kind = .vernaux,
        .last_or_verneed_index = @intCast(file_gop.index),
    };

    const version_id = std.math.cast(u15, elf.next_version_id) orelse {
        return elf.base.comp.link_diags.fail(
            "symbol version identifier '{d}' out of range",
            .{elf.next_version_id},
        );
    };
    elf.next_version_id += 1;

    const new_vernaux = &elf.verneedSlice()[version_gop.index].vernaux;
    new_vernaux.* = .{
        .hash = std.elf.hash.calculate(version.slice(elf)),
        .flags = 0,
        .other = version_id,
        .name = @backingInt(version),
        .next = 0,
    };
    if (elf.targetEndian() != std.lang.Endian.native) {
        std.mem.byteSwapAllFields(std.elf.Vernaux, new_vernaux);
    }

    return version_id;
}
